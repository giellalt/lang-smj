! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no


! ========================================================================== !
!                   Lule Sámi morphophonological rule set                    !
! ========================================================================== !

! twol-smj.txt
! The file contains the rule set for the non-segmental
! Lule Sámi morphphonological rules

!! !!!Background
!! 
!! The file itself is located in {{langs/smj/src/phonology/smj-phon.twolc}}.
!! The file is modeled upon the corresponding file for North Sámi, but has been
!! revised and differs from it on several issues. The grammatical sources are
!! Spiik 1989: Lulesamisk grammatik and Nystø and Johnsen 2001: Sámásta 2.

!! !!!File structure
!!
!! The rule file has the sections Alphabet, Sets, Definition and Rules. The rules are ordered thematically, 
!! with 3 main sections: Consonant alternations (except cg), vowel alternations, and consonant gradation.


Alphabet !!≈ !!!The @CODE@ section

!! !!The real Lule Sámi Alphabet
!! 
!! All Lule Saami letters are listed. The Lule Sámi ENG sound is represented as ñ. 
!! Lule Sámi letter repertoire is not fully standardised. In the source code we write (and you shall write!) æ; ø; ŋ, 
!! but the parser tolerates input written with the the letters ä; ö; ń, ñ (cf. the 4 rules in the file smj/src/orthography/spellrelax.regex).
!!

 a á b c d e f g h i j k l m n ñ ń ŋ o p q			! small
 r s t u v w x y z å ä ø ö å æ:æ æ:ä %-
 é ó ú í à è ò ù ì ë ü ï â ê ô û î ã ý
 ç č đ ð š ŧ þ ß ª

 A Á B C D E F G H I J K L M N Ñ Ń Ŋ O P Q			! capital
 R S T U V W X Y Z Å Ä Ø Ö Å Æ:Æ Æ:Ä
 É Ó Ú Í À È Ò Ù Ì Ë Ü Ï Â Ê Ô Û Î Ã Ý
 Ç Č Đ Ð Š Ŧ þ

!! The 3rd degree mark ' is never realized, hence declared as ':0.
!! {{{
 ':0  !!= @CODE@ = Gradation mark
 %/   !!= @CODE@ = Literal /, not the TWOLC reserved symbol
 '7:' !!= @CODE@ = From sme.
!!! }}}

! Literal quotes and angles must be escaped (cf morpheme boundaries further down):
 »7     ! »
 «7     ! «
 %[%>%] ! >
 %[%<%] ! <

!! h2, g2 etc. are consonants deleted in the Nom. m3, d3 etc. (?) are consonants that undergo certain processes word-finally. 
!! This issue should be looked into. Perhaps the two sets can be unified. 
!! The reason why there are more distinctions than for sme, is that the cns deletion process is more phonological in sme.

 e7:e i7:i o7:o o9:o u7:u z9:z                  ! Morphophonemes  in sme, here temporarily due to common propernoun file

 h8:h g8:g m8:m n8:n		                    ! these are deleted in nom
 b9:b d9:d g9:g l9:l m9:m n9:n h9:h r9:r    	! these can not occur before #
 j9:j k9:k t9:t s9:s p9:p                       ! Non-sámi cons clusters
 a9:a e9:e o9:o æ9:æ ö9:ö ä9:ä ø9:ø             ! Do not change these where they would normally undergo umlaut etc
! Ä9:Ä Ö9:Ö Æ9:Æ Ø9:Ø                           ! Wd-final foreign vow, and non-fluctuating æ/ä

!! !!The Dummy symbols
!!
!! The Dummy symbols are taken from the sme file for convenience, only a small part of them are actually used, 
!! they are defined in the Sets section along the way, included there as soon as they are used. 
!! The set of actually used Dummy symbols is thus the set declared in "Dummy".
!!
!! The Dummy symbols trigger morphophonological rules. X is used for nouns
!! and adjectives, Y for verbs and Q for processes common to all
!! The symbols themselves are used in the following way:

! OBS: the definitions are not all correct or sufficiently specific

 X1:0 !!≈ ; __@CODE@__ : Deletes final consonants in short essive of odd syllables
 X2:0 !!≈ ; __@CODE@__ : WeG and neutralization of g8, etc. (hivsik-hivsiga)
 X3:0 !!≈ ; __@CODE@__ : Weg and deletion of g8, etc. (bena-bednaga)
 X4:0 !!≈ ; __@CODE@__ : e:á and e:å in illatives and px. a:á and o:u in Px and ill of a-stem actors and o-stems
 X5:0 !!≈ ; __@CODE@__ : e:á, e:å and o:u in odd-syllable nouns, but also for some even nouns (o:u f.eks)
 X6:0 !!≈ ; __@CODE@__ : Deviant III-I consonant gradation (in contracted stems, guobbmu:guomoj)
 X7:0 !!≈ ; __@CODE@__ : WeG and e:á, e:å, o:á, o:u in front of diminutives, e:å in -lasj der
 X8:0 !!≈ ; __@CODE@__ : Stem vowel alternations in Px
 X9:0 !!≈ ; __@CODE@__ : Stem-vowel and central consonant shortening in first part(s) of compounds  
 Q1:0 !!≈ ; __@CODE@__ : The general weak grade trigger. Stem vowel change e:i and o:u in fron of j.
 	  !! 				 Dipht. simpl.  Any environment #only# demanding WeG shall use Q1.
 Q2:0 !!≈ ; __@CODE@__ : Vowel harmony: 2nd syll e realized as å whenever 1st syll is å.
 Q3:0 !!≈ ; __@CODE@__ : WeG in contracted, also does not trigger Dipht simpl.
 Q4:0 !!≈ ; __@CODE@__ : Stem vowel change e:i and o:u in fron of j. Dipht. simpl. Like Q1 but strong grade.
 Q5:0 !!≈ ; __@CODE@__ : e:á stem vowel change for word diehtet. Weak grade.
 Q6:0 !!≈ ; __@CODE@__ : e:á stem wovel change for word diehtet. Strong grade.
 Q7:0 !!≈ ; __@CODE@__ : e:á stem vowel change for word diehtte. Extra strong grade
 Q8:0 !!≈ ; __@CODE@__ : Stem vowel deletion, impII of verbs.
 Q9:0 !!≈ ; __@CODE@__ : TBW  
 Y1:0 !!≈ ; __@CODE@__ : Stem vowel deletion, imp 3sg, 3du, 2pl, 3pl of verbs
 Y2:0 !!≈ ; __@CODE@__ : "Indicative Present Singular 3rd Final Vowel in verbs"
 Y3:0 !!≈ ; __@CODE@__ : PrsPrc
 Y4:0 !!≈ ; __@CODE@__ : e &gt; u in front of dersuff, o &gt; u and e &gt; á in front of dersuffix -alla
 Y5:0 !!≈ ; __@CODE@__ : e &gt; a, i &gt; á, o &gt; u, e &gt; å in verb derivation
 Y6:0 !!≈ ; __@CODE@__ : "Consonant insertion as II-III strengthening gradation", verbs +PrsPrt and +Imprt+Du2
 Y7:0 !!≈ ; __@CODE@__ : "Consonant insertion as II-III strengthening gradation", nouns and propernouns
 Y8:0 !!≈ ; __@CODE@__ : "Stem vowel deletion in even-syllable verbs, imp 1du, 1pl"
 Y9:0 !!≈ ; __@CODE@__ : "Stem vowel deletion in short passives of even-syllable verbs
 	  !!		          and Consonant insertion as II-III strengthening gradation"
 Z1:0 !!≈ ; __@CODE@__ : TBW   
    
 
!! !!Morpheme boundaries:
 «  !!= ; __@CODE@__ : Derivational prefix
 »  !!= ; __@CODE@__ : Derivational suffix
 %< !!= ; __@CODE@__ : Inflectional prefx
 %> !!= ; __@CODE@__ : Inflectional suffix
 #  !!= ; __@CODE@__ : Word boundary for both lexicalised and dynamic compounds
 %^ !!= ; __@CODE@__ : (exceptional) soft hyphenation point
 %  !!= ; __@CODE@__ : a space

! End of alphabet definitions
;

Sets !!≈ !!!The @CODE@ section

!! These are the sets:
!! ;Vow     : the vowels
!! ;Cns     : the consonants
!! ;StemCns : consonants that may occur in stem-final position
!! ;DelCns  : the consonants that are deleted in nominative
!! ;Dummy   : the set of dummy symbols, they are there to trigger certain morphophonological symbols
!! ;WeG     : the dummy symbols that trigger weak grade


 Vow     = a á e i o u y æ ä ø ö å
           A Á E I O U Y Æ Ä Ø Ä Å
           é ó ú í à è ò ù ì ë ü ï â ê ô û î ã ý
           É Ó Ú Í À È Ò Ù Ì Ë Ü Ï Â Ê Ô Û Î Ã Ý
           a9 e9 o9 æ9 ä9
!                 Æ9 Ä9
			;
 æä      = æ ä ;
 Cns     = b b9 c d d9 f g g8 g9 h h8 h9 j j9 k l l9 m m8 m9 n n8 n9 ŋ ñ ń p q r r9 s t v w x z z9 ' ; ! All consonants
 Cns7    =      c      f         h       j      l    m       n       ŋ ñ ń p q r    s t v w x z      ; ! Surface cons excl 1st members of xx-type G3
 Cns8    = b    c d    f g       h       j    k l    m       n       ŋ ñ ń p q r    s t v w x z      ; ! All surface consonants
 Cns9    =   b9     d9     g8 g9   h8 h9   j9     l9   m8 m9   n8 n9             r9             '    ; ! Underlying consonants
 Cns4    =             f                        l    m       n       ŋ ñ ń     r        v            ; ! Don't remember ...?
 StemCns = b b9   d d9   g g8 g9 h h8 h9 j j9   l l9 m m8 m9 n n8 n9 ŋ ñ ń     r r9 s                ; ! Can occur stem-finally
 DelCns  =                 g8      h8               m8      n8                                       ; ! deleted in nom...
 WeG     = X2 X3 X7 Y5 Q1 Q2 Q3 Q6                                                                   ;
 Dummy   = X2 X3 X4 X5 X6 X7 X8 Y1 Y2 Y3 Y4 Y5 Y6 Y7 Y8 Y9 Q1 Q2 Q3 Q4 Q5 Q6 Q7 Z1 %> » %^           ;
 Hyph    = %-                                                                                        ;

Definitions !!≈ !!!The @CODE@ section

!! In this section, the consonants are defined. This includes consonant clusters in the various grades and consonant alternations.

 LCnsPhon7 = [ :Cns7 | :n :j | :s :j | :t :j | :t :s ] ;     ! Using Cns7, excluding 1st member of two-cns G3
 LCnsPhon  = [ :Cns8 | :n :j | :s :j | :t :j | :t :s ] ;     ! Using Cns8, without the previous restriction

!! !!G3 vs G2
!! The alternation patterns according to Spiik's alternations series.  
!! | S7 | kkn:k0n           | series 1
!! | S8 | f'f:f0f           | series 2
!! | S9 | jgg:j0g           | series 3
!! | S4 | hkk:h0k           | series 4
!! | S5 |  xy:zy (no zeros) | series 5
!! | S6 |  xx:yy (no zeros) | series 6
!! | S7 |  xy:zy (no zeros) | series 7
!! | S8 |  ----- (no cg)    | series 8

 LowerG2  !!≈ ; @CODE@ : A definition of Grade2 consonant sequences referring mostly to the surface level
 		   = [
               [
                  (Cns:0)  LCnsPhon7 (Cns:0) LCnsPhon (Cns:0)            ! xy that cannot be G3, since x cannot form xy G3.
               |                                                         ! nijbe,
                  [           ! This section is for 3-cons G2. and for 2cns G2 that share the initial cns with 2cns G3
                     (Cns:0) [:j|:l|:m|:n|:v] (Cns:0) :s :t                  ! S9, 3cns-G2 bäjstov, etc.
                  |
                     (Cns:0) [ :l | :r | :n | :j ]      (Cns:0) :s :k                   ! S9, 3cns-G2 sválskes, etc.
                  |
                     (Cns:0) [  ! 2cns G2 that share the initial cns wit 2cns G3
                                :b (Cns:0) [ :d | :m | :j | :l | :n | :n :j | :r | :s | :t :j | :t :s  ] ! S9, initial b
                                |                                                                        ! gábdev
                                :d (Cns:0) [ :j | :n | :n :j ]                                           ! S7, initial d
                                |                                                                        ! iednev
                                :g (Cns:0) :ŋ                                                            ! S7, initial g
                                |                                                                        ! låg0ŋot
                                :k (Cns:0) [ :n | :k ]                                                   ! S7, initial k
                                |
                                :g (Cns:0) :n                                                            !däggna:degna                        
                             ]                                                                           !
                             |
                             :r (Cns:0)   :s :j :t                                                    ! S9, rsjt, bårsjtav
                  ]
               ]
                 - [ ! Exclude the following sequences, since these are in reality G3:
                     [ d t [ s | j ] ]
                     | b b
                     | d d
                     | g g
                     | k [ s | t | t j | t s ]
                     | f ':0 f
                     | l ':0 l
                     | m ':0 m
                     | n ':0 n
                     | n ':0 n j
                     | ŋ ':0 ŋ
                     | r ':0 r
                     | s ':0 s
                     | s ':0 s j
                     | v ':0 v
                   ]
             ];
 LowerG1  !!≈ ; @CODE@ : A definition of Grade 1 consonant sequences
           =   (Cns:0)   LCnsPhon ;
 LowerG12  !!≈ ; @CODE@ : A definition of Grade 1 or 2 consonant sequences 
           = [ LowerG1 | LowerG2 ] ;

 G32  !!≈ ; @CODE@ : A definition of Grade 3 or 2 consonant sequences
 	 =  f ':0 f | l ':0 l | m ':0 m | n ':0 n | n ':0 n j | ŋ ':0 ŋ | r ':0 r | s ':0 s | s ':0 s j | v ':0 v ;

 G3  !!≈ ; @CODE@ : A definition of Grade 3 consonant sequences 
 	=[ Cns Cns Cns Cns |
      :b b m|
      :d d [n|j]|
      :g g ŋ|
      k k n|
      l l [d|j|t]|
      m m [b|p]|
      n n d|
      ŋ ŋ [g|k]|
      p p [s|t]|
      s s[k|m|n|t]|
      b [d d|j j|l l|n n|r r|s s]|
      j [b b|d d|g g|k k|l l|m m| (h) n n|ŋ ŋ|p p|r r|s s|t t|v v]|
      l [b b|f f|g g|k k|m m|ŋ ŋ|p p|s s|v v]|
      m [k k|s s]|
      n s s|
       ŋ [n n |t t]|
       r [b b|[d|:d] d|f f|g g|j j|k k|m m|n n|ŋ ŋ|p p|s s|t t|v v|d d]|
       v [d d|g g|k k|l l| (h) n n|ŋ ŋ|r r|s s|t t]|
       h [[k|:k] k|[p|:p] p|[t|:t] t]|
       d t [j|s]|
       b b|d d|g g|
       k[s|t[j|s]]] ;

!diphtsimpl IF Vow: G32 AND G2 context - [Y6 Y7 Y9];
! oa:å <=> Vow: G32 Vow: WeG ; ! Evt. a subset of WeG, if only some WeG Dummys trigger diphtsimpl

! 1.86, let's see
!
!Definitions
!
! CnsPhon = [ [ Cns7 | Cns8: ] | n j | s j | t j | t s ] ;
! G3      = [ [ CnsPhon CnsPhon [ CnsPhon+ | '] ] - [[[j|l|m|n|v] s t ] | [l|r] s k | r s j t ] ] ;
! G2      = [   CnsPhon CnsPhon |                   [[[j|l|m|n|v] s t ] | [l|r] s k | r s j t ] ] ;
! G1      =     CnsPhon ;
! G12     = [ G1 | G2 ] ;
!
! LowerCnsPhon = [ :Cns7 | :n :j | :s :j | :t :j | :t :s ] ;
! LowerG2  = [ (Cns:0)  LowerCnsPhon (Cns:0) LowerCnsPhon (Cns:0) |
!              (Cns:0) [[[:j|:l|:m|:n|:v] :s :t ] |
!              (Cns:0) [:l|:r] :s :k |
!              (Cns:0)  :r :s :j :t ] ] ;
! LowerG1  = (Cns:0)   LowerCnsPhon ;
! LowerG12 = [ LowerG1 | LowerG2 ] ;



Rules !!≈ !!!The @CODE@ section

!! !!Overview

!! The rules section has the following chapters: Consonant alternations in certain pos, vowel lengthening, diphthong simplification, stem vowel alternations, consonant gradation rules


!! !!Consonant alternations in certain pos

!! All rules deal with word-final position.

! This is a dummy negative test, in order to get the test paradigm work.

!$ a
!$ b


"Word Final Devoicing of Certain Single Consonants d9 etc." 
!! __@RULENAME@__  
 Cx:Cy <=> Vow: (Cns:) (%>:) (»:) ( Dummy: ) _ (%>:) (»:) ( Dummy: ) [ Hyph | # ] ;
       where Cx in ( b9 d9 g9 m9 h9 )
             Cy in ( p  t  k  v  j  )
	   matched ;
!€ iemed9#
!€ iemet#

"Word final weakening -tj and -ttj to -sj part 1"
!! __@RULENAME@__  
 t:s <= Vow: (%>:) (»:) ( Dummy: ) ( d: ) _ j (%>:) (»:) ( Dummy: ) [ Hyph | # ] ;


"Word final weakening -tj and -ttj to -sj part 2"
!! __@RULENAME@__  
 t:s => Vow: (%>:) (»:) ( Dummy: ) ( d: ) _ j (%>:) (»:) ( Dummy: ) [ Hyph | # ] ;
! t:s <=> Vow: (%>:) (»:) ( Dummy: ) _ ( t: ) j (%>:) (»:) ( Dummy: ) (Hyph) #: ;
!€ jågåtj#
!€ jågåsj#

!€ gål'leX7tj#
!€ gål0lå0sj#

!"Word final weakening of -ttj to -sj"
! t:0 <=> Vow: t:s _ j (%>:) (»:) (Hyph) #: ;
! oajválattja : oajválasj This is moved to the t:0 rule set.
! Still, this will fail if and when clitics are added to the word.
! d:s changed to d:

"Word Final Deletion of n8 m8 g8 h8"
!! __@RULENAME@__  
  Cx:0 <=> Vow _  (%>:) (»:) X3: ;
	where Cx in ( DelCns ) ;
! The twoL-sme had [X4:|X8:|X6: Cons:|X9:], be prepared to
! expand this rule accordingly.

!€ loavddag8X3#
!€ l0åv0da00#


"Word Final Neutralization of g8, h8, m8"
!! __@RULENAME@__  
  Cx:Cy <=> Vow _  (%>:) (»:) X2: (Hyph) #  ;
			where Cx in (g8 h8 m8)
			      Cy in (t  t  n )
			matched ;
! The twol-sme had [X2: | X7: ], be prepared to expand this rule accordingly.
! Since we use X2, the "2" in g8 etc. may be superfluous...

!"Word-final Devoicing of d9, m9"
! Cx:Cy <=> Vow _  (%>:) (»:) (Hyph) #: ;
!       where Cx in ( d9 m9 )
!	          Cy in ( t  v  )
!	     matched ;
! iemet:iemeda


"Deleting Final h9 in Short Essive of Uneven Syllables"
!! __@RULENAME@__  
 h9:0 <=> Vow: _ (%>:) (»:) Q1: X1: n ;

"Deleting Final l9 in Short Essive of Uneven Syllables"
!! __@RULENAME@__  
 l9:0 <=> Vow: _ (%>:) (»:) [Q1:|X3:] X1: n ;

"Deleting Final m9 in Short Essive of Uneven Syllables"
!! __@RULENAME@__  
 m9:0 <=> Vow: _ (%>:) (»:) Q1: X1: n ;

"Deleting Final n9 in Short Essive of Uneven Syllables"
!! __@RULENAME@__  
 n9:0 <=> Vow: _ (%>:) (»:) Q1: X1: n ;

"Deleting Final r9 in Short Essive of Uneven Syllables"
!! __@RULENAME@__  
 r9:0 <=> Vow: _ (%>:) (»:) Q1: X1: n ;



!      where Cx in ( s m h ) ;
! originally it was true for s, m, h. Keep an eye on this.

!"Word Final Cluster Simplification"  ! málestit : máles
! t:0 <=> Vow (%>:) (»:) ( Dummy: ) s _ (%>:) (»:) (Hyph) (Hyph) #:  ;
!be prepared to define more rules on cluster simpl.
!moved to Consonant gradation
!€ málest#
!€ máles0#


!! !!Vowel lengthening

!! The second syllable vowel a is lengthened to á whenever the stem consonants are in grade 1 and the first syllable vowel is short. Short vowels cannot preceed and follow a single intervocalic consonant.

"Compulsatory lengthening in grade I even-syllables"
!! __@RULENAME@__  
! a:á <=>  Cns  [ a|i|u ] [ Cns:0 Cns: | Cns: Cns:0 ] _ (%>:) (»:) WeG: ; ! in grade I. Gussa-gusá
  a:á <=> (Cns) [ a|i|u ] [ Cns:0 Cns: (%>:) (»:) | Cns: Cns:0 (%>:) (»:) ] _ (Cns:0 | Cns:) (%>:) (»:) WeG: ; ! in grade I
          (Cns) [ a|i|u ]   Cns:0 [ n | s | t ] j     _ ( StemCns: ) (%>:) (»:) WeG: ; ! nj, sj, tj in grade I
          (Cns) [ a|i|u ]   Cns:0        t s          _ (%>:) (»:) WeG: ; ! ts in grade I
	                        Cns                       _ (%>:) (»:) Y2: ; ! Indicative Present Singular 3rd Final Vowel in verbs. Bassat-bassá
                                                      _ (%>:) (»:) Y6: X4: j ; ! Ill sg of Actor nouns bassat : basse : bas'sáj
                                                      _ (%>:) (»:) Y7: ; ! Diminutive of Actor nouns bassat : basse : bassásj
                                                      _ (%>:) (»:) Y6: X4: ( X8: ) [ m | t | d | s ] ; ! Compulsary e:á in Px, Actors! sállat : sálle : sállám
! where Vx in ( a i u ) ; ! not all simple vowels ( e o å æ ) are evidently long!
!€ gussaQ1#
!€ gu0sá0#
!€ skihpaQ1s#
!€ ski0bá0s#

! the right-context facultative consonant in order to ensure skibás.
!"Compulsatory lengthening in palatal grade I even-syllables"
! a:á <=> Cns [ a|i|u ] [ n | s | t ] j _ WeG: ; ! nj, sj, tj in grade I
! where Vx in ( a i u ) ; ! not all simple vowels ( e o å æ ) are evidently long!
! example?
! delete if preceeding works

!! !!Diphtong simplification

!! The diphthong simplification handles oa:å and æ:e. Phonologically, these are identical processes, but since the dipthong is written by two letters in the former case and by one letter in the latter, the alternations must be handled separately. This section also handles ie:æ, these are in principle the same as oa:å, but the alternation does not occur in so many contexts. 

!!The oa:å simplification has a two-art rule, one for o deletion, and one for a:å change. Otherwise, the rules are identical (this is the type where xfst would have been better than twolc).

"oa:å Diphtong Simplification Part I" !oa:å -> Q1 or Q2 and short second vowel (a,u,i)
!! __@RULENAME@__  
 o:0 <=> [ # | Cns: | (%>:) (»:) Dummy: ] _ a: LowerG12 [ a: | i: ] (%>:) (»:) WeG:          ; !1 WeG of a- and i-stems. Foannda-fånda. 
         [ # | Cns: | (%>:) (»:) Dummy: ] _ a: LowerG12   e:        (%>:) (»:) Q1: ( X5: ) j ; !2 ComSg & G-C Pl of e-stems. Goahte-gådijn
!        [ # | Cns: | (%>:) (»:) Dummy: ] _ a: LowerG12   a:        (%>:) (»:) Y6: ( X5: ) j ; !3 ComSg & G-C Pl of Actor nouns from a-verbs, G1
!        [ # | Cns: | (%>:) (»:) Dummy: ] _ a: LowerG12   e:        (%>:) (»:) Y6: ( X5: ) j ; !4 ComSg & G-C Pl of Actor nouns from e-verbs, G1
         [ # | Cns: | (%>:) (»:) Dummy: ] _ a: LowerG12   e:        (%>:) (»:) Y1: ; !5 Different verb forms. Doajmmat-dåjmav
         [ # | Cns: | (%>:) (»:) Dummy: ] _ a: LowerG12   o:        (%>:) (»:) X4: j ; !6 Ill. Oahppo-åhpujn
         [ # | Cns: | (%>:) (»:) Dummy: ] _ a: LowerG12   o: (Cns:*) (%>:) (»:) [ Y1: | Y2: | Y3: | Y4: | Y5: | X7: | X4: (X8:) | [Q1:|Q4:] X4: X8: ] ; !7 Long pass (?? *bårkuduvvat, doaroduvvat), Dim (bålusj) and Prs Sg3 (?) etc. Px (bålum)
!        [ # | Cns: | (%>:) (»:) Dummy: ] _ a: LowerG12   o: (Cns:*) (%>:) (»:) [ Y1: | Y2: | Y3: | Y4: | Y5: | Y7: | Y9: | X7: ] ; !7 Long pass, Dim and Prs Sg3 etc.
         [ # | Cns: | (%>:) (»:) Dummy: ] _ a: LowerG12   o: Cns:+ (%>:) (»:) X5: Vow ; !8 odd-syll, oblique. Oaloj-ålluha
         [ # | Cns: | (%>:) (»:) Dummy: ] _ a: LowerG12   a  [g8:|l9:|r9:|s] (%>:) (»:) [ X3: | Q1: ] (X1:) [ (Hyph) #  | n ] ; !9 NE låvda - loavddaga
         [ # | Cns: | (%>:) (»:) Dummy: ] _ a: LowerG12   e: (%>:) (»:) Q1:0 t: j [ #  | [Vow:[v|n|h|b|p|# ]]]; !10 Potensialis doadjet:dåjitjav
         [ # | Cns: | (%>:) (»:) Dummy: ] _ a: LowerG12   e: (%>:) (»:) Q4: ( X5: ) j ; !11 ComSg & G-C Pl of e-stems. Goahte-gådijn
         [ # | Cns: | (%>:) (»:) Dummy: ] _ a: LowerG12   o:u (%>:) (»:) [ Q1: | Q4: ] X5: j n ([a:|i:]) ; !12Px Com gådijnam
         [ # | Cns: | (%>:) (»:) Dummy: ] _ a:      G32   o:u (%>:) (»:) WeG: ; ! boallo-bålujn?
         [ # | Cns: | (%>:) (»:) Dummy: ] _ a:      G32   e:i (%>:) (»:) WeG: ; ! oalle-ålijn?
         [ # | Cns: | (%>:) (»:) Dummy: ] _ a:      G32 [ a: | i: ] (%>:) (»:) WeG: ;



"oa:å Diphtong Simplification Part II"
!! __@RULENAME@__  
 a:å <=> [ # | Cns: | (%>:) (»:) Dummy: ] o: _ LowerG12 [ a: | i: ] (%>:) (»:) WeG: ;       !1 WeG of a- and i-stems
         [ # | Cns: | (%>:) (»:) Dummy: ] o: _ LowerG12   e:        (%>:) (»:) Q1: ( X5: ) j ;       !2 ComSg & G-C Pl of e-stems
!        [ # | Cns: | (%>:) (»:) Dummy: ] o: _ LowerG12   a:        (%>:) (»:) Y6: ( X5: ) j ;       !3 ComSg & G-C Pl of Actor nouns from a-verbs, G1
!        [ # | Cns: | (%>:) (»:) Dummy: ] o: _ LowerG12   e:        (%>:) (»:) Y6: ( X5: ) j ;       !4 ComSg & G-C Pl of Actor nouns from e-verbs, G1
         [ # | Cns: | (%>:) (»:) Dummy: ] o: _ LowerG12   e:        (%>:) (»:) Y1: ;       !5 Different verb forms
         [ # | Cns: | (%>:) (»:) Dummy: ] o: _ LowerG12   o:        (%>:) (»:) X4: j ; !6 Ill
         [ # | Cns: | (%>:) (»:) Dummy: ] o: _ LowerG12   o: (Cns:*) (%>:) (»:) [ Y1: | Y2: | Y3: | Y4: | Y5: | X7: | X4: (X8:) | [Q1:|Q4:] X4: X8: ] ; !7 Long pass and Prs Sg3 etc.  Px
!        [ # | Cns: | (%>:) (»:) Dummy: ] o: _ LowerG12   o: (Cns:*) (%>:) (»:) [ Y1: | Y2: | Y3: | Y4: | Y5: | Y7: | Y9: | X7: ] ; !7 Long pass and Prs Sg3 etc.
         [ # | Cns: | (%>:) (»:) Dummy: ] o: _ LowerG12   o: Cns:+ (%>:) (»:) X5: Vow ;       !8 odd-syll, oblique
         [ # | Cns: | (%>:) (»:) Dummy: ] o: _ LowerG12   a [g8:|l9:|r9:|s] (%>:) (»:) [ X3: | Q1: ] (X1:) [ (Hyph) #  | n ] ;       !9 NE låvda
         [ # | Cns: | (%>:) (»:) Dummy: ] o: _ LowerG12   e: (%>:) (»:) Q1:0 t: j [ #  | [Vow:[v|n|h|b|p|# ]]]; !10 Potensialis doadjet:dåjitjav
         [ # | Cns: | (%>:) (»:) Dummy: ] o: _ LowerG12   e: (%>:) (»:) Q4: ( X5: ) j ;       !11 ComSg & G-C Pl of e-stems.
         [ # | Cns: | (%>:) (»:) Dummy: ] o: _ LowerG12   o:u (%>:) (»:) [ Q1: | Q4: ] X5: j n ([a:|i:]) ; !12Px Com
         [ # | Cns: | (%>:) (»:) Dummy: ] o: _      G32   o:u (%>:) (»:) WeG: ; !  X2 X3 X7 Y5 Q1 Q2 Q3
         [ # | Cns: | (%>:) (»:) Dummy: ] o: _      G32   e:i (%>:) (»:) WeG: ;
         [ # | Cns: | (%>:) (»:) Dummy: ] o: _      G32 [ a: | i: ] (%>:) (»:) WeG: ;



!€ toahkkeY6X5jn
!€ toahkki00jn

!$ toahkkeY6X5jn
!$ t0åhkki00jn

!€ boalloX4j
!€ b0ållu0j

! BUG 193:
!€ roavggoX4j
!€ roavggu0j
!$ roavggoX4j
!$ r0åvggu0j

! G3:G2, fake ex
! Here, G2 allows diphtsimpl
!€ toas'soQ1X5jn
!€ t0ås0su00jn

! And vice versa
!$ toas'soQ1X5jn
!$ toas0su00jn

! G3:G3, lexical ex, no CG
! will have to have a different lexicon in noun-smj-lex,
! not pointing to Q1. Cf. GOAHTI/STAHTA sme.
!$ moas'soX5jn
!$ m0ås0su0jn

!€ moas'soX5jn
!€ moas0su0jn

! The next example shows actor noun in G3 (actor nouns are always G3)
!€ goar'roY6X5jn
!€ goar0ru00jn
! This test should hit, but fails, since G3 looks like G2 at the lower level.
! Further clever thinking is needed here.

! this is G2 > G3. Derived G3 from a G2 stem (actor noun)
!€ goarroY6X5jn
!€ goarru00jn
! This example should be like this (no diphsimpl), but at present our analysator gives diphtsimpl
! as it is G2. Whe have two possibilities:
! a. find a way of saying that it is G3 (impossible, I think)
! b. block the diphtsimpl not based upon (invisible) G3, but upon its triggering context.

! The next example shows errouneous dipht simpl for G3.
!$ goar'roY6X5jn
!$ g0år0ru00jn

! Here, G3 blocks diph simpl
!$ goar'roY2
!$ g0år0ru0

! The following example shows correct  diphtong simplification in G2 (3Sg pres)
!€ goarroY2
!€ g0årru0

!€ doad0jeY6
!€ doaddje0

!$ doad0jeY6
!$ d0åddje0

!G3 > G2
!€ goar'roY5d9it
!€ g0år0ru0dit

!$ goar'roY5d9it
!$ goar0ru0dit

! The following tests are using fictive, non-existent verbs, to test the correctnes of the mapping rules in twolc
! the verbs are *toabmot, oadot, etc.
! X4 for o:u in illsg, pres. part

!€ toab0moY6X4j
!€ toabbmu00j

!€ toabmoX4j
!€ t0åbmu0j

!$ toa0mboY6X4j
!$ t0åbbmu00j

!€ toabmoX7dallat
!€ t0å0mu0dallat
!$ toabmoX7dallat
!$ toa0mu0dallat

!€ oaddoY6X4j
!€ oaddu00j

!€ boassjkoQ1X5jn
!€ b0å0sjku00jn

!$ boassjkoQ1X5jn
!$ boas0jku00jn

!€ boajsstoQ1X5jn
!€ b0åj0stu00jn

!$ boajsstoQ1X5jn
!$ boaj0stu00jn

!€ boaggoQ1X5jn
!€ b0åkku00jn

!$ boaggoQ1X5jn
!$ boakku00jn


! All real sámi æ's are also ä's, but not in these contexts, where other rules will take over.
!"æ:ä Blocking"
! æ:ä /<= _ Cns:+ [ o: (%>:) (»:)                                            X4:                            j              |
!                   o:    (Cns:*) (%>:) (»:) [ Y1: | Y2: | Y3: | Y4: | Y5: | X7: | X4: (X8:) | [Q1:|Q4:] X4: X8:]          |
!                   o:     Cns:+ (%>:) (»:)                                  X5:                                           |
!                   a:     (%>:) (»:) WeG:                                                                                 |
!                   o:u   (%>:) (»:) (WeG:)                                             ([Q1:|Q4:] X5:      j n ([a:|i:])) |
! Rationale:
! Stop change to ä in all Real Sámi contexts. The idea is to keep Swedish Snälla from becoming 
! The rule blocks change from Norwegian æ to Swedish ä, thus a more relevant example would be: Hægsæt - it should not become Hägsät


!€mæddoQ1jn
!€mätto0jn

!$mæddoQ1X5jn
!$mättu00jn

!€mæddoX4j
!€mäddu0j

!$Hæ9gsæ9t
!$Hägsät

!$Hæ9rland
!$Härland

"æ:e Diphthong Simplification 1" !æ-e -> Q1 and QII + short second vowel (a,u)
!! __@RULENAME@__  
 æ:e <=  [ #: | Cns: | (%>:) (»:) Dummy: ] _ LowerG12 o: (%>:) (»:) X4: j ; !6 Ill. Ællo-elluj
         [ #: | Cns: | (%>:) (»:) Dummy: ] _ LowerG12 o: (Cns:*) (%>:) (»:) [ Y1: | Y2: | Y3: | Y4: | Y5: | X7: | X4: (X8:) | [Q1:|Q4:] X4: X8:] ; !7 Long pass (gergaduváj), Dim (elusj) and Prs Sg3 (?) etc. !Px Nom, acc, gen (elum)
!        [ #: | Cns: | (%>:) (»:) Dummy: ] _ LowerG12 o: (Cns:*) (%>:) (»:) [ Y1: | Y2: | Y3: | Y4: | Y5: | Y7: | Y9: | X7: ] ; !7 Long pass, Dim and Prs Sg3 etc.
         [ #: | Cns: | (%>:) (»:) Dummy: ] _ LowerG12 o: Cns:+ (%>:) (»:) X5: Vow ; !8 odd-syll, oblique. gæsos-gessusa
         [ #: | Cns: | (%>:) (»:) Dummy: ] _ LowerG12 a: (%>:) (»:) ; !  WeG of a-stems !Errouneously gives vællahit:vellahit?? 
         [ #: | Cns: | (%>:) (»:) Dummy: ] _ LowerG12 o:u (%>:) (»:) [ Q1: | Q4: ] X5: j n ([a:|i:]) ; !Px Com. elujnam
         [ #: | Cns: | (%>:) (»:) Dummy: ] _ G32      o:u (%>:) (»:) WeG: ;
         [ #: | Cns: | (%^:) | (%>:) (»:) Dummy: ] _ G32      a: (%>:) (»:) WeG: ;

"æ:e Diphthong Simplification 2"
!! __@RULENAME@__  
 æ:e  => [ #: | Cns: | (%>:) (»:) Dummy: ] _ LowerG12 o: (%>:) (»:) X4: j ; !6 Ill
         [ #: | Cns: | (%>:) (»:) Dummy: ] _ LowerG12 o: (Cns:*) (%>:) (»:) [ Y1: | Y2: | Y3: | Y4: | Y5: | X7: | X4: (X8:) | [Q1:|Q4:] X4: X8:] ; !7 Long pass, Dim and Prs Sg3 etc. !Px Nom, acc, gen
!        [ #: | Cns: | (%>:) (»:) Dummy: ] _ LowerG12 o: (Cns:*) (%>:) (»:) [ Y1: | Y2: | Y3: | Y4: | Y5: | Y7: | Y9: | X7: ] ; !7 Long pass, Dim and Prs Sg3 etc.
         [ #: | Cns: | (%>:) (»:) Dummy: ] _ LowerG12 o: Cns:+ (%>:) (»:) X5: Vow ; !8 odd-syll, oblique
         [ #: | Cns: | (%>:) (»:) Dummy: ] _ LowerG12 a: (%>:) (»:) ; !  WeG of a-stems
         [ #: | Cns: | (%>:) (»:) Dummy: ] _ LowerG12 o:u (%>:) (»:) [Q1:|Q4:] X5: j n ([a:|i:]) ; !Px Com
         [ #: | Cns: | (%>:) (»:) Dummy: ] _ G32      o:u (%>:) (»:) WeG: ; !  X2 X3 X7 Y5 Q1 Q2 Q3
         [ #: | Cns: | (%^:) | (%>:) (»:) Dummy: ] _ G32      a:  (%>:) (»:) WeG: ;

!€ hærránis
!€ hærránis

!€ hærránis#gæhttjalibme%>
!€ hærránis#gæhttjalibme%>

! Note: These three rules are supposed to operate in tandem, and shound ALWAYS be kept in synch, except for the operator and operand.

"ie:æ Diphthong Simplification Part I" !OBS: this is actually æ:ie, according to Bruce Morén-Duolljá. Spiik: ie:æ when 2syll vowel is á. But really a vowel harmony where æ:ie when second syll vowel is i or e.
!! __@RULENAME@__  
 i:0 <=> [ #  | Cns: | (%>:) (»:) Dummy: ] _ e:       Cns:* e:        (%>:) (»:) X4: ;     !1 ieddne-æddnáj
     	 [ #  | Cns: | (%>:) (»:) Dummy: ] _ e:       Cns:* e:        (%>:) (»:) [ Q2: | Y2: | Y3: ] ;     !2 Prs Sg of e-stem verbs. bielgget-bælggá
     	 [ #  | Cns: | (%>:) (»:) Dummy: ] _ e:       Cns:* e: Cns:   (%>:) (»:) X5: Vow ; !3 odd-syll, oblique. dieres-dærrása
                                           _ e: (Cns:) Cns (:Cns) Cns:* e:á Cns:* (%>:) (»:) [X5:|Y2:|Q2:|Y3:|X7:|Y4:|X4:(X5:|Q1:)|Y6: X4:|Y7:|[Q1:|Q4:] X4: (X4:) X8:]  ; ! Vovel change in front of stem vowel variation
                                           _ e: (Cns:) Cns (:Cns) Cns:* i:á       (%>:) (»:) Z1: Cns:+ ;     !Der/r. Nonword example: Gierri-gærrár
                                           _ e: [ [d|b|g] [:d|:b|:g] Cns:+
                                                  | Cns:+ 
                                                  | h Cns:+ :Cns ]         e:0 (%>:) (»:) [ Y1: | Y9: ] u ; !ielvvet:ælvvu, ielvvet:ælvvup short passive, ImprtII
                                           _ e:       Cns:+                e:0 (%>:) (»:) Y8: o ; !ielvvet:ælvvop, Imprt
                                           _ e:       Cns:+                e:0 (%>:) (»:) Q8: u ; !giesset:gessus, ImprtII
								           _ e:       Cns:*                e:a (%>:) (»:) Q6:0 t: j: ; ! láhppet : láhpatjit

!€ ielvveY9ut
!€ 0ælvv00ut

!€ iehttseY1up
!€ 0æhtts00up

!€ giesseQ8us
!€ g0ess00us

"ie:æ Diphthong Simplification Part IIa"
!! __@RULENAME@__ Norwegian æ is an option...
 e:æ  =>  [ #  | Cns: | (%>:) (»:) Dummy: ] i: _ Cns:* e: (%>:) (»:) X4: ;  !1
          [ #  | Cns: | (%>:) (»:) Dummy: ] i: _ Cns:* e: (%>:) (»:) [ Q2: | Y2: | Y3: ] ;  !2 Prs Sg of e-stem verbs
          [ #  | Cns: | (%>:) (»:) Dummy: ] i: _ Cns:* e: Cns: (%>:) (»:) X5: Vow ;  !3 odd-syll, oblique
                                            i: _ (Cns:) Cns (:Cns) Cns:* e:á (Cns:+) (%>:) (»:) [X5:|Y2:|Q2:|Y3:|X7:|Y4:|X4:(X5:|Q1:)|Y6: X4:|Y7:|[Q1:|Q4:] X4: (X4:) X8:]  ; ! Vovel change in front of stem vowel variation
                                            i: _ (Cns:) Cns (:Cns) Cns:* i:á (%>:) (»:) Z1: Cns:+  ;                                     !Der/r
                                            i: _ [ [d|b|g] [:d|:b|:g] Cns:+ | Cns:+ | h Cns (:Cns) Cns:* ] e:0 (%>:) (»:) [ Y1: | Y9: ] u ; !ielvvet:ælvvu, short passive, ImprtII
                                            i: _ Cns:+ e:0 (%>:) (»:) Y8: o ;                                     !ielvvet:ælvvop, Imprt
          [ #  | Cns: | (%>:) (»:) Dummy: ]    _ Cns  (:Cns) Cns:*  a:á (%>:) (»:) ;                                     ! Verbs: e:æ only Vowel change in front of stem vowel variation, bessat : bæssá
!         [ #: | Cns: | (%>:) (»:) Dummy: ]    _ Cns:* a: (%>:) (»:) Y6: ( X5: ) ;                                     ! Verbs: e:æ only Actor nouns
          [ #  | Cns: | (%>:) (»:) Dummy: ]    _ ( [d|b|g] [:d|:b|:g] Cns:+ | Cns:+ | h Cns: (:Cns) Cns:* ) a: (%>:) (»:) Y6: ( X5: ) ; !bessat:bæsse, imprt, du2 and Actor

"ie:ä Diphthong Simplification Part IIb"
!! __@RULENAME@__ Swedish ä is an option...
 e:ä  =>  [ #  | Cns: | (%>:) (»:) Dummy: ] i: _ Cns:* e: (%>:) (»:) X4: ;  !1
          [ #  | Cns: | (%>:) (»:) Dummy: ] i: _ Cns:* e: (%>:) (»:) [ Q2: | Y2: | Y3: ] ;  !2 Prs Sg of e-stem verbs
          [ #  | Cns: | (%>:) (»:) Dummy: ] i: _ Cns:* e: Cns: (%>:) (»:) X5: Vow ;  !3 odd-syll, oblique
                                            i: _ (Cns:) Cns (:Cns) Cns:* e:á (Cns:+) (%>:) (»:) [X5:|Y2:|Q2:|Y3:|X7:|Y4:|X4:(X5:|Q1:)|Y6: X4:|Y7:|[Q1:|Q4:] X4: (X4:) X8:]  ; ! Vovel change in front of stem vowel variation
                                            i: _ (Cns:) Cns (:Cns) Cns:* i:á (%>:) (»:) Z1: Cns:+  ;                                     !Der/r
                                            i: _ [ [d|b|g] [:d|:b|:g] Cns:+ | Cns:+ | h Cns (:Cns) Cns:* ] e:0 (%>:) (»:) [ Y1: | Y9: ] u ;     !ielvvet:ælvvu, short passive, ImprtII
                                            i: _ Cns:+ e:0 (%>:) (»:) Y8: o ;                                     !ielvvet:ælvvop, Imprt
          [ #  | Cns: | (%>:) (»:) Dummy: ]    _ Cns  (:Cns) Cns:*  a:á (%>:) (»:) ;                                     ! Verbs: e:æ only Vowel change in front of stem vowel variation, bessat : bæssá
!         [ #: | Cns: | (%>:) (»:) Dummy: ]    _ Cns:* a: (%>:) (»:) Y6: ( X5: ) ;                                     ! Verbs: e:æ only Actor nouns
          [ #  | Cns: | (%>:) (»:) Dummy: ]    _ ( [d|b|g] [:d|:b|:g] Cns:+ | Cns:+ | h Cns (:Cns) Cns:* ) a: (%>:) (»:) Y6: ( X5: ) ; !bessat:bæsse, imprt, du2 and Actor

"ie:ä Diphthong Simplification Part IIc"
!! __@RULENAME@__  And there are no other options. This rule to block the e, and allow for the æ and ä.
 e:e  /<= [ #  | Cns: | (%>:) (»:) Dummy: ] i: _ Cns:* e: (%>:) (»:) X4: ;  !1
          [ #  | Cns: | (%>:) (»:) Dummy: ] i: _ Cns:* e: (%>:) (»:) [ Q2: | Y2: | Y3: ] ;  !2 Prs Sg of e-stem verbs
          [ #  | Cns: | (%>:) (»:) Dummy: ] i: _ Cns:* e: Cns: (%>:) (»:) X5: Vow ;  !3 odd-syll, oblique
                                            i: _ (Cns:) Cns (:Cns) Cns:* e:á (Cns:+) (%>:) (»:) [X5:|Y2:|Q2:|Y3:|X7:|Y4:|X4:(X5:|Q1:)|Y6: X4:|Y7:|[Q1:|Q4:] X4: (X4:) X8:]  ; ! Vovel change in front of stem vowel variation
                                            i: _ (Cns:) Cns (:Cns) Cns:* i:á (%>:) (»:) Z1: Cns:+  ;                                     !Der/r
                                            i: _ [ [d|b|g] [:d|:b|:g] Cns:+ | Cns:+ | h Cns (:Cns) Cns:* ] e:0 (%>:) (»:) [ Y1: | Y9: ] u ;     !ielvvet:ælvvu, short passive, ImprtII
                                            i: _ Cns:+ e:0 (%>:) (»:) Y8: o ;                                     !ielvvet:ælvvop, Imprt
          [ #  | Cns: | (%>:) (»:) Dummy: ]    _ Cns  (:Cns) Cns:*  a:á (%>:) (»:) ;                                     ! Verbs: e:æ only Vowel change in front of stem vowel variation, bessat : bæssá
!         [ #: | Cns: | (%>:) (»:) Dummy: ]    _ Cns:* a: (%>:) (»:) Y6: ( X5: ) ;                                     ! Verbs: e:æ only Actor nouns
          [ #  | Cns: | (%>:) (»:) Dummy: ]    _ ( [d|b|g] [:d|:b|:g] Cns:+ | Cns:+ | h Cns (:Cns) Cns:* ) a: (%>:) (»:) Y6: ( X5: ) ; !bessat:bæsse, imprt, du2 and Actor


! Note: Check that Q2 above does not overgenerate to nouns!

!€ jeht0sa>Y6
!€ jæhttse>0

!€ jeht0sa>Y6
!€ jähttse>0

!€ gierre»X7dalla>t
!€ g0æ0rá»0dalla>t

!"Diphthong simplification whenever weak grade in a-stems I"
! Vx:0 <=> _ Vy:Vz Cns:* a ( Cns:0 ) (%>:) (»:) WeG: ;
!      where Vx in ( o i )
!	    Vy in ( a e )
!	    Vz in ( å æ )
!	    matched ;

!"Diphtong simplification whenever weak grade in a-stems II"
! Vy:Vz <=> Vx:0 _ Cns:* a ( Cns:0 ) (%>:) (»:) WeG: ;
!      where Vx in ( o i )
!            Vy in ( a e )
!            Vz in ( å æ )
!	    matched ;
!€ boarkkaQ1
!€ b0år0ka0
!€ loavddag8X3#
!€ l0åv0da00#



!"Vovel change in front of stem vowel variation"
! æ:e <=> _ Cns Cns:+ o:u (%>:) (»:) ; ! ditto

!why is this not part of the general oa:å rule?

"Vowel-change oa:å for verbs part I"
!! __@RULENAME@__  
 å:o <=>  _ 0:                            [ Cns:+ |                :d d ] a:á (%>:) (»:) ; ! hållat:hoallá, presSg3
          _ 0: ( [d|b|g] [:d|:b|:g] Cns:+ | Cns:+ | h Cns :Cns Cns:* | :d d ) a:  (%>:) (»:) [ Y6: | Y7: | Y9: ] ; ! hållat:hoalle, imprtDu2

"Vowel-change oa:å for verbs part II"
!! __@RULENAME@__  
 0:a <=>  å: _                            [ Cns:+ |                :d d ] a:á (%>:) (»:) ;                   
          å: _ ( [d|b|g] [:d|:b|:g] Cns:+ | Cns:+ | h Cns :Cns Cns:* | :d d ) a:  (%>:) (»:) [ Y6: | Y7: | Y9: ] ;

!€ hå0llaY2
!€ hoallá0

!€ gå0d0naY6
!€ goaddne0

!$ hållaY2
!$ hållá0

!??? jf jehtsat. TO: jehtsat:jæhttse
!€ gå0ht0saY6
!€ goahttse0


!! !!Stem vowel alternations

!! This section is divided according to stem vowels: a-, e-, o-, å-stems.

!! !a-stem alternations

!! For a-stems, there is a:e and a:i.  Each alternation is triggered by a combination of phonological content and dummy symbols.

"a:e in Present Participle of even-syllable verbs"
!! __@RULENAME@__  
 a:e <=> :Cns _ (%>:) (»:) Y6: [Hyph     |
                               (Hyph) #  |
                                     .#. |
                               n:|v:|m:|t:|d:|s:] ;
! bassat : bas'se ! SNM: there is no rule for this, and it isn't visible in the
                  ! orthography

!€ bassa>Y6
!€ basse>0

"a:i in Prs Prc of even-syllable verbs"
!! __@RULENAME@__  
 a:i <=> :Cns _ (%>:) (»:) Y6:0 ( X5:0 ) j ;

!€ bas'saY6jt#
!€ bas0si0jt#
! why is this not regulated by the e:i in e-stems rule since prs prc of even-syllable verbs become e-stem nouns...?

"a-stem vowel deletion"
!! __@RULENAME@__  
 a:0 <=> :Cns _ (%>:) (»:) [ Y8: | Y9: ] ; !in even-syllable verbs, imp 1du, 1pl and short passives

!! !e-stem alternations

!! For e-stems, there is e:i, e:á, e:å, e:u and e:a. Each alternation is triggered by a combination of phonological content and dummy symbols.

! e:i

"e:i in e-stems"
!! __@RULENAME@__  
 e:i <=> :Cns _ (%>:) (»:) [ Q1: | Q4: ] ( X5: )    j ; ! 1) WeG or not, e:i, X5 is added to get e:i also in ComSg. Náhkke-náhkijn
         :Cns _ (%>:) (»:) [ Y6: | Q7: ] ( X5: )    j ; ! 2) Actor nouns. Bargge-barggijn
         :Cns _ (%>:) (»:)   Q1:0                t: j [ #  | [Vow: [v|n|h|b|p|# ]]] ; ! 3) "e:i in potentialis". Gállet-gálitja


! 1) manasse -> manassij
!€ manasseQ4j
!€ manassi0j

! 1) biesse -> biesij
!€ biesseQ1j
!€ bie0si0j

! 2) boahtte -> boahttij
!€ boaht0eY6j
!€ boahtti0j

! 3)
!€ gálleQ1tj#
!€ gá0li0sj#

!€ gálleQ1tjav#
!€ gá0li0tjav#

!€ gálleQ1tjin#
!€ gá0li0tjin#

!€ gálleQ1tjihpit#
!€ gá0li0tjihpit#

!€ gálleQ1tjibá#
!€ gá0li0tjibá#

!€ gálleQ1tjip#
!€ gá0li0tjip#

!€ gálleQ1tja#
!€ gá0li0tja#


!e:á

!! The following two rules constitute a <= / => rule pair.

"e:á in certain stem types 1"
!! __@RULENAME@__  
 e:á <=  [ Vow: - å ] ([ 0:Cns Cns: | Cns: 0:Cns ]) (Cns:+) _ (%>:) (»:) (Y6:0) X4:0 (X4:0) (X8:0) [m|t|d|s i [m|t|s|m m e|h t t e|s k a|s á]] ; ! 1) Compulsory e:á in Px! bárnne : bárnnám
[ Vow: - å ] Cns:+ _ (%>:) (»:) X4: (X5: | Q1: )  [ #  | v | n | s | j | t | d | g | b | p ] ; ! 2) Ill Sg (context (1) only in part 2). Barnne-bárnnáj
         [ Vow: - å ] ([ 0:Cns Cns: | Cns: 0:Cns ]) (Cns:+) _ (%>:) (»:) Y6:0 X4:0 j ; ! 3) Ill Sg Actor nouns. Bargge-barggáj
         [ Vow: - å ] Cns:+ _ Cns:+ (%>:) (»:) X5: Vow ;     ! 4) Uneven N.
         [ Vow: - å ] Cns:+ _ (%>:) (»:) Y2: ;        ! 5) Indicative Present Singular 3rd Final Vowel in verbs. Vuolgget-vuolggá
         [ Vow: - å ] Cns:+ _ (%>:) (»:) Y3: ;              ! 6) PrfPrc, gállet : gállám
         [ Vow: - å ] Cns:+ _ (%>:) (»:) Q2: ;              ! 7) gállet : gáláv
         [ Vow: - å ] Cns:+ _ (%>:) (»:) X7: ;              ! 8) báhko : bágusj, goahte : goadásj
         [ Vow: - å ] ([ 0:Cns Cns: | Cns: 0:Cns ]) (Cns:+) _ (%>:) (»:) Y7: ;              ! 9) PrsPrc dim.
	     [ Vow: - å ] Cns:+ _ (%>:) (»:) (Y6:0) [ Q1:0 | Q4:0 ] X4:0 X8:0 ;        ! 10) 1st, 2nd Gen/Acc Px declension bárnne-bárnám
         [ Vow: - å ] Cns:+  _ Cns:* (%>:) (»:) Y4: Vow ;    ! 11)verb der. allat. vuolgget-vuolgádallat
!         [Cns:|#:] [ a|i|u ] [ Cns:0 Cns: | Cns: Cns:0 ] _ (%>:) (»:) Y5: Cns:+ ; !12) verb Der/(u/a/å)d
         d: i: e: h: t: t: _ (%>:) (»:) Q7: X4: ;
! 2)
!€ bálggeX4v
!€ bálggá0v

! 6)
!€ gálleY3m#
!€ gállá0m#

! 7)
!€ gálleQ2v#
!€ gá0lá0v#

! 8)
!€ báhkoX7tj#
!€ bá0gu0sj#

!€ goahteX7tj#
!€ goa0dá0sj#

!$ goahteX7tj#
!$ go00dá0sj#


! e:á <=> [ Vow: - å ] Cns* _ (%>:) (»:) X4:0 [ j #: | s Vow: ] ;

"e:á in certain stem types 2"
!! __@RULENAME@__  
 e:á => [ Vow: - å ] ([ 0:Cns Cns: | Cns: 0:Cns ]) (Cns:+) _ (%>:) (»:) (Y6:0) X4:0 (X4:0)  (X8:0) [m|t|d|s (%>:) i [m|t|s|m m e|h t t e|s k a|s á]] ; ! 1) Compulsary e:á in Px! bárnne : bárnnám
        [ Vow: - å ] Cns:+ _ (%>:) (»:) X4: (X5:)  [ #  | v | n | s | j | t | d | g | b | p ] ; ! 2) Ill Sg
        [ Vow: - å ] ([ 0:Cns Cns: | Cns: 0:Cns ]) (Cns:+) _ (%>:) (»:) Y6:0 X4:0 j ;               ! 3) Ill Sg Actor Nouns
        [ Vow: - å ] Cns:+ _ Cns:+ (%>:) (»:) X5: Vow ;              ! 4) Uneven N
        [ Vow: - å ] Cns:+ _ (%>:) (»:) Y2: ;                 ! 5) Indicative Present Singular 3rd Final Vowel in verbs
        [ Vow: - å ] Cns:+ _ (%>:) (»:) Y3: ;                       ! 6) PrfPrc, gállet : gállám
        [ Vow: - å ] Cns:+ _ (%>:) (»:) Q2: ;                       ! 7) gállet : gáláv
        [ Vow: - å ] Cns:+ _ (%>:) (»:) X7: ;                       ! 8) báhko : bágusj, goahte : godásj
        [ Vow: - å ] ([ 0:Cns Cns: | Cns: 0:Cns ]) (Cns:+) _ (%>:) (»:) Y7: ;                       ! 9) PrsPrc dim
        [ Vow: - å ] Cns:+ _ (%>:) (»:) (Y6:0) [ Q1:0 | Q4:0 ] X4:0 X8:0 ;                 ! 10)1st, 2nd Gen/Acc Px declension
        [ Vow: - å ] Cns:+  _ Cns:* (%>:) (»:) Y4: Vow ;             ! 11)verb der. alla
!       [Cns:|#:] [ a|i|u ] [ Cns:0 Cns: | Cns: Cns:0 ] _ (%>:) (»:) Y5: Cns:+  ; ! 12) verb Der/(u/a/å)d
        d: i: e: h: t: t: _ (%>:) (»:) Q7: X4: ;

! 1)
!€ bárnneX4m
!€ bárnná0m

!$ bárnneX4m
!$ bárnne0m


! The X7 rule could be replaced by a general vowel changer, combined with Q1 in
! the lexicon entry for the Dim suffix.


!"e:á in 1st, 2nd person Gen/Acc Px declension." ! bárnne : bárnám, bárnes
! e:á <=> _ (%>:) (»:) Q1:0 X8:0 ;
! The rule for optional e:á does not work. Thus, it is commented out, and
! the Px paradigm only gets ám etc. Return to this issue.


! e:å

"e:å in certain stem types with å as root vowel"
!! __@RULENAME@__  
 e:å <=> å Cns:* _ (%>:) (»:) X4:0 [ j #  ] ;  ! 1) [ADD examples here]
         å Cns:* _ Cns:+ (%>:) (»:) X5: Vow ;            ! 2) [ADD examples here]
         å Cns:* _ (%>:) (»:) Q2:0 ;                    ! 3) gådet : gådåv, jåhtet : jådåv. but this is really vowel harmony, as there is an underlying a in second syll, not e. ie. a short å in 1.syll demands å in 2.syll (really is a:å).
         å Cns:* _ (%>:) (»:) Y2:0 ;                    ! 4) gådet : gådå,  jåhtet : jåhtå
         å Cns:* _ (%>:) (»:) Y3:0 ;                    ! 5) jåhtet : jåhtåm
         å Cns:* _ (%>:) (»:) Y5:0 ;                    ! 6) verb derivation
         å ([ 0:Cns Cns: | Cns: 0:Cns ]) (Cns:+) _ (%>:) (»:) Y7:0 t: j [ (Hyph) #  | Vow: ] ; ! 7) noun derivation låhkke:låhkkåsj
         å Cns:* _ (%>:) (»:) Y7:0 X7:0 l ;             ! 8) noun derivation låhkke:låhkålasj
         å ([ 0:Cns Cns: | Cns: 0:Cns ]) (Cns:+) _ (%>:) (»:) Y6:0 X4:0 [s (%>:) i ([s ([k a|á])|m ([m e])|(h) t ([t e])]) |  j [[#|#:]|Cns: ]] ;             ! 9) noun derivation
         å Cns:+ _ (%>:) (»:) X7: ;                     ! 10) dåhpe : dåbåsj
         å Cns:+ _ (%>:) (»:) (Y6:0) [ Q1:0 | Q4:0 ] X4:0 X8:0 ;
         å ([ 0:Cns Cns: | Cns: 0:Cns ]) (Cns:+) _ (%>:) (»:) (Y6:0) X4:0 (X4:0)  (X8:0) [m|t|d|s (%>:) i [m|t|s|m m e|h t t e|s k a|s á]] ;

	 	! The first subrule gives Ill. sg. of even-stem
! No shorter version here, bec. of no å-å effect
! The second subrule gives oblique of uneven stem
! The third one is vowel harmony, e:å after root vowel å

! 3)
!€ gådeQ2v
!€ gådå0v
!€ jåhteQ2v
!€ jå0då0v

! 4)
!€ gådeY2
!€ gådå0
!€ jåhteY2
!€ jåhtå0

! 5)
!€ jåhteY3m
!€ jåhtå0m

! 7)
!€ låhkkeY7tj#
!€ låhkkå0sj#



! e:u

!"e:u in Verb Derivations" !This holds only for one word (TO)
! e:u <=> Cns:* _ (%>:) (»:) Y4:0 ;
! árret : árrusit

! e:a

!"e:a in Verb Derivation" !not productive
! e:a <=> [Cns:|#:] [ (Vow:) Vow: - [å:|a:|i:|u:] ] Cns:* _ (%>:) (»:) Y5:0 ; ! láhppet : láhpadit

"e:a in Verb Derivation" ! vádtset:váttsatjit
 e:a <=> _ (%>:) (»:) Q6:0 t: j: ; ! láhppet : láhpatjit

! e:0

"e-stem vowel deletion"
!! __@RULENAME@__  
  e:0 <=> Vow:   (%>:) (»:) (Dummy:) #  g:k _ #  ; !clitic ge:k
          :Cns _ (%>:) (»:) [ Y8: | Y9: ] ; !in even-syllable verbs, imp 1du, 1pl and short passives

!€ ielvveY9ut
!€ 0ælvv00ut




!! !i-stem alternations

!! For i-stems, there is i:á. The alternation is triggered by a combination of phonological content and dummy symbols.

"i:á in Verb Derivation"
!! __@RULENAME@__  
 i:á <=> _ (%>:) (»:) Z1:0 ;
! guollit : guollár


!! !o-stem alternations

!!For o-stems there is o:u and vowel deletion. The alternations are triggered by a combination of phonological content and dummy symbols.

!! The duplicates of the three lines of the two following rules are
!! there to resolve the => conflict between the two rules.

"o:u in certain stem types 1"
!! __@RULENAME@__  
o:u  => Cns:  _ (%>:) (»:) X4: j ; !X4 IllSg. Ællo-elluj
        Cns:  _ (%>:) (»:) (Q1:0 | Q4:0) X5:0 j n ; ! Q1 X5 ComSg. !parenthesis (Q1:0) bec of no CG in certain words. elujn
        Cns:  _ Cns:+ (%>:) (»:) X5: Vow ; !Odd-syllable. Oaloj-ålluha
        Cns:  _ (%>:) (»:) [ Y1: | Y2: | Y3: ] ; ! Y1=  Y2= Y3=PrfPrc. Dádjot-dádjum
        Cns:* _ (%>:) (»:) Y5: ; !verb der. (u/a/å)d. Dádjudit (but actually u is part of the der.suffix..)
        Cns:  _ Cns:* (%>:) (»:) Y4: Vow ; !verb der. alla
        Cns:  _ (%>:) (»:) Y6:0 X5:0 j n ; ! Com sg of Actor nouns. Årrot-årro-årrujn. -ot verbal actor nouns behave like reg o-stem nouns without cg, therefore o:u
        Cns:  _ (%>:) (»:) Y6:0 X4:0 j ; ! IllSg of Actor nouns. årruj
        Cns:* _ (%>:) (»:) X7: ; ! báhko : bágusj, goahte : goadásj
        Cns:* _ (%>:) (»:) Y7: ; ! PrsPrc dim
              _ (%>:) (»:) (Y6:0) X4:0 (X8:0) ;        ! Px áhkko:áhkkum
              _ (%>:) (»:) [Q1:0|Q4:0] X4:0 X8:0 ;        !  1st, 2nd Gen/Acc Px declension
              _ (%>:) (»:) (Y6:0) ([Q1:0|Q4:0]) X5: j n [a [m|t]|i [s|m m e|h t t e|s k a|s á]] ; !Px Com


"o:u in certain stem types 2"
!! __@RULENAME@__  
o:u <= Cns:  _ (%>:) (»:) X4: j ; !X4 IllSg
!       Cns:  _ (%>:) (»:) (Q1:0 | Q4:0) X5:0 j n ; !parenthesis (Q1:0) bec of no CG in certain words. outcommented because rule is facultative:elujn/ælojn
        Cns:  _ Cns:+ (%>:) (»:) X5: Vow ;  !Odd-syllable
        Cns:  _ (%>:) (»:) [ Y1: | Y2: | Y3: ] ; ! Y1=  Y2= Y3=PrfPrc
        Cns:* _ (%>:) (»:) Y5: ; !verb der. (u/a/å)d
        Cns:  _ Cns:* (%>:) (»:) Y4: Vow ; !verb der. alla
        Cns:  _ (%>:) (»:) Y6:0 X5:0 j n ; ! Com sg of Actor nouns elujn
        Cns:  _ (%>:) (»:) Y6:0 X4:0 j ; ! IllSg of Actor nouns
        Cns:* _ (%>:) (»:) X7: ; ! báhko : bágusj, goahte : goadásj
        Cns:* _ (%>:) (»:) Y7: ; ! PrsPrc dim
              _ (%>:) (»:) (Y6:0) X4:0 (X8:0) ;        ! Px áhkko:áhkkum
              _ (%>:) (»:) [Q1:0|Q4:0] X4:0 X8:0 ;        ! 1st, 2nd Gen/Acc Px declension
              _ (%>:) (»:) (Y6:0) ([Q1:0|Q4:0]) X5: j n [a [m|t]|i [s|m m e|h t t e|s k a|s á]] ; !Px Com



!Old o:u rules
! o:u <=> Cns: _ (%>:) (»:) [ X4: j | Q1: X5: ] ; !X4 IllSg, Q1 X5 ComSg.
! o:u <=> Cns: _ (%>:) (»:) X4: j ; !X4 IllSg, Q1 X5 ComSg.

"u:o in contracted nouns"
!! __@RULENAME@__  
 u:o <=> _ (%>:) (»:) [ Q3: | X6: ] ; !æddnu:ænoj, boahttsu:boatsoj

"o-stem vowel deletion"
!! __@RULENAME@__  
 o:0 <=> Cns: _ (%>:) (»:) Y9:0 ; !in short passives

!! !For å-stems there is å:e and å:i and vowel deletion.  Each alternation is triggered by a combination of phonological content and dummy symbols.

"å:e in Present Participle of even-syllable verbs"
!! __@RULENAME@__  
 å:e <=> Cns: _ (%>:) (»:) Y6: [Hyph|(Hyph) # | .#. ] ;
         Cns: _ (%>:) (»:) Y6: [ d a
                               | m (a)
                               | n
                               | n a [ m | t ]
                               | [ s t
                                 | n  ] i [ h t t e
                                          | m m e
                                          | s
                                          | s [ k a | á ]
                                          ]
                               | s (k) (a)
                               | s t a [ m | t ]
                               | t
                               | v
                               ]
                                 :0 - X4: (:0 - X8:) [ :0 - s:
                                                     | :0 - m:
                                                     | :0 - t:
                                                     | :0 - d: (a:)
                                                     ]  ; ! All this to prevent change in Px


! låhkåt : låhkke

"å:i in Actor nouns of even-syllable verbs"
!! __@RULENAME@__  
 å:i <=> Cns: _ (%>:) (»:) Y6: ( X5: ) j ;
! låhkåt : låhkkijt - but not really å becoming i, its å becoming e becoming i like in a reg e-stem noun


"å-stem vowel deletion"
!! __@RULENAME@__  
 å:0 <=> Cns: _ (%>:) (»:) [ Y8: | Y9: ] ; !in even-syllable verbs, imp 1du, 1pl and short passives. bårråt-bårru

!! !alternations valid for several stem types
! - - - - - - - - - - - - - - - - - - - - -

"Stem vowel deletion in even-syllable verbs, imp 3sg, 3du, 2pl, 3pl"
!! __@RULENAME@__  
 Vx:0 <=> Cns: _ (%>:) (»:) [Y1:0|Q8:] ;
      where Vx in ( a å e ) ;

!€ ielvveY1up
!€ 0ælvv00up

!€ giessaY1up
!€ giess00up

!€ bårråY1up
!€ bårr00up

!€
!€


!! !!Consonant gradation rules

!! The consonant gradation rules differ considerably from the corresponding rules for North Sámi. 
!! Instead of generalizing oversets of consonants (Cx:Cy &lt;=&gt; ...), each rule contains the
!! alternation for one consonant only, and to the right of the
!! &lt;=&gt; arrow is listed all the contexts where the relevant
!! alternation appears. The disadvantage with this method is that the
!! same context must be written several times, if e.g. both p, t and k
!! are deleted in the same contexts, each of these contexts must be
!! written several times, one for each consonant. The advantage is that
!! there are no conflicts during compilation, compilation takes 10
!! seconds rather than 3 minutes. The earlier North-Sámi-style rule
!! set was ordered according to CG pattern. This pattern is still
!! visible in the new rules, via the reference S1-3 etc. (Spiik's
!! Series 1, 3-letter pattern, etc) behind each subrule.

!! This actually opens up for a migration to an xfst rule file
!! instead of the current twolc format, since what xfst really cannot
!! do is generalize over sets (Cx:Cy etc.). This is an issue for future
!! revisions to decide.

!! The rules are divided in two subsections, deletion rules and
!! change (alternation) rules.

! New rules, without conflicts
! The numbering of rules refer to the overview in Spiiks grammar
! Thus !S7-3 means "Series 1, 3-letter patterns", etc.

!! !Deletion rules

!! The b, d, g deletion rules are similar, via the optional ( b ) etc. in front of the "_" symbol, both
!! bm:m and bbm:bm alternations are covered. The contexts differ to a certain extent. For
!! b and d, the III-I special gradation bbm:m is covered by two separate rules,
!! and a special Dummy (X6), not part of the ordinary WeG set.

!! Note that one of the rules for t:0 refers to #: as part of its context. As soon as clitics are
!! added to the word form, this rule will thus not be triggered. Look into this when the clitics are added.

"Consonant gradation b:0"
!! __@RULENAME@__  
b:0 <=> Vow: ( [l|b] )     _ m         Vow: (StemCns:) (%>:) (»:) WeG: ; ! S7-3, S7-2 bbn:b0n, bn:0n
!	    Vow:           _ [d|j|l|n|r|s] Vow: (StemCns:) (%>:) (»:) WeG: ; ! S7-2
	    Vow: [ j|l|r ] _ b             Vow: (StemCns:) (%>:) (»:) WeG: ; ! S9-3
	    Vow:           _ b: m          Vow: (%>:) (»:) X6:             ; 
	    Vow: b:        _    m          Vow: (%>:) (»:) X6:             ; ! III-I special gradation ! bbm:m

"Consonant gradation d:0"
!! __@RULENAME@__  
d:0 <=> Vow: ( d )             _ [ n | j ] Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S7-3, S7-2 ddn:d0n, dn:0n
        Vow: ( d )             _   n   j   Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S7-4 and S7, dnj:nj
        Vow: [ b | j | r | v ] _ d         Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9
        Vow:                   _ d: n      Vow: (%>:) (»:) X6: ;             ! III-I special gradation ddn:n
        Vow: d:                _    n      Vow: (%>:) (»:) X6: ;             ! III-I special gradation ddn:n
        Vow:                   _ t:s j (%>:) (»:) (Hyph) #   ;             ! oajváladtj# : oajvála0sj#

!€ bednag8>X3
!€ be0na0>0

"Consonant gradation g:0"
!! __@RULENAME@__  
g:0 <=> Vow: ( g )       _    [ñ|ń|ŋ|l|n] Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S7-3, S7-2
    	Vow: [ j|l|r|v ] _ g          Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9
    	Vow:             _ g: [ñ|ń|ŋ] Vow: (%>:) (»:) X6: ; ! III-I special gradation
    	Vow:   g:        _    [ñ|ń|ŋ] Vow: (%>:) (»:) X6: ; ! III-I special gradation

"Consonant gradation k:0"
!! __@RULENAME@__  
k:0 <=> Vow:     k         _ n   Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S7-3
    	Vow:     k         _ t j Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S7-4
    	Vow: [ j|l|m|r|v ] _ k   Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9
    	Vow:     h         _ k   Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S4, preasp stops

"Consonant gradation l:0"
!! __@RULENAME@__  
l:0 <=> Vow:   l       _ [ d|j|t|n ] Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S7-3
		Vow:   l       _ t j         Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S7-4
		Vow:           _ l           Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S8, II-I
		Vow: [ j|b|v ] (h) _ l       Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9
		Vow:           _ ': l        Vow: (%>:) (»:) X6:               ; 
	    Vow: ':        _    l        Vow: (%>:) (»:) X6:               ; ! III-I special gradation l'l:l

"Consonant gradation m:0"
!! __@RULENAME@__  
m:0 <=> Vow: m         _ [ b|p ] Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S7-3
		Vow:           _ m        Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S8, II-I
		Vow: [ j|l|r|v|t ] _ m       Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9
		Vow:           _ (%>:) (»:) Q1: X1: n ; ! Deleting Final Cns in Short Essive of Uneven Syllables

"Consonant gradation n:0"
!! __@RULENAME@__  
n:0 <=> Vow:         n           _ d           Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S7-3
		Vow:         n           _ t ([ j|s ]) Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S7-4
		Vow:                     _ n ( j )       Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S8, II-I, also palat.
		Vow: [ b|j (h) |l|ñ|ń|ŋ|r|v (h) |m|g ] _ n           Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9, III-II
		Vow: [ b|r|v ]           _ n j         Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9-4, III-II

"Consonant gradation p:0"
!! __@RULENAME@__  
p:0 <=> Vow:         p   _ (%>:) (»:) ( j ) Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S7-3, S7-4 j
        Vow:         p   _ t ([j|s])        Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S7-4 ppts:pts
        Vow: [ j|l|r|v ] _ p             Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9
        Vow:   h         _ p             Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S4 hpp:hp
        Vow:         p   _ [k|s (j)]        Vow: ( StemCns: ) (%>:) (»:) WeG: ;

"Consonant gradation s:0"
!! __@RULENAME@__  
s:0 <=> Vow:                   _ s ( [ j|k|m|n|t|p|v|ń|ŋ|ñ ] ) (%>:) (»:) Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S7-3, S8 II-I
        Vow:                   _ s j [   k|m|    p|v ]                    Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S7-4
        Vow: [ b|j|l|m|n|r|v ] _ s                                        Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9
        Vow: [ r|l|m ]         _ s j                                      Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9-4
        Vow: [ r|l|n|j ]       _ s k                                      Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9-4
        Vow: [ j|m|n|v|l ]     _ s t                                      Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9-4
        Vow: r                 _ s j t                                    Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9 rssjt:rsjt
        Vow: s:                _ (%>:) (»:) (Hyph) #  ;                                         ! This is a word fin simpl, ss# -> s#, and not CG
        Vow:                   _ (%>:) (»:) Q1: X1: n ;                                  ! Deleting Final Cns in Short Essive of Uneven Syllables

!€ russjpeQ1
!€ ru0sjpe0

!$ russjpeQ1
!$ russjpe0

!"Consonant gradation s:0"
!s:0 <=> Vow: s                 _ ( [ j|k|m|n|t ] ) Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S7-3, S8 II-I
!        Vow: s                 _ j [ k|m|v ]       Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S7-4
!        Vow: [ b|j|l|m|n|r|v ] _ s                 Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9
!        Vow: [ r|l|m ]         _ s j               Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9-4
!        Vow: [ r|l|n ]         _ s k               Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9-4
!        Vow: [ j|m|n|v ]       _ s t               Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9-4
!        Vow: r s               _ j t               Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9 rssjt:rsjt
!        Vow: s:                _ (%>:) (»:) (Hyph) #: ;                                       ! This is a word fin simpl, ss# -> s#, and not CG
!        Vow:                   _ (%>:) (»:) Q1: X1: n ;                                ! Deleting Final Cns in Short Essive of Uneven Syllables


"Consonant gradation ŋ:0"
!! __@RULENAME@__  
Cx:0 <=> Vow:             _ Cy ( [ g|k ] ) Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S7-3, S8 II-I
         Vow: [ j|l|r|v ] _ Cy          Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9
         Vow:          Cy _ k t         Vow: ( StemCns: ) (%>:) (»:) WeG: ;
	where Cx in ( ń ŋ ñ )
		  Cy in ( ń ŋ ñ )
    matched ;



"Consonant gradation f:0"
!! __@RULENAME@__  
f:0 <=> Vow: ( [r|l] ) _ f Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S8 ff:f, S9 rff:rf
        Vow:         f _ t Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S8 ff:f, S9 rff:rf


"Consonant gradation r:0"
!! __@RULENAME@__  
r:0 <=> Vow: ( [ b|j|v ] ) _ r Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S8 rr:r, S9 brr etc.

"Consonant gradation v:0"
!! __@RULENAME@__  
v:0 <=> Vow: ( [ j|l|r ] ) _ v Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S8 , S9 jvv etc.

"Consonant gradation j:0"
!! __@RULENAME@__  
j:0 <=> Vow: [b|r] _ j Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9

"Consonant gradation t:0"
!! __@RULENAME@__  
t:0 <=> Vow: [j|ñ|ń|ŋ|r|v] _ t             Vow: ( StemCns: ) (%>:) (»:) WeG: ;             ! S9 jtt:jt etc.
    	Vow: [b|j|r|v]     _ t j           Vow: ( StemCns: ) (%>:) (»:) WeG: ;           ! S9
      	Vow: [b|j|v|r]     _ t s           Vow: ( StemCns: ) (%>:) (»:) WeG: ;           ! S9
    	Vow:       h:      _ t ( [ j|s ] ) Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S4 htt(j)
        Vow:       t:s     _ j (%>:) (»:) (Hyph) #  ;                                 ! Word final weakening of -ttj to -sj
        Vow (%>:) (»:) (Dummy:) s     _ (%>:) (»:) (Hyph) #   ;                           ! W-Final Cl. Simpl. málestit : máles
        Vow:       h:      _ t ( [ j|s ] ) Vow: (%>:) (»:) X6: ;               ! III-I special gradation
                                                  ! Second last rule will fail if and when clitics are added to the word.
                                                  ! Note that there is no weak grade clitic here.

!"Gradation Series 4, II-I, h Loss 1"
!€ oajváladtj#
!€ oajvála0sj#

!h:0 <=> Vow: _ [ k: | p: | t: ] Vow: ( StemCns: ) (%>:) (»:) WeG: ;
!        Vow: _ (%>:) (»:) Q1: X1: n ; ! Deleting Final Cns in Short Essive of Uneven Syllables

"Gradation Series 4, II-I, tj and ts"
!! __@RULENAME@__  
 h:0 <=> Vow: _ [ k: | p: | t: ] Vow: ( StemCns: ) (%>:) (»:) WeG: ;
         Vow: _ t [j|s] Vow: ( StemCns: ) (%>:) (»:) WeG: ;
         Vow: _ t: t: ( [ j|s ] ) Vow: (%>:) (»:) X6: ; ! III-I special gradation
         Vow: _ (%>:) (»:) Q1: X1: n ; ! Deleting Final Cns in Short Essive of Uneven Syllables
! (t:) for III-I CG)

!"Word final double consonant simplification"
!Cy:0 <=> Vow: Cx: _ (%>:) (»:) (Hyph) #: ;
!     where Cx in ( s )
!	   Cy in ( s )
!	   matched ;
! Moved to the CG chapter. But does this rule wait for more cases?



!! !Change rules

!! The Cx:Cy format was kept for hk:g, hp:b, ht:d, since the left context h:0 was unique, 
!! and no compilation conflict thus arose.

!! The bb:pp, gg:kk, dd:tt alternations were split into three rules, 
!! since keeping them in one Cx:Cy rule created compilation conflicts. 
!! Also, d:t contain a rule not found for the other two...


"Gradation Series 4, II-I"
!! __@RULENAME@__  
 Cx:Cy <=> Vow: h:0 _ Vow: ( StemCns: ) (%>:) (»:) WeG: ;
 where Cx in ( k p t )
       Cy in ( g b d )
       matched ;

!"Gradation Series 5, III-II, dtj, dts"
! d:t <=> Vow: (»:) _ t Cx Vow: ( StemCns: ) (%>:) (»:) WeG: ;
! where Cx in ( j s ) ;

"bb:pp"
!! __@RULENAME@__  
b:p <=> Vow:    _ b:  Vow: ( StemCns: ) (%>:) (»:) WeG: ;
        Vow: b: _     Vow: ( StemCns: ) (%>:) (»:) WeG: ;
!       Vow:    _ (%>:) (»:) (Hyph) #:0 ;  ! This gives Jakob:Jakop, and it is unclear what it gives.
! The last rule is for unnep : unnebu. It must be revised in two ways:
! a. it should be extended to more consonants
! b. it should be restricted, perhaps b should be replaced with b8

!€ oabbáQ1
!€ oappá0

"gg:kk"
!! __@RULENAME@__  
g:k <=> Vow:    _ g: Vow: ( StemCns: ) (%>:) (»:) WeG: ;
  	    Vow: g: _ Vow: ( StemCns: ) (%>:) (»:) WeG: ;

!€ vággeQ1
!€ vákke0

!$ vággeQ1
!$ vágge0


"g:k change for clitic -ge"
!! __@RULENAME@__  
g:k => Vow: (%>:) (»:) (Dummy:) #  _ e: #  ; !clitic ge:k

"dd:tt and dtj, dts"
!! __@RULENAME@__  
d:t <=> Vow: _ d: Vow: ( StemCns: ) (%>:) (»:) WeG: ;
	Vow: d: _  Vow: ( StemCns: ) (%>:) (»:) WeG: ;
	Vow: (»:) _ t [ j|s ] (%>:) (»:) Vow: ( StemCns: ) (%>:) (»:) WeG: ;


!"Gradation Series 6, III-II, bb, gg 1"
! Cx:Cy <=> Vow: _ Cz: Vow: ( StemCns: ) (%>:) (»:) WeG: ;
! where Cx in ( b d g )
!       Cy in ( p t k )
!       Cz in ( b d g )
!       matched ;

!"Gradation Series 6, III-II, bb, gg 2"
! Cx:Cy <=> Vow: :Cy _ Vow: ( StemCns: ) (%>:) (»:) WeG: ;
! where Cx in ( b d g )
!       Cy in ( p t k )
!       matched ;

"Gradation Series 7, III-II, ks(t), kt, ktj, kts"
!! __@RULENAME@__  
 k:v <=> Vow: _ [ s|t ] (t) Vow: ( StemCns: ) (%>:) (»:) WeG: ;
	 Vow: _ t [ j|s ] Vow: ( StemCns: ) (%>:) (»:) WeG: ;

!! Exceptional II-III inverse gradation in present participles
! -----------------------------------------------------------

!! This gradation is only for II-I syllable verbs that get III as
!! present participles.

! Candidates:
!! * bbm - bm - m
!! * ddn - dn - n
!! * ddnj- dnj- nj
!! * ggŋ - gŋ - ŋ
!! * ddj - dj - dj
   
!! * hkk - hk - g
!! * hpp - hp - b
    
!! * htt - ht - d
!! * httj- htj- tj
!! * htts- hts- ts

!! Strategy: Do insertion rule for the initial element.

"Consonant insertion as II-III strengthening gradation with bm, gŋ"
!! __@RULENAME@__  
 0:Cx <=> :Vow      Cx _ Cy           Vow: (%>:) (»:) [ Y6: | Y7: | Y9: ] ;
              where Cx in ( b g )
                    Cy in ( m ŋ )
	          matched ;

"Consonant insertion as II-III strengthening gradation with dn/j + as I-III strengthening gradation with d"
!! __@RULENAME@__  
 0:d  <=> :Vow    d _ [ n | n j | j ] Vow: (%>:) (»:) [ Y6: | Y7: | Y9: ] ;
          :Vow    d _                 Vow: (%>:) (»:) [ Y6: | Y7: | Y9: ] ;

"Consonant insertion as II-III strengthening gradation with hk, hp,"
!! __@RULENAME@__  
 0:Cx <=> :Vow h     Cx _             Vow: (%>:) (»:) [ Y6: | Y7: | Y9: ] ;
               where Cx in ( k p ) ;

"Consonant insertion as II-III strengthening gradation with htt(j/s)"
!! __@RULENAME@__  
 0:t  <=> :Vow h t _ ( j | s )        Vow: (%>:) (»:) [ Y6: | Y7: | Y9: ] ; !removed Q7, fixed in smj-lex


"Facultative alternation snj-sjnj"
!! __@RULENAME@__
0:j => [ .#. | # ] s _ n j Vow ;


!! !! Debugging of twol-rules

!! All rule conflicts have been successfully resolved. The rule file
!! should be kept that way. Look out for conflicts in the compilation
!! process, and resolve them as they appear!
