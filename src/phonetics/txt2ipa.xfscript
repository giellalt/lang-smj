! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no

!  =====================================
!! Phonological converter for Julev Sámi
!! =====================================

!! Converts to IPA. Mainly intended for use with TTS.

! It started out as automatically converted
! espeak -> xfscript:  ../speech-smj-private/espeak-ng/smj_rules
! Then manually edited.

define toLower [ A -> a,
                 Á -> á,
                 B -> b,
                 C -> c,
                 D -> d,
                 E -> e,
                 F -> f,
                 G -> g,
                 H -> h,
                 I -> i,
                 J -> j,
                 K -> k,
                 L -> l,
                 M -> m,
                 N -> n,
                 Ŋ -> ŋ,
                 O -> o,
                 P -> p,
                 Q -> q,
                 R -> r,
                 S -> s,
                 T -> t,
                 U -> u,
                 V -> v,
                 W -> w,
                 X -> x,
                 Y -> y,
                 Z -> z,
                 Æ -> æ,
                 Ä -> ä,
                 Ø -> ø,
                 Ö -> ö,
                 Å -> å
               ] ;

define CONS [ b | c | d | f | g | h | j | k | l | m | n | ñ | ń | ŋ | p | q | r | s | t | v | w | x | z ] ; ! C in espeak
define VOW [ a | e | é | i | o | u | y | á | å | æ | ä | ø | ö ] ; ! A in espeak
!define Diphthongs [ i e | u o | e æ | e ä | o a ] ;
define  SYLL CONS* VOW+ CONS+ ; ! @ in espeak
define USYLL CONS* VOW+ CONS+ ; ! & in espeak
define NVOW \VOW ; ! K in espeak
define DIGIT [ %0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9 ] ; ! D in espeak
define CGROUP [ .#. CONS* | CONS* .#. ] ; ! X in espeak
! SMJ translation rules
! This file is UTF-8 encoded

!     _)  a         ! "a" at the start of a word
!         a (CC     ! "a" followed by two consonants
!         a (C%     ! "a" followed by a double consonant (the same letter twice)
!         a (/%     ! "a" followed by a percent sign
!     %C) a         ! "a" preceded by a double consonants

!     @@)  bi      ! "bi" preceded by at least two syllables
!     @@a) bi      ! "bi" preceded by at least 2 syllables and following 'a'

! START SMJ HERE

! groups

!.L01 i j	! used in palatalization rules

define PLOS  [ b | d | g | k | p | t ] ; ! // Plosives

! define L02  [ r | l | j | v ] ; ! // Sonorants

!.L02 b d f g h j l m n r s v

!.L03 l m n r

!.L04 a b c d e f g h i j k l m n o p q r s t u v õ ä ö ü

!.L05 a e i l m n o r u õ ä ö ü


! Epenthetic vowels in Q3 consonant clusters
! r l j v followed by homorganic consonants (geminates)
! barggo, dárbbo, tjoarvve, girjje, álggo, galbba, gålmmå, dálvve, nijbbe, bievdde

! separate rules for all cons. clusters and vowel nucleai?
!       a (L02%C     ! possible to generalize?
!       A) rgg	     r@^g ! Any vowel + schwaa - more general?

!Orthographically homonymous Q3: f, l, m, n, nj, ń, ñ, ŋ, r, s, sj, v
! After numeral 1 -- Q3; after any numeral <1 -- Q2?
! akta @) ff	ff:
! Word order? -- Impossible to have a syntactic disambiguator in Espeak to recognize homonymous sg.nom & sg.gen forms...
! OR if adjusted orthography would be used, e.g.: f'f/fff	ff:
! Vowel compensation/readjustment: V1 and V2 longest in Q1, gradually decreasing in length, but differences very small/not significant.


define groupa [  a  ->  a ː  ||  SYLL  _  ] ! long front in 2nd syllable
          .o. [  a  ->  ɑ    ||  _   \ː  ]  ! back vowel
 ;

define groupá [  á  ->  a ː  ||  SYLL   _  ]  ! in 2nd syll
          .o. [  á  ->  ɑ ː  ]
 ;

define groupæ [ [ä|æ]  ->  e æ  ||  _   SYLL  ] ! in 1st syllable
          .o. [ [ä|æ]  ->  æ ː  ||  SYLL   _  ] ! in 2nd syllable
          .o. [   ä    ->  æ    ]
 ;

define groupå [  å  ->  ɔ ː  ||  SYLL   _  ]  ! in 2nd syll
          .o. [  å  ->  ɔ  ]
 ;

define groupb [  b b  ->  b ː  ]
          .o. [  b    ->  p    || \VOW   _  \VOW  ,  _  \ː  ]
 ;

define groupc [  c  ->  s  ||  _  [e|i|y|ø|ö] ]
          .o. [  c  ->  k  ] ;

define groupd [  d d j  ->  ɟ ː  ||  VOW   _   VOW  ]  ! voiced palatal plosive between vowels
          .o. [  d j    ->  ɟ    ||  VOW   _   VOW  ]  ! voiced palatal plosive between vowels
          .o. [  d t s  ->  d z  ]
          .o. [  d t j  ->  d ʒ  ]
          .o. [  d d    ->  d ː  ]
          .o. [  d j    ->  c    ]
          .o. [  d      ->  t    || \VOW   _  \[z|ʒ|VOW] ]
 ;

define groupe [ e e  ->  ɛ ː ]
          .o. [  e  ->  i ɛ  ||  SYLL   _  ]  ! in 2nd syll
          .o. [  e  ->  ɛ ː  ||  _   SYLL  ]  ! open-mid; 1st syll
          .o. [  e  ->  ɛ  ]
 ;

define groupé [  é  ->  ɛ ː  ] ;

define groupf [  f f  ->  f ː ]
 ;

define groupg [  g g  ->  g ː  ]
          .o. [  g    ->  k    || \VOW   _  \VOW  ,  _  \ː  ]
 ;

define grouph [  h k k    ->  k ː      ]
          .o. [  h k      ->  h k      ]
          .o. [  h p p    ->  p ː      ]
          .o. [  h p      ->  h p      ]
          .o. [  h t t s  ->  h ː t s  ]
          .o. [  h t s    ->  h t s    ]
          .o. [  h t t j  ->  h t ː ʃ  ]
          .o. [  h t j    ->  h t ʃ    ]
          .o. [  h t t    ->  t ː      ]
          .o. [  h t      ->  h t      ]
 ;

define groupi [ i i  ->  i ː  ]
          .o. [ i e  ->  i ɛ  ||  _   SYLL  ] ! 1st syll
 ;

define groupj [ j j  ->  j ː ]
          .o. [ j b b  ->  j ə b  ]
 ;

define groupk [  k k  ->  k ː  ]
          .o. [  k  ->  k h  ||  _   á  ]  ! Aspirated in Scandinavian loanwords
 ;

define groupl [  l l    ->  l ː    ]
          .o. [  l g g  ->  l ə g  ]  ! Q3
          .o. [  l b b  ->  l ə b  ]  ! Q3
          .o. [  l p p  ->  l ə b  ]  ! Q3
          .o. [  l m m  ->  l ə m  ]  ! Q3
          .o. [  l v v  ->  l ə v  ]  ! Q3
 ;

define groupm [  m m  ->  m ː  ]
 ;

define groupn [  n      ->  n     ]
          .o. [  n j    ->  nʲ    ]
          .o. [  n n j  ->  nʲ ː  ]
 ;

define groupŋ [  ñ ñ  ->  ŋ ː  ]
          .o. [  ñ    ->  ŋ    ]
          .o. [  ń ń  ->  ŋ ː  ]
          .o. [  ń    ->  ŋ    ]
          .o. [  ŋ ŋ  ->  ŋ ː  ]
          .o. [  ŋ    ->  ŋ    ]
 ;

define groupo [  o    ->  u o  ||  SYLL   _  ]
          .o. [  o a  ->  u a  ]
          .o. [  o a  ->  o ɑ  ||  _   SYLL  ]
          .o. [  o    ->  o    ]
 ;

define groupø [  [ö ö|ø ø] ->  ø ː ]
          .o. [  [ ö | ø ] ->  ø   ]
 ;

define groupp [  p p  ->  p ː  ]
          .o. [  p    ->  p h  ||  _  VOW ]  ! Aspirated in Scandinavian loanwords
          .o. [  p    ->  p    ]
 ;

!          rl         l. ! Retroflex. in Scandinavian loanwords
!          rn         n. ! Retroflex. in Scandinavian loanwords
!          rt         t. ! Retroflex. in Scandinavian loanwords

define groupr [  r r    ->  r ː    ]
          .o. [  r g g  ->  r ə g  ]  ! Q3
          .o. [  r v v  ->  r ə v  ]  ! Q3
          .o. [  r b b  ->  r ə b  ]  ! Q3
          .o. [  r j j  ->  r ə j  ]  ! Q3
          .o. [  r ŋ ŋ  ->  r ə ŋ  ]  ! Q3
          .o. [  r d    ->  r t    ]
          .o. [  r l    ->  r l    ]
          .o. [  r n    ->  r n    ]
          .o. [  r t    ->  r t    ]
          .o. [  r      ->  r      ]
 ;

define groups [  s s j  ->  ʃ ː  ]
          .o. [  s s    ->  s ː  ]
          .o. [  s j    ->  ʃ    ]
          .o. [  s      ->  s    ]
 ;

define groupt [  t t j  ->  t ː ʃ  ]
          .o. [  t      ->  t h    ||  _   á  ]  ! Aspirated in Scandinavian loanwords
          .o. [  t      ->  h t    ||  _   .#.  ]  ! at the end of a word
          .o. [  t t    ->  t ː    ]
          .o. [  t j    ->  t ʃ    ]
          .o. [  t s    ->  t s    ]
          .o. [  t t s  ->  t s ː  ]
          .o. [  t      ->  t      ]
 ;

!          u          u: !
!          u          U- !
!          u          u- ! in newish Scand. loanwords like <komuvnna>
! List some of most common scand. loanwords?
define groupu [ u u  ->  u ː ]
          .o. [ u    ->  u   ]
 ;

define groupv [  v v    ->  v ː    ]
          .o. [  v      ->  f      ||  _  PLOS  ]  ! before a plosive
          .o. [  v      ->  ʋ      ||  _   .#.  ]  ! in the end of a word, e.g. 1sg suffix
          .o. [  v d d  ->  v ə d  ]  ! Q3
 ;

define groupw [  w  ->  v  ]  ;

define groupx [  x x  ->  k ː s  ]
          .o. [  x    ->  k s    ]
 ;

define groupy [  y  ->  y  ] ;

define groupz [  z    ->  t s    ]
          .o. [  z z  ->  t ː s  ]
          .o. [  z h  ->  ʒ      ||  NVOW   _  ]
 ;

read regex [
    toLower
.o. groupa
.o. groupá
.o. groupæ
.o. groupå
.o. groupb
.o. groupc
.o. groupd
.o. groupe
.o. groupé
.o. groupf
.o. groupg
.o. grouph
.o. groupi
.o. groupj
.o. groupk
.o. groupl
.o. groupm
.o. groupn
.o. groupŋ
.o. groupo
.o. groupø
.o. groupp
.o. groupr
.o. groups
.o. groupt
.o. groupu
.o. groupv
.o. groupw
.o. groupx
.o. groupy
.o. groupz
] ;
