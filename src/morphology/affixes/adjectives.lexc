! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

! ========================================================================== !
!                      Lule Sámi morphological analyser                      !
! ========================================================================== !


! ===========================
! Sublexica for Adjective
! ===========================

! Even-syllable stems
! -------------------

LEXICON GIEVRRA ! Attr WeG, -s
 +A+Attr:%>Q1s ATTR ;
! +A:%>Q1s VUOHTA ; !moved to ATTR
 +A:%> NE-EVENCASEadj ;
 +A:%>X4 ILL-EVENCASEadj ;
 +A:%>Q1 N-EVENWEAKSTEMadj ;
 +A:%>Q1 BU/MUS ;
 +A+Der5+Der/at+Adv:%>Q1t K ;
 +A:%> MUORRACmp ;

LEXICON NUORRA ! Attr =
 +A+Attr:%> ATTR ;
! +A: VUOHTA ; !moved to ATTR
 +A:%> N-EVENSTRONGSTEMadj ;
 +A:%>Q1 N-EVENWEAKSTEMadj ; ! cg in nominal paradigm
 +A:%>Q1 BU/MUS ;
 +A+Der5+Der/at+Adv:%>Q1t K ; ! Presumably WeG as adverb as well
 +A:%> MUORRACmp ;

LEXICON HÁVSSKE ! Attr w/o WeG, -s
 +A+Attr:%>s ATTR ;
! +A:%>s VUOHTA ; !moved to ATTR
 +A:%> NE-EVENCASEadj ;
 +A:%>X4 ILL-EVENCASEadj ;
 +A:%>Q1 N-EVENWEAKSTEMadj ; ! Presumably WeG in nominal paradigm
 +A:%>Q1 BU/MUS ;
 +A+Der5+Der/at+Adv:%>Q1t K ;
 +A:%> MUORRACmp ;

LEXICON TJUODDJE ! Attr w/o WeG, -s
 +A+Attr:i%>s ATTR ;
! +A:%>s VUOHTA ; !moved to ATTR
 +A:e%> NE-EVENCASEadj ;
 +A:e%>X4 ILL-EVENCASEadj ;
 +A:e%>Q1 N-EVENWEAKSTEMadj ; ! Presumably WeG in nominal paradigm
 +A:e%>Q1 BU/MUS ;
 +A+Der5+Der/at+Adv:e%>Q1t K ;
 +A:e%> MUORRACmp ;

LEXICON UNNE ! Attr e > a~e, WeG
 +A+Attr:a%>Q1 ATTR ; ! a-attr.
! +A+Attr:es%>Q1 ATTR ; ! e-attr. !These now hardcoded
! +A:a%>Q1 VUOHTA ; !moved to ATTR
! +A:es%>Q1 VUOHTA ; !moved to ATTR
 +A:e%> NE-EVENCASEadj ; ! presumably normal adjective
 +A:e%>X4 ILL-EVENCASEadj ;
 +A:e%>Q1 N-EVENWEAKSTEMadj ; ! behaviour, with cg, and with
 +A:e%>Q1 BU/MUS ;           ! the e-form as a starting
 +A+Der5+Der/at+Adv:e%>Q1t K ;           ! point.
 +A:e%> MUORRACmp ;

LEXICON TJÁBBE ! Attr e > a~e, WeG Same as UNNE only different adv derivation
 +A+Attr:a%>Q1 ATTR ; ! a-attr.
! +A+Attr:es%>Q1 ATTR ; ! e-attr. !These now hardcoded
! +A:a%>Q1 VUOHTA ; !moved to ATTR
! +A:es%>Q1 VUOHTA ; !moved to ATTR
 +A:e%> NE-EVENCASEadj ; ! presumably normal adjective
 +A:e%>X4 ILL-EVENCASEadj ;
 +A:e%>Q1 N-EVENWEAKSTEMadj ; ! behaviour, with cg, and with
 +A:e%>Q1 BU/MUS ;           ! the e-form as a starting
 +A+Der5+Der/at+Adv:á%>t K ;           ! point.
 +A:e%> MUORRACmp ;

LEXICON SÁVADAHTTE  !Causative-participles
 +A:%> NE-EVENCASEadj ;
 +A:%>X4 ILL-EVENCASEadj ;
 +A:%>Q4 N-EVENWEAKSTEMadj ;
 +A:%> MUORRACmp ;

LEXICON JUHKKE  !participles
 +A+Attr:%>s ATTR ;
 +A:%> NE-EVENCASEadj ;
 +A:%>X4 ILL-EVENCASEadj ;
 +A:%>Q4 N-EVENWEAKSTEMadj ;
 +A:%> MUORRACmp ;

LEXICON ÁRMMOGIS !Adjectives on -is, comp. -abbo
! +A:is VUOHTA ; !moved to ATTR
 +A+Attr:is%> ATTR ; ! twol fixes the ss > s
 +A+Sg+Nom:is%> K ;
 +A+Sg+Nom+Use/Ped+Use/Sub:issa%> PxV ; !
 +A:issa%> ÁRMMOGIS-EVEN ; ! Declined as four-syllable noun
! +A:issa%>Q1 BU/MUS ; !this is wrong:ármmogisáp
 +A:%> ABBO/AMOS ;  !ármmogabbo
 +A+Der5+Der/at+Adv:issa%>Q1t K ;
 +A+SgGenCmp:issa%>Q1 R ;
 +A+PlGenCmp:issa%>Q1j R ;


LEXICON ÅLLAGASJ !as ÁRMMOGIS but no comparatives.
! +A: VUOHTA ; !moved to ATTR
 +A+Attr:%> ATTR ;
 +A+Sg+Nom:%> K ;
 +A+Sg+Nom+Use/Ped+Use/Sub:a%> PxV ; !
 +A:a%> ÁRMMOGIS-EVEN ; ! Declined as four-syllable noun
! +A:a%>Q1 BU/MUS ;
 +A+Der5+Der/at+Adv:a%>Q1t K ;
 +A+SgNomCmp:%> R ;
 +A+SgGenCmp:a%>Q1 R ;
 +A+PlGenCmp:a%>Q1j R ;


LEXICON ASIDASJ
 +A:asj VUOHTA ;
! +A:is VUOHTA ; !moved to ATTR
 +A+Attr:is%> ATTR ; ! Special Attr form
 +A+Sg+Nom:asj%> K ;
 +A+Sg+Nom+Use/Ped+Use/Sub:adtja%> PxV ; !
 +A+Sg+Ill:adtja%>j K ;
 +A+Sg+Ill+Use/Ped+Use/Sub:adtja%>s%> PxCi ; !
 +A+Ess:adtja%>n K ;
 +A:attja%> N-EVENWEAKSTEMadj ; !This seems right. TO
! +A:attja%> BU/MUS ; !unusual comp. and superl.
 +A:%> ABBO/AMOS ;
 +A+Der5+Der/at+Adv:attja%>t K ;
 +A+SgNomCmp:asj%> R ;
 +A+SgGenCmp:attja%> R ;
 +A+PlGenCmp:attja%>j R ;

LEXICON DÁRBBOLASJ !comparatives -abbo
! +A:asj VUOHTA ; !moved to ATTR
 +A+Sg+Nom:asj%> K ;
 +A+Attr:asj%> ATTR ;
 +A+Sg+Nom+Use/Ped+Use/Sub:adtja%> PxV ; !
 +A+Sg+Ill:adtja%>j K ;
 +A+Sg+Ill+Use/Ped+Use/Sub:adtja%>s%> PxCi ; !
 +A+Ess:adtja%>n K ;
 +A:attja%> N-EVENWEAKSTEMadj ; !This seems right. TO
! +A:attja%> BU/MUS ; !unusual comp. and superl.
 +A:%> ABBO/AMOS ;
 +A+Der5+Der/at+Adv:attja%>t K ;
 +A+SgNomCmp:asj%> R ;
 +A+SgGenCmp:attja%> R ;
 +A+PlGenCmp:attja%>j R ;

LEXICON UDNODIBME ! Earlier this was TJALMEDIBME
! +A:is VUOHTA ; !moved to ATTR
 +A+Attr:is%> ATTR ; ! Special Attr form
 +A:ibme%> NE-EVENCASEadj ;
 +A:ibme%>X4 ILL-EVENCASEadj ;
 +A:ibme%>Q1 N-EVENWEAKSTEMadj ;
 +A:%> ABBO/AMOS ;
 +A+Der5+Der/at+Adv:ibme%>Q1t K ;
 +A+SgGenCmp:ibme%>Q1 R ;
 +A+PlGenCmp:ibme%>Q1j R ;
 +A: DUVVAT ;

LEXICON TJALMEDIBME ! Like UDNODIBME but no comparatives.These adjektives have the meening fravær av/uten+adjective, so it's not possible to compare this meaning.
! +A:is VUOHTA ; !moved to ATTR
 +A+Attr:is%> ATTR ; ! Special Attr form
 +A:ibme%> NE-EVENCASEadj ;
 +A:ibme%>X4 ILL-EVENCASEadj ;
 +A:ibme%>Q1 N-EVENWEAKSTEMadj ;
 +A+Der5+Der/at+Adv:ibme%>Q1t K ;
 +A+SgGenCmp:ibme%>Q1 R ;
 +A+PlGenCmp:ibme%>Q1j R ;
 +A: DUVVAT ;

LEXICON SUOLASIEHKE ! evenstem No attribute
 +A:hke%> NE-EVENCASEadj ;
 +A+Sg+Nom:hke%> K ;
 +A+Attr:k%> ATTR ;
 +A:hke%>X4 ILL-EVENCASEadj ;
 +A:hke%>Q1 N-EVENWEAKSTEMadj ;
! +A:hke%>Q1 BU/MUS ; !no comparatives for these
 +A+Der5+Der/at+Adv:hke%>Q1t K ;

LEXICON NUORTTALABBO   !Inherent comparatives
 +A:%> ABBO/AMOS ;

LEXICON GASSKALAMOS   !Inherent superlatives
 +A+Superl:%> AMOS ;
 +A+Superl+Der5+Der/at+Adv:%>abmus%>it K ;

LEXICON DÁBBELIJ
 +A+Sg+Nom:ij%> K ;
 +A+Attr:ij%> ATTR ;
! +A:ij VUOHTA ; !moved to ATTR
 +A+Sg+Nom+Use/Ped+Use/Sub:ahá%> PxV ; !
 +A+Ess:ahá%>n K ;
 +A:ahá%> ILL-EVENCASEadj ;
 +A:ahá%> N-EVENWEAKSTEMadj ;
 +A+Der5+Der/at+Adv:ahá%>t K ;
 +A:ahá%> BU/MUS ;
 +A+SgGenCmp:ahá%> R ;
 +A+PlGenCmp:ahá%>j R ;
 
LEXICON BOREÁLA !Like NUORRA, but no comparatives (loanwords)
 +A+Attr:%> ATTR ;
! +A: VUOHTA ; !moved to ATTR
 +A:%> N-EVENSTRONGSTEMadj ;
 +A:%>Q1 N-EVENWEAKSTEMadj ; ! cg in nominal paradigm
 +A+Der5+Der/at+Adv:%>Q1t K ; ! Presumably WeG as adverb as well
 +A:%> MUORRACmp ;

LEXICON SJONÁLLA
          +Dial/-SW:sjon ÁLLA ;
 +Use/Sub+Dial/-NOR:tion ÁLLA ;

LEXICON ÁLLA
                   +A:áll   ÁLLA-ÆLLA ;
           +A+Use/Sub:el'l  ÁLLA-ÆLLA ;
  +A+Dial/-SW+Use/Sub:æ9l'l ÁLLA-ÆLLA ;
 +A+Dial/-NOR+Use/Sub:ä9l'l ÁLLA-ÆLLA ;

LEXICON ÆLLA
  +A+Dial/-SW:æ9l'l ÁLLA-ÆLLA ;
 +A+Dial/-NOR:ä9l'l ÁLLA-ÆLLA ;
   +A+Use/Sub:áll   ÁLLA-ÆLLA ;
   +A+Use/Sub:el'l  ÁLLA-ÆLLA ;

LEXICON ÁLLA-ÆLLA
            +Attr:a%>Q1    ATTR ;
                 :a%>    N-EVENSTRONGSTEMadj ;
                 :a%>Q1  N-EVENWEAKSTEMadj ; ! cg in nominal paradigm
 +Der5+Der/at+Adv:a%>Q1t K ; ! Presumably WeG as adverb as well
                 :a%>    MUORRACmp ;



! Odd-syllable stems
! ------------------

LEXICON TJIEGOS ! a. Adjs on -k, negating adjs on -dahkes sm adjs on -os
 +A+Attr:%>X2 ATTR ;
! +A:%>X2 VUOHTA ; !moved to ATTR
 +A:%>X2 N-ODDCASE ; ! no pointer to E-ODDCASE, as adjs have no short Ess
 +A:%>X5 OTH-ODDCASEadj ;
 +A:%>X5 ABBO/AMOS ;
 +A+Der5+Der/at+Adv:%>X5it K ;
 +A:%>X5 ODDCmp ;


LEXICON BASSTEL ! b. Adjs on -et, -l, -r, sm om -k, -sj with -is-attr
 +A+Attr:is%> ATTR ;
! +A:is VUOHTA ; !moved to ATTR
 +A: VUOHTA ;
 +A:%> N-ODDCASE ; ! no pointer to E-ODDCASE, as adjs have no short Ess
 +A:%> OTH-ODDCASEadj ;
 +A:%> ABBO/AMOS ;
 +A+Der5+Der/at+Adv:%>it K ;
 +A+SgNomCmp:%> R ;
 +A:%> ODDCmp ;


LEXICON MUTTÁK ! c. Adjs on -ák, Attr -is or =
 +A+Attr:%> ATTR ;
 +A+Attr:is%> ATTR ;
! +A: VUOHTA ; !moved to ATTR
! +A:is VUOHTA ; !moved to ATTR
 +A:%> N-ODDCASE ; ! no pointer to E-ODDCASE, as adjs have no short Ess
 +A:%> OTH-ODDCASEadj ;
 +A:%> ABBO/AMOS ;
 +A+Der5+Der/at+Adv:%>it K ;
 +A+SgNomCmp:%> R ;
 +A:%> ODDCmp ;

LEXICON ALLAK ! d. Adjs on -ak, attr. on -a
 +A+Attr:a%> ATTR ;
! +A:a VUOHTA ; !moved to ATTR
 +A:ak VUOHTA ;
 +A:ak%> N-ODDCASE ; ! no pointer to E-ODDCASE, as adjs have no short Ess 
 +A:ag%> OTH-ODDCASEadj ;
! +A:e%>Q1 BU/MUS ; !replaced to avoid adv. form alibut
 +A+Comp:e%>Q1 BUStem ;
 +A+Superl:e%>Q1muss BUOREMUS ;
 +A+Der5+Der/at+Adv+Comp:ag%>appot K ;
 +A+Superl+Der5+Der/at+Adv:ag%>abmus%>it K ;
 +A+Comp+Der1+Der/Dimin+Sg+Nom:e»Q1busj%> K ;
 +A+Comp+Der1+Der/Dimin+Sg+Attr:e»Q1busj%> ATTR ;
 +A+Comp+Der1+Der/Dimin:e»Q1budtja%> ÁRMMOGIS-EVEN ;
 +A+Comp+Der1+Der/Dimin+Der5+Der/at+Adv:e»Q1buttja%>t K ;
 +A+Der5+Der/at+Adv:ag%>it K ;
 +A+SgNomCmp:ak%> R ;
 +A:ag%> ODDCmp ;

LEXICON GÅBDDÅK ! d8. Adjs on -åk, attr. on -å
 +A+Attr:å%> ATTR ;
! +A:å VUOHTA ; !moved to ATTR
 +A:åk VUOHTA ;
 +A:åk%> N-ODDCASE ; ! no pointer to E-ODDCASE, as adjs have no short Ess 
 +A:åg%> OTH-ODDCASEadj ;
! +A:e%>Q1 BU/MUS ; !replaced to avoid adv. form gåbdibut
 +A+Comp:e%>Q1 BUStem ;
 +A+Superl:e%>Q1muss BUOREMUS ;
 +A+Der5+Der/at+Adv+Comp:åg%>appot K ;
 +A+Superl+Der5+Der/at+Adv:åg%>abmus%>it K ;
 +A+Comp+Der1+Der/Dimin+Sg+Nom:e»Q1busj%> K ;
 +A+Comp+Der1+Der/Dimin+Sg+Attr:e»Q1busj%> ATTR ;
 +A+Comp+Der1+Der/Dimin:e»Q1budtja%> ÁRMMOGIS-EVEN ;
 +A+Comp+Der1+Der/Dimin+Der5+Der/at+Adv:e»Q1buttja%>t K ;
 +A+Der5+Der/at+Adv:åg%>it K ;
 +A+SgNomCmp:åk%> R ;
 +A:åg%> ODDCmp ;


LEXICON GALMAS
 +A+Attr:%> ATTR ;
! +A: VUOHTA ; !moved to ATTR
 +A:s%>X2 VUOHTA ;
 +A:s%>X2 N-ODDCASE ; ! no pointer to E-ODDCASE, as adjs have no short Ess
 +A:s%>X5 OTH-ODDCASEadj ;
 +A:s%>X5 ABBO/AMOS ;
 +A+Der5+Der/at+Adv:s%>it K ;
 +A+SgNomCmp:s%>X2 R ;
 +A:s%>X5 ODDCmp ;

LEXICON SUOHKAT
 +A+Attr:is%> ATTR ;
! +A:is VUOHTA ; !moved to ATTR
 +A:a%>Q1t VUOHTA ;
 +A:a%>Q1t N-ODDCASE ; ! no pointer to E-ODDCASE, as adjs have no short Ess
 +A:a%>Q1d OTH-ODDCASEadj ;
 +A:a%>Q1d ABBO/AMOS ;
 +A+Der5+Der/at+Adv:a%>Q1dit K ;
 +A+SgNomCmp:a%>Q1t R ;
 +A:a%>Q1d ODDCmp ;

LEXICON SKÁRBAK ! Only for skárbak, because it has skárbbis attr not skárbagis
 +A+Attr:bis%> ATTR ;
! +A:is VUOHTA ; !moved to ATTR
 +A:a%>Q1k VUOHTA ;
 +A:a%>Q1k N-ODDCASE ; ! no pointer to E-ODDCASE, as adjs have no short Ess
 +A:a%>Q1g OTH-ODDCASEadj ;
 +A:a%>Q1g ABBO/AMOS ;
 +A+Der5+Der/at+Adv:a%>Q1git K ;
 +A+SgNomCmp:a%>Q1t R ;
 +A:a%>Q1g ODDCmp ;

LEXICON LÅSSÅT
 +A+Attr:is%> ATTR ;
 +A+Attr:å%> ATTR ;
! +A:is VUOHTA ; !moved to ATTR
! +A:å VUOHTA ; !moved to ATTR
 +A:åt VUOHTA ;
 +A:åt%> N-ODDCASE ; ! no pointer to E-ODDCASE, as adjs have no short Ess
 +A:åd%> OTH-ODDCASEadj ;
 +A:åd%> ABBO/AMOS ;
! +A:e%>Q1 BU/MUS ; !replaced to avoid adv. form låsibut
 +A+Comp:e%>Q1 BUStem ;
 +A+Superl:e%>Q1muss BUOREMUS ;
 +A+Comp+Der1+Der/Dimin+Sg+Nom:e»Q1busj%> K ;
 +A+Comp+Der1+Der/Dimin+Sg+Attr:e»Q1busj%> ATTR ;
 +A+Comp+Der1+Der/Dimin:e»Q1budtja%> ÁRMMOGIS-EVEN ;
 +A+Comp+Der1+Der/Dimin+Der5+Der/at+Adv:e»Q1buttja%>t K ;
 +A+Der5+Der/at+Adv:åd%>it K ;
 +A+SgNomCmp:åt%> R ;
 +A:åd%> ODDCmp ;


LEXICON DIMES
 +A+Attr:a%> ATTR ;
! +A:a VUOHTA ; !moved to ATTR
 +A:es%>Q1 VUOHTA ;
 +A:es%>Q1 N-ODDCASE ; ! no pointer to E-ODDCASE, as adjs have no short Ess
 +A:ás%> OTH-ODDCASEadj ;
 +A:ás%> ABBO/AMOS ;
 +A+Der5+Der/at+Adv:ás%>it K ;
 +A+SgNomCmp:es%>Q1 R ;
 +A:ás%> ODDCmp ;

LEXICON BITJES !bitjes:bihtjas-
 +A+Attr:a%> ATTR ;
! +A:a VUOHTA ; !moved to ATTR
 +A:es%>Q1 VUOHTA ;
 +A:es%>Q1 N-ODDCASE ; ! no pointer to E-ODDCASE, as adjs have no short Ess
 +A:as%> OTH-ODDCASEadj ;
 +A:as%> ABBO/AMOS ;
 +A+Der5+Der/at+Adv:as%>it K ;
 +A+SgNomCmp:es%>Q1 R ;
 +A:as%> ODDCmp ;

LEXICON LUOBES ! same as BITJES only attribute same as nom
 +A+Attr:es%>Q1 ATTR ;
! +A:a VUOHTA ; !moved to ATTR
 +A:es%>Q1 VUOHTA ;
 +A:es%>Q1 N-ODDCASE ; ! no pointer to E-ODDCASE, as adjs have no short Ess
 +A:as%> OTH-ODDCASEadj ;
 +A:as%> ABBO/AMOS ;
 +A+Der5+Der/at+Adv:as%>it K ;
 +A+SgNomCmp:es%>Q1 R ;
 +A:as%> ODDCmp ;

LEXICON OAMES
 +A+Attr:%> ATTR ;
! +A: VUOHTA ; !moved to ATTR
 +A:s%>Q1 VUOHTA ;
 +A:s%>Q1 N-ODDCASE ; ! no pointer to E-ODDCASE, as adjs have no short Ess
 +A:%>X4s OTH-ODDCASEadj ;
 +A:%>X4s ABBO/AMOS ;
 +A+Der5+Der/at+Adv:%>X4sit K ;
 +A+SgNomCmp:s%>Q1 R ;
 +A:%>X4s ODDCmp ;

LEXICON SÁJSAS
 +A+Attr:e%> ATTR ;
! +A: VUOHTA ; !moved to ATTR
 +A:as%>Q1 VUOHTA ;
 +A:as%>Q1 N-ODDCASE ; ! no pointer to E-ODDCASE, as adjs have no short Ess
 +A:a%>X4s OTH-ODDCASEadj ;
 +A:a%>X4s ABBO/AMOS ;
 +A+Der5+Der/at+Adv:a%>X4sit K ;
 +A+SgNomCmp:as%>Q1 R ;
 +A:a%>X4s ODDCmp ;

LEXICON TJALMMIS
 +A+Attr:%>X2 ATTR ;
 +A+Attr:is%> ATTR ;
! +A:is VUOHTA ; !moved to ATTR
! +A:%>X2 VUOHTA ; !moved to ATTR
 +A:%>X2 N-ODDCASE ; ! no pointer to E-ODDCASE, as adjs have no short Ess
 +A:%>X5 OTH-ODDCASEadj ;
 +A:%>X5 ABBO/AMOS ;
 +A+Der5+Der/at+Adv:%>it K ; !Can these really form adverbs?
 +A:%>X5 ODDCmp ;

LEXICON STUORAK
 +A+Attr:%> ATTR ;
 +A+Attr:ra%> ATTR ;
! +A:ra VUOHTA ; !moved to ATTR
 +A:ak VUOHTA ;
 +A:ak%> N-ODDCASE ;
 +A:ag%> OTH-ODDCASEadj ;
 +A:á%> BU/MUS ;
 +A+Der5+Der/at+Adv:ag%>it K ;
 +A+SgNomCmp:ak%> R ;
 +A:ag%> ODDCmp ;


LEXICON SJÆVNNJAT ! miscellanious uneven No attribute, stemvowel change
 +A:%>Q1 VUOHTA ;
 +A:%>Q1 N-ODDCASE ;
 +A:%>X5 OTH-ODDCASEadj ;
 +A:%>X5 ABBO/AMOS ;
 +A+Der5+Der/at+Adv:%>X5it K ;
 +A+SgNomCmp:%>Q1 R ;
 +A:%>X5 ODDCmp ;


LEXICON RIHTSOK ! miscellanious uneven No attribute, no stemvowel change
 +A:%>Q1 VUOHTA ;
 +A:%>Q1 N-ODDCASE ;
 +A:%> OTH-ODDCASEadj ;
 +A:%> ABBO/AMOS ;
 +A+Der5+Der/at+Adv:%>it K ;
 +A+SgNomCmp:%>Q1 R ;
 +A:%> ODDCmp ;



LEXICON OANEP  !Inherent comparatives
 +A:%> BU/MUS ;

LEXICON TJAVGGÁMUS   !Inherent superlatives
 +A+Superl:muss BUOREMUS ;
 +A+Superl+Der5+Der/at+Adv:musá%>t K ;


LEXICON TSIBTSA !attr =
 +A+Attr:%>X3 ATTR ;
! +A:%>X3 VUOHTA ; !moved to ATTR
 +A+Sg+Nom:%>X3 K ;
 +A:%>X5 OTH-ODDCASEadj ;
 +A:%>X5 ABBO/AMOS ;
 +A+Der5+Der/at+Adv:%>X5it K ;
 +A:%>X5 ODDCmp ;


LEXICON IENNILS !no comparatives
 +A: VUOHTA ;
 +A:%> N-ODDCASE ;
 +A:%> OTH-ODDCASEadj ;
 +A: NAMÁK ;
 +A:%> R ;

LEXICON GÅNTSAS
 +A+Attr:%>X3 ATTR ;
! +A:%>X3 VUOHTA ; !moved to ATTR
 +A:%>X3 N-ODDCASE ; ! no pointer to E-ODDCASE, as adjs have no short Ess
 +A:%>X5 OTH-ODDCASEadj ;
 +A:%>X5 ABBO/AMOS ;
 +A+Der5+Der/at+Adv:%>X5it K ;
 +A:%>X5 ODDCmp ;

LEXICON DABÁR !like BASSTEL but with CG
 +A+Attr:%>Q1is ATTR ;
! +A:%>Q1is VUOHTA ; !moved to ATTR
 +A:%>Q1 VUOHTA ;
 +A:%>Q1 N-ODDCASE ; ! no pointer to E-ODDCASE, as adjs have no short Ess
 +A:%> OTH-ODDCASEadj ;
 +A:%> ABBO/AMOS ;
 +A+Der5+Der/at+Adv:%>it K ;
 +A+SgNomCmp:%>Q1 R ;
 +A:%> ODDCmp ;

LEXICON SNÁVES    !snáves:snávvas-
 +A+Attr:es%> ATTR ;
! +A:es VUOHTA ; !moved to ATTR
 +A:es%> N-ODDCASE ; ! no pointer to E-ODDCASE, as adjs have no short Ess
 +A:vas%>X5 OTH-ODDCASEadj ;
 +A:vas%>X5 ABBO/AMOS ;
 +A+Der5+Der/at+Adv:vas%>X5it K ;
 +A:vas%>X5 ODDCmp ;




! Contracted stems
! ----------------

LEXICON SÁDNES ! attr =
 +A+Attr:%>Q1s ATTR ;
! +A:%>Q1s VUOHTA ; !moved to ATTR
 +A+Sg+Nom:%>Q1s K ;
 +A:%>X4 ILL-EVENCASEadj ;
 +A:%>X4 N-EVENWEAKSTEMadj ;
 +A+Sg+Nom+Use/Ped+Use/Sub:%>X4 PxV ; !
 +A+Ess:%>X4n K ;
 +A:%>X4 BU/MUS ;
 +A+Der5+Der/at+Adv:%>X4t K ;
 +A+SgGenCmp:%>X4 R ;
 +A+PlGenCmp:%>X4j R ;


LEXICON GOAVSOS  !attr =
 +A+Attr:%>Q3s ATTR ;
! +A:%>Q3s VUOHTA ; !moved to ATTR
 +A+Sg+Nom:%>Q3s K ;
 +A:%>X4 ILL-EVENCASEadj ;
 +A:%>X4 N-EVENWEAKSTEMadj ;
 +A+Sg+Nom+Use/Ped+Use/Sub:%>X4 PxV ; !
 +A+Ess:%>X4n K ;
 +A:%>X4 BU/MUS ;
 +A+Der5+Der/at+Adv:%>X4t K ;
 +A+SgGenCmp:%>X4 R ;
 +A+PlGenCmp:%>X4j R ;


LEXICON SUVRES  !attr suvra
 +A+Attr:a%>Q1 ATTR ;
! +A:a%>Q1 VUOHTA ; !moved to ATTR
 +A:e%>Q1s VUOHTA ;
 +A+Sg+Nom:e%>Q1s K ;
 +A:á%> ILL-EVENCASEadj ;
 +A:á%> N-EVENWEAKSTEMadj ;
 +A+Sg+Nom+Use/Ped+Use/Sub:á%> PxV ; !
 +A+Ess:á%>n K ;
 +A:á%> BU/MUS ;
 +A+Der5+Der/at+Adv:á%>t K ;
 +A+SgNomCmp:e%>Q1s R ;
 +A+SgGenCmp:á%> R ;
 +A+PlGenCmp:á%>j R ;

LEXICON RÁDAS !rádas:ráddasa
 +A+Attr:as%> ATTR ;
! +A:as%> VUOHTA ; !moved to ATTR
 +A:as%> N-ODDCASE ; ! no pointer to E-ODDCASE, as adjs have no short Ess
 +A:das%> OTH-ODDCASEadj ;
 +A:das%> ABBO/AMOS ;
 +A+Der5+Der/at+Adv:das%>it K ;
 +A:das%> ODDCmp ;





! Comparation
! -----------

LEXICON BU/MUS ! even-syll adjectives
 +Comp: BUStem ;
 +Superl:muss BUOREMUS ;
 +Comp+Der5+Der/at+Adv:but K ;
 +Superl+Der5+Der/at+Adv:musá%>t K ;
 +Comp+Der1+Der/Dimin+Sg+Nom:bu»sj%> K ;
 +Comp+Der1+Der/Dimin+Sg+Attr:bu»sj%> ATTR ;
 +Comp+Der1+Der/Dimin:bu»dtja%> CompSup-EVEN ;
 +Comp+Der1+Der/Dimin+Der5+Der/at+Adv:bu»ttja%>t K ;


LEXICON ABBO/AMOS ! odd-syll adjectives
 !+Comp+Sg+Nom:ap%> K ;
 !+Comp+Sg+Nom:up%> K ;
! +Comp+Sg+Nom:abbo%> K ;
! +Comp+Sg+Nom:ubbo%> K ;
! +Comp+Sg+Nom+Use/Sub:æbbo%> K ; !
 +Comp+Attr:ap%> ATTR ;
 +Comp+Attr:up%> ATTR ;
 	  +Comp: 	 ABBO ;
 	+Superl: 	 AMOS ;

 +Der5+Der/at+Adv+Comp:appo%>t K ;
 +Superl+Der5+Der/at+Adv:abmus%>it K ;
 +Comp+Der1+Der/Dimin+Sg+Nom:appu»sj%> K ;
 +Comp+Der1+Der/Dimin+Sg+Attr:appu»sj%> ATTR ;
 +Comp+Der1+Der/Dimin:appu»tj%> CompSup-ODDCASE ;
 +Comp+Der1+Der/Dimin+Der5+Der/at+Adv:appu»tj%>it K ;
 +Comp+Der5+Der/at+Adv:appo%>t K ;
 +Superl+Der5+Der/at+Adv:abmus%>it K ;

 +Der5+Der/at+Adv+Comp+Use/Sub:æppo%>t K ; !
 +Superl+Der5+Der/at+Adv+Use/Sub:ebmus%>it K ; !
 +Comp+Der1+Der/Dimin+Sg+Nom+Use/Sub:eppu»sj%> K ; !
 +Comp+Der1+Der/Dimin+Sg+Nom+Use/Sub:uppu»sj%> K ;
 +Comp+Der1+Der/Dimin+Sg+Attr+Use/Sub:eppu»sj%> ATTR ; !
 +Comp+Der1+Der/Dimin+Sg+Attr+Use/Sub:uppu»sj%> ATTR ;
 +Comp+Der1+Der/Dimin+Use/Sub:eppu»tj%> CompSup-ODDCASE ; !
 +Comp+Der1+Der/Dimin+Use/Sub:uppu»tj%> CompSup-ODDCASE ;
 +Comp+Der1+Der/Dimin+Der5+Der/at+Adv+Use/Sub:eppu»tj%>it K ; !
 +Comp+Der1+Der/Dimin+Der5+Der/at+Adv+Use/Sub:uppu»tj%>it K ;




! Comparative
! -----------

LEXICON BUStem
 +Attr:p ATTR ;
 +Sg+Nom:p K ;
 +Sg+Nom+Use/Marg+Use/Ped+Use/Sub:bu%> PxV ; ! !
 +Sg+Gen:bu K ;
 +Sg+Abe:bu#dagi K ;
 +Sg+Abe:bu#dagá K ;
 +Sg+Abe:bu#tagá K ;
 +Sg+Gen+Use/Marg+Use/Ped+Use/Sub:bu%> PxV ; ! !
 +Sg+Acc:buv K ;
 +Sg+Acc+Use/Marg+Use/Ped+Use/Sub:bu%> PxV ; ! !
 +Sg+Ill:buj K ;
 +Sg+Ine:bun K ;
 +Sg+Ine+Use/Marg+Use/Ped+Use/Sub:bun%> PxCs ; ! !
 +Sg+Ela:bus K ;
 +Sg+Ela+Use/Marg+Use/Ped+Use/Sub:bust%> PxCs ; ! !
 +Sg+Com:bujn K ;
 +Sg+Com+Use/Sub:bujna K ; !
 +Sg+Com+Use/Marg+Use/Ped+Use/Sub:bujn%> PxCs ; ! !
 +Ess:bun K ;
 +Pl+Nom:bu K ;
 +Pl+Gen:buj K ;
 +Pl+Abe:buj#dagi K ;
 +Pl+Abe:buj#dagá K ;
 +Pl+Abe:buj#tagá K ;
 +Pl+Acc:bujt K ;
 +Pl+Acc+Use/Marg+Use/Ped+Use/Sub:bujd%> PxCs ; ! !
 +Pl+Ill:bujda K ;
 +Pl+Ine:bujn K ;
 +Pl+Ine+Use/Marg+Use/Ped+Use/Sub:bujn%> PxCs ; ! !
 +Pl+Ela:bujs K ;
 +Pl+Ela+Use/Marg+Use/Ped+Use/Sub:bujst%> PxCs ; ! !
 +Pl+Com:buj K ;

LEXICON ABBO
! -abbo
		 +Sg+Nom:abbo%> K ;
		 +Sg+Gen:appo%> K ;
		 +Sg+Abe:appo%>#dagi K ;
		 +Sg+Abe:appo%>#dagá K ;
		 +Sg+Abe:appo%>#tagá K ;
		 +Sg+Acc:appo%>v K ;
		 +Sg+Ill:abbu%>j K ;
		 +Sg+Ine:appo%>n K ;
		 +Sg+Ela:appo%>s K ;
		 +Sg+Com:appu%>jn K ;
		 +Sg+Com:appo%>jn K ;
 +Sg+Com+Use/Sub:appu%>jna K ; !
 +Sg+Com+Use/Sub:appo%>jna K ; !
		 	+Ess:abbo%>n K ;
		 +Pl+Nom:appo%> K ;
		 +Pl+Gen:appo%>j K ;
		 +Pl+Abe:appo%>j#dagi K ;
		 +Pl+Abe:appo%>j#dagá K ;
		 +Pl+Abe:appo%>j#tagá K ;
		 +Pl+Acc:appo%>jt K ;
		 +Pl+Ill:appo%>jda K ;
		 +Pl+Ine:appo%>jn K ;
		 +Pl+Ela:appo%>js K ;
		 +Pl+Com:appo%>j K ;
! -ubbo
 +Sg+Nom+Use/Sub:ubbo%> K ;
 +Sg+Gen+Use/Sub:uppo%> K ;
 +Sg+Abe+Use/Sub:uppo%>#dagi K ;
 +Sg+Abe+Use/Sub:uppo%>#dagá K ;
 +Sg+Abe+Use/Sub:uppo%>#tagá K ;
 +Sg+Acc+Use/Sub:uppo%>v K ;
 +Sg+Ill+Use/Sub:ubbu%>j K ;
 +Sg+Ine+Use/Sub:uppo%>n K ;
 +Sg+Ela+Use/Sub:uppo%>s K ;
 +Sg+Com+Use/Sub:uppu%>jn K ;
 +Sg+Com+Use/Sub:uppo%>jn K ;
 +Sg+Com+Use/Sub:uppu%>jna K ; !
 +Sg+Com+Use/Sub:uppo%>jna K ; !
 	+Ess+Use/Sub:ubbo%>n K ;
 +Pl+Nom+Use/Sub:uppo%> K ;
 +Pl+Gen+Use/Sub:uppo%>j K ;
 +Pl+Abe+Use/Sub:uppo%>j#dagi K ;
 +Pl+Abe+Use/Sub:uppo%>j#dagá K ;
 +Pl+Abe+Use/Sub:uppo%>j#tagá K ;
 +Pl+Acc+Use/Sub:uppo%>jt K ;
 +Pl+Ill+Use/Sub:uppo%>jda K ;
 +Pl+Ine+Use/Sub:uppo%>jn K ;
 +Pl+Ela+Use/Sub:uppo%>js K ;
 +Pl+Com+Use/Sub:uppo%>j K ;
! -æbbo
 +Sg+Nom+Use/Sub:æbbo%> K ; !
 +Sg+Gen+Use/Sub:æppo%> K ; !
 +Sg+Abe+Use/Sub:æppo%>#dagi K ; !
 +Sg+Abe+Use/Sub:æppo%>#dagá K ; !
 +Sg+Abe+Use/Sub:æppo%>#tagá K ; !
 +Sg+Acc+Use/Sub:æppo%>v K ; !
 +Sg+Ill+Use/Sub:æbbu%>j K ; !
 +Sg+Ine+Use/Sub:æppo%>n K ; !
 +Sg+Ela+Use/Sub:æppo%>s K ; !
 +Sg+Com+Use/Sub:eppu%>jn K ; !
 +Sg+Com+Use/Sub:æppo%>jn K ; !
 +Sg+Com+Use/Sub:eppu%>jna K ; !
 +Sg+Com+Use/Sub:æppo%>jna K ; !
 	+Ess+Use/Sub:æbbo%>n K ; !
 +Pl+Nom+Use/Sub:æppo%> K ; !
 +Pl+Gen+Use/Sub:æppo%>j K ; !
 +Pl+Abe+Use/Sub:æppo%>j#dagi K ; !
 +Pl+Abe+Use/Sub:æppo%>j#dagá K ; !
 +Pl+Abe+Use/Sub:æppo%>j#tagá K ; !
 +Pl+Acc+Use/Sub:æppo%>jt K ; !
 +Pl+Ill+Use/Sub:æppo%>jda K ; !
 +Pl+Ine+Use/Sub:æppo%>jn K ; !
 +Pl+Ela+Use/Sub:æppo%>js K ; !
 +Pl+Com+Use/Sub:æppo%>j K ; !


LEXICON AMOS
 +Attr:amos%> K ;
 +Sg+Nom:amos%> K ;
 +Sg+Nom+Use/Marg+Use/Ped+Use/Sub:abmus%>a%> PxV ; ! !
 +Sg+Gen:abmus%>a K ;
 +Sg+Abe:abmus%>a#dagi K ;
 +Sg+Abe:abmus%>a#dagá K ;
 +Sg+Abe:abmus%>a#tagá K ;
 +Sg+Gen+Use/Marg+Use/Ped+Use/Sub:abmus%>a%> PxV ; ! !
 +Sg+Acc:abmus%>av K ;
 +Sg+Acc+Use/Marg+Use/Ped+Use/Sub:abmus%>a%> PxV ; ! !
 +Sg+Ill:abmus%>ij K ;
 +Sg+Ine:abmus%>in K ;
 +Sg+Ine+Use/Marg+Use/Ped+Use/Sub:abmus%>in%> PxCs ; ! !
 +Sg+Ela:abmus%>is K ;
 +Sg+Ela+Use/Marg+Use/Ped+Use/Sub:abmus%>ist%> PxCs ; ! !
 +Sg+Com:abmus%>ijn K ;
 +Sg+Com+Use/Sub:abmus%>ijna K ; !
 +Sg+Com+Use/Marg+Use/Ped+Use/Sub:abmus%>ijn%> PxCs ; ! !
 +Ess:abmus%>in K ;
 +Pl+Nom:abmus%>a K ;
 +Pl+Gen:abmus%>ij K ;
 +Pl+Abe:abmus%>ij#dagi K ;
 +Pl+Abe:abmus%>ij#dagá K ;
 +Pl+Abe:abmus%>ij#tagá K ;
 +Pl+Acc:abmus%>ijt K ;
 +Pl+Acc+Use/Marg+Use/Ped+Use/Sub:abmus%>ijd%> PxCs ; ! !
 +Pl+Ill:abmus%>ijda K ;
 +Pl+Ine:abmus%>ijn K ;
 +Pl+Ine+Use/Marg+Use/Ped+Use/Sub:abmus%>ijn%> PxCs ; ! !
 +Pl+Ela:abmus%>ijs K ;
 +Pl+Ela+Use/Marg+Use/Ped+Use/Sub:abmus%>ijst%> PxCs ; ! !
 +Pl+Com:abmus%>ij K ;
 
 +Attr+Use/Sub:umos%> K ;
 +Sg+Nom+Use/Sub:umos%> K ;
 +Sg+Nom+Use/Marg+Use/Ped+Use/Sub:ubmus%>a%> PxV ; ! !
 +Sg+Gen+Use/Sub:ubmus%>a K ;
 +Sg+Abe+Use/Sub:ubmus%>a#dagi K ;
 +Sg+Abe+Use/Sub:ubmus%>a#dagá K ;
 +Sg+Abe+Use/Sub:ubmus%>a#tagá K ;
 +Sg+Gen+Use/Marg+Use/Ped+Use/Sub:ubmus%>a%> PxV ; ! !
 +Sg+Acc+Use/Sub:ubmus%>av K ;
 +Sg+Acc+Use/Marg+Use/Ped+Use/Sub:ubmus%>a%> PxV ; ! !
 +Sg+Ill+Use/Sub:ubmus%>ij K ;
 +Sg+Ine+Use/Sub:ubmus%>in K ;
 +Sg+Ine+Use/Marg+Use/Ped+Use/Sub:ubmus%>in%> PxCs ; ! !
 +Sg+Ela+Use/Sub:ubmus%>is K ;
 +Sg+Ela+Use/Marg+Use/Ped+Use/Sub:ubmus%>ist%> PxCs ; ! !
 +Sg+Com+Use/Sub:ubmus%>ijn K ;
 +Sg+Com+Use/Sub:ubmus%>ijna K ;
 +Sg+Com+Use/Marg+Use/Ped+Use/Sub:ubmus%>ijn%> PxCs ; ! !
 +Ess+Use/Sub:ubmus%>in K ;
 +Pl+Nom+Use/Sub:ubmus%>a K ;
 +Pl+Gen+Use/Sub:ubmus%>ij K ;
 +Pl+Abe+Use/Sub:ubmus%>ij#dagi K ;
 +Pl+Abe+Use/Sub:ubmus%>ij#dagá K ;
 +Pl+Abe+Use/Sub:ubmus%>ij#tagá K ;
 +Pl+Acc+Use/Sub:ubmus%>ijt K ;
 +Pl+Acc+Use/Marg+Use/Ped+Use/Sub:ubmus%>ijd%> PxCs ; ! !
 +Pl+Ill+Use/Sub:ubmus%>ijda K ;
 +Pl+Ine+Use/Sub:ubmus%>ijn K ;
 +Pl+Ine+Use/Marg+Use/Ped+Use/Sub:ubmus%>ijn%> PxCs ; ! !
 +Pl+Ela+Use/Sub:ubmus%>ijs K ;
 +Pl+Ela+Use/Marg+Use/Ped+Use/Sub:ubmus%>ijst%> PxCs ; ! !
 +Pl+Com+Use/Sub:ubmus%>ij K ;
 
 +Attr+Use/Sub:æmos%> K ; !
 +Sg+Nom+Use/Sub:æmos%> K ; !
 +Sg+Nom+Use/Sub+Use/Marg+Use/Ped+Use/Sub:ebmus%>a%> PxV ; ! ! !
 +Sg+Gen+Use/Sub:ebmus%>a K ; !
 +Sg+Abe+Use/Sub:ebmus%>a#dagi K ; !
 +Sg+Abe+Use/Sub:ebmus%>a#dagá K ; !
 +Sg+Abe+Use/Sub:ebmus%>a#tagá K ; !
 +Sg+Gen+Use/Sub+Use/Marg+Use/Ped+Use/Sub:ebmus%>a%> PxV ; ! ! !
 +Sg+Acc+Use/Sub:ebmus%>av K ; !
 +Sg+Acc+Use/Sub+Use/Marg+Use/Ped+Use/Sub:ebmus%>a%> PxV ; ! ! !
 +Sg+Ill+Use/Sub:ebmus%>ij K ; !
 +Sg+Ine+Use/Sub:ebmus%>in K ; !
 +Sg+Ine+Use/Sub+Use/Marg+Use/Ped+Use/Sub:ebmus%>in%> PxCs ; ! ! !
 +Sg+Ela+Use/Sub:ebmus%>is K ; !
 +Sg+Ela+Use/Sub+Use/Marg+Use/Ped+Use/Sub:ebmus%>ist%> PxCs ; ! ! !
 +Sg+Com+Use/Sub:ebmus%>ijn K ; !
 +Sg+Com+Use/Sub:ebmus%>ijna K ; ! !SUB
 +Sg+Com+Use/Sub+Use/Marg+Use/Ped+Use/Sub:ebmus%>ijn%> PxCs ; ! ! !
 +Ess+Use/Sub:ebmus%>in K ; !
 +Pl+Nom+Use/Sub:ebmus%>a K ; !
 +Pl+Gen+Use/Sub:ebmus%>ij K ; !
 +Pl+Abe+Use/Sub:ebmus%>ij#dagi K ; !
 +Pl+Abe+Use/Sub:ebmus%>ij#dagá K ; !
 +Pl+Abe+Use/Sub:ebmus%>ij#tagá K ; !
 +Pl+Acc+Use/Sub:ebmus%>ijt K ; !
 +Pl+Acc+Use/Sub+Use/Marg+Use/Ped+Use/Sub:ebmus%>ijd%> PxCs ; ! ! !
 +Pl+Ill+Use/Sub:ebmus%>ijda K ; !
 +Pl+Ine+Use/Sub:ebmus%>ijn K ; !
 +Pl+Ine+Use/Sub+Use/Marg+Use/Ped+Use/Sub:ebmus%>ijn%> PxCs ; ! ! !
 +Pl+Ela+Use/Sub:ebmus%>ijs K ; !
 +Pl+Ela+Use/Sub+Use/Marg+Use/Ped+Use/Sub:ebmus%>ijst%> PxCs ; ! ! !
 +Pl+Com+Use/Sub:ebmus%>ij K ; !


! Superlative
! -----------

LEXICON BUOREMUS
 +Attr:%> K ;
 +Sg+Nom:%> K ; ! the short form, twol shortens ss#
 +Sg+Nom+Use/Marg+Use/Ped+Use/Sub:a%> PxV ; ! ! ! separate nom bec of the short form
 :a%> BUOREMUS-OBL ; ! Add -a, directly to the obliques

LEXICON BUOREMUS-OBL
 +Ess:n K ; ! essive has strong stem
 +Sg+Ill:j K ; ! so has illative
! +Sg+Ill+Use/Sub:s%> PxC ; !^P^ !MARG ! this Px form not mentioned in Spiik
 :Q1 CompSup-EVENWEAKSTEM ; ! These words go like muorra


! Comparative and Superlative sub-lexica
! -----------

LEXICON CompSup-EVEN
 +Sg+Nom+Use/Marg+Use/Ped+Use/Sub: PxV ; ! ! !Spiik not explicit here, thus guessing.
 +Ess:n K ;
! +Ess+Use/Sub:n%> PxC ; !^P^ !MARG !No Ess+Px according to Spiik, Grundstrom
 +Sg+Ill:j K ;
 +Sg+Ill+Use/Marg+Use/Ped+Use/Sub:s%> PxCi ; ! !
 :Q1 CompSup-EVENWEAKSTEM ;

LEXICON CompSup-EVENWEAKSTEM
 +Sg+Gen: K ;
 +Sg+Abe:#dagi K ;
 +Sg+Abe:#dagá K ;
 +Sg+Abe:#tagá K ;
 +Sg+Gen+Use/Marg+Use/Ped+Use/Sub: PxV ; ! !
 +Sg+Acc:v K ;
 +Sg+Acc+Use/Marg+Use/Ped+Use/Sub: PxV ; ! !
 +Sg+Ine:n K ;
 +Sg+Ine+Use/Marg+Use/Ped+Use/Sub:n%> PxC ; ! !
 +Sg+Ela:s K ;
 +Sg+Ela+Use/Marg+Use/Ped+Use/Sub:st%> PxC ; ! !
 +Pl+Nom: K ;
 +Pl+Nom+Use/Marg+Use/Ped+Use/Sub: PxNomPlV ; ! !
 +Pl+Gen:j K ;
 +Pl+Abe:j#dagi K ;
 +Pl+Abe:j#dagá K ;
 +Pl+Abe:j#tagá K ;
 +Pl+Gen+Use/Marg+Use/Ped+Use/Sub:j%> PxC ; ! !
 +Pl+Acc:jt K ;
 +Pl+Acc+Use/Marg+Use/Ped+Use/Sub:jd%> PxC ; ! !
 +Pl+Ill:jda K ;
 +Pl+Ill+Use/Marg+Use/Ped+Use/Sub:jdis%> PxCs ; ! !
 +Pl+Ine:jn K ;
 +Pl+Ine+Use/Marg+Use/Ped+Use/Sub:jn%> PxC ; ! !
 +Pl+Ela:js K ;
 +Pl+Ela+Use/Marg+Use/Ped+Use/Sub:jst%> PxC ; ! !
 +Pl+Com:j K ;
 +Pl+Com+Use/Marg+Use/Ped+Use/Sub:j%> PxC ; ! !
 +Sg+Com+Use/Sub:X5jna K ; !
 +Sg+Com:X5jn K ; ! X5 for o:u in ComSg. Note two dummies in a row!
 +Sg+Com+Use/Marg+Use/Ped+Use/Sub:X5jn%> PxC ; ! !

LEXICON CompSup-ODDCASE
 +Sg+Nom+Use/Marg+Use/Ped+Use/Sub: PxC ; ! !
 +Sg+Gen:a K ;
 +Sg+Abe:a#dagi K ;
 +Sg+Abe:a#dagá K ;
 +Sg+Abe:a#tagá K ;
 +Sg+Gen+Use/Marg+Use/Ped+Use/Sub: PxC ; ! !
 +Sg+Acc:av K ;
 +Sg+Acc+Use/Marg+Use/Ped+Use/Sub: PxC ; ! !
 +Sg+Ill:ij K ;  ! Px ?? !!
 +Sg+Ill+Use/Marg+Use/Ped+Use/Sub:as%> PxCs ; ! !
 +Sg+Ine:in K ;
 +Sg+Ine+Use/Marg+Use/Ped+Use/Sub:in%> PxCs ; ! !
 +Sg+Ela:is K ;
 +Sg+Ela+Use/Marg+Use/Ped+Use/Sub:ist%> PxCs ; ! !
 +Sg+Com:ijn K ;
 +Sg+Com+Use/Sub:ijna K ; !
 +Sg+Com+Use/Marg+Use/Ped+Use/Sub:ijn%> PxCs; ! !
 +Ess:in K ;
 +Pl+Nom:a K ;
 +Pl+Gen:ij K ;
 +Pl+Abe:ij#dagi K ;
 +Pl+Abe:ij#dagá K ;
 +Pl+Abe:ij#tagá K ;
 +Pl+Gen+Use/Marg+Use/Ped+Use/Sub:ij%> PxCs ; ! !
 +Pl+Acc:ijt K ;
 +Pl+Acc+Use/Marg+Use/Ped+Use/Sub:ijd%> PxCs ; ! !
 +Pl+Ill:ijda K ;
 +Pl+Ill+Use/Marg+Use/Ped+Use/Sub:ijdas%> PxC ; ! !
 +Pl+Ine:ijn K ;
 +Pl+Ine+Use/Marg+Use/Ped+Use/Sub:ijn%> PxCs ; ! !
 +Pl+Ela:ijs K ;
 +Pl+Ela+Use/Marg+Use/Ped+Use/Sub:ijst%> PxCs ; ! !
 +Pl+Com:ij K ;
 +Pl+Com+Use/Marg+Use/Ped+Use/Sub:ij%> PxCs ; ! !





! Adj declension
! ---------------

LEXICON ATTR
 : NAMÁK ;
 K ;                  ! Plain attributive
 R ;
 VUOHTA ;
! -:#%- ProperNoun ;   !already in LEXICON R !
! : ALIT ;  ! both comp and independent adj




! Derivation of adjectives
! ------------------------

LEXICON DenominalAdjsV1
 +Der1+Der/dibme+A:Q1 DIBME ;
 +Der1+Der/dibme+A+Attr:Q1dis%> ATTR ;
! +Der1+Der/dibme+A:Q1dis VUOHTA ; !moved to ATTR
 +Der1+Der/dibme+A:Q1d DUVVAT ;
 +Abe:Q1dak K ;
 +Abe:Q1da K ;

LEXICON DenominalAdjsV2
 +Der1+Der/lasj:X7l DÁRBBOLASJ ;

LEXICON DenominalAdjsKINO
 +Der1+Der/dibme+A: DIBME ;
 +Der1+Der/dibme+A+Attr:dis%> ATTR ;
! +Der1+Der/dibme+A:dis VUOHTA ; !moved to ATTR
 +Der1+Der/dibme+A:d DUVVAT ;
 +Der4+Der/ahtes:g AHTES ;
 +Der4+Der/ahtes:d AHTES ;
 +Abe:dak K ;
 +Abe:da K ;
 +Der1+Der/lasj:l DÁRBBOLASJ ;


LEXICON DenominalAdjsODD
 +Der4+Der/ahtes: AHTES ;
 +Abe:adak K ;
 +Abe:ada K ;

LEXICON DenominalAdjsContr
 +Der1+Der/dibme+A: DIBME ;
 +Der1+Der/dibme+A+Attr:dis%> ATTR ;
! +Der1+Der/dibme+A:dis VUOHTA ; !moved to ATTR
 +Der1+Der/dibme+A:d DUVVAT ;
 +Abe:dak K ;
 +Abe:da K ;


LEXICON AHTES
 +A+Attr:ahtes%> ATTR ;
 +A+Der5+Der/at+Adv:ahtá%> K ;
 +A+Der5+Der/at+Adv:ahti%> K ;
 +A+Sg+Nom:ahtes%> K ;
 +A+Sg+Nom+Use/Ped+Use/Sub:ahttá%> PxV ; !
 +A+Ess:ahttá%>n K ;
! +A:ahtes VUOHTA ; !moved to ATTR
 +A:ahttá%> ILL-EVENCASEadj ;
 +A:ahttá%> N-EVENWEAKSTEMadj ;
 +A+SgGenCmp:ahttá%> R ;
 +A+PlGenCmp:ahttá%>j R ;
 +A:ahttá%> BU/MUS ;
 +A:ahttás%> OTH-ODDCASEadj ;
 +A:ahttás%> ABBO/AMOS ;
 +A+Der5+Der/at+Adv:ahttás%>it K ;
 +A:ahttás%> ODDCmp ;



LEXICON DIBME
 :dibme%> NE-EVENCASEadj ;
 :dibme%>X4 ILL-EVENCASEadj ;
 :dibme%>Q1 N-EVENWEAKSTEMadj ;
 +Use/-Spell:d ABBO/AMOS ;
 +SgGenCmp:dibme%>Q1 R ;
 +PlGenCmp:dibme%>Q1j R ;
 +Der5+Der/at+Adv:dibme%>Q1t K ;


LEXICON LIS
 +Attr:%> ATTR ;
 +Sg+Nom:%> K ;
 +Sg+Nom+Use/Ped+Use/Sub:a%> PxV ; !
 +SgGenCmp:a%>Q1 R ;
 +PlGenCmp:a%>Q1j R ;
 +Ess:a%>n K ;
 :a%>Q1 N-EVENWEAKSTEM-OTHadj ;
 :a%> ILL-EVENCASEadj ;
 :a%>Q1 N-EVENWEAKSTEM-SG-COMadj ;
! : VUOHTA ; !moved to ATTR


LEXICON LÁGÁSJ
 +A+Attr:%> K ;
 +A+SgGenCmp:%>a R ;
 +A+PlGenCmp:%>ij R ;
 +A: VUOHTA ;
 +A:%> N-ODDCASE ;
 +A:%> OTH-ODDCASEadj ;
 -+Use/Circ:#%- ProperNoun ;		! ! Definitely circular x 35000.



LEXICON AGAdj
 +A+Attr:%> K ;
 +A+SgGenCmp:%>a R ;
 +A+PlGenCmp:%>ij R ;
 +A: VUOHTA ;
 +A:%> N-ODDCASE ;
 +A:%> OTH-ODDCASEadj ;

LEXICON Ahkásasj
 +A+Attr:%> K ;
 +A+SgGenCmp:a%>Q1 R ;
 +A+PlGenCmp:a%>Q1j R ;
 +A+Sg+Nom:%> K ;
 +A:a%> ÁRMMOGIS-EVEN ;
 +A: VUOHTA ;



LEXICON STÁVAL
 +A+Attr:%>Q1 K ;
 +A+SgGenCmp:%>a R ;
 +A+PlGenCmp:%>ij R ;
 +A:%>Q1 VUOHTA ;
 +A:%>Q1 N-ODDCASE ;
 +A:%> OTH-ODDCASEadj ;

