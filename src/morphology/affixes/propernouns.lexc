! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

! ================================ !
!! !!!Lule Sáme Proper noun morphology !
! ================================ !



! Name lexica

!! !!Even syllable
! =============

!! Words in ACCRA lexicons end on vowel, have no CG and get "even-syllable" case marking. Illative change e to i, but not o to u. Last syllable is stressed.


LEXICON ACCRA-ani !!= @CODE@ Vowel-final names where case endings are added directly, no cg. Illative e changes to i. Animales.
 +N+Prop+Sem/Ani@U.Cap.Obl@:%>@U.Cap.Obl@ ACCRADECL ;

!!€gt-norm: Tjuorri # Even-syllable test
!!€ Tjuorri               Tjuorri+N+Prop+Sem/Ani+Sg+Nom
!!€ Tjuorrij              Tjuorri+N+Prop+Sem/Ani+Sg+Ill
!!€ Tjuorris              Tjuorri+N+Prop+Sem/Ani+Sg+Ela


LEXICON ACCRA-obj !!= @CODE@ Vowel-final names where case endings are added directly, no cg. Object names
 +N+Prop+Sem/Obj:%> ACCRADECL ;

!!€gt-norm: Gestapo # Even-syllable test
!!€ Gestapo               Gestapo+N+Prop+Sem/Obj+Sg+Nom
!!€ Gestapoj              Gestapo+N+Prop+Sem/Obj+Sg+Ill
!!€ Gestapos              Gestapo+N+Prop+Sem/Obj+Sg+Ela

LEXICON ACCRA-org !!= @CODE@ Vowel-final names where caseendings are added directly, no cg. organizations
 +N+Prop+Sem/Org:%> ACCRADECL-ORG ;

!!€gt-norm: Giellatekno # Even-syllable test
!!€ Giellatekno               Giellatekno+N+Prop+Sem/Org+Sg+Nom
!!€ Giellateknoj              Giellatekno+N+Prop+Sem/Org+Sg+Ill
!!€ Giellateknos              Giellatekno+N+Prop+Sem/Org+Sg+Ela

LEXICON ACCRA-mal !!= @CODE@ Vowel-final names where case are added directly, no cg. Male names
                            +N+Prop+Sem/Mal:%>            ACCRADECL ;
 +N+Prop+Sem/Mal+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #         ; !

!!€gt-norm: Antonio # Even-syllable test
!!€ Antonio               Antonio+N+Prop+Sem/Mal+Sg+Nom
!!€ Antonioj              Antonio+N+Prop+Sem/Mal+Sg+Ill
!!€ Antonios              Antonio+N+Prop+Sem/Mal+Sg+Ela

LEXICON ACCRA-fem !!= @CODE@ Vowel-final names where case endings are added directly, no cg. Female names
                            +N+Prop+Sem/Fem:%>            ACCRADECL ;
 +N+Prop+Sem/Fem+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #         ; !

!!€gt-norm: Barbro # Even-syllable test
!!€ Barbro               Barbro+N+Prop+Sem/Fem+Sg+Nom
!!€ Barbroj              Barbro+N+Prop+Sem/Fem+Sg+Ill
!!€ Barbros              Barbro+N+Prop+Sem/Fem+Sg+Ela

LEXICON ACCRA-femsur !!= @CODE@ Vowel-final names where case endings are added directly, no cg. Female names also used as surnames
                            +N+Prop+Sem/Fem:%>            ACCRADECL ;
                            +N+Prop+Sem/Sur:%>            ACCRADECL ;
 +N+Prop+Sem/Fem+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #         ; !

!!€gt-norm: Sara # Even-syllable test
!!€ Sara               Sara+N+Prop+Sem/Fem+Sg+Nom
!!€ Saraj               Sara+N+Prop+Sem/Fem+Sg+Ill
!!€ Saraj               Sara+N+Prop+Sem/Sur+Sg+Ill
!!€ Saras               Sara+N+Prop+Sem/Fem+Sg+Ela

LEXICON ACCRA-malfem !!= @CODE@ Vowel-final names where case endings are added directly, no cg. Names that can be both female and male names
                            +N+Prop+Sem/Fem:%>            ACCRADECL ;
                            +N+Prop+Sem/Mal:%>            ACCRADECL ;
 +N+Prop+Sem/Fem+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #         ; !
 +N+Prop+Sem/Mal+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #         ; !

!!€gt-norm: Janne # Even-syllable test
!!€ Janne               Janne+N+Prop+Sem/Mal+Sg+Nom
!!€ Janne               Janne+N+Prop+Sem/Fem+Sg+Nom
!!€ Jannij              Janne+N+Prop+Sem/Mal+Sg+Ill
!!€ Jannes              Janne+N+Prop+Sem/Mal+Sg+Ela

LEXICON ACCRA-objplc !!= @CODE@ Vowel-final names where case endings are added directly, no cg. Names that can be both objects and place names
                   +N+Prop+Sem/Obj:%>            ACCRADECL     ;
                   +N+Prop+Sem/Plc:%>            ACCRADECL-PLC ;
 +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@:%>@U.Cap.Opt@ VUONAK        ;

!!€gt-norm: Soria-Moria # Even-syllable test
!!€ Soria-Moria               Soria-Moria+N+Prop+Sem/Obj+Sg+Nom
!!€ Soria-Moriaj              Soria-Moria+N+Prop+Sem/Obj+Sg+Ill
!!€ Soria-Morias              Soria-Moria+N+Prop+Sem/Obj+Sg+Ela
!!€ Soria-Moriajda              Soria-Moria+N+Prop+Sem/Plc+Sg+Ill
!!€ Soria-Moriak              Soria-Moria+N+Prop+Sem/Plc+Sg+Gen+Der/k+N+Sg+Nom
!!€ Soria-Morialasj              Soria-Moria+N+Prop+Sem/Plc+Der/lasj+N+Sg+Nom


LEXICON ACCRA-femplc !!= @CODE@ Vowel-final names where case endings are added directly, no cg. Names that can be both female and place names
                   +N+Prop+Sem/Fem:%>            ACCRADECL     ;
                   +N+Prop+Sem/Plc:%>            ACCRADECL-PLC ;
 +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@:%>@U.Cap.Opt@ VUONAK        ;

!!€gt-norm: Salla # Even-syllable test
!!€ Salla               Salla+N+Prop+Sem/Fem+Sg+Nom
!!€ Salla               Salla+N+Prop+Sem/Plc+Pl+Nom
!!€ Sallaj              Salla+N+Prop+Sem/Fem+Sg+Ill
!!€ Sallas              Salla+N+Prop+Sem/Fem+Sg+Ela
!!€ Sallak              Salla+N+Prop+Sem/Plc+Sg+Gen+Der/k+N+Sg+Nom
!!€ Sallalasj           Salla+N+Prop+Sem/Plc+Der/lasj+N+Sg+Nom

LEXICON ACCRA-sur !!= @CODE@ Vowel-final names where case endings are added directly, no cg. Surnames
                            +N+Prop+Sem/Sur:%>            ACCRADECL ;
 +N+Prop+Sem/Sur+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #         ; !

!!€gt-norm: Tønne # Even-syllable test
!!€ Tønne              Tønne+N+Prop+Sem/Sur+Sg+Nom
!!€ Tønnej              Tønne+N+Prop+Sem/Sur+Sg+Ill
!!€ Tønnes              Tønne+N+Prop+Sem/Sur+Sg+Ela

LEXICON ACCRA-malsur !!= @CODE@  Vowel-final names where case endings are added directly, no cg. Names that can be both male- and surnames
                            +N+Prop+Sem/Sur:%>            ACCRADECL ;
 +N+Prop+Sem/Sur+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #         ; !
                            +N+Prop+Sem/Mal:%>            ACCRADECL ;
 +N+Prop+Sem/Mal+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #         ; !

!!€gt-norm: Valio # Even-syllable test
!!€ Valio               Valio+N+Prop+Sem/Sur+Sg+Nom
!!€ Valio               Valio+N+Prop+Sem/Mal+Sg+Nom
!!€ Valioj              Valio+N+Prop+Sem/Sur+Sg+Ill
!!€ Valios              Valio+N+Prop+Sem/Sur+Sg+Ela

LEXICON ACCRA-plc !!= @CODE@ Vowel-final names where caseendings are added directly, no cg. Place names
                   +N+Prop+Sem/Plc:%>            ACCRADECL-PLC ;
 +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@:%>@U.Cap.Opt@ VUONAK        ;

!!€gt-norm: Burma # Even-syllable test
!!€ Burma               Burma+N+Prop+Sem/Plc+Sg+Nom
!!€ Burmaj              Burma+N+Prop+Sem/Plc+Sg+Ill
!!€ Burmas              Burma+N+Prop+Sem/Plc+Sg+Ela
!!€ Burmak              Burma+N+Prop+Sem/Plc+Sg+Gen+Der/k+N+Sg+Nom
!!€ Burmalasj           Burma+N+Prop+Sem/Plc+Der/lasj+N+Sg+Nom

LEXICON ACCRA-surplc !!= @CODE@ Vowel-final names where case endings are added directly, no cg. Names that are both surnames and place names
                            +N+Prop+Sem/Sur:%>            ACCRADECL     ;
 +N+Prop+Sem/Sur+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #             ; !
                            +N+Prop+Sem/Plc:%>            ACCRADECL-PLC ;
          +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@:%>@U.Cap.Opt@ VUONAK        ;

!!€gt-norm: Veitsiluoto # Even-syllable test
!!€ Veitsiluoto               Veitsiluoto+N+Prop+Sem/Sur+Sg+Nom
!!€ Veitsiluoto               Veitsiluoto+N+Prop+Sem/Plc+Sg+Nom
!!€ Veitsiluotoj              Veitsiluoto+N+Prop+Sem/Sur+Sg+Ill
!!€ Veitsiluotos              Veitsiluoto+N+Prop+Sem/Sur+Sg+Ela
!!€ Veitsiluotok              Veitsiluoto+N+Prop+Sem/Plc+Sg+Gen+Der/k+N+Sg+Nom
!!€ Veitsiluotolasj           Veitsiluoto+N+Prop+Sem/Plc+Der/lasj+N+Sg+Nom

LEXICON ACCRA-LOAN-org !!= @CODE@ Only nominatives. Vowel-final names where case endings are added directly, no cg. organizations
 +N+Prop+Sem/Org:%> ACCRADECL-LOAN ;

!!€gt-norm: Samenes% Idrettsforbund-Norge # Even-syllable test
!!€ Samenes% Idrettsforbund-Norge              Samenes% Idrettsforbund-Norge+N+Prop+Sem/Org+Sg+Nom
!!$ Samenes% Idrettsforbund-Norge              Samenes% Idrettsforbund-Norge+N+Prop+Sem/Org+Sg+Ela # negative test

LEXICON ACCRA-LOAN-obj !!= @CODE@ Only nominatives. Vowel-final names where case endings are added directly, no cg. Object names
 +N+Prop+Sem/Obj:%> ACCRADECL-LOAN ;

!!€gt-norm: The Norwegian Sami Experience # Even-syllable test
!!€ The Norwegian Sami Experience        The Norwegian Sami Experience+N+Prop+Sem/Obj+Sg+Nom
!!$ The Norwegian Sami Experience        The Norwegian Sami Experience+N+Prop+Sem/Obj+Sg+Ill # Negative test

LEXICON ACCRA-LOAN-plc !!= @CODE@ Only nominatives. Vowel-final names where case endings are added directly, no cg.Place names
 +N+Prop+Sem/Plc:%> ACCRADECL-LOAN ;
! +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@+Err/Sub:%>@U.Cap.Opt@ VUONAK ; !

!!€gt-norm: Kautokeino # Even-syllable test
!!€ Kautokeino              Kautokeino+N+Prop+Sem/Org+Sg+Nom
!!$ Kautokeinos              Kautokeino+N+Prop+Sem/Org+Sg+Ela # negative test


LEXICON ACCRA ! Vowel-final names without Gradation.. Not in use?
 +N+Prop:%> ACCRADECL ;

!LEXICON ACCRA-LOAN-surplc ! Names that are both surnames and place names
! +N+Prop+Sem/Sur:%> ACCRADECL-LOAN ;
! +N+Prop+Sem/Sur+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ # ; !
! +N+Prop+Sem/Plc:%> ACCRADECL-LOAN ;
! +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@+Err/Sub:%>@U.Cap.Opt@ VUONAK ; !

LEXICON ACCRADECL
! These sublexica are irrelevant for ACCRA, but added
! for the sake of the lexicon MARJA
 @U.Cap.Obl@ ACCRA-DC ;

LEXICON ACCRADECL-ORG
! These sublexica are irrelevant for ACCRA, but added
! for the sake of the lexicon MARJA
 @U.Cap.Obl@ ACCRA-DC-ORG ;

LEXICON ACCRADECL-PLC
! These sublexica are irrelevant for ACCRA, but added
! for the sake of the lexicon MARJA
 @U.Cap.Obl@ ACCRA-DC ;
 @U.Cap.Opt@ LASJ     ;

LEXICON ACCRADECL-LOAN
! These sublexica are irrelevant for ACCRA, but added
! for the sake of the lexicon MARJA
 @U.Cap.Obl@ ACCRA-DC-LOAN ;
 @U.Cap.Opt@ LASJ          ;


LEXICON ACCRA-DC
         +Sg+Nom:      K     ;
         +Sg+Nom:      RHyph ;
            +Ess:n     K     ;
         +Sg+Ill:Q4j   K     ; !Q4 to get e:i
         +Sg+Gen:      K     ;
         +Sg+Gen:      RHyph ;
         +Sg+Acc:v     K     ;
         +Sg+Ine:n     K     ;
         +Sg+Ela:s     K     ;
         +Pl+Nom:      K     ;
         +Pl+Gen:Q4j   K     ; !Q4 to get e:i
         +Pl+Gen:Q4j   K     ; !Q4 to get e:i
         +Pl+Acc:Q4jt  K     ;
         +Pl+Ill:Q4jda K     ;
         +Pl+Ine:Q4jn  K     ;
         +Pl+Ela:Q4js  K     ;
         +Pl+Com:Q4j   K     ; !Q4 to get e:i
         +Sg+Com:Q4jn  K     ;
 +Sg+Com+Err/Sub:X5jn  K     ; ! Norgejn

LEXICON ACCRA-DC-ORG
         +Sg+Nom:     K     ;
         +Sg+Nom:     RHyph ;
            +Ess:n    K     ;
         +Sg+Ill:Q4j  K     ; !Q4 to get e:i
         +Sg+Gen:     K     ;
         +Sg+Gen:     RHyph ;
         +Sg+Acc:v    K     ;
         +Sg+Ine:n    K     ;
         +Sg+Ela:s    K     ;
! +Pl+Nom: K ;
! +Pl+Gen:Q4j K ; !Q4 to get e:i
! +Pl+Gen:Q4j K ; !Q4 to get e:i
! +Pl+Acc:Q4jt K ;
! +Pl+Ill:Q4jda K ;
! +Pl+Ine:Q4jn K ;
! +Pl+Ela:Q4js K ;
! +Pl+Com:Q4j K ; !Q4 to get e:i
         +Sg+Com:Q4jn K     ;
 +Sg+Com+Err/Sub:X5jn K     ; ! Norgejn


LEXICON ACCRA-DC-LOAN
         +Sg+Nom:     K     ;
         +Sg+Nom:     RHyph ;
    +Ess+Err/Sub:n    K     ; !
 +Sg+Ill+Err/Sub:Q4j  K     ; ! !Q4 to get e:i
 +Sg+Gen+Err/Sub:     K     ; !
 +Sg+Gen+Err/Sub:     RHyph ; !
 +Sg+Acc+Err/Sub:v    K     ; !
 +Sg+Ine+Err/Sub:n    K     ; !
 +Sg+Ela+Err/Sub:s    K     ; !
! +Pl+Nom+Err/Sub: K ; !
! +Pl+Gen+Err/Sub:Q4j K ; ! !Q4 to get e:i
! +Pl+Gen+Err/Sub:Q4j K ; ! !Q4 to get e:i
! +Pl+Acc+Err/Sub:Q4jt K ; !
! +Pl+Ill+Err/Sub:Q4jda K ; !
! +Pl+Ine+Err/Sub:Q4jn K ; !
! +Pl+Ela+Err/Sub:Q4js K ; !
! +Pl+Com+Err/Sub:Q4j K ; ! !Q4 to get e:i
 +Sg+Com+Err/Sub:Q4jn K     ; !
 +Sg+Com+Err/Sub:X5jn K     ; ! !SUB Norgejn


!! In smj RONDANE is same as ACCRA, in use in smi because of diffrences in sme. No -lasj or -k.


LEXICON RONDANE-plc !!= @CODE@ E-final names, with no cg. elative -s, ill -ij. Place names
 +N+Prop+Sem/Plc:e%> RONDANEDECL ;


!!€gt-norm: Bakkane # Even-syllable test
!!€ Bakkane               Bakkane+N+Prop+Sem/Plc+Sg+Nom
!!€ Bakkane               Bakkane+N+Prop+Sem/Plc+Sg+Gen
!!€ Bakkanev               Bakkane+N+Prop+Sem/Plc+Sg+Acc
!!€ Bakkanen               Bakkane+N+Prop+Sem/Plc+Sg+Ine
!!€ Bakkanij              Bakkane+N+Prop+Sem/Plc+Sg+Ill
!!€ Bakkanes              Bakkane+N+Prop+Sem/Plc+Sg+Ela
!!€ Bakkanijn               Bakkane+N+Prop+Sem/Plc+Sg+Com
!!€ Bakkanijda              Bakkane+N+Prop+Sem/Plc+Pl+Ill
!!€ Bakkanijs              Bakkane+N+Prop+Sem/Plc+Pl+Ela
!!$ Bakkanek              Bakkane+N+Prop+Sem/Plc+Sg+Gen+Der/k+N+Sg+Nom
!!$ Bakkanelasj           Bakkane+N+Prop+Sem/Plc+Der/lasj+N+Sg+Nom

LEXICON RONDANE-SG-plc !!= @CODE@ E-final names, with no cg. elative -s, ill -ij. Place names
 +N+Prop+Sem/Plc:e%> RONDANEDECL ;

!!€gt-norm: Bakkane # Even-syllable test
!!€ Bakkane               Bakkane+N+Prop+Sem/Plc+Sg+Nom
!!€ Bakkane               Bakkane+N+Prop+Sem/Plc+Sg+Gen
!!€ Bakkanev               Bakkane+N+Prop+Sem/Plc+Sg+Acc



LEXICON RONDANE-LOAN !!= @CODE@ Only nominative.Place names
 +N+Prop+Sem/Plc:e%> RONDANEDECL-LOAN ;

!!€gt-norm: Azorene # Even-syllable test
!!€ Azorene               Azorene+N+Prop+Sem/Plc+Sg+Nom
!!$ Azorenij              Azorene+N+Prop+Sem/Plc+Sg+Ill # Negative test


LEXICON RONDANE-SG-LOAN !!= @CODE@ Only nominative. Place names
 +N+Prop+Sem/Plc:e%> RONDANEDECL-LOAN ;


LEXICON RONDANE-sur !!= @CODE@ Surnames
 +N+Prop+Sem/Sur:e%> RONDANEDECL ;

!!€gt-norm: Benneche # Even-syllable test
!!€ Benneche               Benneche+N+Prop+Sem/Sur+Sg+Nom
!!€ Bennechij              Benneche+N+Prop+Sem/Sur+Sg+Ill
!!€ Benneches              Benneche+N+Prop+Sem/Sur+Sg+Ela

LEXICON RONDANE-obj !!= @CODE@ Objects
 +N+Prop+Sem/Obj:e%> RONDANEDECL ;

!!€gt-norm: Office # Even-syllable test
!!€ Office              Office+N+Prop+Sem/Obj+Sg+Nom
!!€ Offica              Office+N+Prop+Sem/Obj+Sg+Gen
!!€ Officav             Office+N+Prop+Sem/Obj+Sg+Acc
!!€ Officin             Office+N+Prop+Sem/Obj+Sg+Ine
!!€ Officij             Office+N+Prop+Sem/Obj+Sg+Ill
!!€ Officis             Office+N+Prop+Sem/Obj+Sg+Ela
!!€ Officijn            Office+N+Prop+Sem/Obj+Sg+Com

LEXICON RONDANE-org !!= @CODE@ Organizations
 +N+Prop+Sem/Org:e%> RONDANEDECL ;

!!€gt-norm: Picture # Even-syllable test
!!€ Picture               Picture+N+Prop+Sem/Org+Sg+Nom
!!€ Picturij              Picture+N+Prop+Sem/Org+Sg+Ill
!!€ Pictures              Picture+N+Prop+Sem/Org+Sg+Ela

LEXICON RONDANE-mal !!= @CODE@ Male names
 +N+Prop+Sem/Mal:e%> RONDANEDECL ;

!!€gt-norm: Lawrence # Even-syllable test
!!€ Lawrence               Lawrence+N+Prop+Sem/Mal+Sg+Nom
!!€ Lawrencij              Lawrence+N+Prop+Sem/Mal+Sg+Ill
!!€ Lawrences              Lawrence+N+Prop+Sem/Mal+Sg+Ela

LEXICON RONDANE-fem !!= @CODE@ Female names
 +N+Prop+Sem/Fem:e%> RONDANEDECL ;

!!€gt-norm: Jannike # Even-syllable test
!!€ Jannike               Jannike+N+Prop+Sem/Fem+Sg+Nom
!!€ Jannikij              Jannike+N+Prop+Sem/Fem+Sg+Ill
!!€ Jannikes              Jannike+N+Prop+Sem/Fem+Sg+Ela

LEXICON RONDANEDECL
! These sublexica are irrelevant for ACCRA, but added
! for the sake of the lexicon MARJA
 @U.Cap.Obl@ RONDANE-DC ;

LEXICON RONDANEDECL-ORG
! These sublexica are irrelevant for ACCRA, but added
! for the sake of the lexicon MARJA
 @U.Cap.Obl@ RONDANE-DC-ORG ;

LEXICON RONDANEDECL-LOAN
! These sublexica are irrelevant for ACCRA, but added
! for the sake of the lexicon MARJA
 @U.Cap.Obl@ RONDANE-DC-LOAN ;


LEXICON RONDANE-DC
         +Sg+Nom:      K     ;
         +Sg+Nom:      RHyph ;
            +Ess:n     K     ;
         +Sg+Ill:Q4j   K     ; !Q4 to get e:i
         +Sg+Gen:      K     ;
         +Sg+Gen:      RHyph ;
         +Sg+Acc:v     K     ;
         +Sg+Ine:n     K     ;
         +Sg+Ela:s     K     ;
         +Pl+Nom:      K     ;
         +Pl+Gen:Q4j   K     ; !Q4 to get e:i
         +Pl+Gen:Q4j   K     ; !Q4 to get e:i
         +Pl+Acc:Q4jt  K     ;
         +Pl+Ill:Q4jda K     ;
         +Pl+Ine:Q4jn  K     ;
         +Pl+Ela:Q4js  K     ;
         +Pl+Com:Q4j   K     ; !Q4 to get e:i
         +Sg+Com:Q4jn  K     ;
 +Sg+Com+Err/Sub:X5jn  K     ; ! Norgejn

LEXICON RONDANE-DC-ORG
         +Sg+Nom:     K     ;
         +Sg+Nom:     RHyph ;
            +Ess:n    K     ;
         +Sg+Ill:Q4j  K     ; !Q4 to get e:i
         +Sg+Gen:     K     ;
         +Sg+Gen:     RHyph ;
         +Sg+Acc:v    K     ;
         +Sg+Ine:n    K     ;
         +Sg+Ela:s    K     ;
! +Pl+Nom: K ;
! +Pl+Gen:Q4j K ; !Q4 to get e:i
! +Pl+Gen:Q4j K ; !Q4 to get e:i
! +Pl+Acc:Q4jt K ;
! +Pl+Ill:Q4jda K ;
! +Pl+Ine:Q4jn K ;
! +Pl+Ela:Q4js K ;
! +Pl+Com:Q4j K ; !Q4 to get e:i
         +Sg+Com:Q4jn K     ;
 +Sg+Com+Err/Sub:X5jn K     ; ! Norgejn


LEXICON RONDANE-DC-LOAN
         +Sg+Nom:     K     ;
         +Sg+Nom:     RHyph ;
    +Ess+Err/Sub:n    K     ; !
 +Sg+Ill+Err/Sub:Q4j  K     ; ! !Q4 to get e:i
 +Sg+Gen+Err/Sub:     K     ; !
 +Sg+Gen+Err/Sub:     RHyph ; !
 +Sg+Acc+Err/Sub:v    K     ; !
 +Sg+Ine+Err/Sub:n    K     ; !
 +Sg+Ela+Err/Sub:s    K     ; !
! +Pl+Nom+Err/Sub: K ; !
! +Pl+Gen+Err/Sub:Q4j K ; ! !Q4 to get e:i
! +Pl+Gen+Err/Sub:Q4j K ; ! !Q4 to get e:i
! +Pl+Acc+Err/Sub:Q4jt K ; !
! +Pl+Ill+Err/Sub:Q4jda K ; !
! +Pl+Ine+Err/Sub:Q4jn K ; !
! +Pl+Ela+Err/Sub:Q4js K ; !
! +Pl+Com+Err/Sub:Q4j K ; ! !Q4 to get e:i
 +Sg+Com+Err/Sub:Q4jn K     ; !
 +Sg+Com+Err/Sub:X5jn K     ; ! !SUB Norgejn

! sulexica

!! Words in DUBAI lexicon end on vowel+vowel, no cg, gets odd-syllable case marking with -aj, -as. Last syllable is stressed.

LEXICON DUBAI-fem !!= @CODE@ I-final names. No cg. Female names
! Different lexicon for female persons. Audhild.
                            +N+Prop+Sem/Fem:%>             DUBAIDECL ;
 +N+Prop+Sem/Fem+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@i #         ; !

!!€gt-norm: Mai  # Even-syllable test
!!€ Mai                Mai+N+Prop+Sem/Fem+Sg+Nom
!!€ Maiaj              Mai+N+Prop+Sem/Fem+Sg+Ill
!!€ Maias              Mai+N+Prop+Sem/Fem+Sg+Ela

LEXICON DUBAI-obj !!= @CODE@ I-final names. No cg. Object names
 +N+Prop+Sem/Obj:%> DUBAIDECL ;

!!€gt-norm: Hyundai  # Even-syllable test
!!€ Hyundai                Hyundai+N+Prop+Sem/Obj+Sg+Nom
!!€ Hyundaiaj              Hyundai+N+Prop+Sem/Obj+Sg+Ill
!!€ Hyundaias              Hyundai+N+Prop+Sem/Obj+Sg+Ela

LEXICON DUBAI-org !!= @CODE@ Organizations
! Different lexicon for organisations. Microsoft.
 +N+Prop+Sem/Org:%> DUBAIDECL-ORG ;

!!€gt-norm: Khoi # Even-syllable test
!!€ Khoi               Khoi+N+Prop+Sem/Org+Sg+Nom
!!€ Khoiaj              Khoi+N+Prop+Sem/Org+Sg+Ill
!!€ Khoias              Khoi+N+Prop+Sem/Org+Sg+Ela

LEXICON DUBAI-mal !!= @CODE@ Male names
! Different lexicon for male persons. Finn. Torfinn.
                            +N+Prop+Sem/Mal:%>             DUBAIDECL ;
 +N+Prop+Sem/Mal+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@i #         ; !

!!€gt-norm: Kublai # Even-syllable test
!!€ Kublai               Kublai+N+Prop+Sem/Mal+Sg+Nom
!!€ Kublaiaj              Kublai+N+Prop+Sem/Mal+Sg+Ill
!!€ Kublaias              Kublai+N+Prop+Sem/Mal+Sg+Ela

LEXICON DUBAI-sur !!= @CODE@ Surnames
! Different lexicon for personal surnames. Blind
                            +N+Prop+Sem/Sur:%>             DUBAIDECL ;
 +N+Prop+Sem/Sur+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@i #         ; !

!!€gt-norm: Maarthai # Even-syllable test
!!€ Maarthai               Maarthai+N+Prop+Sem/Sur+Sg+Nom
!!€ Maarthaiaj              Maarthai+N+Prop+Sem/Sur+Sg+Ill
!!€ Maarthaias              Maarthai+N+Prop+Sem/Sur+Sg+Ela

LEXICON DUBAI-plc !!= @CODE@ Place names
! Different lexicon for place names. Bern.
                   +N+Prop+Sem/Plc:%>              DUBAIDECL-PLC ;
 +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@:i%>a@U.Cap.Opt@ VUONAK        ;

!!€gt-norm: Madurai # Even-syllable test
!!€ Madurai                 Madurai+N+Prop+Sem/Plc+Sg+Nom
!!€ Maduraiaj              Madurai+N+Prop+Sem/Plc+Sg+Ill
!!€ Maduraias              Madurai+N+Prop+Sem/Plc+Sg+Ela
!!€ Maduraiak              Madurai+N+Prop+Sem/Plc+Sg+Gen+Der/k+N+Sg+Nom
!!€ Maduraialasj           Madurai+N+Prop+Sem/Plc+Der/lasj+N+Sg+Nom

LEXICON DUBAIDECL
 @U.Cap.Obl@ DUBAIUCASE ;

LEXICON DUBAIDECL-PLC
         @U.Cap.Obl@               DUBAIUCASE ;
 +Err/Sub@U.Cap.Opt@:ia@U.Cap.Opt@ LASJ       ;

LEXICON DUBAIDECL-ORG
 @U.Cap.Obl@ DUBAIUCASE-ORG ;


LEXICON DUBAIUCASE ! Final foot structure (X) => Loc:as
 +Sg+Nom:i     K     ;
 +Sg+Nom:i     RHyph ;
 +Sg+Ill:iaj   K     ;
    +Ess:ian   K     ;
 +Sg+Gen:ia    K     ;
 +Sg+Gen:ia    K     ;
 +Sg+Gen:ia    RHyph ;
 +Sg+Acc:iav   K     ;
 +Sg+Ine:ian   K     ;
 +Sg+Ela:ias   K     ;
 +Pl+Nom:ia    K     ;
 +Pl+Gen:iaj   K     ;
 +Pl+Gen:iaj   RHyph ;
 +Pl+Acc:iajt  K     ;
 +Pl+Ill:iajda K     ;
 +Pl+Ine:iajn  K     ;
 +Pl+Ela:iajs  K     ;
 +Pl+Com:iaj   K     ;
 +Sg+Com:iajn  K     ;


LEXICON DUBAIUCASE-ORG ! Final foot structure (X) => Loc:as
 +Sg+Nom:i    K     ;
 +Sg+Nom:i    RHyph ;
 +Sg+Ill:iaj  K     ;
    +Ess:ian  K     ;
 +Sg+Gen:ia   K     ;
 +Sg+Gen:ia   K     ;
 +Sg+Gen:ia   RHyph ;
 +Sg+Acc:iav  K     ;
 +Sg+Ine:ian  K     ;
 +Sg+Ela:ias  K     ;
! +Pl+Nom:ija K ;
! +Pl+Gen:ijaj K ;
! +Pl+Gen:ijaj RHyph ;
! +Pl+Acc:ijajt K ;
! +Pl+Ill:ijajda K ;
! +Pl+Ine:ijajn K ;
! +Pl+Ela:ijajs K ;
! +Pl+Com:ijaj K ;
 +Sg+Com:iajn K     ;


!! Words in PIPPI lexicons end on vowel i, have no CG and get "odd-syllable" case marking, with Pippihav as acc. Same as "riebij" but without the -j in nominative, it should maybe be Sirij and Pippij in nom? The last syllable is unstressed.


LEXICON PIPPI-ani !!= @CODE@ IVowel-final names where case endings are added directly, no cg. Animales.
 +N+Prop+Sem/Ani:%> PIPPIDECL ;

!!€gt-norm: Rullahuuli # Even-syllable test
!!€ Rullahuuli               Rullahuuli+N+Prop+Sem/Ani+Sg+Nom
!!€ Rullahuulihij              Rullahuuli+N+Prop+Sem/Ani+Sg+Ill
!!€ Rullahuulihis              Rullahuuli+N+Prop+Sem/Ani+Sg+Ela


LEXICON PIPPI-obj !!= @CODE@ Vowel-final names where case endings are added directly, no cg. Object names
 +N+Prop+Sem/Obj:%> PIPPIDECL ;

!!€gt-norm: Tunturihotelli # Even-syllable test
!!€ Suzuki                 Suzuki+N+Prop+Sem/Obj+Sg+Nom
!!€ Suzukihij              Suzuki+N+Prop+Sem/Obj+Sg+Ill
!!€ Suzukihis              Suzuki+N+Prop+Sem/Obj+Sg+Ela

LEXICON PIPPI-org !!= @CODE@ Vowel-final names where caseendings are added directly, no cg. organizations
 +N+Prop+Sem/Org:%> PIPPIDECL-ORG ;

!!€gt-norm: Kon-Tiki # Even-syllable test
!!€ Kon-Tiki               Kon-Tiki+N+Prop+Sem/Org+Sg+Nom
!!€ Kon-Tikihij              Kon-Tiki+N+Prop+Sem/Org+Sg+Ill
!!€ Kon-Tikihis              Kon-Tiki+N+Prop+Sem/Org+Sg+Ela

LEXICON PIPPI-mal !!= @CODE@ Vowel-final names where case are added directly, no cg. Male names
                            +N+Prop+Sem/Mal:%>            PIPPIDECL ;
 +N+Prop+Sem/Mal+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #         ; !

!!€gt-norm: Gianni # Even-syllable test
!!€ Gianni               Gianni+N+Prop+Sem/Mal+Sg+Nom
!!€ Giannihij              Gianni+N+Prop+Sem/Mal+Sg+Ill
!!€ Giannihis              Gianni+N+Prop+Sem/Mal+Sg+Ela

LEXICON PIPPI-fem !!= @CODE@ Vowel-final names where case endings are added directly, no cg. Female names
                            +N+Prop+Sem/Fem:%>            PIPPIDECL ;
 +N+Prop+Sem/Fem+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #         ; !

!!€gt-norm: Guri # Even-syllable test
!!€ Guri               Guri+N+Prop+Sem/Fem+Sg+Nom
!!€ Gurihij              Guri+N+Prop+Sem/Fem+Sg+Ill
!!€ Gurihis              Guri+N+Prop+Sem/Fem+Sg+Ela

LEXICON PIPPI-femsur !!= @CODE@ Vowel-final names where case endings are added directly, no cg. Female names also used as surnames
                            +N+Prop+Sem/Fem:%>            PIPPIDECL ;
                            +N+Prop+Sem/Sur:%>            PIPPIDECL ;
 +N+Prop+Sem/Fem+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #         ; !

!!€gt-norm: Turi # Even-syllable test
!!€ Turi               Turi+N+Prop+Sem/Fem+Sg+Nom
!!€ Turihij              Turi+N+Prop+Sem/Fem+Sg+Ill
!!€ Turihij              Turi+N+Prop+Sem/Sur+Sg+Ill
!!€ Turihis              Turi+N+Prop+Sem/Fem+Sg+Ela

LEXICON PIPPI-malfem !!= @CODE@ Vowel-final names where case endings are added directly, no cg. Names that can be both female and male names
                            +N+Prop+Sem/Fem:%>            PIPPIDECL ;
                            +N+Prop+Sem/Mal:%>            PIPPIDECL ;
 +N+Prop+Sem/Fem+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #         ; !
 +N+Prop+Sem/Mal+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #         ; !

!!€gt-norm: Kari # Even-syllable test
!!€ Kari               Kari+N+Prop+Sem/Mal+Sg+Nom
!!€ Kari               Kari+N+Prop+Sem/Fem+Sg+Nom
!!€ Karihij              Kari+N+Prop+Sem/Mal+Sg+Ill
!!€ Karihis              Kari+N+Prop+Sem/Mal+Sg+Ela

LEXICON PIPPI-objplc !!= @CODE@ Vowel-final names where case endings are added directly, no cg. Names that can be both objects and place names
                   +N+Prop+Sem/Obj:%>            PIPPIDECL     ;
                   +N+Prop+Sem/Plc:%>            PIPPIDECL-PLC ;
 +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@:%>@U.Cap.Opt@ VUONAK        ;

!!€gt-norm:  # Even-syllable test


LEXICON PIPPI-femplc !!= @CODE@ Vowel-final names where case endings are added directly, no cg. Names that can be both female and place names
                   +N+Prop+Sem/Fem:%>            PIPPIDECL     ;
                   +N+Prop+Sem/Plc:%>            PIPPIDECL-PLC ;
 +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@:%>@U.Cap.Opt@ VUONAK        ;

!!€gt-norm:  # Even-syllable test


LEXICON PIPPI-sur !!= @CODE@ Vowel-final names where case endings are added directly, no cg. Surnames
                            +N+Prop+Sem/Sur:%>            PIPPIDECL ;
 +N+Prop+Sem/Sur+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #         ; !

!!€gt-norm: Sammallahti # Even-syllable test
!!€ Sammallahti               Sammallahti+N+Prop+Sem/Sur+Sg+Nom
!!€ Sammallahtihij              Sammallahti+N+Prop+Sem/Sur+Sg+Ill
!!€ Sammallahtihis              Sammallahti+N+Prop+Sem/Sur+Sg+Ela

LEXICON PIPPI-malsur !!= @CODE@  Vowel-final names where case endings are added directly, no cg. Names that can be both male- and surnames
                            +N+Prop+Sem/Sur:%>            PIPPIDECL ;
 +N+Prop+Sem/Sur+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #         ; !
                            +N+Prop+Sem/Mal:%>            PIPPIDECL ;
 +N+Prop+Sem/Mal+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #         ; !

!!€gt-norm:  # Even-syllable test

LEXICON PIPPI-plc !!= @CODE@ Vowel-final names where caseendings are added directly, no cg. Place names
                   +N+Prop+Sem/Plc:%>            PIPPIDECL-PLC ;
 +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@:%>@U.Cap.Opt@ VUONAK        ;

!!€gt-norm: Lapinlampi # Even-syllable test
!!€ Lapinlampi                 Lapinlampi+N+Prop+Sem/Plc+Sg+Nom
!!€ Lapinlampihij              Lapinlampi+N+Prop+Sem/Plc+Sg+Ill
!!€ Lapinlampihis              Lapinlampi+N+Prop+Sem/Plc+Sg+Ela



LEXICON PIPPI-surplc !!= @CODE@ Vowel-final names where case endings are added directly, no cg. Names that are both surnames and place names
                            +N+Prop+Sem/Sur:%>            PIPPIDECL     ;
 +N+Prop+Sem/Sur+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #             ; !
                            +N+Prop+Sem/Plc:%>            PIPPIDECL-PLC ;
          +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@:%>@U.Cap.Opt@ VUONAK        ;

!!€gt-norm:  # Even-syllable test

LEXICON PIPPI-LOAN-plc !!= @CODE@ Only nominatives. Vowel-final names where case endings are added directly, no cg.Place names
                           +N+Prop+Sem/Plc:%>            PIPPIDECL-LOAN ;
 +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@+Err/Sub:%>@U.Cap.Opt@ VUONAK         ; ! Galggá gus dát?

!!€gt-norm: Haltiatunturi # Even-syllable test
!!€ Haltiatunturi               Haltiatunturi+N+Prop+Sem/Plc+Sg+Nom
!!$ Haltiatunturijij              Haltiatunturi+N+Prop+Sem/Plc+Sg+Ill
!!$ Haltiatunturijis              Haltiatunturi+N+Prop+Sem/Plc+Sg+Ela

LEXICON PIPPI-LOAN-org !!= @CODE@ Only nominatives. Vowel-final names where case endings are added directly, no cg. organizations
 +N+Prop+Sem/Org:%> PIPPIDECL-LOAN ;

!!€gt-norm:  # Even-syllable test

LEXICON PIPPI-LOAN-obj !!= @CODE@ Only nominatives. Vowel-final names where case endings are added directly, no cg. Object names
 +N+Prop+Sem/Obj:%> PIPPIDECL-LOAN ;

!!€gt-norm:  # Even-syllable test


!LEXICON PIPPI-LOAN-surplc ! Names that are both surnames and place names
! +N+Prop+Sem/Sur:%> PIPPIDECL-LOAN ;
! +N+Prop+Sem/Sur+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ # ; !
! +N+Prop+Sem/Plc:%> PIPPIDECL-LOAN ;
! +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@+Err/Sub:%>@U.Cap.Opt@ VUONAK ; !


LEXICON PIPPIDECL
! These sublexica are irrelevant for PIPPI, but added
! for the sake of the lexicon MARJA
 @U.Cap.Obl@ PIPPI-DC ;

LEXICON PIPPIDECL-ORG
! These sublexica are irrelevant for PIPPI, but added
! for the sake of the lexicon MARJA
 @U.Cap.Obl@ PIPPI-DC-ORG ;

LEXICON PIPPIDECL-PLC
! These sublexica are irrelevant for PIPPI, but added
! for the sake of the lexicon MARJA
 @U.Cap.Obl@ PIPPI-DC ;
 @U.Cap.Opt@ LASJ     ; ! XXX +Err/Sub ?

LEXICON PIPPIDECL-LOAN
! These sublexica are irrelevant for PIPPI, but added
! for the sake of the lexicon MARJA
 @U.Cap.Obl@ PIPPI-DC-LOAN ;
 !@U.Cap.Opt@+Err/Sub:@U.Cap.Opt@ LASJ          ; !


LEXICON PIPPI-DC
         +Sg+Nom:      K     ;
         +Sg+Nom:      RHyph ;
            +Ess:hin   K     ;
         +Sg+Ill:hij   K     ;
         +Sg+Gen:ha    K     ;
         +Sg+Gen:ha    RHyph ;
         +Sg+Acc:hav   K     ;
         +Sg+Ine:hin   K     ;
         +Sg+Ela:his   K     ;
         +Pl+Nom:ha    K     ;
         +Pl+Gen:hij   K     ;
         +Pl+Gen:hij   K     ;
         +Pl+Acc:hijt  K     ;
         +Pl+Ill:hijda K     ;
         +Pl+Ine:hijn  K     ;
         +Pl+Ela:hijs  K     ;
         +Pl+Com:hij   K     ;
         +Sg+Com:hijn  K     ;
 +Sg+Com+Err/Sub:hijn  K     ;

LEXICON PIPPI-DC-ORG
         +Sg+Nom:     K     ;
         +Sg+Nom:     RHyph ;
            +Ess:hin  K     ;
         +Sg+Ill:hij  K     ;
         +Sg+Gen:ha   K     ;
         +Sg+Gen:ha   RHyph ;
         +Sg+Acc:hav  K     ;
         +Sg+Ine:hin  K     ;
         +Sg+Ela:his  K     ;
! +Pl+Nom: K ;
! +Pl+Gen:Q4j K ; !Q4 to get e:i
! +Pl+Gen:Q4j K ; !Q4 to get e:i
! +Pl+Acc:Q4jt K ;
! +Pl+Ill:Q4jda K ;
! +Pl+Ine:Q4jn K ;
! +Pl+Ela:Q4js K ;
! +Pl+Com:Q4j K ; !Q4 to get e:i
         +Sg+Com:jijn K     ;
 +Sg+Com+Err/Sub:jijn K     ;


LEXICON PIPPI-DC-LOAN
         +Sg+Nom:     K             ;
         +Sg+Nom:     RHyph         ;
    +Ess+Err/Sub:hin  K             ; !
 +Sg+Ill+Err/Sub:hij  K             ; !
 +Sg+Gen+Err/Sub:ha   K             ; !
 +Sg+Gen+Err/Sub:ha   RHyph         ; !
 +Sg+Acc+Err/Sub:hav  K             ; !
 +Sg+Ine+Err/Sub:hin  K             ; !
 +Sg+Ela+Err/Sub:his  K             ; !
! +Pl+Nom+Err/Sub: K ; !
! +Pl+Gen+Err/Sub:Q4j K ; ! !Q4 to get e:i
! +Pl+Gen+Err/Sub:Q4j K ; ! !Q4 to get e:i
! +Pl+Acc+Err/Sub:Q4jt K ; !
! +Pl+Ill+Err/Sub:Q4jda K ; !
! +Pl+Ine+Err/Sub:Q4jn K ; !
! +Pl+Ela+Err/Sub:Q4js K ; !
! +Pl+Com+Err/Sub:Q4j K ; ! !Q4 to get e:i
 +Sg+Com+Err/Sub:hijn K             ; !
 +Sg+Com+Err/Sub:hijn K             ; !

 LEXICON GATA-plc  !!= @CODE@ Norwegian place names, that end on -gata. Gets even-syllable casemarking.
     +N+Prop+Sem/Plc:gata ACCRADECL-PLC ;

!!€gt-norm: Munkegata # Even-syllable test
!!€ Munkegata               Munkegata+N+Prop+Sem/Plc+Sg+Nom
!!€ Munkegataj              Munkegata+N+Prop+Sem/Plc+Sg+Ill
!!€ Munkegatas              Munkegata+N+Prop+Sem/Plc+Sg+Ela

LEXICON BETFAGE-plc  !!= @CODE@ Place names Like ACCRA but with sub: -ej, and NORM: -ij. Only singular.
 +N+Prop+Sem/Plc:e%> BETFAGEDECL-PLC ;

!!€gt-norm: Betfage # Even-syllable test
!!€ Betfage               Betfage+N+Prop+Sem/Plc+Sg+Nom
!!€ Betfagej              Betfage+N+Prop+Sem/Plc+Sg+Ill
!!€ Betfagij              Betfage+N+Prop+Sem/Plc+Sg+Ill
!!€ Betfages              Betfage+N+Prop+Sem/Plc+Sg+Ela

LEXICON BETFAGE-fem  !!= @CODE@ Female names
                            +N+Prop+Sem/Fem:e%>            BETFAGEDECL ;
 +N+Prop+Sem/Fem+Sg+Attr@U.Cap.Obl@+Err/Sub:e%>@U.Cap.Obl@ #           ; !

!!€gt-norm: Foibe # Even-syllable test
!!€ Foibe               Foibe+N+Prop+Sem/Fem+Sg+Nom
!!€ Foibej              Foibe+N+Prop+Sem/Fem+Sg+Ill
!!€ Foibij              Foibe+N+Prop+Sem/Fem+Sg+Ill
!!€ Foibes              Foibe+N+Prop+Sem/Fem+Sg+Ela

LEXICON BETFAGE-mal  !!= @CODE@ Male names
                            +N+Prop+Sem/Mal:e%>            BETFAGEDECL ;
 +N+Prop+Sem/Mal+Sg+Attr@U.Cap.Obl@+Err/Sub:e%>@U.Cap.Obl@ #           ; !

!!€gt-norm: Manasse # Even-syllable test
!!€ Manasse               Manasse+N+Prop+Sem/Mal+Sg+Nom
!!€ Manassej              Manasse+N+Prop+Sem/Mal+Sg+Ill
!!€ Manassij              Manasse+N+Prop+Sem/Mal+Sg+Ill
!!€ Manasses              Manasse+N+Prop+Sem/Mal+Sg+Ela

LEXICON BETFAGEDECL
! These sublexica are irrelevant for ACCRA, but added
! for the sake of the lexicon MARJA
 @U.Cap.Obl@ BETFAGE-DC ;

LEXICON BETFAGEDECL-PLC
! These sublexica are irrelevant for ACCRA, but added
! for the sake of the lexicon MARJA
 @U.Cap.Obl@ BETFAGE-DC ;
 @U.Cap.Opt@ LASJ       ; ! XXX Sub?

LEXICON BETFAGEDECL-ORG
! These sublexica are irrelevant for ACCRA, but added
! for the sake of the lexicon MARJA
 @U.Cap.Obl@ BETFAGE-DC-ORG ;


LEXICON BETFAGE-DC
         +Sg+Nom:      K     ;
         +Sg+Nom:      RHyph ;
            +Ess:n     K     ;
         +Sg+Ill:Q4j   K     ; !Q4 to get e:i
 +Sg+Ill+Err/Sub:j     K     ; !
         +Sg+Gen:      K     ;
         +Sg+Gen:      RHyph ;
         +Sg+Acc:v     K     ;
         +Sg+Ine:n     K     ;
         +Sg+Ela:s     K     ;
         +Pl+Nom:      K     ;
         +Pl+Gen:Q4j   K     ; !Q4 to get e:i
         +Pl+Gen:Q4j   K     ; !Q4 to get e:i
         +Pl+Acc:Q4jt  K     ;
         +Pl+Ill:Q4jda K     ;
         +Pl+Ine:Q4jn  K     ;
         +Pl+Ela:Q4js  K     ;
         +Pl+Com:Q4j   K     ; !Q4 to get e:i
         +Sg+Com:Q4jn  K     ;
 +Sg+Com+Err/Sub:X5jn  K     ; ! Norgejn
 +Pl+Gen+Err/Sub:j     K     ; !
 +Pl+Gen+Err/Sub:j     K     ; !
 +Pl+Acc+Err/Sub:jt    K     ; !
 +Pl+Ill+Err/Sub:jda   K     ; !
 +Pl+Ine+Err/Sub:jn    K     ; !
 +Pl+Ela+Err/Sub:js    K     ; !
 +Pl+Com+Err/Sub:j     K     ; !

LEXICON BETFAGE-DC-ORG
         +Sg+Nom:     K     ;
         +Sg+Nom:     RHyph ;
            +Ess:n    K     ;
         +Sg+Ill:Q4j  K     ; !Q4 to get e:i
 +Sg+Ill+Err/Sub:j    K     ; !
         +Sg+Gen:     K     ;
         +Sg+Gen:     RHyph ;
         +Sg+Acc:v    K     ;
         +Sg+Ine:n    K     ;
         +Sg+Ela:s    K     ;
! +Pl+Nom: K ;
! +Pl+Gen:Q4j K ; !Q4 to get e:i
! +Pl+Gen:Q4j K ; !Q4 to get e:i
! +Pl+Acc:Q4jt K ;
! +Pl+Ill:Q4jda K ;
! +Pl+Ine:Q4jn K ;
! +Pl+Ela:Q4js K ;
! +Pl+Com:Q4j K ; !Q4 to get e:i
         +Sg+Com:Q4jn K     ;
 +Sg+Com+Err/Sub:X5jn K     ; ! Norgejn
! +Pl+Gen+Err/Sub:j K ; !
! +Pl+Gen+Err/Sub:j K ; !
! +Pl+Acc+Err/Sub:jt K ; !
! +Pl+Ill+Err/Sub:jda K ; !
! +Pl+Ine+Err/Sub:jn K ; !
! +Pl+Ela+Err/Sub:js K ; !
! +Pl+Com+Err/Sub:j K ; !


!! Words in MARJA end on vowel, with CG, even-syllable case marking. Illative change e to á, illative i stays i.

LEXICON MARJA-fem !!= @CODE@ Odd-syllable with cg. Female names
                            +N+Prop+Sem/Fem:%>            MARJADECL ;
 +N+Prop+Sem/Fem+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #         ; !
                            +N+Prop+Sem/Fem:»    DiminutiveEVEN ;

!!€gt-norm: Mágddá # Even-syllable test
!!€ Mágddá               Mágddá+N+Prop+Sem/Fem+Sg+Nom
!!€ Mágddáj              Mágddá+N+Prop+Sem/Fem+Sg+Ill
!!€ Mágdás              Mágddá+N+Prop+Sem/Fem+Sg+Ela

LEXICON MARJA-I-fem !!= @CODE@ Female names ending on i.
                            +N+Prop+Sem/Fem:%>            MARJADECL ;
 +N+Prop+Sem/Fem+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #         ; !

!!€gt-norm: Kirsti # Even-syllable test
!!€ Kirsti               Kirsti+N+Prop+Sem/Fem+Sg+Nom
!!€ Kirstijij              Kirsti+N+Prop+Sem/Fem+Sg+Ill
!!€ Kirstijis              Kirsti+N+Prop+Sem/Fem+Sg+Ela

LEXICON MARJA-U-fem !!= @CODE@ Female names ending on u
                            +N+Prop+Sem/Fem:%>            MARJADECL ;
 +N+Prop+Sem/Fem+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #         ; !

!!€gt-norm: Sussu # Even-syllable test
!!€ Sussu               Sussu+N+Prop+Sem/Fem+Sg+Nom
!!€ Sussuj              Sussu+N+Prop+Sem/Fem+Sg+Ill
!!€ Sussus              Sussu+N+Prop+Sem/Fem+Sg+Ela

LEXICON MARJA-A-fem !!= @CODE@ Female names ending on a.
                            +N+Prop+Sem/Fem:%>            MARJADECL ;
 +N+Prop+Sem/Fem+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #         ; !

!!€gt-norm: Rawdna # Even-syllable test
!!€ Rawdna                Rawdna+N+Prop+Sem/Fem+Sg+Nom
!!€ Rawdnaj               Rawdna+N+Prop+Sem/Fem+Sg+Ill
!!€ Rawdnas                Rawdna+N+Prop+Sem/Fem+Sg+Ela

LEXICON MARJA-ani !!= @CODE@ Animal names
 +N+Prop+Sem/Ani:%> MARJADECL ;

!!€gt-norm: Vilge # Even-syllable test
!!€ Vilgge               Vilge+N+Prop+Sem/Ani+Sg+Nom
!!€ Vilggáj              Vilge+N+Prop+Sem/Ani+Sg+Ill
!!€ Vilges              Vilge+N+Prop+Sem/Ani+Sg+Ela

LEXICON MARJA-A-ani !!= @CODE@ Animal names ending on a.
 +N+Prop+Sem/Ani:%> MARJADECL ;

!!€gt-norm: S-Gabba # Even-syllable test
!!€ S-Gabba               S-Gabba+N+Prop+Sem/Fem+Sg+Nom
!!€ S-Gabbaj               S-Gabba+N+Prop+Sem/Fem+Sg+Ill
!!€ S-Gappas              S-Gabba+N+Prop+Sem/Fem+Sg+Ela

LEXICON MARJA-mal !!= @CODE@ Male names
                            +N+Prop+Sem/Mal:%>            MARJADECL ;
 +N+Prop+Sem/Mal+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #         ; !
                            +N+Prop+Sem/Fem:»    DiminutiveEVEN ;


!!€gt-norm: Biet-Ánnda # Even-syllable test
!!€ Biet-Ánnda               Biet-Ánnda+N+Prop+Sem/Mal+Sg+Nom
!!€ Biet-Ánndaj              Biet-Ánnda+N+Prop+Sem/Mal+Sg+Ill
!!€ Biet-Ándas              Biet-Ánnda+N+Prop+Sem/Mal+Sg+Ela

LEXICON MARJA-I-mal !!= @CODE@ Male named ending on i.
                            +N+Prop+Sem/Mal:%>            MARJADECL ;
 +N+Prop+Sem/Mal+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #         ; !

!!€gt-norm: Jovni # Even-syllable test
!!€ Jovni               Jovni+N+Prop+Sem/Mal+Sg+Nom
!!€ Jovnijij              Jovni+N+Prop+Sem/Mal+Sg+Ill
!!€ Jovnijis              Jovni+N+Prop+Sem/Mal+Sg+Ela

LEXICON MARJA-A-mal !!= @CODE@ Male named ending on a.
                            +N+Prop+Sem/Mal:%>            MARJADECL ;
 +N+Prop+Sem/Mal+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #         ; !

!!€gt-norm: Lársa # Even-syllable test
!!€ Lárssa               Lársa+N+Prop+Sem/Mal+Sg+Nom
!!€ Lárssaj              Lársa+N+Prop+Sem/Mal+Sg+Ill
!!€ Lársas              Lársa+N+Prop+Sem/Mal+Sg+Ela

LEXICON MARJA-U-mal !!= @CODE@ Male named ending on u.
                            +N+Prop+Sem/Mal:%>            MARJADECL ;
 +N+Prop+Sem/Mal+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #         ; !

!!€gt-norm: Áilu # Even-syllable test
!!€ Áilu               Áilu+N+Prop+Sem/Fem+Sg+Nom
!!€ Áiluj              Áilu+N+Prop+Sem/Fem+Sg+Ill
!!€ Áilus              Áilu+N+Prop+Sem/Fem+Sg+Ela

LEXICON MARJA-obj !!= @CODE@ Objects
 +N+Prop+Sem/Obj:%> MARJADECL ;

!!€gt-norm: Bern-Konvensjåvnnå # Even-syllable test
!!€ Bern-Konvensjåvnnå               Bern-Konvensjåvnnå+N+Prop+Sem/Obj+Sg+Nom
!!€ Bern-Konvensjåvnnåj              Bern-Konvensjåvnnå+N+Prop+Sem/Obj+Sg+Ill
!!€ Bern-Konvensjåvnås               Bern-Konvensjåvnnå+N+Prop+Sem/Obj+Sg+Ela

LEXICON MARJA-A-obj !!= @CODE@ Objectes ending on a.
 +N+Prop+Sem/Obj:%> MARJADECL ;

!!€gt-norm: Bárentsprográmma # Even-syllable test
!!€ Bárentsprográmma               Bárentsprográmma+N+Prop+Sem/Obj+Sg+Nom
!!€ Bárentsprográmmaj              Bárentsprográmma+N+Prop+Sem/Obj+Sg+Ill
!!€ Bárentsprográmmas              Bárentsprográmma+N+Prop+Sem/Obj+Sg+Ela

LEXICON MARJA-I-obj !!= @CODE@ Objects ending on i.
 +N+Prop+Sem/Obj:%> MARJADECL ;

!!€gt-norm: Lávrasiid% áigi # Even-syllable test
!!€ Lávrasiid% áigi               Lávrasiid% áigi+N+Prop+Sem/Obj+Sg+Nom
!!€ Lávrasiid% áigij              Lávrasiid% áigi+N+Prop+Sem/Obj+Sg+Ill
!!€ Lávrasiid% áigis              Lávrasiid% áigi+N+Prop+Sem/Obj+Sg+Ela

LEXICON MARJA-U-obj !!= @CODE@ Objects ending on u.
 +N+Prop+Sem/Obj:%> MARJADECL ;

!!€gt-norm: WaiRiddu # Even-syllable test
!!€ WaiRiddu               WaiRiddu+N+Prop+Sem/Obj+Sg+Nom
!!€ WaiRidduj              WaiRiddu+N+Prop+Sem/Obj+Sg+Ill
!!€ WaiRittus              WaiRiddu+N+Prop+Sem/Obj+Sg+Ela

LEXICON MARJA-org !!= @CODE@ Organizations
 +N+Prop+Sem/Org:%> MARJADECL-ORG ;

!!€gt-norm: Domænadoajmma # Even-syllable test
!!€ Domænadoajmma               Domænadoajmma+N+Prop+Sem/Org+Sg+Nom
!!€ Domänadoajmma               Domænadoajmma+N+Prop+Sem/Org+Sg+Nom
!!€ Domænadoajmmaj              Domænadoajmma+N+Prop+Sem/Org+Sg+Ill
!!€ Domänadoajmmaj              Domænadoajmma+N+Prop+Sem/Org+Sg+Ill
!!€ Domænadåjmas              Domænadoajmma+N+Prop+Sem/Org+Sg+Ela
!!€ Domänadåjmas              Domænadoajmma+N+Prop+Sem/Org+Sg+Ela

LEXICON MARJA-A-org !!= @CODE@ Organizations ending on a.
 +N+Prop+Sem/Org:%> MARJADECL-ORG ;

!!€gt-norm: Giellagas-instituhtta # Even-syllable test
!!€ Giellagas-instituhtta               Giellagas-instituhtta+N+Prop+Sem/Org+Sg+Nom
!!€ Giellagas-instituhttaj              Giellagas-instituhtta+N+Prop+Sem/Org+Sg+Ill
!!€ Giellagas-instituhtas               Giellagas-instituhtta+N+Prop+Sem/Org+Sg+Ela

LEXICON MARJA-U-org !!= @CODE@ Organizations ending on u.
 +N+Prop+Sem/Org:%> MARJADECL-ORG ;

!!€gt-norm: Márkomeannu # Even-syllable test
!!€ Márkomeannu               Márkomeannu+N+Prop+Sem/Org+Sg+Nom
!!€ Márkomeannuj              Márkomeannu+N+Prop+Sem/Org+Sg+Ill
!!€ Márkomeanus              Márkomeannu+N+Prop+Sem/Org+Sg+Ela

LEXICON MARJA-I-org !!= @CODE@ Organizations ending on i.
 +N+Prop+Sem/Org:%> MARJADECL-ORG ;

!!€gt-norm: Várdobáiki # Even-syllable test
!!€ Várdobáiki               Várdobáiki+N+Prop+Sem/Org+Sg+Nom
!!€ Várdobájkkáj              Várdobáiki+N+Prop+Sem/Org+Sg+Ill
!!€ Várdobájkes              Várdobáiki+N+Prop+Sem/Org+Sg+Ela

LEXICON MARJA-plc !!= @CODE@ Vowel final names with Gradation and Ill change (place names)
                   +N+Prop+Sem/Plc:%>              MARJADECL-PLC ; ! Flags added in MARJADECL-PLC
 +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@:%>Q1@U.Cap.Opt@ VUONAK        ;

!!€gt-norm: Dundarevuobme # Even-syllable test
!!€ Dundarevuobme               Dundarevuobme+N+Prop+Sem/Plc+Sg+Nom
!!€ Dundarevuobmáj              Dundarevuobme+N+Prop+Sem/Plc+Sg+Ill
!!€ Dundarevuomes              Dundarevuobme+N+Prop+Sem/Plc+Sg+Ela

LEXICON MARJA-A-plc !!= @CODE@ Vowel final names with Gradation and Ill change (place names)
                   +N+Prop+Sem/Plc:%>              MARJADECL-PLC ;
 +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@:%>Q1@U.Cap.Opt@ VUONAK        ;

!!€gt-norm: Giranjárga # Even-syllable test
!!€ Giranjárgga               Giranjárga+N+Prop+Sem/Plc+Sg+Nom
!!€ Giranjárggaj              Giranjárga+N+Prop+Sem/Plc+Sg+Ill
!!€ Giranjárgas              Giranjárga+N+Prop+Sem/Plc+Sg+Ela

LEXICON MARJA-I-plc !!= @CODE@ Vowel final names with Gradation and Ill change (place names)
                   +N+Prop+Sem/Plc:%>              MARJADECL-PLC ;
 +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@:%>Q1@U.Cap.Opt@ VUONAK        ;

!!€gt-norm: Lulip% Stálujávri # Even-syllable test
!!€ Lulip% Stálujávrre               Lulip% Stálujávri+N+Prop+Sem/Plc+Sg+Nom
!!€ Lulip% Stálujávrráj              Lulip% Stálujávri+N+Prop+Sem/Plc+Sg+Ill
!!€ Lulip% Stálujávres              Lulip% Stálujávri+N+Prop+Sem/Plc+Sg+Ela

LEXICON MARJA-U-plc !!= @CODE@ Vowel final names with Gradation and Ill change (place names)
                   +N+Prop+Sem/Plc:%>              MARJADECL-PLC ;
 +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@:%>Q1@U.Cap.Opt@ VUONAK        ;

!!€gt-norm: Foibe # Even-syllable test
!!€ Foibe               Foibe+N+Prop+Sem/Fem+Sg+Nom
!!€ Foibej              Foibe+N+Prop+Sem/Fem+Sg+Ill
!!€ Foibes              Foibe+N+Prop+Sem/Fem+Sg+Ela

LEXICON MARJA-sur !!= @CODE@ Surnames
                            +N+Prop+Sem/Sur:%>            MARJADECL ;
 +N+Prop+Sem/Sur+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #         ; !

!!€gt-norm: Skálltje # Even-syllable test
!!€ Skálltje               Skálltje+N+Prop+Sem/Sur+Sg+Nom
!!€ Skálltjáj              Skálltje+N+Prop+Sem/Sur+Sg+Ill
!!€ Skáltjes              Skálltje+N+Prop+Sem/Sur+Sg+Ela

LEXICON MARJA-U-sur !!= @CODE@ Surnames ending on u.
                            +N+Prop+Sem/Sur:%>            MARJADECL ;
 +N+Prop+Sem/Sur+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #         ; !

!!€gt-norm: Boŋgu # Even-syllable test
!!€ Boŋgu               Boŋgu+N+Prop+Sem/Sur+Sg+Nom
!!€ Boŋguj              Boŋgu+N+Prop+Sem/Sur+Sg+Ill
!!€ Boŋgus              Boŋgu+N+Prop+Sem/Sur+Sg+Ela

 LEXICON MARJA-I-sur !!= @CODE@ Surnames ending on i.
                            +N+Prop+Sem/Sur:%>            MARJADECL ;
 +N+Prop+Sem/Sur+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #         ; !

!!€gt-norm: Dolpi # Even-syllable test
!!€ Dolpi               Dolpi+N+Prop+Sem/Fem+Sg+Nom
!!€ Dolpijij              Dolpi+N+Prop+Sem/Fem+Sg+Ill
!!€ Dolpijis              Dolpi+N+Prop+Sem/Fem+Sg+Ela

 LEXICON MARJA-A-sur !!= @CODE@ Surnames ending on a.
                            +N+Prop+Sem/Sur:%>            MARJADECL ;
 +N+Prop+Sem/Sur+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #         ; !

!!€gt-norm: Eira # Even-syllable test
!!€ Eira               Eira+N+Prop+Sem/Sur+Sg+Nom
!!€ Eiraj              Eira+N+Prop+Sem/Sur+Sg+Ill
!!€ Eiras              Eira+N+Prop+Sem/Sur+Sg+Ela

LEXICON MARJA-LOAN-plc !!= @CODE@ Vowel final names with Gradation and Ill change (place names)
 +N+Prop+Sem/Plc:%>@U.Cap.Obl@ MARJA-LOAN-DC ;
! +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@:%>Q1@U.Cap.Opt@ VUONAK ;

!!€gt-norm: Rostoeatnu # Even-syllable test
!!€ Rostoeatnu               Rostoeatnu+N+Prop+Sem/Plc+Sg+Nom
!!€ Rostoeatnuj              Rostoeatnu+N+Prop+Sem/Plc+Sg+Ill
!!€ Rostoeatnus              Rostoeatnu+N+Prop+Sem/Plc+Sg+Ela

LEXICON SUOBMA-plc !!= @CODE@ Placenames. Like MARJA but no derivation
 +N+Prop+Sem/Plc:%>@U.Cap.Obl@ SUOBMADECL ;

!!€gt-norm: Suobma # Even-syllable test
!!€ Suobma               Suobma+N+Prop+Sem/Plc+Sg+Nom
!!€ Suobmaj              Suobma+N+Prop+Sem/Plc+Sg+Ill
!!€ Suomas              Suobma+N+Prop+Sem/Plc+Sg+Ela

LEXICON SUOBMADECL
 @U.Cap.Obl@ MARJA-DC ;

LEXICON MARJADECL-ORG ! Vowel-final names with Gradation and Ill change (person names)
 @U.Cap.Obl@ MARJA-DC-ORG ;

LEXICON MARJADECL-PLC ! Vowel-final names with Gradation and Ill change (person names)
 @U.Cap.Obl@ MARJA-DC ;
 @U.Cap.Opt@ LASJ ;

LEXICON MARJADECL ! Vowel-final names with Gradation and Ill change (person names)
 @U.Cap.Obl@ MARJA-DC ;


LEXICON MARJA-DC
 +Sg+Nom:       K     ;
 +Sg+Nom:       RHyph ;
    +Ess:n      K     ;
 +Sg+Ill:X4j    K     ;
 +Sg+Gen:Q1     K     ;
 +Sg+Gen:Q1     RHyph ;
 +Sg+Acc:Q1v    K     ;
 +Sg+Ine:Q1n    K     ;
 +Sg+Ela:Q1s    K     ;
 +Pl+Nom:Q1     K     ;
 +Pl+Gen:Q1j    K     ;
 +Pl+Gen:Q1j    RHyph ;
 +Pl+Acc:Q1jt   K     ;
 +Pl+Ill:Q1jda  K     ;
 +Pl+Ine:Q1jn   K     ;
 +Pl+Ela:Q1js   K     ;
 +Pl+Com:Q1j    K     ;
 +Sg+Com:Q1X5jn K     ;

LEXICON MARJA-DC-ORG
 +Sg+Nom:       K     ;
 +Sg+Nom:       RHyph ;
    +Ess:n      K     ;
 +Sg+Ill:X4j    K     ;
 +Sg+Gen:Q1     K     ;
 +Sg+Gen:Q1     RHyph ;
 +Sg+Acc:Q1v    K     ;
 +Sg+Ine:Q1n    K     ;
 +Sg+Ela:Q1s    K     ;
! +Pl+Nom:Q1 K ;
! +Pl+Gen:Q1j K ;
! +Pl+Gen:Q1j RHyph ;
! +Pl+Acc:Q1jt K ;
! +Pl+Ill:Q1jda K ;
! +Pl+Ine:Q1jn K ;
! +Pl+Ela:Q1js K ;
! +Pl+Com:Q1j K ;
 +Sg+Com:Q1X5jn K     ;


LEXICON MARJA-LOAN-DC
         +Sg+Nom:       K     ;
         +Sg+Nom:       RHyph ;
    +Ess+Err/Sub:n      K     ; !
 +Sg+Ill+Err/Sub:X4j    K     ; !
 +Sg+Gen+Err/Sub:Q1     K     ; !
 +Sg+Gen+Err/Sub:Q1     RHyph ; !
 +Sg+Acc+Err/Sub:Q1v    K     ; !
 +Sg+Ine+Err/Sub:Q1n    K     ; !
 +Sg+Ela+Err/Sub:Q1s    K     ; !
! +Pl+Nom+Err/Sub:Q1 K ; !
! +Pl+Gen+Err/Sub:Q1j K ; !
! +Pl+Gen+Err/Sub:Q1j RHyph ; !
! +Pl+Acc+Err/Sub:Q1jt K ; !
! +Pl+Ill+Err/Sub:Q1jda K ; !
! +Pl+Ine+Err/Sub:Q1jn K ; !
! +Pl+Ela+Err/Sub:Q1js K ; !
! +Pl+Com+Err/Sub:Q1j K ; !
 +Sg+Com+Err/Sub:Q1X5jn K     ; !

LEXICON DiminutiveEVEN
@U.Cap.Obl@ DiminutiveEVENDECL   ;

LEXICON DiminutiveEVENDECL
                           +Der1+Der/Dimin+N+Prop:X7sj        N-ODDCASE    ;
                           +Der1+Der/Dimin+N+Prop:X7tj        OTH-ODDCASE  ;
   +Der1+Der/Dimin+N+Der2+Der/Dimin+N+Prop+Sg+Nom:X7tja»sj    K            ;
          +Der1+Der/Dimin+N+Der2+Der/Dimin+N+Prop:X7tja»dtja  GÅNÅGIS-EVEN ;

LEXICON DiminutiveODD
@U.Cap.Obl@ DiminutiveODD  ;

LEXICON DiminutiveODDDECL
                    +Der1+Der/Dimin+N+Sg+Nom:asj          K            ;
                           +Der1+Der/Dimin+N:adtja        GÅNÅGIS-EVEN ;
          +Der1+Der/Dimin+N+Der2+Der/Dimin+N:adtja»X7sj   N-ODDCASE    ;
          +Der1+Der/Dimin+N+Der2+Der/Dimin+N:adtja»X7tj   OTH-ODDCASE  ;


LEXICON JOKULL-plc
 +N+Prop+Sem/Plc: LONDONDECL-PLC ; !!= @CODE@ Placenames. NB added to make the code compile, needs revision.

!!€gt-norm: Cornwall # Even-syllable test
!!€ Cornwall               Cornwall+N+Prop+Sem/Plc+Sg+Nom
!!€ Cornwallaj              Cornwall+N+Prop+Sem/Plc+Sg+Ill
!!€ Cornwallas              Cornwall+N+Prop+Sem/Plc+Sg+Ela

LEXICON JAVRRIE-plc !!= @CODE@  Placenames. This is a lex for Umesami names on -ie that have CG
 +N+Prop+Sem/Plc: JAVRRIEDECL ;

!!€gt-norm: Måehvie # Even-syllable test
!!€ Måehvie                Måehvie+N+Prop+Sem/Plc+Sg+Nom
!!€ Måehviej              Måehvie+N+Prop+Sem/Plc+Sg+Ill
!!€ Måehvies              Måehvie+N+Prop+Sem/Plc+Sg+Ela
!!€ Måehviejda              Måehvie+N+Prop+Sem/Plc+Pl+Ill
!!€ Måehviejijs              Måehvie+N+Prop+Sem/Plc+Pl+Ela

LEXICON JAVRRIEDECL
 @U.Cap.Obl@ JAVRRIE-DC ;

LEXICON JAVRRIE-DC
 +Sg+Nom:i%>e      K     ;
 +Sg+Nom:i%>e      RHyph ;
    +Ess:i%>en     K     ;
 +Sg+Ill:á%>X4j    K     ;
 +Sg+Gen:i%>Q1e    K     ;
 +Sg+Gen:i%>Q1e    RHyph ;
 +Sg+Acc:i%>Q1ev   K     ;
 +Sg+Ine:i%>Q1en   K     ;
 +Sg+Ela:i%>Q1es   K     ;
 +Pl+Nom:i%>Q1e    K     ;
 +Pl+Gen:i%>Q1j    K     ;
 +Pl+Gen:i%>Q1j    RHyph ;
 +Pl+Acc:i%>Q1jt   K     ;
 +Pl+Ill:i%>Q1jda  K     ;
 +Pl+Ine:i%>Q1jn   K     ;
 +Pl+Ela:i%>Q1js   K     ;
 +Pl+Com:i%>Q1j    K     ;
 +Sg+Com:i%>Q1X5jn K     ;


!LEXICON NIKOSIIJA-plc !XXX Check.
! +N+Prop+Sem/Plc:%> NIKOSIIJADECL ;
! +N+Prop+Sem/Plc+Der/lasj@U.Cap.Opt@:%>@U.Cap.Opt@laddj DEARVVASLAS ;	!SUB
! +N+Prop+Sem/Plc+Der/lasj+A+Attr@U.Cap.Opt@:%>@U.Cap.Opt@laddj ATTR ;
! +N+Prop+Sem/Plc+Der/lasj+A@U.Cap.Opt@:%>@U.Cap.Opt@laddj JOHTOLAT ;
! +N+Prop+Sem/Plc+Der/lasj+A@U.Cap.Opt@:%>@U.Cap.Opt@laddja ISSORASSA- ;
! +N+Prop+Sem/Plc+Der/lasj+A+Sg+Nom@U.Cap.Opt@:%>@U.Cap.Opt@laddja PxV ; ! tentative.
! +N+Prop+Sem/Plc+Der/lasj+A+Ess@U.Cap.Opt@:%>@U.Cap.Opt@laddjan K ;
! +N+Prop+Sem/Plc+Der/lasj+A+Ess@U.Cap.Opt@:%>@U.Cap.Opt@laddjan PxC ; ! tentative, check.
! +N+Prop+Sem/Plc+Der/lasj+A@U.Cap.Opt@:%>@U.Cap.Opt@laddja EABBUCASE2 ;
!+N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@:%>@U.Cap.Opt@ VUONAK ;

!LEXICON NIKOSIIJADECL !Names with ending -iija, a-illative
!ACCRA-DC_PLC-ORG ;

!! Words in lexicon NYSTØ end on vowel, no cg, case marking with ill -aj, ela -as. Last syllable is stressed.

LEXICON NYSTØ-ani !!= @CODE@ Animal names
 +N+Prop+Sem/Ani:%> NYSTØDECL ;

!!€gt-norm: Ski-Doo # Even-syllable test
!!€ Ski-Doo               Ski-Doo+N+Prop+Sem/Ani+Sg+Nom
!!€ Ski-Dooj              Ski-Doo+N+Prop+Sem/Ani+Sg+Ill
!!€ Ski-Doos              Ski-Doo+N+Prop+Sem/Ani+Sg+Ela

LEXICON NYSTØ-fem !!= @CODE@ Femal names
                            +N+Prop+Sem/Fem:%>            NYSTØDECL ;
 +N+Prop+Sem/Fem+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #         ; !

!!€gt-norm: Britney # Even-syllable test
!!€ Britney                Britney+N+Prop+Sem/Fem+Sg+Nom
!!€ Britneyaj              Britney+N+Prop+Sem/Fem+Sg+Ill
!!€ Britneyas              Britney+N+Prop+Sem/Fem+Sg+Ela

LEXICON NYSTØ-femsur !!= @CODE@ Names that can be both female and surnames.
   NYSTØ-fem ;
   NYSTØ-sur ;

!!€gt-norm: Sarai # Even-syllable test
!!€ Sarai               Sarai+N+Prop+Sem/Fem+Sg+Nom
!!€ Saraijij              Sarai+N+Prop+Sem/Fem+Sg+Ill
!!€ Saraijis              Sarai+N+Prop+Sem/Fem+Sg+Ela

LEXICON NYSTØ-mal !!= @CODE@ Male name
                            +N+Prop+Sem/Mal:%>            NYSTØDECL ;
 +N+Prop+Sem/Mal+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #         ; !

!!€gt-norm: Taneli # Even-syllable test
!!€ Taneli               Taneli+N+Prop+Sem/Mal+Sg+Nom
!!€ Taneliaj              Taneli+N+Prop+Sem/Mal+Sg+Ill
!!€ Tanelias              Taneli+N+Prop+Sem/Mal+Sg+Ela

LEXICON NYSTØ-obj !!= @CODE@ Objects
 +N+Prop+Sem/Obj:%> NYSTØDECL ;

!!€gt-norm: Infiniti # Even-syllable test
!!€ Infiniti                Infiniti+N+Prop+Sem/Obj+Sg+Nom
!!€ Infinitiaj               Infiniti+N+Prop+Sem/Obj+Sg+Ill
!!€ Infinitias               Infiniti+N+Prop+Sem/Obj+Sg+Ela

LEXICON NYSTØ-org !!= @CODE@  Organizations
 +N+Prop+Sem/Org:%> NYSTØDECL-ORG ;

!!€gt-norm: Kulturby # Even-syllable test
!!€ Kulturby                Kulturby+N+Prop+Sem/Org+Sg+Nom
!!€ Kulturbyaj              Kulturby+N+Prop+Sem/Org+Sg+Ill
!!€ Kulturbyas              Kulturby+N+Prop+Sem/Org+Sg+Ela

LEXICON NYSTØ-LOAN-org !!= @CODE@ Organizations loan
 +N+Prop+Sem/Org:%> NYSTØDECL-LOAN ;

!!€gt-norm: Sameby # Even-syllable test
!!€ Sameby                Sameby+N+Prop+Sem/Org+Sg+Nom
!!$ Samebyaj              Sameby+N+Prop+Sem/Org+Sg+Ill
!!$ Samebyas              Sameby+N+Prop+Sem/Org+Sg+Ela

LEXICON NYSTØ-sur !!= @CODE@ Sur names
                            +N+Prop+Sem/Sur:%>            NYSTØDECL ;
 +N+Prop+Sem/Sur+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #         ; !

!!€gt-norm: Sandoz # Even-syllable test
!!€ Sandoz                Sandoz+N+Prop+Sem/Sur+Sg+Nom
!!€ Sandozaj              Sandoz+N+Prop+Sem/Sur+Sg+Ill
!!€ Sandozas              Sandoz+N+Prop+Sem/Sur+Sg+Ela

LEXICON NYSTØ-surplc !!= @CODE@  Names that can be both sur- and placenames.
   NYSTØ-sur ;
   NYSTØ-plc ;

!!€gt-norm: Teigmo # Even-syllable test
!!€ Teigmo               Teigmo+N+Prop+Sem/Plc+Sg+Nom
!!€ Teigmoaj              Teigmo+N+Prop+Sem/Plc+Sg+Ill
!!€ Teigmoas              Teigmo+N+Prop+Sem/Plc+Sg+Ela

LEXICON NYSTØ-LOAN-plc !!= @CODE@ Place names loan
                           +N+Prop+Sem/Plc:%>             NYSTØDECL-LOAN ;
 +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@+Err/Sub:%>a@U.Cap.Opt@ VUONAK         ; !

!!€gt-norm: Bodø # Even-syllable test
!!€ Bodø                Bodø+N+Prop+Sem/Plc+Sg+Nom
!!$ Bodøaj              Bodø+N+Prop+Sem/Plc+Sg+Ill
!!$ Bodøas              Bodø+N+Prop+Sem/Plc+Sg+Ela

LEXICON NYSTØ-plc !!= @CODE@ Place names
                   +N+Prop+Sem/Plc:%>             NYSTØDECL-PLC ;
 +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@:%>a@U.Cap.Opt@ VUONAK        ;

!!€gt-norm: Borgå # Even-syllable test
!!€ Borgå               Borgå+N+Prop+Sem/Plc+Sg+Nom
!!€ Borgåaj              Borgå+N+Prop+Sem/Plc+Sg+Ill
!!€ Borgåas              Borgå+N+Prop+Sem/Plc+Sg+Ela

!LEXICON NYSTØ !!= @CODE@  Vowel-final, heavy syll
! +N+Prop:%> NYSTØDECL ;

LEXICON NYSTØDECL
 @U.Cap.Obl@ NYSTØ-OBL ;

LEXICON NYSTØDECL-PLC
 @U.Cap.Obl@ NYSTØ-OBL ;
 @U.Cap.Opt@ LASJ      ; ! XXX Sub?

LEXICON NYSTØDECL-ORG
 @U.Cap.Obl@ NYSTØ-OBL-ORG ;

LEXICON NYSTØDECL-LOAN
 @U.Cap.Obl@ NYSTØ-OBL-LOAN ;
 @U.Cap.Opt@ LASJ           ; ! XXX Sub?


LEXICON NYSTØ-OBL
          +Sg+Nom:   K     ;
          +Sg+Nom:   RHyph ;
! +Sg+Ela:%:s K ; ! Nystø:s  !SUB
! +Sg+Ine:%:n K ; ! Nystø:s  !SUB
 +Sg+Gen#+Err/Sub:a# RHyph ; ! !for placenames
         +Sg+Gen#:#  RHyph ; ! for placenames
                     NYOBL ;
         +Err/Sub:%: NYOBL ; ! ! to NYOBL, optional delimiter marks. sub?
         +Err/Sub:%- NYOBL ; !
         +Err/Sub:1  NYOBL ; !

LEXICON NYSTØ-OBL-ORG
          +Sg+Nom:   K         ;
          +Sg+Nom:   RHyph     ;
! +Sg+Ela:%:s K ; ! Nystø:s  !SUB
! +Sg+Ine:%:n K ; ! Nystø:s  !SUB
 +Sg+Gen#+Err/Sub:a# RHyph     ; ! !for placenames
         +Sg+Gen#:#  RHyph     ; ! for placenames
                     NYOBL-ORG ;
         +Err/Sub:%: NYOBL-ORG ; ! ! to NYOBL, optional delimiter marks. sub?
         +Err/Sub:%- NYOBL-ORG ; !
         +Err/Sub:1  NYOBL-ORG ; !


LEXICON NYSTØ-OBL-LOAN
          +Sg+Nom:   K         ;
          +Sg+Nom:   RHyph     ;
! +Sg+Ela:%:s K ; ! Nystø:s  !SUB
! +Sg+Ine:%:n K ; ! Nystø:s  !SUB
 +Sg+Gen#+Err/Sub:a# RHyph     ; ! !for placenames
 +Sg+Gen#+Err/Sub:#  RHyph     ; ! ! for placenames
         +Err/Sub:   NYOBL-ORG ; !
         +Err/Sub:%: NYOBL-ORG ; ! ! to NYOBL, optional delimiter marks. sub?
         +Err/Sub:%- NYOBL-ORG ; !
         +Err/Sub:1  NYOBL-ORG ; !


!LEXICON NYSTØ-DC
! +laš:lažž DEARVVASLAS ;

LEXICON NYOBL
                  +Sg+Nom:     K     ;
                  +Sg+Nom:     RHyph ;
                  +Sg+Ill:j    K     ;
                     +Ess:n    K     ;
                  +Sg+Gen:     K     ;
                  +Sg+Gen:     RHyph ;
                  +Sg+Acc:v    K     ;
                  +Sg+Ine:n    K     ;
                  +Sg+Ela:s    K     ;
                  +Pl+Nom:     K     ;
                  +Pl+Gen:j    K     ;
                  +Pl+Gen:j    RHyph ;
                  +Pl+Acc:jt   K     ;
                  +Pl+Ill:jda  K     ;
                  +Pl+Ine:jn   K     ;
                  +Pl+Ela:js   K     ;
                  +Pl+Com:j    K     ;
                  +Sg+Com:jn   K     ;
          +Sg+Nom+Err/Sub:a    K     ; !
          +Sg+Nom+Err/Sub:a    RHyph ; !
          +Sg+Ill+Err/Sub:aj   K     ; !
             +Ess+Err/Sub:an   K     ; !
          +Sg+Gen+Err/Sub:a    K     ; !
 +Sg+Gen+Err/Sub+Use/Circ:a    RHyph ; !  !
          +Sg+Acc+Err/Sub:av   K     ; !
          +Sg+Ine+Err/Sub:an   K     ; !
          +Sg+Ela+Err/Sub:as   K     ; !
          +Pl+Nom+Err/Sub:a    K     ; !
          +Pl+Gen+Err/Sub:aj   K     ; !
 +Pl+Gen+Err/Sub+Use/Circ:aj   RHyph ; ! !
          +Pl+Acc+Err/Sub:ajt  K     ; !
          +Pl+Ill+Err/Sub:ajda K     ; !
          +Pl+Ine+Err/Sub:ajn  K     ; !
          +Pl+Ela+Err/Sub:ajs  K     ; !
          +Pl+Com+Err/Sub:aj   K     ; !
          +Sg+Com+Err/Sub:ajn  K     ; !

LEXICON NYOBL-ORG
                  +Sg+Nom:    K     ;
                  +Sg+Nom:    RHyph ;
                  +Sg+Ill:j   K     ;
                     +Ess:n   K     ;
                  +Sg+Gen:    K     ;
                  +Sg+Gen:    RHyph ;
                  +Sg+Acc:v   K     ;
                  +Sg+Ine:n   K     ;
                  +Sg+Ela:s   K     ;
! +Pl+Nom: K ;
! +Pl+Gen:j K ;
! +Pl+Gen:j RHyph ;
! +Pl+Acc:jt K ;
! +Pl+Ill:jda K ;
! +Pl+Ine:jn K ;
! +Pl+Ela:js K ;
! +Pl+Com:j K ;
                  +Sg+Com:jn  K     ;
          +Sg+Nom+Err/Sub:a   K     ; !
          +Sg+Nom+Err/Sub:a   RHyph ; !
          +Sg+Ill+Err/Sub:aj  K     ; !
             +Ess+Err/Sub:an  K     ; !
          +Sg+Gen+Err/Sub:a   K     ; !
 +Sg+Gen+Err/Sub+Use/Circ:a   RHyph ; !  !
          +Sg+Acc+Err/Sub:av  K     ; !
          +Sg+Ine+Err/Sub:an  K     ; !
          +Sg+Ela+Err/Sub:as  K     ; !
! +Pl+Nom+Err/Sub:a K ;   !
! +Pl+Gen+Err/Sub:aj K ;   !
! +Pl+Gen+Err/Sub+Use/Circ:aj RHyph ;     ! !
! +Pl+Acc+Err/Sub:ajt K ;   !
! +Pl+Ill+Err/Sub:ajda K ;   !
! +Pl+Ine+Err/Sub:ajn K ;   !
! +Pl+Ela+Err/Sub:ajs K ;   !
! +Pl+Com+Err/Sub:aj K ;   !
          +Sg+Com+Err/Sub:ajn K     ; !

!! Words in lexicon BERN end on conconant, no cg, case marking with -aj, -as. Last syllable is stressed.

LEXICON BERN-ani !!= @CODE@  Animals
!Different lexicon for animals. Eurokas.
 +N+Prop+Sem/Ani:%> BERNDECL ;

!!€gt-norm: Lillemor # Even-syllable test
!!€ Lillemor               Lillemor+N+Prop+Sem/Ani+Sg+Nom
!!€ Lillemoraj              Lillemor+N+Prop+Sem/Ani+Sg+Ill
!!€ Lillemoras              Lillemor+N+Prop+Sem/Ani+Sg+Ela

LEXICON BERN-mal !!= @CODE@  Male names
! Different lexicon for male persons. Finn. Torfinn.
                            +N+Prop+Sem/Mal:%>            BERNDECL ;
 +N+Prop+Sem/Mal+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #        ; !

!!€gt-norm: Ever # Even-syllable test
!!€ Ever               Ever+N+Prop+Sem/Mal+Sg+Nom
!!€ Everij              Ever+N+Prop+Sem/Mal+Sg+Ill
!!€ Everis              Ever+N+Prop+Sem/Mal+Sg+Ela


LEXICON BERN-surmal !!= @CODE@  name that are both sur- and male names
! Different lexicon for male persons & personal surnames.
                            +N+Prop+Sem/Mal:%>            BERNDECL ;
                            +N+Prop+Sem/Sur:%>            BERNDECL ;
 +N+Prop+Sem/Mal+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #        ; !

!!€gt-norm: Pipin # Even-syllable test
!!€ Pipin               Pipin+N+Prop+Sem/Sur+Sg+Nom
!!€ Pipinij              Pipin+N+Prop+Sem/Sur+Sg+Ill
!!€ Pipinis              Pipin+N+Prop+Sem/Sur+Sg+Ela


LEXICON BERN-fem !!= @CODE@  Female name
! Different lexicon for female persons. Audhild.
                            +N+Prop+Sem/Fem:%>            BERNDECL ;
 +N+Prop+Sem/Fem+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #        ; !

!!€gt-norm: Ragnfrid # Even-syllable test
!!€ Ragnfrid               Ragnfrid+N+Prop+Sem/Fem+Sg+Nom
!!€ Ragnfridaj              Ragnfrid+N+Prop+Sem/Fem+Sg+Ill
!!€ Ragnfridas              Ragnfrid+N+Prop+Sem/Fem+Sg+Ela

LEXICON BERN-femplc !!= @CODE@ Both female and placenames. Not used in smj-propernouns
! Different lexicon for female persons. Audhild.
                            +N+Prop+Sem/Fem:%>             BERNDECL     ;
 +N+Prop+Sem/Fem+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@  #            ; !
                            +N+Prop+Sem/Plc:%>             BERNDECL-PLC ;
          +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@:%>a@U.Cap.Opt@ VUONAK       ;

!!€gt-norm: Maurid # Even-syllable test
!!€ Maurid               Maurid+N+Prop+Sem/Plc+Sg+Nom
!!€ Mauridaj              Maurid+N+Prop+Sem/Plc+Sg+Ill
!!€ Mauridas              Maurid+N+Prop+Sem/Plc+Sg+Ela

LEXICON BERN-sur !!= @CODE@ Surnames
! Different lexicon for personal surnames. Blind
                            +N+Prop+Sem/Sur:%>            BERNDECL ;
 +N+Prop+Sem/Sur+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #        ; !

!!€gt-norm: Lind # Even-syllable test
!!€ Lind               Lind+N+Prop+Sem/Sur+Sg+Nom
!!€ Lindaj              Lind+N+Prop+Sem/Sur+Sg+Ill
!!€ Lindas              Lind+N+Prop+Sem/Sur+Sg+Ela

LEXICON OY-plc
BERN-plc ;

LEXICON BERN-plc !!= @CODE@ Placenames
! Different lexicon for place names. Bern.
                   +N+Prop+Sem/Plc:%>             BERNDECL-PLC ;
 +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@:%>a@U.Cap.Opt@ VUONAK       ;

!!€gt-norm: Beijing # Even-syllable test
!!€ Beijing               Beijing+N+Prop+Sem/Plc+Sg+Nom
!!€ Beijingaj              Beijing+N+Prop+Sem/Plc+Sg+Ill
!!€ Beijingas              Beijing+N+Prop+Sem/Plc+Sg+Ela


LEXICON BERN-objsur !!= @CODE@  Names used as both objects and surnames. Not used in smj-propernouns
! Different lexicon for names that are both surnames and places.
 +N+Prop+Sem/Sur:%> BERNDECL ;
 +N+Prop+Sem/Obj:%> BERNDECL ;

!!€gt-norm: Stenbukk # Even-syllable test
!!€ Stenbukk                Stenbukk+N+Prop+Sem/Obj+Sg+Nom
!!€ Stenbukkaj              Stenbukk+N+Prop+Sem/Obj+Sg+Ill
!!€ Stenbukkas              Stenbukk+N+Prop+Sem/Obj+Sg+Ela

LEXICON BERN-orgsur !!= @CODE@ Names used for both organizations and surnames.Not used in smj-propernouns
! Different lexicon for names that are both surnames and places.
 +N+Prop+Sem/Sur:%> BERNDECL     ;
 +N+Prop+Sem/Org:%> BERNDECL-ORG ;

!!€gt-norm: Nord # Even-syllable test
!!€ Nord               Nord+N+Prop+Sem/Org+Sg+Nom
!!€ Nordaj              Nord+N+Prop+Sem/Org+Sg+Ill
!!€ Nordas              Nord+N+Prop+Sem/Org+Sg+Ela

LEXICON BERN-obj !!= @CODE@ Objects
! Different lexicon for organisations. Microsoft.
 +N+Prop+Sem/Obj:%> BERNDECL ;

!!€gt-norm: Sult # Even-syllable test
!!€ Sult                Sult+N+Prop+Sem/Obj+Sg+Nom
!!€ Sultaj              Sult+N+Prop+Sem/Obj+Sg+Ill
!!€ Sultas              Sult+N+Prop+Sem/Obj+Sg+Ela

LEXICON BERN-org !!= @CODE@ Organizations
! Different lexicon for organisations. Microsoft.
 +N+Prop+Sem/Org:%> BERNDECL-ORG ;

!!€gt-norm: Laks # Even-syllable test
!!€ Laks              Laks+N+Prop+Sem/Org+Sg+Nom
!!€ Laksaj              Laks+N+Prop+Sem/Org+Sg+Ill
!!€ Laksas              Laks+N+Prop+Sem/Org+Sg+Ela

LEXICON BERN-LOAN-org !!= @CODE@ Organizations loan.  Not used in smj-propernouns
! Different lexicon for organisations. Microsoft.
 +N+Prop+Sem/Org:%> BERNDECL-LOAN ;

!!€gt-norm: Reinsamelag # Even-syllable test
!!€ Reinsamelag               Reinsamelag+N+Prop+Sem/Org+Sg+Nom
!!$ Reinsamelagaj              Reinsamelag+N+Prop+Sem/Org+Sg+Ill
!!$ Reinsamelagas              Reinsamelag+N+Prop+Sem/Org+Sg+Ela

LEXICON BERN-LOAN-plc !!= @CODE@ Placenames loan. Not used in smj-propernouns
! Different lexicon for place names. Bern.
                           +N+Prop+Sem/Plc:%>             BERNDECL-LOAN ;
 +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@+Err/Sub:%>a@U.Cap.Opt@ VUONAK        ; !

!!€gt-norm: Mehamn # Even-syllable test
!!€ Mehamn               Mehamn+N+Prop+Sem/Plc+Sg+Nom
!!$ Mehamnaj              Mehamn+N+Prop+Sem/Plc+Sg+Ill
!!$ Mehamnas              Mehamn+N+Prop+Sem/Plc+Sg+Ela

LEXICON BERN-LOAN-obj !!= @CODE@ Objects loan. Not used in smj-propernouns
! Different lexicon for objects, concrete and abstract. Word.
 +N+Prop+Sem/Obj:%> BERNDECL-LOAN ;

!!€gt-norm: Verneplan # Even-syllable test
!!€ Verneplan                Verneplan+N+Prop+Sem/Obj+Sg+Nom
!!$ Verneplanaj              Verneplan+N+Prop+Sem/Obj+Sg+Ill
!!$ Verneplanas              Verneplan+N+Prop+Sem/Obj+Sg+Ela

!LEXICON BERN-femsur ! Not in use.
! Different lexicon for female persons & personal surnames.
! +N+Prop+Sem/Fem:%> BERNDECL ;
! +N+Prop+Sem/Sur:%> BERNDECL ;
! +N+Prop+Sem/Fem+Sg+Attr@U.Cap.Obl@:%>@U.Cap.Obl@ # ; !SUB

!LEXICON BERN-surplc
!! Different lexicon for names that are both surnames and places.
! +N+Prop+Sem/Sur:%> BERNDECL ;
! +N+Prop+Sem/Plc:%> BERNDECL_PLC-ORG ;
! +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@:%>a@U.Cap.Opt@ VUONAK ;

LEXICON BERN
 +N+Prop:%> BERNDECL ;

LEXICON BERNDECL
 @U.Cap.Obl@ BERNUCASE ;

LEXICON BERNDECL-ORG
 @U.Cap.Obl@ BERNUCASE-ORG ;

LEXICON BERNDECL-PLC
 @U.Cap.Obl@ BERNUCASE ;
 @U.Cap.Opt@ LASJ      ; ! XXX Sub?

LEXICON BERNDECL-LOAN
 @U.Cap.Obl@ BERNUCASE-LOAN ;
 @U.Cap.Opt@ LASJ           ; ! XXX Sub?


LEXICON BERNUCASE ! Final foot structure (X) => Loc:as
 +Sg+Nom:     K     ;
 +Sg+Nom:     RHyph ;
 +Sg+Ill:aj   K     ;
    +Ess:an   K     ;
 +Sg+Gen:a    K     ;
 +Sg+Gen:a    RHyph ;
 +Sg+Acc:av   K     ;
 +Sg+Ine:an   K     ;
 +Sg+Ela:as   K     ;
 +Pl+Nom:a    K     ;
 +Pl+Gen:aj   K     ;
 +Pl+Gen:aj   RHyph ;
 +Pl+Acc:ajt  K     ;
 +Pl+Ill:ajda K     ;
 +Pl+Ine:ajn  K     ;
 +Pl+Ela:ajs  K     ;
 +Pl+Com:aj   K     ;
 +Sg+Com:ajn  K     ;

LEXICON BERNUCASE-ORG ! Final foot structure (X) => Loc:as
 +Sg+Nom:    K     ;
 +Sg+Nom:    RHyph ;
 +Sg+Ill:aj  K     ;
    +Ess:an  K     ;
 +Sg+Gen:a   K     ;
 +Sg+Gen:a   RHyph ;
 +Sg+Acc:av  K     ;
 +Sg+Ine:an  K     ;
 +Sg+Ela:as  K     ;
! +Pl+Nom:a K ;
! +Pl+Gen:aj K ;
! +Pl+Gen:aj RHyph ;
! +Pl+Acc:ajt K ;
! +Pl+Ill:ajda K ;
! +Pl+Ine:ajn K ;
! +Pl+Ela:ajs K ;
! +Pl+Com:aj K ;
 +Sg+Com:ajn K     ;


LEXICON BERNUCASE-LOAN ! Final foot structure (X) => Loc:as
         +Sg+Nom:    K     ;
         +Sg+Nom:    RHyph ;
 +Sg+Ill+Err/Sub:aj  K     ; !
    +Ess+Err/Sub:an  K     ; !
 +Sg+Gen+Err/Sub:a   K     ; !
 +Sg+Gen+Err/Sub:a   RHyph ; !
 +Sg+Acc+Err/Sub:av  K     ; !
 +Sg+Ine+Err/Sub:an  K     ; !
 +Sg+Ela+Err/Sub:as  K     ; !
! +Pl+Nom+Err/Sub:a K ; !
! +Pl+Gen+Err/Sub:aj K ; !
! +Pl+Gen+Err/Sub:aj RHyph ; !
! +Pl+Acc+Err/Sub:ajt K ; !
! +Pl+Ill+Err/Sub:ajda K ; !
! +Pl+Ine+Err/Sub:ajn K ; !
! +Pl+Ela+Err/Sub:ajs K ; !
! +Pl+Com+Err/Sub:aj K ; !
 +Sg+Com+Err/Sub:ajn K     ; !

!! Words in lexicon LONDON end on conconant, no cg, case marking with -ij, -is. Last syllable is unstressed.

LEXICON LONDON-atr		!SUB
 +N+Prop+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ # ; !

LEXICON LONDON-sur !!= @CODE@ Odd-syllable. Surnames. Final foot structure (X.) and (X..) => Loc:%>is
! Different lexicon for personal surnames.
                            +N+Prop+Sem/Sur:%>            LONDONDECL ;
 +N+Prop+Sem/Sur+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #          ; ! ! it shall be used in cases like Konrad Nielsenii

!!€gt-norm: Åstot # Odd-syllable test
!!€ Åstot                Åstot+N+Prop+Sem/Sur+Sg+Nom
!!€ Åstodij              Åstot+N+Prop+Sem/Sur+Sg+Ill
!!€ Åstodis              Åstot+N+Prop+Sem/Sur+Sg+Ela

LEXICON LONDON-ani !!= @CODE@ Animals
 +N+Prop+Sem/Ani:%> LONDONDECL ;
!!€gt-norm: Jubmel # Odd-syllable test
!!€ Jubmel                Jubmel+N+Prop+Sem/Ani+Sg+Nom
!!€ Jubmelij              Jubmel+N+Prop+Sem/Ani+Sg+Ill
!!€ Jubmelis              Jubmel+N+Prop+Sem/Ani+Sg+Ela

LEXICON LONDON-org !!= @CODE@  Only singular Organizations
 +N+Prop+Sem/Org:%> LONDONDECL-ORG ;

!!€gt-norm: Klassekampen # Odd-syllable test
!!€ Klassekampen                Klassekampen+N+Prop+Sem/Org+Sg+Nom
!!€ Klassekampenij              Klassekampen+N+Prop+Sem/Org+Sg+Ill
!!€ Klassekampenis              Klassekampen+N+Prop+Sem/Org+Sg+Ela

LEXICON LONDON-mal !!= @CODE@ Male names
                            +N+Prop+Sem/Mal:%>            LONDONDECL ;
 +N+Prop+Sem/Mal+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #          ; ! ! it shall be used in cases like Konrad Nielsenii

!!€gt-norm: Mátteus # Odd-syllable test
!!€ Matteus                Matteus+N+Prop+Sem/Mal+Sg+Nom
!!€ Matteusij              Matteus+N+Prop+Sem/Mal+Sg+Ill
!!€ Matteusis              Matteus+N+Prop+Sem/Mal+Sg+Ela

LEXICON LONDON-malsur !!= @CODE@ Names that can be both male- and surnames. Not used in smj-propernouns
                            +N+Prop+Sem/Mal:%>            LONDONDECL ;
                            +N+Prop+Sem/Sur:%>            LONDONDECL ;
 +N+Prop+Sem/Mal+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #          ; ! ! it shall be used in cases like Konrad Nielsenii

!!€gt-norm: Timeus # Odd-syllable test
!!€ Timeus                Timeus+N+Prop+Sem/Mal+Sg+Nom
!!€ Timeusij              Timeus+N+Prop+Sem/Mal+Sg+Ill
!!€ Timeusis              Timeus+N+Prop+Sem/Mal+Sg+Ela

LEXICON LONDON-fem !!= @CODE@ Female names
                            +N+Prop+Sem/Fem:%>            LONDONDECL ;
 +N+Prop+Sem/Fem+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #          ; ! ! it shall be used in cases like Konrad Nielsenii

!!€gt-norm: Luhták # Odd-syllable test
!!€ Luhták                Luhták+N+Prop+Sem/Fem+Sg+Nom
!!€ Luhtágij              Luhták+N+Prop+Sem/Fem+Sg+Ill
!!€ Luhtágis              Luhták+N+Prop+Sem/Fem+Sg+Ela

LEXICON LONDON-malfem !!= @CODE@ Names that can be both male and female names.Not used in smj-propernouns
 +N+Prop+Sem/Fem:%> LONDONDECL ;
 +N+Prop+Sem/Mal:%> LONDONDECL ;

!!€gt-norm: Robin # Odd-syllable test
!!€ Robin                Robin+N+Prop+Sem/Fem+Sg+Nom
!!€ Robinij              Robin+N+Prop+Sem/Fem+Sg+Ill
!!€ Robinis              Robin+N+Prop+Sem/Fem+Sg+Ela

LEXICON LONDON-malplc !!= @CODE@ Names that can be both male- and placenames.Not used in smj-propernouns
 +N+Prop+Sem/Plc:%> LONDONDECL-PLC ;
 +N+Prop+Sem/Mal:%> LONDONDECL     ;

!!€gt-norm: Jergol # Odd-syllable test
!!€ Jergol               Jergol+N+Prop+Sem/Mal+Sg+Nom
!!€ Jergolij              Jergol+N+Prop+Sem/Mal+Sg+Ill
!!€ Jergolis              Jergol+N+Prop+Sem/Mal+Sg+Ela

LEXICON LONDON-plc !!= @CODE@ Only singular. Placenames
 +N+Prop+Sem/Plc:%> LONDONDECL-PLC ;

!!€gt-norm: Njierek # Odd-syllable test
!!€ Njierek                Njierek+N+Prop+Sem/Plc+Sg+Nom
!!€ Njieregij              Njierek+N+Prop+Sem/Plc+Sg+Ill
!!€ Njieregis              Njierek+N+Prop+Sem/Plc+Sg+Ela

LEXICON LONDON-orgsur !!= @CODE@ Names that can be both organizations and surnames.Not used in Smj-propernouns
                            +N+Prop+Sem/Org:%>            LONDONDECL-ORG ;
                            +N+Prop+Sem/Sur:%>            LONDONDECL     ;
 +N+Prop+Sem/Sur+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ #              ; ! ! it shall be used in cases like Konrad Nielsenii

!!€gt-norm: Rieser # Odd-syllable test
!!€ Rieser                Rieser+N+Prop+Sem/Sur+Sg+Nom
!!€ Rieserij              Rieser+N+Prop+Sem/Sur+Sg+Ill
!!€ Rieseris              Rieser+N+Prop+Sem/Sur+Sg+Ela

LEXICON LONDON-obj !!= @CODE@ Objects. Not used in smj-propernouns
 +N+Prop+Sem/Obj:%> LONDONDECL ;

!!€gt-norm: Rovdjursutredningen # Odd-syllable test
!!€ Rovdjursutredningen                Rovdjursutredningen+N+Prop+Sem/Obj+Sg+Nom
!!€ Rovdjursutredningenij              Rovdjursutredningen+N+Prop+Sem/Obj+Sg+Ill
!!€ Rovdjursutredningenis              Rovdjursutredningen+N+Prop+Sem/Obj+Sg+Ela

LEXICON LONDON-LOAN-obj !!= @CODE@ Objects loan. Not used in smj-propernouns
 +N+Prop+Sem/Obj:%> LONDONDECL-LOAN ;

!!€gt-norm: Sameloven # Odd-syllable test
!!€ Sameloven                Sameloven+N+Prop+Sem/Obj+Sg+Nom
!!$ Samelovenij              Sameloven+N+Prop+Sem/Obj+Sg+Ill
!!$ Samelovenis              Sameloven+N+Prop+Sem/Obj+Sg+Ela

LEXICON LONDON-LOAN-plc !!= @CODE@ Only nominatives. Placenames loan. Not used in Smj-propernouns
 +N+Prop+Sem/Plc:%> LONDONDECL-LOAN ;

!!€gt-norm: Jordandalen # Odd-syllable test
!!€ Jordandalen                Jordandalen+N+Prop+Sem/Plc+Sg+Nom
!!$ Jordandalenij              Jordandalen+N+Prop+Sem/Plc+Sg+Ill
!!$ Jordandalenis              Jordandalen+N+Prop+Sem/Plc+Sg+Ela

LEXICON LONDON-LOAN-org !!= @CODE@  Only nominative. Organizations loan.Not used in smj-propernouns
 +N+Prop+Sem/Org:%> LONDONDECL-LOAN ;

!!€gt-norm: Samfunnsavdelingen # Odd-syllable test
!!€ Samfunnsavdelingen                Samfunnsavdelingen+N+Prop+Sem/Org+Sg+Nom
!!$ Samfunnsavdelingenij              Samfunnsavdelingen+N+Prop+Sem/Org+Sg+Ill
!!$ Samfunnsavdelingenis              Samfunnsavdelingen+N+Prop+Sem/Org+Sg+Ela

!LEXICON LONDON-surplc
! +N+Prop+Sem/Plc:%> LONDONDECL_PLC-ORG ;
! +N+Prop+Sem/Sur:%> LONDONDECL ;
! +N+Prop+Sem/Sur+Sg+Attr@U.Cap.Obl@+Err/Sub:%>@U.Cap.Obl@ # ;  ! ! it shall be used in cases like Konrad Nielsenii

LEXICON LONDONDECL
 @U.Cap.Obl@ LONDONUCASE ;

LEXICON LONDONDECL-ORG
 @U.Cap.Obl@ LONDONUCASE-ORG ;

LEXICON LONDONDECL-PLC
 @U.Cap.Obl@ LONDONUCASE ;
 @U.Cap.Opt@ LASJ        ;

LEXICON LONDONDECL-LOAN
 @U.Cap.Obl@ LONDONUCASE-LOAN ;
 @U.Cap.Opt@ LASJ             ;


LEXICON LONDONUCASE ! Final foot structure (X.) and (X..) => Loc:is
         +Sg+Nom:     K     ;
         +Sg+Nom:     RHyph ;
         +Sg+Gen:a    K     ;
         +Sg+Gen:a    RHyph ;
         +Sg+Acc:av   K     ;
         +Sg+Ill:ij   K     ;
         +Sg+Ine:in   K     ;
         +Sg+Ela:is   K     ;
 +Sg+Ill+Err/Sub:aj   K     ; !
 +Sg+Ine+Err/Sub:an   K     ; !
 +Sg+Ela+Err/Sub:as   K     ; !
         +Sg+Com:ijn  K     ;
            +Ess:in   K     ;
         +Pl+Nom:a    K     ;
         +Pl+Gen:ij   K     ;
         +Pl+Gen:ij   RHyph ;
         +Pl+Acc:ijt  K     ;
         +Pl+Ill:ijda K     ;
         +Pl+Ine:ijn  K     ;
         +Pl+Ela:ijs  K     ;
         +Pl+Com:ij   K     ;

LEXICON LONDONUCASE-ORG ! Final foot structure (X.) and (X..) => Loc:is
         +Sg+Nom:    K     ;
         +Sg+Nom:    RHyph ;
         +Sg+Gen:a   K     ;
         +Sg+Gen:a   RHyph ;
         +Sg+Acc:av  K     ;
         +Sg+Ill:ij  K     ;
         +Sg+Ine:in  K     ;
         +Sg+Ela:is  K     ;
 +Sg+Ill+Err/Sub:aj  K     ; !
 +Sg+Ine+Err/Sub:an  K     ; !
 +Sg+Ela+Err/Sub:as  K     ; !
         +Sg+Com:ijn K     ;
            +Ess:in  K     ;
! +Pl+Nom:a K ;
! +Pl+Gen:ij K ;
! +Pl+Gen:ij RHyph ;
! +Pl+Acc:ijt K ;
! +Pl+Ill:ijda K ;
! +Pl+Ine:ijn K ;
! +Pl+Ela:ijs K ;
! +Pl+Com:ij K ;


LEXICON LONDONUCASE-LOAN ! Final foot structure (X.) and (X..) => Loc:is
         +Sg+Nom:    K     ;
         +Sg+Nom:    RHyph ;
 +Sg+Gen+Err/Sub:a   K     ; !
 +Sg+Gen+Err/Sub:a   RHyph ; !
 +Sg+Acc+Err/Sub:av  K     ; !
 +Sg+Ill+Err/Sub:ij  K     ; !
 +Sg+Ine+Err/Sub:in  K     ; !
 +Sg+Ela+Err/Sub:is  K     ; !
 +Sg+Ill+Err/Sub:aj  K     ; !
 +Sg+Ine+Err/Sub:an  K     ; !
 +Sg+Ela+Err/Sub:as  K     ; !
 +Sg+Com+Err/Sub:ijn K     ; !
    +Ess+Err/Sub:in  K     ; !
! +Pl+Nom+Err/Sub:a K ; !
! +Pl+Gen+Err/Sub:ij K ; !
! +Pl+Gen+Err/Sub:ij RHyph ;      !
! +Pl+Acc+Err/Sub:ijt K ; !
! +Pl+Ill+Err/Sub:ijda K ; !
! +Pl+Ine+Err/Sub:ijn K ; !
! +Pl+Ela+Err/Sub:ijs K ; !
! +Pl+Com+Err/Sub:ij K ; !


!! Words in lexicon ANAR are trisyllable, end on conconant, no cg. Ill -ij, ela -is. Same as LONDON?

LEXICON ANAR-ani !!= @CODE@ Animals. Not used in smj-propernouns
 +N+Prop+Sem/Ani:%> ANARDECL ;

!!€gt-norm: R-tjuoivvat # Odd-syllable test
!!€ R-tjuoivvat                R-tjuoivvat+N+Prop+Sem/Ani+Sg+Nom
!!€ R-tjuoivvagij              R-tjuoivvat+N+Prop+Sem/Ani+Sg+Ill
!!€ R-tjuoivvagis              R-tjuoivvat+N+Prop+Sem/Ani+Sg+Ela

LEXICON ANAR-obj !!= @CODE@ Objects. Not used in smj-propernouns
 +N+Prop+Sem/Obj:%> ANARDECL ;

!!€gt-norm: Divvun # Odd-syllable test
!!€ Divvom                Divvun+N+Prop+Sem/Obj+Sg+Nom
!!€ Divvomij              Divvun+N+Prop+Sem/Obj+Sg+Ill
!!€ Divvomis              Divvun+N+Prop+Sem/Obj+Sg+Ela

LEXICON ANAR-org !!= @CODE@ Organizations. Not used in smj-propernouns
 +N+Prop+Sem/Org:%> ANARDECL-ORG ;

!!€gt-norm: Nussir # Odd-syllable test
!!€ Nussir                Nussir+N+Prop+Sem/Org+Sg+Nom
!!€ Nussirij              Nussir+N+Prop+Sem/Org+Sg+Ill
!!€ Nussiris              Nussir+N+Prop+Sem/Org+Sg+Ela

LEXICON ANAR-mal !!= @CODE@ Male names
 +N+Prop+Sem/Mal:%> ANARDECL ;

!!€gt-norm: Joret # Odd-syllable test
!!€ Joret                Joret+N+Prop+Sem/Mal+Sg+Nom
!!€ Joredij              Joret+N+Prop+Sem/Mal+Sg+Ill
!!€ Joredis              Joret+N+Prop+Sem/Mal+Sg+Ela

LEXICON ANAR-fem !!= @CODE@ Female names. Not used in smj-propernouns
 +N+Prop+Sem/Fem:%> ANARDECL ;

!!€gt-norm: Biret # Odd-syllable test
!!€ Biret                Biret+N+Prop+Sem/Fem+Sg+Nom
!!€ Biredij              Biret+N+Prop+Sem/Fem+Sg+Ill
!!€ Biredis              Biret+N+Prop+Sem/Fem+Sg+Ela

LEXICON ANAR-sur !!= @CODE@ Surnames. Not used in smj-propernouns
 +N+Prop+Sem/Sur:%> ANARDECL ;

!!€gt-norm: Guhtor # Odd-syllable test
!!€ Guhtor                Guhtor+N+Prop+Sem/Sur+Sg+Nom
!!€ Guhtorij              Guhtor+N+Prop+Sem/Sur+Sg+Ill
!!€ Guhtoris              Guhtor+N+Prop+Sem/Sur+Sg+Ela

LEXICON ANAR-plc !!= @CODE@  Place names
 +N+Prop+Sem/Plc:%> ANARDECL-PLC ;

!!€gt-norm: Sáller # Odd-syllable test
!!€ Sáller                Sáller+N+Prop+Sem/Plc+Sg+Nom
!!€ Sállerij              Sáller+N+Prop+Sem/Plc+Sg+Ill
!!€ Sálleris              Sáller+N+Prop+Sem/Plc+Sg+Ela


LEXICON ANARDECL ! Cns-fin, no cons.grad.
 @U.Cap.Obl@ ANAR-OBL ;

LEXICON ANARDECL-ORG ! Cns-fin, no cons.grad.
 @U.Cap.Obl@ ANAR-OBL-ORG ;

LEXICON ANARDECL-PLC ! Cns-fin, no cons.grad.
 @U.Cap.Obl@ ANAR-OBL ;
 @U.Cap.Opt@ LASJ     ; ! XXX Sub?

LEXICON ANAR-OBL
 @U.Cap.Obl@ LONDONDECL ;

LEXICON ANAR-OBL-ORG
 @U.Cap.Obl@ LONDONDECL-ORG ;


!! Words in lexicon DUORTNUS end on conconant and have cg. Ill -ij, ela -is.

LEXICON DUORTNUS-mal !!= @CODE@ Male names
 +N+Prop+Sem/Mal:%> DUORTNUSDECL ;

!!€gt-norm: Mihkal # Odd-syllable test
!!€ Mihkal                Mihkal+N+Prop+Sem/Mal+Sg+Nom
!!€ Mihkkalij               Mihkal+N+Prop+Sem/Mal+Sg+Ill
!!€ Mihkkalis               Mihkal+N+Prop+Sem/Mal+Sg+Ela

LEXICON DUORTNUS-sur !!= @CODE@ Male names
 +N+Prop+Sem/Sur:%> DUORTNUSDECL ;


LEXICON DUORTNUS-obj !!= @CODE@ Odd-syllable ending on consonant, with cg. Objects
 +N+Prop+Sem/Obj:%> DUORTNUSDECL ;

!!€gt-norm: Artemistiempel # Odd-syllable test
!!€ Artemistiempel                Artemistiempel+N+Prop+Sem/Obj+Sg+Nom
!!€ Artemistiemmpelij               Artemistiempel+N+Prop+Sem/Obj+Sg+Ill
!!€ Artemistiemmpelis               Artemistiempel+N+Prop+Sem/Obj+Sg+Ela

LEXICON DUORTNUS-org !!= @CODE@ Odd-syllable ending on consonant, with cg. Organizations
 +N+Prop+Sem/Org:%> DUORTNUSDECL-ORG ;

!!€gt-norm: Europarádeministarjuogos # Odd-syllable test
!!€ Europarádeministarjuogos                Europarádeministarjuogos+N+Prop+Sem/Org+Sg+Nom
!!€ Europarádeministarjuohkusij               Europarádeministarjuogos+N+Prop+Sem/Org+Sg+Ill
!!€ Europarádeministarjuohkusis               Europarádeministarjuogos+N+Prop+Sem/Org+Sg+Ela

LEXICON DUORTNUS-plc !!= @CODE@ Odd-syllable ending on consonant, with cg.Placenames
 +N+Prop+Sem/Plc:%> DUORTNUSDECL-PLC ;

!!€gt-norm: Hardangerduottar # Odd-syllable test
!!€ Hardangerduottar               Hardangerduottar+N+Prop+Sem/Plc+Sg+Nom
!!€ Hardangerduoddara               Hardangerduottar+N+Prop+Sem/Plc+Sg+Gen
!!€ Hardangerduoddarij               Hardangerduottar+N+Prop+Sem/Plc+Sg+Ill
!!€ Hardangerduoddaris               Hardangerduottar+N+Prop+Sem/Plc+Sg+Ela

!LEXICON DUORTNUS-fem
! +N+Prop+Sem/Fem:%> DUORTNUSDECL ;

!LEXICON DUORTNUS-sur
! +N+Prop+Sem/Sur:%> DUORTNUSDECL ;

LEXICON DUORTNUSDECL
 @U.Cap.Obl@ DUORTNUSUCASE ;

LEXICON DUORTNUSDECL-ORG
 @U.Cap.Obl@ DUORTNUSUCASE-ORG ;

LEXICON DUORTNUSDECL-PLC
 @U.Cap.Obl@ DUORTNUSUCASE ;
 @U.Cap.Opt@ LASJ          ; ! XXX Sub?

LEXICON DUORTNUSUCASE ! Cns-final, cons.grad.
        :Q1     NE-ODDCASE ; ! Q1 is general WeG
 +Sg+Nom:       RHyph      ;
 +Sg+Gen:X5a    K          ;
 +Sg+Gen:X5a    RHyph      ;
 +Sg+Acc:X5av   K          ;
 +Sg+Ill:X5ij   K          ;
 +Sg+Ine:X5in   K          ;
 +Sg+Ela:X5is   K          ;
 +Sg+Com:X5ijn  K          ;
    +Ess:X5in   K          ;
 +Pl+Nom:X5a    K          ;
 +Pl+Gen:X5ij   K          ;
 +Pl+Gen:X5ij   RHyph      ;
 +Pl+Acc:X5ijt  K          ;
 +Pl+Ill:X5ijda K          ;
 +Pl+Ine:X5ijn  K          ;
 +Pl+Ela:X5ijs  K          ;
 +Pl+Com:X5ij   K          ;

LEXICON DUORTNUSUCASE-ORG ! Cns-final, cons.grad.
        :Q1    NE-ODDCASE ; ! Q1 is general WeG
 +Sg+Nom:      RHyph      ;
 +Sg+Gen:X5a   K          ;
 +Sg+Gen:X5a   RHyph      ;
 +Sg+Acc:X5av  K          ;
 +Sg+Ill:X5ij  K          ;
 +Sg+Ine:X5in  K          ;
 +Sg+Ela:X5is  K          ;
 +Sg+Com:X5ijn K          ;
    +Ess:X5in  K          ;
! +Pl+Nom:X5a K ;
! +Pl+Gen:X5ij K ;
! +Pl+Gen:X5ij RHyph ;
! +Pl+Acc:X5ijt K ;
! +Pl+Ill:X5ijda K ;
! +Pl+Ine:X5ijn K ;
! +Pl+Ela:X5ijs K ;
! +Pl+Com:X5ij K ;


LEXICON HEANDARAT-plc !!= @CODE@  Not in use in smj-propernouns. Place names.
                   +N+Prop+Sem/Plc:%>               HEANDARATDECL-PLC ;
 +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@:%>aQ1@U.Cap.Opt@ VUONAK            ;

!!€gt-norm: Cohkolat # 4-syllable test
!!€ Cohkolat                 Cohkolat+N+Prop+Sem/Plc+Sg+Nom
!!€ Cohkolatij               Cohkolat+N+Prop+Sem/Plc+Sg+Ill
!!€ Cohkolatis               Cohkolat+N+Prop+Sem/Plc+Sg+Ela

!LEXICON HEANDARAT-ani
! +N+Prop+Sem/Ani:%> HEANDARATDECL ;

!LEXICON HEANDARAT-obj
! +N+Prop+Sem/Obj:%> HEANDARATDECL ;

!LEXICON HEANDARAT-org
! +N+Prop+Sem/Org:%> HEANDARATDECL_PLC-ORG ;

!LEXICON HEANDARAT-mal
! +N+Prop+Sem/Mal:%>  HEANDARATDECL ;

!LEXICON HEANDARAT-fem
! +N+Prop+Sem/Fem:%>  HEANDARATDECL ;

!LEXICON HEANDARAT-sur
! +N+Prop+Sem/Sur:%> HEANDARATDECL ;

!LEXICON HEANDARATDECL
! @U.Cap.Obl@ HEANDARATUCASE ;

LEXICON HEANDARATDECL-PLC
 @U.Cap.Obl@ HEANDARATUCASE ;
 @U.Cap.Opt@ LASJ           ; ! XXX Sub?


LEXICON HEANDARATUCASE ! Bisyll. Non-Gradating C-Proper Names
 +Sg+Nom:a       K     ;
 +Sg+Nom:a       RHyph ;
    +Ess:an      K     ;
 +Sg+Ill:aj      K     ;
 +Sg+Gen:aQ1     K     ;
 +Sg+Gen:aQ1     RHyph ;
 +Sg+Acc:aQ1v    K     ;
 +Sg+Ine:aQ1n    K     ;
 +Sg+Ela:aQ1s    K     ;
 +Pl+Nom:aQ1     K     ;
 +Pl+Gen:aQ1j    K     ;
 +Pl+Gen:aQ1j    RHyph ;
 +Pl+Acc:aQ1jt   K     ;
 +Pl+Ill:aQ1jda  K     ;
 +Pl+Ine:aQ1jn   K     ;
 +Pl+Ela:aQ1js   K     ;
 +Pl+Com:aQ1j    K     ;
 +Sg+Com:aQ1X5jn K     ;

LEXICON HEANDARATUCASE-ORG ! Bisyll. Non-Gradating C-Proper Names
 +Sg+Nom:a       K     ;
 +Sg+Nom:a       RHyph ;
    +Ess:an      K     ;
 +Sg+Ill:aj      K     ;
 +Sg+Gen:aQ1     K     ;
 +Sg+Gen:aQ1     RHyph ;
 +Sg+Acc:aQ1v    K     ;
 +Sg+Ine:aQ1n    K     ;
 +Sg+Ela:aQ1s    K     ;
! +Pl+Nom:aQ1 K ;
! +Pl+Gen:aQ1j K ;
! +Pl+Gen:aQ1j RHyph ;
! +Pl+Acc:aQ1jt K ;
! +Pl+Ill:aQ1jda K ;
! +Pl+Ine:aQ1jn K ;
! +Pl+Ela:aQ1js K ;
! +Pl+Com:aQ1j K ;
 +Sg+Com:aQ1X5jn K     ;

!! Words in lexicon BASUDIS are 4-syllable, end on conconant and have cg. Ill -aj, ela -as.


LEXICON BASUDIS-org!!= @CODE@  Only singular. Placenames
 +N+Prop+Sem/Org:%> BASUDISDECL-ORG ;

!!€gt-norm: Ádjagas # 4-syllable test
!!€ Ádjagas                 Ádjagas+N+Prop+Sem/Org+Sg+Nom
!!€ Ádjagassaj              Ádjagas+N+Prop+Sem/Org+Sg+Ill
!!€ Ádjagasás               Ádjagas+N+Prop+Sem/Org+Sg+Ela

LEXICON BASUDIS-mal !!= @CODE@  Male names
 +N+Prop+Sem/Mal:%> BASUDISDECL ;

!!€gt-norm: Ájluhasj # 4-syllable test
!!€ Ájluhasj                 Ájluhasj+N+Prop+Sem/Mal+Sg+Nom
!!€ Ájluhattjaj              Ájluhasj+N+Prop+Sem/Mal+Sg+Ill
!!€ Ájluhattjas               Ájluhasj+N+Prop+Sem/Mal+Sg+Ela

LEXICON BASUDIS-fem !!= @CODE@ Female names
 +N+Prop+Sem/Fem:%> BASUDISDECL ;

!!€gt-norm: Magdalas # 4-syllable test
!!€ Magdalas                Magdalas+N+Prop+Sem/Fem+Sg+Nom
!!€ Magdalassaj              Magdalas+N+Prop+Sem/Fem+Sg+Ill
!!€ Magdalasás               Magdalas+N+Prop+Sem/Fem+Sg+Ela

LEXICON BASUDIS-sur !!= @CODE@ Surnames
 +N+Prop+Sem/Sur:%> BASUDISDECL ;

!!€gt-norm: Listárjjus # 4-syllable test
!!€ Listárjjus                 Listárjjus+N+Prop+Sem/Sur+Sg+Nom
!!€ Listárjjusij              Listárjjus+N+Prop+Sem/Sur+Sg+Ill
!!€ Listárjjusis               Listárjjus+N+Prop+Sem/Sur+Sg+Ela

LEXICON BASUDIS-plc !!= @CODE@ Place names
                   +N+Prop+Sem/Plc:%>               BASUDISDECL-PLC ;
 +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@:%>aQ1@U.Cap.Opt@ VUONAK          ;

!!€gt-norm: Ulldevis # 4-syllable test
!!€ Ulldevis                 Ulldevis+N+Prop+Sem/Plc+Sg+Nom
!!€ Ulldevissaj              Ulldevis+N+Prop+Sem/Plc+Sg+Ill
!!€ Ulldevisás               Ulldevis+N+Prop+Sem/Plc+Sg+Ela

!LEXICON BASUDIS-obj
! +N+Prop+Sem/Obj:%> BASUDISDECL ;

LEXICON BASUDISDECL
 @U.Cap.Obl@ BASUDISUCASE ;

LEXICON BASUDISDECL-PLC
 @U.Cap.Obl@ BASUDISUCASE ;
! @U.Cap.Opt@+Err/Sub:@U.Cap.Opt@  LASJ; !

LEXICON BASUDISDECL-ORG
 @U.Cap.Obl@ BASUDISUCASE-ORG ;

LEXICON BASUDISUCASE ! Bisyll. Non-Gradating C-Proper Names
 +Sg+Nom:        K     ;
 +Sg+Nom:        RHyph ;
    +Ess:an      K     ;
 +Sg+Ill:aj      K     ;
 +Sg+Gen:aQ1     K     ;
 +Sg+Gen:aQ1     RHyph ;
 +Sg+Acc:aQ1v    K     ;
 +Sg+Ine:aQ1n    K     ;
 +Sg+Ela:aQ1s    K     ;
 +Pl+Nom:aQ1     K     ;
 +Pl+Gen:aQ1j    K     ;
 +Pl+Gen:aQ1j    RHyph ;
 +Pl+Acc:aQ1jt   K     ;
 +Pl+Ill:aQ1jda  K     ;
 +Pl+Ine:aQ1jn   K     ;
 +Pl+Ela:aQ1js   K     ;
 +Pl+Com:aQ1j    K     ;
 +Sg+Com:aQ1X5jn K     ;

LEXICON BASUDISUCASE-ORG ! Bisyll. Non-Gradating C-Proper Names
 +Sg+Nom:        K     ;
 +Sg+Nom:        RHyph ;
    +Ess:an      K     ;
 +Sg+Ill:aj      K     ;
 +Sg+Gen:aQ1     K     ;
 +Sg+Gen:aQ1     RHyph ;
 +Sg+Acc:aQ1v    K     ;
 +Sg+Ine:aQ1n    K     ;
 +Sg+Ela:aQ1s    K     ;
! +Pl+Nom:aQ1 K ;
! +Pl+Gen:aQ1j K ;
! +Pl+Gen:aQ1j RHyph ;
! +Pl+Acc:aQ1jt K ;
! +Pl+Ill:aQ1jda K ;
! +Pl+Ine:aQ1jn K ;
! +Pl+Ela:aQ1js K ;
! +Pl+Com:aQ1j K ;
 +Sg+Com:aQ1X5jn K     ;

!! Words in lexicon DAVVISUOLLU are contracted propernouns edning on -j.

LEXICON DAVVISUOLU-plc !!= @CODE@ Contracted stems ending on -oj. Place names.
                   +N+Prop+Sem/Plc:%>            DAVVISUOLUDECL ;
 +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@:%>@U.Cap.Opt@ VUONAK         ;

!!€gt-norm: Victoriasuoloj # Contracted test
!!€ Victoriasuoloj                Victoriasuoloj+N+Prop+Sem/Plc+Sg+Nom
!!€ Victoriasuolluj               Victoriasuoloj+N+Prop+Sem/Plc+Sg+Ill
!!€ Victoriasuollus               Victoriasuoloj+N+Prop+Sem/Plc+Sg+Ela

LEXICON DAVVISUOLUDECL !Inanim. Contracted -Names - from DAVVISUOLU
 @U.Cap.Obl@ DAVVISUOLU-OBL ;

LEXICON DAVVISUOLU-OBL
   +Sg+Nom:Q3j    K     ;
      +Ess:Q3n    K     ;
 +CmpN/SgN:Q3j    RHyph ;
   +Sg+Ill:X4j    K     ;
   +Sg+Gen:X4     K     ;
   +Sg+Acc:X4v    K     ;
   +Sg+Ine:X4n    K     ;
   +Sg+Ela:X4s    K     ;
! +Pl+Nom:X4 K ;
! +Pl+Gen:X4j K ;
! +Pl+Acc:X4jt K ;
! +Pl+Ill:X4jda K ;
! +Pl+Ine:X4jn K ;
! +Pl+Ela:X4js K ;
! +Pl+Com:X4j K ;
   +Sg+Com:X4X5jn K     ; ! X5 for o:u in ComSg. Note two dummies in a row!
      +Ess:n      K     ;

!! Words in lexicon GEAVNNIS are contracted propernouns ending on -s.

!LEXICON GEAVNNIS-obj
! +N+Prop+Sem/Obj:%> GEAVNNISDECL ;

!LEXICON GEAVNNIS-org
! +N+Prop+Sem/Org:%> GEAVNNISDECL_PLC-ORG ;

!LEXICON GEAVNNIS-mal
! +N+Prop+Sem/Mal:%>  GEAVNNISDECL ;

!LEXICON GEAVNNIS-fem
! +N+Prop+Sem/Fem:%>  GEAVNNISDECL ;

!LEXICON GEAVNNIS-sur
! +N+Prop+Sem/Sur:%> GEAVNNISDECL ;


LEXICON GEAVNNIS-plc !!= @CODE@  Contracted stems ending on -s Place names
                   +N+Prop+Sem/Plc:%>              GEAVNNISDECL-PLC ;
 +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@:%>Y7@U.Cap.Opt@ VUONAK           ;

!!€gt-norm: Gaza-Sárges # Contracted test
!!€ Gaza-Sárges                 Gaza-Sárges+N+Prop+Sem/Plc+Sg+Nom
!!€ Gaza-Sárggáj              Gaza-Sárges+N+Prop+Sem/Plc+Sg+Ill
!!€ Gaza-Sárggás               Gaza-Sárges+N+Prop+Sem/Plc+Sg+Ela


LEXICON GEAVNNISDECL
 @U.Cap.Obl@ GEAVNNISUCASE ;

LEXICON GEAVNNISDECL-ORG
 @U.Cap.Obl@ GEAVNNISUCASE-ORG ;

LEXICON GEAVNNISDECL-PLC
 @U.Cap.Obl@ GEAVNNISUCASE ;
 @U.Cap.Opt@ LASJ          ; ! XXX Sub?

LEXICON GEAVNNISUCASE
 +Sg+Nom:Q1s    K     ;
 +Sg+Nom:Q1s    RHyph ;
    +Ess:Q1n    K     ;
 +Sg+Ill:X4j    K     ;
    +Ess:X4n    K     ;
 +Sg+Gen:X4     K     ;
 +Sg+Gen:X4     RHyph ;
 +Sg+Acc:X4v    K     ;
 +Sg+Ine:X4n    K     ;
 +Sg+Ela:X4s    K     ;
 +Pl+Nom:X4     K     ;
 +Pl+Gen:X4j    K     ;
 +Pl+Gen:X4j    RHyph ;
 +Pl+Acc:X4jt   K     ;
 +Pl+Ill:X4jda  K     ;
 +Pl+Ine:X4jn   K     ;
 +Pl+Ela:X4js   K     ;
 +Pl+Com:X4j    K     ;
 +Sg+Com:X4X5jn K     ;

LEXICON GEAVNNISUCASE-ORG
 +Sg+Nom:Q1s    K     ;
 +Sg+Nom:Q1s    RHyph ;
    +Ess:Q1n    K     ;
 +Sg+Ill:X4j    K     ;
    +Ess:X4n    K     ;
 +Sg+Gen:X4     K     ;
 +Sg+Gen:X4     RHyph ;
 +Sg+Acc:X4v    K     ;
 +Sg+Ine:X4n    K     ;
 +Sg+Ela:X4s    K     ;
! +Pl+Nom:X4 K ;
! +Pl+Gen:X4j K ;
! +Pl+Gen:X4j RHyph ;
! +Pl+Acc:X4jt K ;
! +Pl+Ill:X4jda K ;
! +Pl+Ine:X4jn K ;
! +Pl+Ela:X4js K ;
! +Pl+Com:X4j K ;
 +Sg+Com:X4X5jn K     ;

!! Words in lexicon VARGGAT even-syllable sámi plurals .

LEXICON VARGGAT-plc !!= @CODE@  Plural stems, sáme names.  Place names
                   +N+Prop+Sem/Plc:%>              VARGGATDECL ;
 +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@:%>Q1@U.Cap.Opt@ VUONAK      ;

!!€gt-norm: Bieva #  test
!!€ Bieva               Bieva+N+Prop+Sem/Plc+Sg+Nom
!!€ Bievijda              Bieva+N+Prop+Sem/Plc+Sg+Ill
!!€ Bievijs              Bieva+N+Prop+Sem/Plc+Sg+Ela

LEXICON VARGGAT-org !!= @CODE@  Organizations. Not In use
 +N+Prop+Sem/Org:%> VARGGATDECL ;
! +N+Prop+Sem/Org+Sg+Gen@U.Cap.Opt@:%>Q1@U.Cap.Opt@ VUONAK ;

!LEXICON VARGGAT-obj
! +N+Prop+Sem/Obj:%> VARGGATDECL ;
! +N+Prop+Sem/Obj+Sg+Gen@U.Cap.Opt@:%>Q1@U.Cap.Opt@ VUONAK ;


LEXICON VARGGATDECL
 @U.Cap.Obl@ VARGGATUCASE ;
 @U.Cap.Opt@ LASJ         ; ! XXX Sub?

LEXICON VARGGATUCASE
    +Ess:n     K     ;
 +Pl+Nom:Q1    K     ;
 +Pl+Gen:Q1j   K     ;
 +Pl+Gen:Q1j   RHyph ;
 +Pl+Acc:Q1jt  K     ;
 +Pl+Ill:Q1jda K     ;
 +Pl+Ine:Q1jn  K     ;
 +Pl+Ela:Q1js  K     ;
 +Pl+Com:Q1j   K     ;
! +Sg+Com:Q1X5jn K ;



!! Words in lexicon ALEUHTAT even-syllables plurals that aren't sámi..

LEXICON ALEUHTAT-plc !!= @CODE@ Plural names, not sami names. like -váre, -gårtje
        +N+Prop+Sem/Plc: ALEUHTATDECL-PLC ;

!!€gt-norm: Altaiváre #  test
!!$ Altaivárre              Altaiváre+N+Prop+Sem/Plc+Sg+Nom
!!€ Altaiváre               Altaiváre+N+Prop+Sem/Plc+Pl+Nom
!!€ Altaivárijda            Altaiváre+N+Prop+Sem/Plc+Pl+Ill
!!€ Altaivárijs             Altaiváre+N+Prop+Sem/Plc+Pl+Ela

!LEXICON ALEUHTAT-org
! +N+Prop+Sem/Org: ALEUHTATDECL-ORG ;

LEXICON ALEUHTATDECL-PLC
        @U.Cap.Obl@             ALEUHTATCASE ;
        @U.Cap.Opt@             LASJ         ; ! XXX Sub?
 +Sg+Gen@U.Cap.Opt@:@U.Cap.Opt@ VUONAK       ;

!LEXICON ALEUHTATDECL-ORG
!        @U.Cap.Obl@             ALEUHTATCASE ;
!        @U.Cap.Opt@             LASJ         ; ! XXX Sub?

LEXICON ALEUHTATCASE
    +Ess:%>n   K     ;
 +Pl+Nom:      K     ;
 +Pl+Gen:%>j   K     ;
 +Pl+Gen:%>j   RHyph ;
 +Pl+Acc:%>jt  K     ;
 +Pl+Ill:%>jda K     ;
 +Pl+Ine:%>jn  K     ;
 +Pl+Ela:%>js  K     ;
 +Pl+Com:%>j   K     ;
!+Sg+Com:%>jn  K     ;

!! Words in lexicon SUOLLOT are contracted plurals .

LEXICON SULLOT-plc !!= @CODE@ Plural names, only names ending on -suollu.
        +N+Prop+Sem/Plc: SULLOTDECL-PLC ;

LEXICON SULLOTDECL-PLC
        @U.Cap.Obl@             SULLOTCASE ;
        @U.Cap.Opt@             LASJ       ;
 +Sg+Gen@U.Cap.Opt@:@U.Cap.Opt@ VUONAK     ;

LEXICON SULLOTCASE
    +Ess:%>Q3n    K     ;
    +Ess:%>X4n    K     ;
 +Pl+Nom:%>X4     K     ;
 +Pl+Gen:%>X4j    K     ;
 +Pl+Gen:%>X4j    RHyph ;
 +Pl+Acc:%>X4jt   K     ;
 +Pl+Ill:%>X4jda  K     ;
 +Pl+Ine:%>X4jn   K     ;
 +Pl+Ela:%>X4js   K     ;
 +Pl+Com:%>X4j    K     ;
!+Sg+Com:%>X4X5jn K     ;

!! Words in lexicon EATNAMAT are odd-syllable plurals .

LEXICON EATNAMAT-plc
 +N+Prop+Sem/Plc: EATNAMATDECL-PLC ;

LEXICON EATNAMAT-org !!= @CODE@  Organizations
 +N+Prop+Sem/Org: EATNAMATDECL-ORG ;

!!€gt-norm: Vuorká-Dávvira # Contrcted test
!!$ Vuorká-Dávvir               Vuorká-Dávvir+N+Prop+Sem/Plc+Sg+Nom
!!€ Vuorká-Dávvira              Vuorká-Dávvira+N+Prop+Sem/Plc+Pl+Nom
!!€ Vuorká-Dávvirijda              Vuorká-Dávvira+N+Prop+Sem/Plc+Pl+Ill
!!€ Vuorká-Dávvirijs              Vuorká-Dávvira+N+Prop+Sem/Plc+Pl+Ela

LEXICON EATNAMATDECL-PLC
 @U.Cap.Obl@ EATNAMATCASE ;
 @U.Cap.Opt@ LASJ         ;

LEXICON EATNAMATDECL-ORG
 @U.Cap.Obl@ EATNAMATCASE ;

LEXICON EATNAMATCASE
    +Ess:%>in   K     ;
 +Pl+Nom:%>a    K     ;
 +Pl+Gen:%>ij   K     ;
 +Pl+Gen:%>ij   RHyph ;
 +Pl+Acc:%>ijt  K     ;
 +Pl+Ill:%>ijda K     ;
 +Pl+Ine:%>ijn  K     ;
 +Pl+Ela:%>ijs  K     ;
 +Pl+Com:%>ij   K     ;

!! ERVASTI is only used in smi-propenouns. Ervasti names are 3-syllable and are needed as a seperate lexicon because of sma. ERVASTI get both illative -aj and -jij. This because it depends on

LEXICON ERVASTI-fem
   PIPPI-fem ;
   NYSTØ-fem ;

LEXICON ERVASTI-mal
   PIPPI-mal ;
   NYSTØ-mal ;

LEXICON ERVASTI-ani
   PIPPI-ani ;
   NYSTØ-ani ;

LEXICON ERVASTI-obj
   PIPPI-obj ;
   NYSTØ-obj ;

LEXICON ERVASTI-sur
   PIPPI-sur ;
   NYSTØ-sur ;

LEXICON ERVASTI-org
   PIPPI-org ;
   NYSTØ-org ;

LEXICON ERVASTI-plc
   PIPPI-plc ;
   NYSTØ-plc ;

LEXICON ERVASTI-LOAN-plc
   PIPPI-LOAN-plc ;
   NYSTØ-LOAN-plc ;

!! MÄKI is only used in smi-propenouns. Mäki names are even-syllable finnish names and are needed as a seperate lexicon because of sma. MÄKI is same as PIPPI in smj.

LEXICON MAKI-fem
   PIPPI-fem ;

LEXICON MAKI-mal
   PIPPI-mal ;

LEXICON MAKI-sur
   PIPPI-sur ;

LEXICON MAKI-org
   PIPPI-org ;

LEXICON MAKI-plc
   PIPPI-plc ;

LEXICON MAKI-LOAN-plc !Unødvendig lexicon, alle MAKI er LOAN, endres i smi
   PIPPI-LOAN-plc ;
   
LEXICON NIEMI 
   PIPPI-plc ;
   
LEXICON flagK
@U.Cap.Obl@ K ;
