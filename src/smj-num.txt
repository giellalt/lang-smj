!Distributed under the terms of the GNU General Public License version 2 
! or any later version.

LEXICON Root
HUNDREDSM ;		     ! 200M
tjuohte:1 HUNDREDM ; ! 100M
TENSM ;		         ! 20-99M
TEENSM ;	         ! 10-19M
1MILJON ;            ! 1M
ONESM ;		         ! 1-9M
HUNDREDST ;	         ! 200000-999999
tjuohte:1 HUNDREDT ; ! 100000-100999
TENST ;			     ! 20000-99999,10000-10999
TEENST ;		     ! 11000-19999
ONEST ;			     ! 2000-9999
tuvsán:1 THOUSAND ; ! 1000-1999
UNDERTHOUSAND ;		 ! 100-999
TENS ;			     ! 20-99
TEENS ;			     ! 10-19
ONES ;			     ! 1-9

LEXICON HUNDREDSM
guokta:2  CUODIM ;
gålmmå:3 CUODIM ;
niellja:4 CUODIM ;
vihtta:5 CUODIM ;
guhtta:6 CUODIM ;
gietjav:7 CUODIM ;
gáktsa:8 CUODIM ;
aktse:9 CUODIM ;

LEXICON CUODIM
tjuode: HUNDREDM ; !200, not 100

LEXICON HUNDREDM
TENSM ;
TEENSM ;
:%0 ONESM ;
:%0%0 MILJON ;

LEXICON TEENSM
:1 TEENM ;

LEXICON TEENM
akta:1 LOHKAIM ;
guokta:2 LOHKAIM ;
gålmmå:3 LOHKAIM ;
niellja:4 LOHKAIM ;
vihtta:5 LOHKAIM ;
guhtta:6 LOHKAIM ;
gietjav:7 LOHKAIM ;
gáktsa:8 LOHKAIM ;
aktse:9 LOHKAIM ;

LEXICON LOHKAIM
lågenan: MILJON ;

LEXICON TENSM
låhke:1%0 MILJON ;
guokta:2 LÅGEVM ;
gålmmå:3 LÅGEVM ;
niellja:4 LÅGEVM ;
vihtta:5 LÅGEVM ;
guhtta:6 LÅGEVM ;
gietjav:7 LÅGEVM ;
gáktsa:8 LÅGEVM ;
aktse:9 LÅGEVM ;

LEXICON LÅGEVM
låhke:%0 MILJON ;
låk: ONESM ;

LEXICON ONESM
akta% :  1MILJON ;
guokta:2 MILJON ;
gålmmå:3 MILJON ;
niellja:4 MILJON ;
vihtta:5 MILJON ;
guhtta:6 MILJON ;
gietjav:7 MILJON ;
gáktsa:8 MILJON ;
aktse:9 MILJON ; 

LEXICON 1MILJON
miljon% :1 OVERTHOUSANDS ;

LEXICON MILJON
% miljona% : OVERTHOUSANDS ;

! =================
! Under the million
! =================

LEXICON OVERTHOUSANDS
HUNDREDST ;		        ! x00.000.000
tjuohte:1 HUNDREDT ;    ! 100.000.000
:%0 TENST ;		        !  20.000.000
:%0 TEENST ;		    !  10.000.000
:%0%0 ONEST ;		    !   2.000.000
tuvsán:%0%01 THOUSAND ;	!   1.000.000
:%0%0%0 UNDERTHOUSAND ;	
:%0%0%0%0 TENS ;
:%0%0%0%0 TEENS ;
:%0%0%0%0%0 ONES ;
:%0%0%0%0%0%0 # ;	     !  x000000

LEXICON HUNDREDST
guokta:2  CUODIT ;	! 200000-299999
gålmmå:3 CUODIT ;	! 300000-399999
niellja:4 CUODIT ;	! 400000-499999
vihtta:5 CUODIT ;	! 500000-599999
guhtta:6 CUODIT ;	! 600000-699999
gietjav:7 CUODIT ;	! 700000-799999
gáktsa:8 CUODIT ;	! 800000-899999
aktse:9 CUODIT ;	! 900000-999999

LEXICON CUODIT
tjuode: HUNDREDT ;	! 200000-199999

LEXICON HUNDREDT	     ! X = 1-9, Y = 0-9
TENST ;			     ! X2XYYY, X10YYY
TEENST ;		     ! X1XYYY
akta:%01 THOUSANDS ;
:%0 ONEST ;		     ! XX0YYY
:%0%0 THOUSANDS ;	 ! X00YYY

LEXICON TEENST
:1 TEENT ;

LEXICON TEENT
lågev:%0 THOUSANDS ;
akta:1 LOHKAIT ;
guokta:2 LOHKAIT ;
gålmmå:3 LOHKAIT ;
niellja:4 LOHKAIT ;
vihtta:5 LOHKAIT ;
guhtta:6 LOHKAIT ;
gietjav:7 LOHKAIT ;
gáktsa:8 LOHKAIT ;
aktse:9 LOHKAIT ;

LEXICON LOHKAIT
lågenan: THOUSANDS ;

LEXICON TENST
guokta:2 LÅGEVT ;
gålmmå:3 LÅGEVT ;
niellja:4 LÅGEVT ;
vihtta:5 LÅGEVT ;
guhtta:6 LÅGEVT ;
gietjav:7 LÅGEVT ;
gáktsa:8 LÅGEVT ;
aktse:9 LÅGEVT ;

LEXICON LÅGEVT
låhke:%0 THOUSANDS ;
låhke#akta#tuvsán:1 THOUSAND ;
låk: ONEST ;

LEXICON ONEST
guokta:2 THOUSANDS ;
gålmmå:3 THOUSANDS ;
niellja:4 THOUSANDS ;
vihtta:5 THOUSANDS ;
guhtta:6 THOUSANDS ;
gietjav:7 THOUSANDS ;
gáktsa:8 THOUSANDS ;
aktse:9 THOUSANDS ;

LEXICON THOUSANDS      ! x > 1
tuvsána: THOUSAND ;

LEXICON THOUSAND
UNDERTHOUSAND ;	   ! 1100-1999
:%0 TENS ;	   ! 1020-1099
:%0 TEENS ;	   ! 1010-1019
:%0%0 ONES ;	   ! 1001-1009
:%0%0%0 # ;	   ! 1000

!===========================
!Here starts the 999 numbers
!===========================

LEXICON UNDERTHOUSAND
HUNDREDS ;
tjuohte:1 HUNDRED ;

LEXICON HUNDREDS
guokta:2  CUODI ;
gålmmå:3 CUODI ;
niellja:4 CUODI ;
vihtta:5 CUODI ;
guhtta:6 CUODI ;
gietjav:7 CUODI ;
gáktsa:8 CUODI ;
aktse:9 CUODI ;

LEXICON CUODI
tjuode: HUNDRED ;

LEXICON HUNDRED
TENS ;
TEENS ;
:%0 ONES ;
:%0%0 # ;

LEXICON TEENS
:1 TEEN ;
lågenan:1 TEEN2 ;

LEXICON TEEN
akta:1 LOHKAI ;
guokta:2 LOHKAI ;
gålmmå:3 LOHKAI ;
niellja:4 LOHKAI ;
vihtta:5 LOHKAI ;
guhtta:6 LOHKAI ;
gietjav:7 LOHKAI ;
gáktsa:8 LOHKAI ;
aktse:9 LOHKAI ;

LEXICON TEEN2
akta:1 # ;
guokta:2 # ;
gålmmå:3 # ;
niellja:4 # ;
vihtta:5 # ;
guhtta:6 # ;
gietjav:7 # ;
gáktsa:8 # ;
aktse:9 # ;

LEXICON LOHKAI
lågenan: # ;

LEXICON TENS
lågev:1%0 # ;
guokta:2 LÅGEV ;
gålmmå:3 LÅGEV ;
niellja:4 LÅGEV ;
vihtta:5 LÅGEV ;
guhtta:6 LÅGEV ;
gietjav:7 LÅGEV ;
gáktsa:8 LÅGEV ;
aktse:9 LÅGEV ;

LEXICON LÅGEV
låhke:%0 # ;
låk: ONES ;

LEXICON ONES
akta:1 # ;
guokta:2 # ;
gålmmå:3 # ;
niellja:4 # ;
vihtta:5 # ;
guhtta:6 # ;
gietjav:7 # ;
gáktsa:8 # ;
aktse:9 # ;

