! ========================================================================== !
!                      Lule Sámi morphological analyser                      !
! ========================================================================== !
!
! smj-lex.txt
! The file contains morphological affixes for the nouns, verbs and adjectives
!



Multichar_Symbols
 +N +A +Adv +V
 +Pron +CS +CC +Po +Pr +Interj +Pcle +Qst
 +Pers +Dem +Interr +Refl +Recipr +Rel +Indef
 +Sg +Pl +Nom +Gen +Acc +Ill +Ine +Ela +Com +Ess +Par +Abe +SgCmp +SgNomCmp +SgGenCmp +PlGenCmp +Cmpnd +Cmpnd+
 +PxSg1 +PxSg2 +PxSg3 +PxDu1 +PxDu2 +PxDu3 +PxPl1 +PxPl2 +PxPl3
 +Comp +Superl +Actor +Actio
 +Clt +Attr +Dim +Inch 
 +Pass +Ind +Prs +Prt +Pot +Cond +Imprt +ImprtII +Sup +IV +TV
 +Sg1 +Sg2 +Sg3 +Du1 +Du2 +Du3 +Pl1 +Pl2 +Pl3
 +Inf +Ger +GerII +ConNeg +Neg +PrsPrc +PrfPrc +VGen +VAbess 
 +CLB +PUNCT +HYPH +PAR 
 +Der/r +Der/d +Der/dalla +Der/l +Der/st +Der/h +Der/dahtte +Der/dasste +Der/stalla +Der/stahtte +Der/stasste +Der/ladda +Der/lahtte +Der/lasste +Der/u/a/år +Der/ahtte +Der/alla +Der/asste +Der/vuohta +Der/dibme +Der/lasj +Der/lis +Der/mus +Der/dahka +Der/us +Der/lágásj +Der/lágán +Der/ahtes +Der/ahkes +Der/duvva +Der/duhtte +Der/juolgak +Der/luottak +Der/suttak +Der/uvsuk +Der/jienak +Der/rattak +Der/hekkak +Der/åjvak +Der/belak +Der/sijduk +Der/giedak +Der/gielak +Der/muoduk +Der/varák +Der/báguk +Der/nálak +Der/rabdak +Der/rávak +Der/ájvuk+Der/såjak +Der/giesjkak +Der/bának +Der/njálmak +Der/nierak +Der/merkak +Der/suorgak +Der/suorak +Der/gattsak +Der/siejbak +Der/siejnak +Der/hájak +Der/habsak +Der/hápsak +Der/hábjak +Der/sáv'vak +Der/namák +Der/lanják +Der/vuoduk +Der/hámak +Der/linnjak +Der/lijkak +Der/dávtak +Der/tjårvak +Der/gietjak +Der/bieljak +Der/tjuvdak +Der/dábak +Der/tjiegak +Der/gerdak +Der/vuobdak +Der/uvsak +Der/ivnak +Der/ferjak +Der/bájnuk +Der/luonduk +Der/dávdak +Der/jergak +Der/mielak +Der/jiermak +Der/njunjak +Der/guovluk +Der/dáfuk +Der/vájmuk +Der/lahkak +Der/åsak +Der/garák +Der/tjuolmak +Der/tjålak +Der/segak +Der/tjalmak +Der/vigák +Der/girjak +Der/stával +Der/agák +Der/jagák

! Morphophonemes and Sámi letters
 e1 i1 o1 o3 u1   ! Morphophonemes in sme, here temporarily due to common propernoun file
 g2 m2 n2 h2
 b3 d3 g3 h3 l3 m3 n3 r3 

! Triggers for morphophonological rules
 X1 X2 X3 X4 X5 X6 X7 X8 X9
 Q1 Q2 Q3 Q4 Q5 Q6 Q7 Q8 Q9
 Y1 Y2 Y3 Y4 Y5 Y6 Y7 Y8 Y9

! Flag diacritics
 @P.NeedNoun.ON@  @D.NeedNoun.ON@  @C.NeedNoun@ ! (Dis)allowing compounds
 @U.Cap.Obl@  @U.Cap.Opt@			! Allowing downcasing of
						! derived names: deatnulaš. 



! =================================================
! Basic lexica, pointing to the other lexicon files
! =================================================
  
LEXICON Root
 Prefixes ;					!^G^ !^C^
 +Cmpnd+:%- NounRoot ;				!^G^  !^C^
! @U.Cap.Obl@	ProperNoun ;			!^G^  ! These flags are for
! @U.Cap.Opt@ ProperNoun ;			!^G^  ! downcasing the propernouns
! AspellProp ;				! Only for aspell target !!
 NounRoot ;					!^G^
! AspellAffix ;				!Only for aspell target !!
! ProperNounFirstPart ;				!^G^
! ProperNoun ;
 AdjectiveRoot ;                     !^G^
 VerbRoot ;                           !^G^
 Adverb ;                           !^G^
 Particle ;                         !^G^
 Subjunction ;                          !^G^
 Conjunction ;                          !^G^
 Adposition ;                               !^G^
! Postposition ;                        !^G^
! Preposition ;                     !^G^
 Interjection ;                     !^G^
 Pronoun ;                          !^G^
 Numeral ;                              !^G^
 Punctuation ;                              !^G^



! ======================
! Sublexica for NounRoot
! ======================


! Prefixing and compounding
! =========================

LEXICON Prefixes	!^C^
iehpe# NounRoot ;       !^C^
iehpe# AdjectiveRoot ;       !^C^
iehpe# VerbRoot ;       !^C^
ij%- AdjectiveRoot ;  ! ij-biologalasj	!^C^


LEXICON R !^C^
 # NounRoot ;                      ! This is the normal case.		    ^C^
! # NounSecond ;			   ! Testing some quasisuff, cf. noun file  ^C^
 #@P.NeedNoun.ON@  VerbRoot ;      ! Flag diacr declares that the compound  ^C^
 #@P.NeedNoun.ON@  AdjectiveRoot ; ! needs a +N tag later in the derivation ^C^
 #:%- NounRoot ;                        ! These 3 cases add a hyphen, so    ^C^
 #:%-@P.NeedNoun.ON@  VerbRoot ;        ! Sem-julggaštus and the like	    ^C^
 #:%-@P.NeedNoun.ON@  AdjectiveRoot ;   ! is allowed.			    ^C^
!# ProperNoun ;     ! Now, let us allow proper nouns as the 2nd part of com-^C^
!#:%- ProperNoun ;  ! pounds. Thus: London-Hull, LondonHull, Hull-viessu    ^C^
                    ! and viessuLondon are now all ok. Some refinement could^C^
                    ! perhaps be done, but corpus research is needed.	    ^C^
 +Cmpnd:%- # ;     ! This to take care of Oahpo- ja dutkandept.		    ^C^


! It seems the P flag in the AdjectiveRoot entry is irrelevant, since there
! are no N + [A-N] compounds. The pointers to AdjectiveRoot are temorarily 
! (030129) left for inspection, they (line 3 and 6 of R) should be removed
! if no deadjectival nouns turn up.





! Even-syllable stems
! -------------------

! 2syll stems
! - - - - - -

! 2syll stem with cg (note Q1)
LEXICON MUORRA
 +N: N-EVENSTRONGSTEM ;
 +N:Q1 N-EVENWEAKSTEM ; ! Weak grade Q1
 +N: DenominalNounsMUORRA ;
 +N: DenominalAdjsV1 ;
 +N: DenominalAdjsV2 ;
 +N: MUORRACmp ;                     !^C^
 

 

! 2syll stem without cg and in weak grade
! Many of these are  pres ptc, they should be derived from
! the verbs, and taken from there
LEXICON NOADE
 +N: N-EVENSTRONGSTEM ;
 +N:Q4 N-EVENWEAKSTEM ; 
 +N: DenominalNounsMUORRA ;
 +N: DenominalAdjsV1 ;
 +N:Y7 DenominalAdjsV2 ;
 +N: NOADECmp ;                       !^C^

! 2syll stems without any morphophonology
LEXICON KINO ! no diacritics whatsoever
 +N: NE-EVENCASE ;
 +N: ILL-EVENCASE ;
 +N: N-EVENWEAKSTEM-OTH ;
 +N: N-EVENWEAKSTEM-SG-COM ;
 +N+Dim+N:sj N-ODDCASE ;              !^C^
 +N+Dim+N:tj OTH-ODDCASE ;                   !^C^
 +N: DenominalAdjsKINO ;
 +N: DenominalNounsKINO ;
 +N+SgNomCmp: R ;        !^C^
 +N+SgGenCmp:Q1 R ;          !^C^ 
 +N+PlGenCmp:j R ;          !^C^ 



! 4syll stems
! - - - - - - 
LEXICON GÅNÅGIS
 +N: GÅNÅGIS-C ;
 +N:a GÅNÅGIS-EVEN ;
 +N:aQ1 DenominalNounsMUORRA ;
 

LEXICON GÅNÅGIS-C
 +Sg+Nom: K ;
 +Der/ahtes+A+Attr:#ahtes ATTR ;
 +Der/ahtes+Adv:#ahtá K ;
 +Der/ahtes+A+Sg+Nom:#ahtes K ;
 +Der/ahtes+A+Sg+Nom:#ahttá PxV ;
 +Der/ahtes+A+Ess:#ahttán K ;
 +Der/ahtes+A+Der/vuohta:#ahtes VUOHTA ;      !^C^
 +Der/ahtes+A:#ahttá ILL-EVENCASE ;
 +Der/ahtes+A:#ahttá N-EVENWEAKSTEM ;
 +Der/ahtes+A:#ahttá BU/MUS ;
! +Abess:aQ1dagi K ;           !^C^    !should be written separately
! +Abess:aQ1dagá K ;           !^C^    !!should be written separately
 +Abess:aQ1dak K ;           !^C^
 +Abess:aQ1da K ;           !^C^
 +SgNomCmp: R ;        !^C^
 +SgGenCmp:aQ1 R ;          !^C^ 
 +PlGenCmp:ajQ1 R ;          !^C^


LEXICON GÅNÅGIS-EVEN
 +Sg+Nom:X4 PxV ; !Spiik not explicit here, thus guessing.
 +Ess:n K ;
! +Ess:n PxC ; !No Ess+Px according to Spiik, Grundstrom
 ILL-EVENCASE ;
 :Q1 N-EVENWEAKSTEM ;

LEXICON CELSIUS
 +N: GÅNÅGIS-C ; ! just as gånågis
 +N:a CELSIUS-EVEN ; ! as gånågis, but no Q1 (WeG)
 +N:a DenominalNounsMUORRA ;

LEXICON CELSIUS-EVEN
 +Ess:n K ;
 +Ess:n PxC ;
 ILL-EVENCASE ;
 N-EVENWEAKSTEM ;


! Nominal substems
! - - - - - - - -  

LEXICON N-EVENSTRONGSTEM
 NE-EVENCASE ;
 :X4 ILL-EVENCASE ; ! X4 for the e:á and e:å alternation

LEXICON NE-EVENCASE
 +Sg+Nom: K ;
 +Sg+Nom:X4 PxV ;
 +Ess:n K ;
! +Ess:n PxC ; ! parallel with CELCIUS above (but this shouldn´t be here(?) TO)


LEXICON ILL-EVENCASE
 +Sg+Ill:j K ;
 +Sg+Ill:s PxCi ;

LEXICON N-EVENWEAKSTEM
 N-EVENWEAKSTEM-OTH ;
 N-EVENWEAKSTEM-SG-COM ; ! separate bec of X5

LEXICON N-EVENWEAKSTEM-OTH
 +Sg+Gen: K ;
 +Sg+Gen: PxV ;
 +Sg+Acc:v K ;
 +Sg+Acc: PxV ;
 +Sg+Ine:n K ;
 +Sg+Ine:n PxCi ;
 +Sg+Ela:s K ;
 +Sg+Ela:st PxCi ;
 +Pl+Nom: K ;
 +Pl+Gen:j K ;
 +Pl+Acc:jt K ;
 +Pl+Acc:jd PxC ;
 +Pl+Ill:jda K ;
 +Pl+Ine:jn K ;
 +Pl+Ine:jn PxC ;
 +Pl+Ela:js K ;
 +Pl+Ela:jst PxC ;
 +Pl+Com:j K ;
! ess & ill pl do not use Px that often, left out.
! But what about nom pl, gen pl?
! Spiik: Nominativ, genitiv och illativ pluralis antager sällan
! suffix och har därför ej medtagits i paradigmen.
! Here we thus wait for alternative input.

LEXICON N-EVENWEAKSTEM-SG-COM
 +Sg+Com:X5jn K ; ! X5 for o:u in ComSg. Note two dummies in a row!
 +Sg+Com:X5jn PxCi ;

LEXICON MUORRACmp    !^C^
 +SgCmp:X9 R ;        !^C^
 +SgNomCmp: R ;        !^C^
 +SgGenCmp:Q1 R ;          !^C^ 
 +PlGenCmp:Q1j R ;          !^C^ 
 
 LEXICON NOADECmp    !^C^
 +SgNomCmp: R ;        !^C^
 +SgCmp:X9 R ;        !^C^
 +SgGenCmp: R ;          !^C^ 
 +PlGenCmp:Y6j R ;          !^C^ 
 
 LEXICON ACTORCmp    !^C^
 +SgNomCmp: R ;        !^C^
 +SgGenCmp: R ;          !^C^ 
 +PlGenCmp:j R ;          !^C^ 

 
 LEXICON ODDCmp    !^C^
 +SgGenCmp:a R ;          !^C^ 
 +PlGenCmp:ij R ;          !^C^ 
 
 



! Odd-syllable stems
! ------------------

! oddsyll, cg, C-final, 2ndsyll vowchange
LEXICON ÅRES
 +N:Q1 NE-ODDCASE ; ! Q1 is general WeG
 +N:X5 OTH-ODDCASE ; ! X5 is e:å and o:u
 +N:X5 DenominalNounsODD ;
 +N:X5 DenominalAdjsC ;
 +N:X5 ODDCmp ;             !^C^
 +N+SgNomCmp:Q1 R ;        !^C^


 
! oddsyll, cg, C-final, no 2ndsyll vowchange
LEXICON GÁMAS ! Odd-syllable C-final noun with CG but no VChange
 +N:Q1 NE-ODDCASE ;
 +N: OTH-ODDCASE ;
 +N:X5 DenominalNounsODD ;
 +N: DenominalAdjsC ;
 +N:X5 ODDCmp ;             !^C^
 +N+SgNomCmp:Q1 R ;        !^C^


! oddsyll, cg, C-final, 2ndsyll vowchange, only long essive
LEXICON SÅHKÅR 
 +N:Q1 N-ODDCASE ; ! To Nom only, since SÅHKÅR has no short essive.
 +N:X5 OTH-ODDCASE ;
 +N:X5 DenominalNounsODD ;
 +N:X5 DenominalAdjsC ;
 +N:X5 ODDCmp ;             !^C^
 +N+SgNomCmp:Q1 R ;        !^C^



! oddsyll, no cg, no vowchange
LEXICON GAHPER
 +N: N-ODDCASE ; ! no pointer to E-ODDCASE, as gahper has no short Ess
 +N: OTH-ODDCASE ;
 +N: DenominalNounsODD ;
 +N: DenominalAdjsC ;
 +N: ODDCmp ;             !^C^
 +N+SgNomCmp: R ;        !^C^


! oddsyll, cg, vowchange, V-final nominative
LEXICON BENA
 +N:X3 BENA-NE ;
 +N:X5 OTH-ODDCASE ;
 +N:X5 DenominalNounsODD ;
 +N:X5 DenominalAdjsC ;
 +N:X5 ODDCmp ;             !^C^
 +N+SgNomCmp:X3 R ;        !^C^
 ! X5 for vow change

! Odd-syllable stems, sublexica
! - - - - - - - - - - - - - - - 

LEXICON NE-ODDCASE ! The lexicon is split bec. of gahper's missing short Ess
 N-ODDCASE ;
 E-ODDCASE ;

LEXICON N-ODDCASE
 +Sg+Nom: K ;
! +Sg+Nom: PxC ; !moved to OTH-ODDCASE

LEXICON E-ODDCASE
 +Ess:X1n K ; ! X1 to delete cns before short ess


LEXICON OTH-ODDCASE
 +Sg+Nom: PxC ;
 +Sg+Gen:a K ;
 +Sg+Gen: PxC ;
 +Sg+Acc:av K ;
 +Sg+Acc: PxC ;
 +Sg+Ill:ij K ;  ! Px ?? !!
 +Sg+Ine:in K ;
 +Sg+Ine:in PxCs ;
 +Sg+Ela:is K ;
 +Sg+Ela:ist PxCs ;
 +Sg+Com:ijn K ;
 +Sg+Com:ijn PxCs;
 +Ess:in K ;
 +Pl+Nom:a K ;
 +Pl+Gen:ij K ;
 +Pl+Acc:ijt K ;
 +Pl+Acc:ijd PxCs ;
 +Pl+Ill:ijda K ;
 +Pl+Ine:ijn K ;
 +Pl+Ine:ijn PxCs ;
 +Pl+Ela:ijs K ;
 +Pl+Ela:ijst PxCs ;
 +Pl+Com:ij K ;

LEXICON BENA-NE
 +Sg+Nom: K ;
 +SgNomCmp: R ;        !^C^
 +Ess:n K ;

!LEXICON BENA-OBL
! +Sg+Nom:00 PxC ;
! +Sg+Gen:a K ;
! +Sg+Gen: PxC ;
! +Sg+Acc:a K ;
! +Sg+Acc: K ;
! +Sg+Acc: PxC ;
! +Pl+Nom:at K ;
! +Sg+Ill:asa PxV ;
! :i MALLASI-/NUORABU- ;
! :i MALLASI- ;


! Contracted stems
! ----------------


LEXICON SARVES
 +N:Q1 SARVES-NE ;
 +N:X4 CONTR-OTH ;
 +N:X4 DenominalNounsCONTR ;
 +N:X4 DenominalAdjsContr ;


LEXICON SUOLOJ
 +N:Q3 SUOLOJ-NE ;
 +N:X4 CONTR-OTH ;
 +N:X4 DenominalNounsCONTR ; 
 +N:X4 DenominalAdjsContr ;
! +N:X5 CONTR-OTH ; ! o:u as well!

LEXICON GUOMOJ
 +N:X4 DenominalNounsCONTR ; 
 +N:X4 DenominalAdjsContr ;
 +N:X6 SUOLOJ-NE ; ! III-I CG
 +N:X5 ILL-EVENCASE ;
 +N:X4 N-EVENWEAKSTEM ;
 +N+Sg+Nom: PxV ;
 +N+Ess:n K ;



LEXICON SARVES-NE
 +Sg+Nom:s K ;
! +Sg+Nom:s PxC ; !Moved to CONTR-OTH
 +Ess:n K ;
 +SgNomCmp:s R ;        !^C^

 
LEXICON SUOLOJ-NE
 +Sg+Nom:j K ;
! +Sg+Nom:j PxC ; !Moved to CONTR-OTH
 +Ess:n K ;
 +SgNomCmp:j R ;        !^C^

 
LEXICON CONTR-OTH
 ILL-EVENCASE ;
 N-EVENWEAKSTEM ;
 +Sg+Nom: PxV ;
 +Ess:n K ;
 +SgGenCmp: R ;          !^C^
 +PlGenCmp:j R ;          !^C^ 
 
! ComSg may cause trouble, since there is a dummy for it



! Denominal nouns
! ===============

LEXICON DenominalNounsMUORRA  !^C^
 +Dim+N:X7sj N-ODDCASE ;              !^C^
 +Dim+N:X7tj OTH-ODDCASE ;                   !^C^
 +Dim+N+Der/ahtes:X7tj AHTES ;             !^C^
 +Dim+N+Abess:X7tjadak K ;           !^C^
 +Dim+N+Abess:X7tjada K ;           !^C^
 +Dim+N:X7tj ODDCmp ;   !^C^
 +Dim+N+SgNomCmp:X7sj R ;        !^C^
 +Dim+N:X7tj DenominalNounsODD ;              !^C^
 
LEXICON DenominalNounsKINO  !^C^
 +Dim+N:sj N-ODDCASE ;              !^C^
 +Dim+N:tj OTH-ODDCASE ;                   !^C^
 +Dim+N+Der/ahtes:tj AHTES ;             !^C^
 +Dim+N+Abess:tjadak K ;           !^C^
 +Dim+N+Abess:tjada K ;           !^C^
 +Dim+N:tj ODDCmp ;   !^C^
 +Dim+N+SgNomCmp:sj R ;        !^C^
 +Dim+N:tj DenominalNounsODD ;              !^C^


LEXICON DenominalNounsODD    !^C^
 +Dim+N+Sg+Nom:asj K ; !^C^! added a
 +Dim+N+SgNomCmp:asj R ; !^C^! added a
 +Dim+N+SgGenCmp:attja R ; !^C^! added a
 +Dim+N+PlGenCmp:attjaj R ; !^C^! added a
 +Dim+N:adtja GÅNÅGIS-EVEN ; !^C^! added a
 +Dim+N+Der/ahtes:atj AHTES ;             !^C^
! +Dim+N+Abess:attjadagi K ;           !^C^     !should be written separately
! +Dim+N+Abess:attjadagá K ;           !^C^     !should be written separately
 +Dim+N+Abess:attjadak K ;           !^C^
 +Dim+N+Abess:attjada K ;           !^C^
 +Dim+N:adtja DenominalNounsMUORRA ; !^C^! 

 

! LEXICON DenominalNounsODD-OBL !^C^

LEXICON DenominalNounsCONTR     !^C^
 +Dim+N:tj OTH-ODDCASE ;     !^C^
 +Dim+N+Sg+Nom:sj K ;
 +Dim+N:tj ODDCmp ;              !^C^
 +Dim+N+SgNomCmp:sj R ;        !^C^
 +Dim+N+Der/ahtes:tj AHTES ;             !^C^
! +Dim+N+Abess:tjadagi K ;           !^C^     !should be written separately
! +Dim+N+Abess:tjadagá K ;           !^C^      !should be written separately
 +Dim+N+Abess:tjadak K ;           !^C^
 +Dim+N+Abess:tjada K ;           !^C^
 +Dim+N:tj DenominalNounsODD ;              !^C^

 
  
LEXICON DenominalNounsActor  !^C^
 +Dim+N:Y7tj N-ODDCASE ;              !^C^
 +Dim+N:Y7tj OTH-ODDCASE ;                   !^C^
 +Dim+N+Der/ahtes:Y7tj AHTES ;             !^C^
! +Dim+N+Abess:Y7tjadagi K ;           !^C^     !should be written separately
! +Dim+N+Abess:Y7tjadagá K ;           !^C^     !should be written separately
 +Dim+N+Abess:Y7tjadak K ;           !^C^
 +Dim+N+Abess:Y7tjada K ;           !^C^
 +Dim+N:Y7tj ODDCmp ;              !^C^
 +Dim+N+SgNomCmp:Y7sj R ;        !^C^
 +Dim+N:Y7tj DenominalNounsODD ;              !^C^


LEXICON VUOHTA					!^C^
 +N:#vuohta@C.NeedNoun@ VUOHTAMORPH ; !^C^
 	
LEXICON VUOHTAMORPH
 NE-EVENCASE ;
 +SgNomCmp: R ;        !^C^
 +SgCmp:X9 R ;        !^C^
 +SgGenCmp:Q1 R ;          !^C^ 
 +PlGenCmp:jQ1 R ;          !^C^ 
 :X4 ILL-EVENCASE ;
 :Q1 N-EVENWEAKSTEM ;
! +Abess:Q1dagi K ;           !^C^     !should be written separately
! +Abess:Q1dagá K ;           !^C^     !should be written separately
 +Abess:Q1dak K ;           !^C^
 +Abess:Q1da K ;           !^C^
 +Der/ahtes:Q1g AHTES ;       !^C^
 
LEXICON LASJ !from placenames
 +Der/lasj:ladtj GÅNÅGIS ;			!^C^
 +Der/lasj+Dim:lattjatj GÁMAS ;   !^C^




! ===========================
! Sublexica for AdjectiveRoot
! ===========================

! Even-syllable stems
! -------------------

LEXICON GIEVRRA ! Attr WeG, -s
 +A+Attr:Q1s ATTR ;
 +A+Der/vuohta:Q1s VUOHTA ;              !^C^
 +A: NE-EVENCASE ;
 +A:X4 ILL-EVENCASE ;
 +A:Q1 N-EVENWEAKSTEM ;
 +A:Q1 BU/MUS ;
 +A+Adv:Q1t K ;

LEXICON NUORRA ! Attr =
 +A+Attr: ATTR ;
 +A+Der/vuohta: VUOHTA ;                !^C^
 +A: N-EVENSTRONGSTEM ;
 +A:Q1 N-EVENWEAKSTEM ; ! cg in nominal paradigm
 +A:Q1 BU/MUS ;
 +A+Adv:Q1t K ; ! Presumably WeG as adverb as well

LEXICON HÁVSSKE ! Attr w/o WeG, -s
 +A+Attr:s ATTR ;
 +A+Der/vuohta:s VUOHTA ;                  !^C^
 +A: NE-EVENCASE ;
 +A:X4 ILL-EVENCASE ;
 +A:Q1 N-EVENWEAKSTEM ; ! Presumably WeG in nominal paradigm
 +A:Q1 BU/MUS ;
 +A+Adv:Q1t K ;

LEXICON UNNE ! Attr e > a~e, WeG
 +A+Attr:aQ1 ATTR ; ! a-attr.
! +A+Attr:esQ1 ATTR ; ! e-attr. !These now hardcoded
 +A+Der/vuohta:aQ1 VUOHTA ;                   !^C^
 +A+Der/vuohta:esQ1 VUOHTA ;                   !^C^
 +A:e NE-EVENCASE ; ! presumably normal adjective
 +A:eX4 ILL-EVENCASE ;
 +A:eQ1 N-EVENWEAKSTEM ; ! behaviour, with cg, and with
 +A:eQ1 BU/MUS ;           ! the e-form as a starting
 +A+Adv:eQ1t K ;           ! point.

LEXICON SÁVADAHTTE  !Causative-participles
 +A: NE-EVENCASE ;
 +A:X4 ILL-EVENCASE ;
 +A:Q1 N-EVENWEAKSTEM ;


LEXICON ÁRMMOGIS ! Attr = Pred. Nom. Sg.
 +A+Der/vuohta: VUOHTA ;                       !^C^
 +A+Attr: ATTR ; ! twol fixes the ss > s
 +A+Sg+Nom: K ;
 +A+Sg+Nom:a PxV ;
 +A:a GÅNÅGIS-EVEN ; ! Declined as four-syllable noun
 +A:aQ1 BU/MUS ;
 +A+Adv:aQ1t K ;
 
LEXICON MÁHTULASJ !as ÁRMMOGIS but no attr.
 +A+Der/vuohta: VUOHTA ;                       !^C^
 +A+Sg+Nom: K ;
 +A+Sg+Nom:a PxV ;
 +A:a GÅNÅGIS-EVEN ; ! Declined as four-syllable noun
 +A:aQ1 BU/MUS ;
 +A+Adv:aQ1t K ; 
 

LEXICON ASIDASJ
 +A+Der/vuohta:asj VUOHTA ;                    !^C^
 +A+Der/vuohta:is VUOHTA ;                     !^C^
 +A+Attr:is ATTR ; ! Special Attr form
 +A+Sg+Nom:asj K ;
 +A+Sg+Nom:adtja PxV ;
 +A+Sg+Ill:adtjaj K ;
 +A+Sg+Ill:adtjas PxCi ;
 +A+Ess:adtjan K ;
 !+A:attja OTH-ODDCASE ; ! Declined as four-syllable noun (but this generates wrong. TO)
 +A:attja N-EVENWEAKSTEM ; !This seems right. TO
! +A:attja BU/MUS ; !unusual comp. and superl.
 +A: ABBO/AMOS ; 
 +A+Adv:attjaQ1t K ; 

LEXICON TJALMEDIBME
 +A+Der/vuohta:is VUOHTA ;                     !^C^
 +A+Attr:is ATTR ; ! Special Attr form
 +A:ibme NE-EVENCASE ;
 +A:ibmeX4 ILL-EVENCASE ;
 +A:ibmeQ1 N-EVENWEAKSTEM ;
 +A:ibmeQ1 BU/MUS ;
 +A+Adv:ibmeQ1t K ;
 +A: DUVVAT ; 

LEXICON SUOLASIEHKE ! evenstem No attribute
 +A: NE-EVENCASE ;
 +A:X4 ILL-EVENCASE ;
 +A:Q1 N-EVENWEAKSTEM ;
 +A:Q1 BU/MUS ;
 +A+Adv:Q1t K ;
 
 
LEXICON NUORTTALABBO   !Inherent comparatives
 +A: ABBO/AMOS ;
 
LEXICON GASSKALAMOS   !Inherent superlatives
 +A+Superl: AMOS ; 
 +A+Superl+Adv:abmusit K ;
 




! Odd-syllable stems
! ------------------

LEXICON TJIEGOS ! a. Adjs on -k, negating adjs on -dahkes, sm adjs on -os
 +A+Attr:X2 ATTR ;
 +A+Der/vuohta:X2 VUOHTA ;               !^C^
 +A:X2 N-ODDCASE ; ! no pointer to E-ODDCASE, as adjs have no short Ess
 +A:X5 OTH-ODDCASE ;
 +A:X5 ABBO/AMOS ;
 +A+Adv:X5it K ; 
 
LEXICON BASSTEL ! b. Adjs on -et, -l, -r, sm om -k, -sj with -is-attr
 +A+Attr:is ATTR ;
 +A+Der/vuohta:is VUOHTA ;               !^C^
  +A+Der/vuohta: VUOHTA ;                !^C^
 +A: N-ODDCASE ; ! no pointer to E-ODDCASE, as adjs have no short Ess
 +A: OTH-ODDCASE ;
 +A: ABBO/AMOS ;
 +A+Adv:it K ; 
 
LEXICON MUTTÁK ! c. Adjs on -ák, Attr -is or =
 +A+Attr: ATTR ;
 +A+Attr:is ATTR ;
 +A+Der/vuohta: VUOHTA ;                    !^C^
 +A+Der/vuohta:is VUOHTA ;                  !^C^
 +A: N-ODDCASE ; ! no pointer to E-ODDCASE, as adjs have no short Ess
 +A: OTH-ODDCASE ;
 +A: ABBO/AMOS ;
 +A+Adv:it K ; 
 
LEXICON ALLAK ! d. Adjs on -ak, attr. on -a
 +A+Attr:a ATTR ;
 +A+Der/vuohta:a VUOHTA ;                   !^C^
 +A+Der/vuohta:ak VUOHTA ;                  !^C^
 +A:ak N-ODDCASE ; ! no pointer to E-ODDCASE, as adjs have no short Ess ;
 +A:ag OTH-ODDCASE ;
! +A:eQ1 BU/MUS ; !replaced to avoid adv. form alibut
 +A+Comp:eQ1 BUStem ;
 +A+Superl:eQ1muss BUOREMUS ;
 +A+Comp+Adv:agappot K ;
 +A+Superl+Adv:agabmusit K ;
 +A+Comp+Dim+Der/vuohta:eQ1busj VUOHTA ;  !^C^
 +A+Comp+Dim+Sg+Nom:eQ1busj K ;
 +A+Comp+Dim+Sg+Attr:eQ1busj ATTR ;
 +A+Comp+Dim:eQ1budtja GÅNÅGIS-EVEN ; 
 +A+Comp+Dim+Adv:eQ1buttjat K ;
 +A+Adv:agit K ;
 
LEXICON GÅBDDÅK ! d2. Adjs on -åk, attr. on -å
 +A+Attr:a ATTR ;
 +A+Der/vuohta:a VUOHTA ;                       !^C^
 +A+Der/vuohta:åk VUOHTA ;                     !^C^
 +A:åk N-ODDCASE ; ! no pointer to E-ODDCASE, as adjs have no short Ess ;
 +A:åg OTH-ODDCASE ;
! +A:eQ1 BU/MUS ; !replaced to avoid adv. form gåbdibut
 +A+Comp:eQ1 BUStem ;
 +A+Superl:eQ1muss BUOREMUS ;
 +A+Comp+Adv:ågappot K ;
 +A+Superl+Adv:ågabmusit K ;
 +A+Comp+Dim+Der/vuohta:eQ1busj VUOHTA ;  !^C^
 +A+Comp+Dim+Sg+Nom:eQ1busj K ;
 +A+Comp+Dim+Sg+Attr:eQ1busj ATTR ;
 +A+Comp+Dim:eQ1budtja GÅNÅGIS-EVEN ; 
 +A+Comp+Dim+Adv:eQ1buttjat K ;
 +A+Adv:ågit K ;
 
LEXICON GALMAS
 +A+Attr: ATTR ;
 +A+Der/vuohta: VUOHTA ;                    !^C^
 +A+Der/vuohta:sX2 VUOHTA ;                !^C^
 +A:sX2 N-ODDCASE ; ! no pointer to E-ODDCASE, as adjs have no short Ess
 +A:sX5 OTH-ODDCASE ;
 +A:sX5 ABBO/AMOS ;
 +A+Adv:sit K ; 
 
LEXICON SUOHKAT
 +A+Attr:is ATTR ;
 +A+Der/vuohta:is VUOHTA ;                  !^C^
 +A+Der/vuohta:aQ1t VUOHTA ;                 !^C^
 +A:aQ1t N-ODDCASE ; ! no pointer to E-ODDCASE, as adjs have no short Ess
 +A:aQ1d OTH-ODDCASE ;
 +A:aQ1d ABBO/AMOS ;
 +A+Adv:aQ1dit K ; 
 
LEXICON LÅSSÅT
 +A+Attr:is ATTR ;
 +A+Attr:å ATTR ;
 +A+Der/vuohta:is VUOHTA ;                   !^C^
 +A+Der/vuohta:å VUOHTA ;                     !^C^
 +A+Der/vuohta:åt VUOHTA ;                    !^C^
 +A:åt N-ODDCASE ; ! no pointer to E-ODDCASE, as adjs have no short Ess
 +A:åd OTH-ODDCASE ;
 +A:åd ABBO/AMOS ;
! +A:eQ1 BU/MUS ; !replaced to avoid adv. form låsibut
 +A+Comp:eQ1 BUStem ;
 +A+Superl:eQ1muss BUOREMUS ;
 +A+Comp+Dim+Der/vuohta:eQ1busj VUOHTA ;  !^C^
 +A+Comp+Dim+Sg+Nom:eQ1busj K ;
 +A+Comp+Dim+Sg+Attr:eQ1busj ATTR ;
 +A+Comp+Dim:eQ1budtja GÅNÅGIS-EVEN ; 
 +A+Comp+Dim+Adv:eQ1buttjat K ;
 +A+Adv:ådit K ; 

LEXICON DIMES
 +A+Attr:a ATTR ;
 +A+Der/vuohta:a VUOHTA ;           !^C^
 +A+Der/vuohta:esQ1 VUOHTA ;        !^C^
 +A:esQ1 N-ODDCASE ; ! no pointer to E-ODDCASE, as adjs have no short Ess
 +A:ás OTH-ODDCASE ;
 +A:ás ABBO/AMOS ;
 +A+Adv:ásit K ;
 
LEXICON OAMES
 +A+Attr: ATTR ;
 +A+Der/vuohta: VUOHTA ;                 !^C^
 +A:sQ1 N-ODDCASE ; ! no pointer to E-ODDCASE, as adjs have no short Ess
 +A:X4s OTH-ODDCASE ;
 +A:X4s ABBO/AMOS ;
 +A+Adv:X4sit K ;
 
LEXICON TJALMMIS
 +A+Attr:X2 ATTR ;
 +A+Attr:is ATTR ;
 +A+Der/vuohta:is VUOHTA ;           !^C^
 +A+Der/vuohta:X2 VUOHTA ;           !^C^
 +A:X2 N-ODDCASE ; ! no pointer to E-ODDCASE, as adjs have no short Ess
 +A:X5 OTH-ODDCASE ;
 +A:X5 ABBO/AMOS ;
 +A+Adv:it K ; !Can these really form adverbs?
 
LEXICON STUORAK
 +A+Attr: ATTR ;
 +A+Attr:ra ATTR ;
 +A+Der/vuohta:ra VUOHTA ;              !^C^
 +A+Der/vuohta:ak VUOHTA ;              !^C^
 +A:ak N-ODDCASE ; 
 +A:ag OTH-ODDCASE ;
 +A:á BU/MUS ;
 +A+Adv:agit K ; 
 
LEXICON SJÆVNNJAT ! miscellanious uneven No attribute, stemvowel change
 +A+Der/vuohta:Q1 VUOHTA ;                !^C^
 +A:Q1 N-ODDCASE ; 
 +A:X5 OTH-ODDCASE ;
 +A:X5 ABBO/AMOS ;
 +A+Adv:X5it K ;
 
LEXICON RIHTSOK ! miscellanious uneven No attribute, no stemvowel change
 +A+Der/vuohta:Q1 VUOHTA ;                !^C^
 +A:Q1 N-ODDCASE ; 
 +A: OTH-ODDCASE ;
 +A: ABBO/AMOS ;
 +A+Adv:it K ;
 
 
LEXICON OANEP  !Inherent comparatives
 +A: BU/MUS ; 
 
LEXICON TSIBTSA !attr =
 +A+Attr:X3 ATTR ;
 +A+Der/vuohta:X3 VUOHTA ;              !^C^
 +A+Sg+Nom:X3 K ;
 +A:X5 OTH-ODDCASE ;
 +A:X5 ABBO/AMOS ;
 +A+Adv:X5it K ;




 
 
! Contracted stems
! ----------------

LEXICON SÁDNES ! attr =
 +A+Attr:Q1s ATTR ;
 +A+Der/vuohta:Q1s VUOHTA ;              !^C^
 +A+Sg+Nom:Q1s K ;
 +A:X4 ILL-EVENCASE ;
 +A:X4 N-EVENWEAKSTEM ;
 +A+Sg+Nom:X4 PxV ;
 +A+Ess:X4n K ;
 +A:X4 BU/MUS ;
 +A+Adv:X4t K ;
 
LEXICON GOAVSOS  !attr =
 +A+Attr:Q3s ATTR ;
 +A+Der/vuohta:Q3s VUOHTA ;              !^C^
 +A+Sg+Nom:Q3s K ;
 +A:X4 ILL-EVENCASE ;
 +A:X4 N-EVENWEAKSTEM ;
 +A+Sg+Nom:X4 PxV ;
 +A+Ess:X4n K ;
 +A:X4 BU/MUS ;
 +A+Adv:X4t K ;
 
LEXICON SUVRES  !attr suvra
 +A+Attr:Q1a ATTR ;
 +A+Der/vuohta:Q1a VUOHTA ;              !^C^
 +A+Der/vuohta:Q1es VUOHTA ;              !^C^
 +A+Sg+Nom:Q1es K ;
 +A:á ILL-EVENCASE ;
 +A:á N-EVENWEAKSTEM ;
 +A+Sg+Nom:á PxV ;
 +A+Ess:án K ;
 +A:á BU/MUS ;
 +A+Adv:át K ;
 




! Comparation
! -----------

LEXICON BU/MUS ! even-syll adjectives
 +Comp: BUStem ;
 +Superl:muss BUOREMUS ;
 +Comp+Adv:but K ;
 +Superl+Adv:musát K ;
 +Comp+Dim+Der/vuohta:busj VUOHTA ;  !^C^
 +Comp+Dim+Sg+Nom:busj K ;
 +Comp+Dim+Sg+Attr:busj ATTR ;
 +Comp+Dim:budtja GÅNÅGIS-EVEN ; 
 +Comp+Dim+Adv:buttjat K ;
            
 
LEXICON ABBO/AMOS ! odd-syll adjectives
 +Comp+Sg+Nom:ap K ;
 +Comp+Sg+Nom:abbo K ;
 +Comp+Attr:ap ATTR ;
 +Comp: ABBO ;
 +Superl: AMOS ; 
 +Comp+Adv:appot K ;
 +Superl+Adv:abmusit K ;
 +Comp+Dim+Der/vuohta:appusj VUOHTA ;  !^C^
 +Comp+Dim+Sg+Nom:appusj K ;
 +Comp+Dim+Sg+Attr:appusj ATTR ;
 +Comp+Dim:apputj OTH-ODDCASE ; 
 +Comp+Dim+Adv:apputjit K ;
  


! Comparative
! -----------

LEXICON BUStem
 +Der/vuohta:p VUOHTA ;
 +Attr:p ATTR ;
 +Sg+Nom:p K ;
 +Sg+Nom:bu PxV ;
 +Sg+Gen:bu K ;
 +Sg+Gen:bu PxV ;
 +Sg+Acc:buv K ;
 +Sg+Acc:bu PxV ;
 +Sg+Ill:buj K ;
 +Sg+Ine:bun K ;
 +Sg+Ine:bun PxCs ;
 +Sg+Ela:bus K ;
 +Sg+Ela:bust PxCs ;
 +Sg+Com:bujn K ;
 +Sg+Com:bujn PxCs ;
 +Ess:bun K ;
 +Pl+Nom:bu K ;
 +Pl+Gen:buj K ;
 +Pl+Acc:bujt K ;
 +Pl+Acc:bujd PxCs ;
 +Pl+Ill:bujda K ;
 +Pl+Ine:bujn K ;
 +Pl+Ine:bujn PxCs ;
 +Pl+Ela:bujs K ;
 +Pl+Ela:bujst PxCs ;
 +Pl+Com:buj K ;
 
LEXICON ABBO
 +Der/vuohta:ap VUOHTA ;
 +Sg+Nom:abbo K ;
 +Sg+Gen:appo K ;
 +Sg+Acc:appov K ;
 +Sg+Ill:abbuj K ;
 +Sg+Ine:appon K ;
 +Sg+Ela:appos K ;
 +Sg+Com:appujn K ;
 +Ess:abbon K ;
 +Pl+Nom:appo K ;
 +Pl+Gen:appoj K ;
 +Pl+Acc:appojt K ;
 +Pl+Ill:appojda K ;
 +Pl+Ine:appojn K ;
 +Pl+Ela:appojs K ;
 +Pl+Com:appoj K ;

LEXICON AMOS
 +Der/vuohta:amos VUOHTA ;
 +Attr:amos K ;
 +Sg+Nom:amos K ;
 +Sg+Nom:abmusa PxV ;
 +Sg+Gen:abmusa K ;
 +Sg+Gen:abmusa PxV ;
 +Sg+Acc:abmusav K ;
 +Sg+Acc:abmusa PxV ;
 +Sg+Ill:abmusij K ;
 +Sg+Ine:abmusin K ;
 +Sg+Ine:abmusin PxCs ;
 +Sg+Ela:abmusis K ;
 +Sg+Ela:abmusist PxCs ;
 +Sg+Com:abmusijn K ;
 +Sg+Com:abmusijn PxCs ;
 +Ess:abmusin K ;
 +Pl+Nom:abmusa K ;
 +Pl+Gen:abmusij K ;
 +Pl+Acc:abmusijt K ;
 +Pl+Acc:abmusijd PxCs ;
 +Pl+Ill:abmusijda K ;
 +Pl+Ine:abmusijn K ;
 +Pl+Ine:abmusijn PxCs ;
 +Pl+Ela:abmusijs K ;
 +Pl+Ela:abmusijst PxCs ;
 +Pl+Com:abmusij K ;

! Superlative
! -----------

LEXICON BUOREMUS
 +Der/vuohta: VUOHTA ;
 +Attr: K ;
 +Sg+Nom: K ; ! the short form, twol shortens ss#
 +Sg+Nom:a PxV ; ! separate nom bec of the short form
 :a BUOREMUS-OBL ; ! Add -a, directly to the obliques

LEXICON BUOREMUS-OBL
 +Ess:n K ; ! essive has strong stem
 +Sg+Ill:j K ; ! so has illative
! +Sg+Ill:s PxC ; ! this Px form not mentioned in Spiik
 :Q1 N-EVENWEAKSTEM ; ! These words go like muorra
 
 
! Adj declension
! ---------------

LEXICON ATTR
 NAMÁK ;			!^C^
 K ;                  ! Plain attributive
 R ;                    !^C^
! -:#%- ProperNoun ;   ! Compounds		!^C^
! # ALIT ;  ! both comp and independent adj !^C^


 

! Derivation of adjectives
! ------------------------

LEXICON DenominalAdjsV1			!^C^
 +Der/dibme+A:Q1dibme DIBME ;		!^C^
 +Der/dibme+A+Attr:Q1dis ATTR ;		!^C^
 +Der/dibme+A+Der/vuohta:Q1dis VUOHTA ;		!^C^
 +Der/dibme+A:Q1d DUVVAT ;                 !^C^
 +Der/ahtes:Q1g AHTES ;       !^C^     
! +Abess:Q1dagi K ;           !^C^      !should be written separately
! +Abess:Q1dagá K ;           !^C^     !should be written separately
 +Abess:Q1dak K ;           !^C^
 +Abess:Q1da K ;           !^C^
 

LEXICON DenominalAdjsV2			!^C^
 +Der/lasj:X7ladtj ÁRMMOGIS ;			!^C^
 +Der/lasj+A+Dim:X7lattjatj TJIEGOS ;   !^C^
 
LEXICON DenominalAdjsKINO			!^C^
 +Der/dibme+A:dibme DIBME ;		!^C^
 +Der/dibme+A+Attr:dis ATTR ;		!^C^
 +Der/dibme+A+Der/vuohta:dis VUOHTA ;		!^C^
 +Der/dibme+A:d DUVVAT ;                 !^C^
 +Der/ahtes:g AHTES ;       !^C^     
 +Abess:dak K ;           !^C^
 +Abess:da K ;           !^C^
 +Der/lasj:ladtj ÁRMMOGIS ;			!^C^
 +Der/lasj+A+Dim:lattjatj TJIEGOS ;   !^C^


LEXICON DenominalAdjsC			!^C^  
 +Der/ahtes: AHTES ;       !^C^ 
! +Abess:adagi K ;           !^C^       !should be written separately
! +Abess:adagá K ;           !^C^       !should be written separately
 +Abess:adak K ;           !^C^
 +Abess:ada K ;           !^C^
! +laš:#lažž ISSORAS ;		!^C^  !Doesn´t seem very productive

LEXICON DenominalAdjsContr			!^C^
 +Der/dibme+A:dibme DIBME ;		!^C^
 +Der/dibme+A+Attr:dis ATTR ;		!^C^
 +Der/dibme+A+Der/vuohta:dis VUOHTA ;		!^C^
 +Der/dibme+A:d DUVVAT ;                 !^C^
 +Der/ahtes:g AHTES ;       !^C^
! +Abess:dagi K ;           !^C^   !should be written separately
! +Abess:dagá K ;           !^C^     !should be written separately
 +Abess:dak K ;           !^C^
 +Abess:da K ;           !^C^


LEXICON AHTES
 +A+Attr:ahtes ATTR ;
 +A+Adv:ahtá K ;
 +A+Sg+Nom:ahtes K ;
 +A+Sg+Nom:ahttá PxV ;
 +A+Ess:ahttán K ;
 +A+Der/vuohta:ahtes VUOHTA ;
 +A:ahttá ILL-EVENCASE ;
 +A:ahttá N-EVENWEAKSTEM ;
 +A:ahttá BU/MUS ;
 
 
LEXICON DIBME
 +Dim:X7tj TJIEGOS ;
 NE-EVENCASE ;
 :X4 ILL-EVENCASE ; 
 :Q1 N-EVENWEAKSTEM ; 
 :Q1 BU/MUS ;           
 +Adv:Q1t K ;  
  

LEXICON LIS  !^C^
 +Attr: ATTR ;  !^C^
 +Sg+Nom: K ;
 +Sg+Nom:a PxV ;
 +Ess:an K ;
 :aQ1 N-EVENWEAKSTEM-OTH ;
 :a ILL-EVENCASE ;
 :aQ1 N-EVENWEAKSTEM-SG-COM ;
 +Der/vuohta: VUOHTA ;    !^C^

 
LEXICON LÁGÁSJ					
 +A+Attr: K ; 
 +A+Der/vuohta: VUOHTA ;  !^C^                      
 +A: N-ODDCASE ;                      
 +A: OTH-ODDCASE ;                   
 +A: DenominalAdjsC ;       
! -:#%- ProperNoun ;		!^C^	



LEXICON AGAdj					
 +A+Attr: K ;
 +A+Der/vuohta: VUOHTA ;         !^C^          
 +A: N-ODDCASE ;                
 +A: OTH-ODDCASE ;              
 +A+Der/ahtes: AHTES ;
! +A+Abess:adagi K ;           
! +A+Abess:adagá K ;           
 +A+Abess:adak K ;           
 +A+Abess:ada K ;           
 +A+Dim+N+Sg+Nom:asj K ; 
 +A+Dim+N:adtja GÅNÅGIS-EVEN ; 
 +A+Dim+N+Der/vuohta:asj VUOHTA ;              !^C^
 +A+Dim+N+Der/ahtes:atj AHTES ;         
! +A+Dim+N+Abess:attjadagi K ;           
! +A+Dim+N+Abess:attjadagá K ;           
 +A+Dim+N+Abess:attjadak K ;           
 +A+Dim+N+Abess:attjada K ;           
 +A+Dim+N:adtja DenominalNounsMUORRA ;
 
LEXICON Ahkásasj					
 +A+Attr: K ;                     
 +A+Sg+Nom: K ;                      
 +A:a GÅNÅGIS-EVEN ;       
 +A+Der/vuohta: VUOHTA ;       !^C^


LEXICON STÁVAL                     
 +A+Attr:Q1 K ;  
 +A+Der/vuohta: VUOHTA ;         !^C^              
 +A:Q1 N-ODDCASE ;               
 +A: OTH-ODDCASE ;               
	


! ======================
! Sublexica for VerbRoot
! ======================

! Even-syllable stems
! -------------------

! Modals
! - - - -

LEXICON GALGGAT
+V+Ind+Prs: I-even-V ;
+V+Ind+Prt: II-even-V ;
+V+Pot:Q1tj I-odd-V ;
+V+Inf:t K ;
+V+PrfPrc:Y3m K ;
+V+Actio:m VSUBST-ODD ;
+V+Ind+ConNeg:Q1 K ;
+V+Pot+ConNeg:Q1tja K ;
+V+Der/mus+N:muss@C.NeedNoun@ MUS ;	!^C^

! This is for auxiliaries, they do not have passive.
! Thus, when passive is added, it will include BASSAT, but not GALGGAT


! Ordinary main verbs
! - - - - - - - - - - 

LEXICON EVEN-NOTCLASSIFIED
BASSAT ;

LEXICON RAVGGAT
+V+IV: BASSATStem ;
+V+IV: DeverbalVerbsBASSAT ; !^C^! 


LEXICON BASSAT
+V+TV: BASSATStem ;
+V+TV: DeverbalVerbsBASSAT ; !^C^! 

LEXICON BASSATStem
 BASSATINCH ;
 EVEN-PASSIVE ; ! ONly TV to Pass! Fix!

LEXICON BASSATINCH
 +Inch+V:goahte INFL-EVEN ;
 INFL-EVEN ;
 DeverbalNounsBASSAT ; !^C^


LEXICON GÁDJOT
+V: GÁDJOTStem ;
+V: DeverbalVerbsBASSAT ; !^C^! 

LEXICON GÁDJOTStem
 GÁDJOTINCH ;
 EVEN-PASSIVE ;

LEXICON GÁDJOTINCH
 +Inch+V:goahte INFL-EVEN ;
 INFL-o- ;
 DeverbalNounsBASSAT ; !^C^




! Odd-syllable stems
! ------------------

LEXICON ODD-NOTCLASSIFIED
UNNEDIT ;


LEXICON UNNEDIT ! Transitives
 +V+TV: JÅLLÅRITStem ;
 +V+TV: DeverbalVerbsJÅLLÅRIT ; !^C^


LEXICON JÅLLÅRIT !At the moment IV, we may perhaps change IV/TV.
 +V+IV: JÅLLÅRITStem ;
 +V+IV: DeverbalVerbsJÅLLÅRIT ; !^C^

LEXICON JÅLLÅRITStem
 JÅLLÅRITINCH ;
 ODD-PASSIVE ; ! this must go for TV only.

LEXICON JÅLLÅRITINCH
 +Inch+V:isjgoahte INFL-EVEN ;
 INFL-ODD ;
 DeverbalNounsJÅLLÅRIT ; !^C^


! Contracted stems
! ----------------

LEXICON OADDÁT ! Do not make nouns via -ár derivation
 +V: OADDÁTStem ;
 +V: DeverbalVerbsOADDÁT ; !^C^

LEXICON TSIEGGIT ! Make nouns via -ár derivation
 +V: OADDÁTStem ;
 +V: DeverbalVerbsOADDÁT ; !^C^
 +V+Der/r:Y5r GAHPER ; ! -ár derivation 


LEXICON OADDÁTStem
 OADDÁTINCH ;
 CONTR-PASSIVE ;

LEXICON OADDÁTINCH
 +Inch+V:goahte INFL-EVEN ;
 INFL-CONTRACTED ;
 DeverbalNounsOADDÁT ; !^C^


! Main inflectional categories
! ----------------------------

LEXICON INFL-EVEN
! Finite
+Ind+Prs: I-even-V ;
+Ind+Prt: II-even-V ;
+Pot:Q1tj I-odd-V ;
+Imprt+Sg2:Q1 K ; ! Basic Imperative, like ConNeg
+Imprt+Du2:Y6 K ; ! Ad hoc solution. We need a II => III operation here !! (todo)
+Imprt:Y1 IMP1-EVEN ;
+Imprt+Du1:Y8on K ;
+Imprt+Pl1:Y8op K ;
+ImprtII:Y1u IMP2-EVEN ; 
! Infinite
+Inf:t K ;
+PrfPrc:Y3m K ;
!+PrsPrc: NOADE ; ! Noun, not correct, III grade!
+Ger:Q1dijn K ;
+GerII:min K ;
+Sup:Q2tjit K ;
+VAbess:Q1k K ;
+VAbess:Q1dak K ;
+VAbess+Der/vuohta:Q1k VUOHTA ;
+VAbess+Der/vuohta:Q1dak VUOHTA ;
+VGen:Q1 K ;
! ConNeg
+Ind+ConNeg:Q1 K ;
!+Ind+Prs+ConNeg:Q1 K ;  ! What is this?
!+Ind+Prt+ConNeg:Y3m K ; ! What is this?
+Pot+ConNeg:Q1tja K ;
+Imprt+ConNeg:Q1 K ;
+ImprtII+ConNeg: ImprtIIConNeg ;
!+Actio: VSUBST-ODD ; 

 

LEXICON INFL-o-
! Finite
+Ind+Prs: I-o-V ;
+Ind+Prt: II-o-V ;
+Pot:Q1tj I-odd-V ;
+Imprt+Sg2:Q1 K ; ! Basic Imperative, like ConNeg
+Imprt+Du2:Y6 K ; ! Ad hoc solution. We need a II => III operation here !! (todo)
+Imprt: IMP1-o- ;
+ImprtII:Y1 IMP2-EVEN ; 
! Infinite
+Inf:t K ;
+PrfPrc:Y3m K ;
!+PrsPrc:Y6 NOADE ; ! Noun, not correct, III grade!
+Ger:Q1dijn K ;
+GerII:min K ;
+Sup:Q1tjit K ;
+Sup:X7tjit K ;
+VAbess:Q1k K ;
+VAbess:Q1dak K ;
+VAbess+Der/vuohta:Q1k VUOHTA ;
+VAbess+Der/vuohta:Q1dak VUOHTA ;
+VGen:Q1 K ;
! ConNeg
+Ind+ConNeg:Q1 K ;
+Pot+ConNeg:Q1tja K ;
+Imprt+ConNeg:Q1 K ;
+ImprtII+ConNeg: ImprtIIConNeg-o- ;
!+Actio: VSUBST-ODD ; 
!+Ind+Prs+ConNeg:Q1 K ; 
!+Ind+Prt+ConNeg:Y3m K ;

LEXICON INFL-ODD
! Finite
 +Ind+Prs: I-odd-V ;
 +Ind+Prt: II-odd-V ;
 +Pot+Sg1:ittjav K ;
 +Pot+Sg2:ittja K ;
 +Pot+Sg3:isj K ;
 +Pot+Du1:ittjin K ;
 +Pot+Du2:ittjabihtte K ;
 +Pot+Du3:ittjaba K ;
 +Pot+Pl1:ittjap K ;
 +Pot+Pl2:ittjabihtit K ;
 +Pot+Pl3:ittji K ;
 +Imprt+Sg2:a K ;
 +Imprt: IMP1-ODD ; 
 +ImprtII:u IMP2-ODD ; 
! Infinite
 +Inf:it K ;
 +PrfPrc:am K ;
! +PrsPrc:iddje NOADE ; ! as nouns
 +Ger:attijn K ;
 +GerII:ime K ;
 +Sup:ittjat K ;
 +VAbess:ik K ;
 +VAbess+Der/vuohta:ik VUOHTA ;
 +VGen:a K ;
 +Ind+ConNeg:a K ;
 +Pot+ConNeg:ittja K ; !tentative
 +Ind+ConNeg:a K ;
 +ImprtII+ConNeg: ImprtIIConNeg ;
! +Actio: VSUBST-EVEN ;



LEXICON INFL-CONTRACTED
! Finite
 +Ind+Prs: I-contr-V ;
 +Ind+Prt: II-contr-V ;
 +Pot:tj I-odd-V ;
 +Imprt+Sg2: K ;
 +Imprt+Du2:jihtte K ;
 +Imprt+Pl2:jit K ;
 +Imprt:ji IMP-CONTRACTED ;
 +ImprtII+Sg2:ju K ;
 +ImprtII+Du2:juhtu K ;
 +ImprtII+Pl2:juhtut K ;
 +ImprtII:ju IMP-CONTRACTED ; 
! Infinite
 +Inf:t K ;
 +PrfPrc:m K ;
! +PrsPrc:jiddje NOADE ; 
 +GerII:min K ;
 +Ger:dijn K ;
 +Sup:tjit K ;
 +VAbess:k K ;
 +VAbess:dak K ;
 +VAbess+Der/vuohta:k VUOHTA ;
 +VAbess+Der/vuohta:dak VUOHTA ;
 +VGen: K ;
 +Ind+ConNeg: K ;
 +Pot+ConNeg:tja K ; !tentative
 +Ind+ConNeg: K ;
 +ImprtII+ConNeg:j ImprtIIConNeg ;
! +Actio: VSUBST-ODD ;
 




! Intermediate suffix lexica
! --------------------------

! These intermediate lexica are here for 
! morphophonological reasons. 

LEXICON I-even-V
 :Q2 I-sg12 ;     ! Q2 governs WeG and e:á
 :Y2 I-sg3 ;	  ! Y2 gives á in sg3 of verbs
     I-du23pl12 ; ! No diacritic, no stem changes
 :Y1 I-du1pl3 ;	  ! Y1 deletes stem vowels in front of j

LEXICON II-even-V
 :Y1 II-sg12pl3 ; ! Y1 deletes stem vowels in front of j
 :Q1 II-other ;   ! Q1, WeG. Q1 deletes stem vow bef. j
                  ! Y1 does not give WeG as Q1 does, therefore the split

LEXICON I-o-V
 :Q2 I-sg12 ;	   ! Q2 governs WeG and e:á
 :Y2 I-sg3 ;	   ! Y2 gives á in sg3 of verbs
     I-du23pl12 ;  ! No diacritic, no stem changes
 :Y1 I-du1pl3-o- ; ! Y1 deletes stem vowels in front of j
 
LEXICON II-o-V
 :Y1 II-sg12pl3-o- ; ! Y1 deletes stem vowels in front of j
 :Q1 II-other ;	     ! Q1, WeG. Q1 deletes stem vow bef. j
		     ! Y1 does not give WeG as Q1 does, therefore the split
 
LEXICON II-odd-V
 II-sg12pl3 ;
 :i II-other ;

LEXICON I-contr-V
 I-sg12 ;
 I-sg3 ;
 I-du23pl12 ;
 :j I-du1pl3 ;

LEXICON II-contr-V
 :j II-sg12pl3 ;
 : II-other ; !w/o j, since j in target lexicon
 


! Final suffix lexica
! -------------------

! Suffix series I
! - - - - - - - - 

LEXICON I-sg12
+Sg1:v K ;
+Sg2: K ;

LEXICON I-sg3
+Sg3: K ;

LEXICON I-du23pl12
+Du2:bihtte K ;
+Du3:ba K ;
+Pl1:p K ;
+Pl2:bihtit K ;

LEXICON I-du1pl3
+Du1:in K ;
+Pl3:i K ;

LEXICON I-du1pl3-o-
+Du1:n K ;
+Pl3: K ;

LEXICON I-odd-V
+Sg1:av K ;
+Sg2:a K ;
+Sg3: K ;
+Du1:in K ;
+Du2:ihppe K ;
+Du3:ibá K ;
+Pl1:ip K ;
+Pl2:ihpit K ;
+Pl3:i K ;


! Suffix series II
! - - - - - - - - 

LEXICON II-sg12pl3
+Sg1:iv K ;
+Sg2:i K ;
+Pl3:in K ;

LEXICON II-sg12pl3-o-
+Sg1:v K ;
+Sg2: K ;
+Pl3:n K ;

LEXICON II-other
+Sg3:j K ;
+Du1:jma K ;
+Du2:jda K ;
+Du3:jga K ;
+Pl1:jma K ;
+Pl2:jda K ;


! Imperative series
! - - - - - - - - - 

! Affirmative

LEXICON IMP1-EVEN
 +Sg3:is K ;
 +Du3:iska K ;
 +Pl2:it K ;
 +Pl3:isa K ;
 
LEXICON IMP1-o-
 +Sg3:Y1s K ;
 +Du1:n K ;
 +Du3:Y1ska K ;
 +Pl1:p K ;
 +Pl2:Y1t K ;
 +Pl3:Y1sa K ;

LEXICON IMP2-EVEN
 +Sg2: K ;
 +Sg3:s K ;
 +Du1:n K ;
 +Du2:da K ;
 +Du3:ska K ;
 +Pl1:p K ;
 +Pl2:t K ;
 +Pl3:sa K ;


LEXICON IMP1-ODD
 +Sg3:is K ;
 +Du1:in'no K ;
 +Du2:ihtte K ;
 +Du3:isá K ;
 +Pl1:ip K ;
 +Pl2:it K ;
 +Pl3:isá K ;

LEXICON IMP2-ODD
 +Sg2: K ;
 +Sg3:s K ;
 +Du1:n'no K ;
 +Du2:htu K ;
 +Du3:ska K ;
 +Pl1:p K ;
 +Pl2:htut K ;
 +Pl3:sá K ;


LEXICON IMP-CONTRACTED !Imp1 & Imp2 have common suffs
! +Sg2: K ;
 +Sg3:s K ;
 +Du1:n'no K ;
 +Du3:ska K ;
 +Pl1:p K ;
 +Pl3:sá K ;


! Negative

LEXICON ImprtIIConNeg
+Sg2:Y1u K ;
+Du2:Y1u K ;
+Pl2:Y1u K ;


LEXICON ImprtIIConNeg-o-
+Sg2:Y1 K ;
+Du2:Y1 K ;
+Pl2:Y1 K ;

!We probably don't want to generate these.
!We use the ordinary ConNeg instead. Cf. Spiik p. 75.
!+Sg3:Q1 K ;
!+Du1:Q1 K ;
!+Du3:Q1 K ;
!+Pl1:Q1 K ;
!+Pl3:Q1 K ;


! Verb Derivation
! --------------------


! Passive derivation
! - - - - - - - - - -

LEXICON EVEN-PASSIVE
 +Pass+V:Y9u OADDÁTINCH ;
 +Pass+V:Q1duvva BASSATINCH ;
 +Pass+Der/duhtte+V:Q1duhtte BASSATINCH ;
 +Pass+V:X7dalla BASSATINCH ;
 +Pass+Der/dahtte+V:X7dahtte BASSATINCH ;
 +Pass+V+PrfPrc:Q1dum K ;

LEXICON ODD-PASSIVE
 +Pass+V:uvva BASSATINCH ;
 +Pass+Der/duhtte+V:uhtte BASSATINCH ;
 +Pass+V+PrfPrc:um K ;
 

LEXICON CONTR-PASSIVE
 +Pass+V:duvva BASSATINCH ;
 +Pass+Der/duhtte+V:duhtte BASSATINCH ;
 +Pass+V+PrfPrc:dum K ;
 
 
LEXICON DUVVAT !deadjectival verbs from -dibme
 +Der/duvva+V:uvva BASSATINCH ;
 +Der/duhtte+V:uhtte BASSATINCH ;



! Even-syllabic stems
! - - - - - - - - - - 

LEXICON DeverbalVerbsBASSAT           !^C^
 +Der/d+V:Q1d3 JÅLLÅRITINCH ;             !^C^ !Causative
 +Der/dalla+V:X7dalla BASSATINCH ;     !^C^ !Caus-Freq
 +Der/dahtte+V:Q1dahtte BASSATINCH ;   !^C^ !Double Caus
 +Der/dasste+V:Q1dasste BASSATINCH ;   !^C^ !Caus-Dim
 +Der/l+V:l JÅLLÅRITINCH ;                !^C^ !Subitive
 +Der/ladda+V:Y3ladda BASSATINCH ;     !^C^ !Subitive-Freq
 +Der/lahtte+V:lahtte BASSATINCH ;     !^C^ !Subitive-Caus
 +Der/lasste+V:lasste BASSATINCH ;     !^C^ !Subitive-Dim
 +Der/st+V:Q1st JÅLLÅRITINCH ;            !^C^ !Dim
 +Der/stalla+V:X7stalla BASSATINCH ;   !^C^ !Dim-Freq
 +Der/stahtte+V:Q1stahtte BASSATINCH ; !^C^ !Dim-Caus
 +Der/stasste+V:Q1stasste BASSATINCH ; !^C^ !Double dim
 +Der/u/a/åd+V:Y5d3 JÅLLÅRITINCH ;      !^C^  !Dem-cont-freq
! +Der/s+V:Y4s JÅLLÅRITINCH ; !^C^    !Passive, doesn´t seem frequent enough (TO)
! +Der/tj+V:Y5tj JÅLLÅRITINCH ; !^C^   !Only e-verbs (TO) 


LEXICON DeverbalNounsBASSAT       !^C^
 +Actor:@C.NeedNoun@  ACTOR ;	!^C^
 +Actor+N:@C.NeedNoun@ DenominalNounsActor ;    !^C^
 +Der/lis+A:Y3liss LIS ;            !^C^
 +Actio:m@C.NeedNoun@ BOAHTEM ;		!^C^
 +Der/mus+N:muss@C.NeedNoun@ MUS ;	!^C^
 +Der/dahka:X7dahka MUORRA;	    !^C^
! +meahttun+A:X7#meahttu MEAHTTUN ;	!^C^ !Not in Kintel					


! Odd-syllable stems
!---------------------

LEXICON DeverbalVerbsJÅLLÅRIT            !^C^
 +Der/ahtte+V:ahtte BASSATINCH ;          !^C^  !Caus
 +Der/alla+V:Y4alla BASSATINCH ;          !^C^  !Freq-cont 
 +Der/asste+V:asste BASSATINCH ;          !^C^  !Dim

LEXICON DeverbalNounsJÅLLÅRIT     !^C^
 +Actio:@C.NeedNoun@ IBME ;	  !^C^		
! +eamoš+N:eapmo1š@C.NeedNoun@ EAMOSH ; !^C^ !Not in Kintel
! +meahttun+A:#meahttu MEAHTTUN ;	!^C^	!Not in Kintel
 +Der/us+N:uss@C.NeedNoun@ JÅLLÅRUS ;  !^C^   
 +Actor:iddje@C.NeedNoun@ ACTOR ;		!^C^ 
 +Actor+N:iddje@C.NeedNoun@ DenominalNounsActor ;	!^C^
 +Der/ahkes:ahkkes TJIEGOS ;         !^C^

		



! Contracted stems
! - - - - - - - - - - 

LEXICON DeverbalVerbsOADDÁT         !^C^
 +Der/d+V:d3 JÅLLÅRITINCH ;             !^C^ !Causative
 +Der/dalla+V:dalla BASSATINCH ;     !^C^ !Caus-Freq
 +Der/dahtte+V:dahtte BASSATINCH ;   !^C^ !Double Caus
 +Der/dasste+V:dasste BASSATINCH ;   !^C^ !Caus-Dim;
 +Der/st+V:st JÅLLÅRITINCH ;            !^C^ !Dim
 +Der/stalla+V:stalla BASSATINCH ;   !^C^ !Dim-Freq
 +Der/stahtte+V:stahtte BASSATINCH ; !^C^ !Dim-Caus
 +Der/stasste+V:stasste BASSATINCH ; !^C^ !Double dim
 +Der/u/a/åd+V:d3 JÅLLÅRITINCH ;      !^C^ !Dem-cont-freq
 

LEXICON DeverbalNounsOADDÁT     !^C^
 +Actor:jiddje@C.NeedNoun@  ACTOR ;	!^C^
 +Actor+N:jiddje@C.NeedNoun@ DenominalNounsActor ;   !^C^
 +Actio:m@C.NeedNoun@ BOAHTEM ;		!^C^
 +Der/mus+N:muss@C.NeedNoun@ MUS ;	!^C^
 +Der/dahka:dahka MUORRA;	    !^C^


 
 
!Sublexicas deverbal nouns
!---------------------------

! vsubst
! These are actio forms

LEXICON VSUBST-ODD
 +N+Sg+Nom:m K ;
 +N+SgNomCmp:m R ;            !^C^
 +N:m OTH-ODDCASE ;
 +N+Der/ahtes:m AHTES ;       !^C^
 
LEXICON VSUBST-EVEN
 +N+Sg+Nom:ibme K ;
 +N+Sg+Nom:ibmeX4 PxV ;
 +N+SgNomCmp:im R ;        !^C^
 +N+Sg+Nom+Der/ahtes:im AHTES ;       !^C^
 +N+Ess:ibmeQ1 K ;
 +N:ibmeX4 ILL-EVENCASE ;
 +N:ibmeQ1 N-EVENWEAKSTEM ;
 

LEXICON MUS
 +Sg+Nom: K ;
 :a MUSSA ;

LEXICON MUSSA
 +Sg+Nom: PxV ;
 +Ess:n K ;
 :Q1 N-EVENWEAKSTEM-OTH ;
 ILL-EVENCASE ;
 :Q1 N-EVENWEAKSTEM-SG-COM ;
 +Der/ahtes:Q1g AHTES ;       !^C^
! +Abess:Q1dagi K ;           !^C^   !should be written separately
! +Abess:Q1dagá K ;           !^C^     !should be written separately
 +Abess:Q1dak K ;           !^C^
 +Abess:Q1da K ;           !^C^
 
LEXICON ACTOR			!^C^
 +A+Attr:Y6 ATTR ;        !^C^
 +N:Y6 NE-EVENCASE ;    !^C^
 +N:Y6 ACTORILL ;        !^C^
 +N:Y6 N-EVENWEAKSTEM ;  !^C^
 +N:Y6 DenominalAdjsV1 ; !^C^
 +N:Y7 DenominalAdjsV2 ; !^C^
 +N:Y6 ACTORCmp ;        !^C^


LEXICON ACTORILL          !^C^
 +Sg+Ill:X4j K ;          !^C^
 +Sg+Ill:X4s PxCi ;        !^C^
 
LEXICON BOAHTEM !^C^
 +N+Sg+Nom: K ; !^C^
 +N+SgNomCmp: R ;	!^C^
 +N: OTH-ODDCASE ; !^C^
 +N+Der/ahtes: AHTES ;       !^C^
  
LEXICON IBME           !^C^
 VSUBST-EVEN ; !^C^
 +N+Sg+Nom+Der/ahtes:im AHTES ;       !^C^

 
LEXICON JÅLLÅRUS
 +Sg+Nom: K ;
 +SgNomCmp: R ;			!^C^
 +SgGenCmp:aQ1 R ;			!^C^
 +PlGenCmp:ajQ1 R ;			!^C^
 +Sg+Nom:a PxV ; 
 +Ess:an K ;
 :aQ1 N-EVENWEAKSTEM-OTH ;
 :a ILL-EVENCASE ;
 :aQ1 N-EVENWEAKSTEM-SG-COM ;
 +Der/ahtes:#ág AHTES ;       !^C^
! +Abess:#ádagi K ;           !^C^   !should be written separately
! +Abess:#ádagá K ;           !^C^   !should be written separately
 +Abess:#ádak K ;           !^C^
 +Abess:#áda K ;           !^C^
 :a DenominalNounsMUORRA ;           !^C^
 +Dim+N+Sg+Nom: K ; 
 +Dim+N:aQ1tj OTH-ODDCASE ;
 +Dim+N:aQ1tj DenominalNounsODD ; 
 		


! Auxiliary verbs
! ---------------

LEXICON NEG
+Prs+Sg1:iv K ;
+Prs+Sg2:i K ;
+Prs+Sg3:ij K ;
+Prs+Du1:en K ;
+Prs+Du2:æhppe K ;
+Prs+Du3:æbá K ;
+Prs+Pl1:ep K ;
+Prs+Pl2:ehpit K ;
+Prs+Pl3:e K ;

+Prt+Sg1:ittjiv K ;
+Prt+Sg2:ittji K ;
+Prt+Sg3:ittjij K ;
+Prt+Du1:ejma K ;
+Prt+Du2:ejda K ;
+Prt+Du3:ejga K ;
+Prt+Pl1:ejma K ;
+Prt+Pl2:ejda K ;
+Prt+Pl3:ettjin K ;
 
+Imprt+Sg2:ale K ;
+Imprt+Sg3:allis K ;
+Imprt+Du1:allon K ;
+Imprt+Du2:al'le K ;
+Imprt+Du3:alliska K ;
+Imprt+Pl1:allop K ;
+Imprt+Pl2:allit K ;
+Imprt+Pl3:allisa K ;

+ImprtII+Sg2:allu K ;
+ImprtII+Sg3:allus K ;
+ImprtII+Du1:allun K ;
+ImprtII+Du2:alluda K ;
+ImprtII+Du3:alluska K ;
+ImprtII+Pl1:allup K ;
+ImprtII+Pl2:allut K ;
+ImprtII+Pl3:allusa K ;

LEXICON ÅRROT
+Inf:rot K ;

+Imprt+Sg2:o K ;
+Imprt+Sg3:rus K ;
+Imprt+Du1:ron K ;
+Imprt+Du2:'ro K ;
+Imprt+Du3:ruska K ;
+Imprt+Pl1:rup K ;
+Imprt+Pl2:rut K ;
+Imprt+Pl3:rusa K ;

LEXICON LIEHKET
+Inf:liehket K ; ! should I use årrot? (No! TO)

! Present
+Ind+Prs+Sg1:lav K ;
+Ind+Prs+Sg2:la K ;
+Ind+Prs+Sg3:la K ;
+Ind+Prs+Sg3:l K ;
+Ind+Prs+Du1:lin K ;
+Ind+Prs+Du2:lihppe K ;
+Ind+Prs+Du3:libá K ;
+Ind+Prs+Pl1:lip K ;
+Ind+Prs+Pl2:lihpit K ;
+Ind+Prs+Pl3:li K ;
+Ind+Prs+Pl3:l K ;

! Past
+Ind+Prt+Sg1:lidjiv K ;
+Ind+Prt+Sg2:lidji K ;
+Ind+Prt+Sg3:lij K ;
+Ind+Prt+Du1:lijma K ;
+Ind+Prt+Du2:lijda K ;
+Ind+Prt+Du3:lijga K ;
+Ind+Prt+Pl1:lijma K ;
+Ind+Prt+Pl2:lijda K ;
+Ind+Prt+Pl3:lidjin K ;


! Present in questions
+Ind+Prs+Sg1:lev K ;
+Ind+Prs+Sg2:le K ;
+Ind+Prs+Sg3:le K ;
+Ind+Prs+Du1:len K ;
+Ind+Prs+Du2:lähppe K ;
+Ind+Prs+Du3:läbá K ;
+Ind+Prs+Pl1:lep K ;
+Ind+Prs+Pl2:lehpit K ;
+Ind+Prs+Pl3:le K ;

! Past in questions
+Ind+Prt+Sg1:ledjiv K ;
+Ind+Prt+Sg2:ledji K ;
+Ind+Prt+Sg3:lej K ;
+Ind+Prt+Du1:lejma K ;
+Ind+Prt+Du2:lejda K ;
+Ind+Prt+Du3:lejga K ;
+Ind+Prt+Pl1:lejma K ;
+Ind+Prt+Pl2:lejda K ;
+Ind+Prt+Pl3:ledjin K ;

! Contracted Neg + Cop forms
+Neg+Ind+Prs+Sg2:il'la K ;
+Neg+Ind+Prs+Pl3:æl'la K ;
+Neg+Ind+Prt+Sg2:il'lim K ;
+Neg+Ind+Prt+Pl3:æl'lim K ;
+Neg+Pot+Pl3:æl'littja K ;

! Imperative
+Imprt+Sg2:ale K ;
+Imprt+Sg3:allis K ;
+Imprt+Du1:allon K ;
+Imprt+Du2:al'le K ;
+Imprt+Du3:alliska K ;
+Imprt+Pl1:allop K ;
+Imprt+Pl2:allit K ;
+Imprt+Pl3:allisa K ;

! Negative forms
+Ind+Prs+ConNeg:le K ;
+Ind+Prs+ConNeg:la K ;
+Ind+Prt+ConNeg:lim K ;
+Cond+ConNeg:lulu K ;
+Pot+ConNeg:littja K ;

! Conditional and Potentional
LULU ;
LITTJA ;

LEXICON LULU
+Cond:lulu V-ODD-PRET ;

LEXICON LITTJA
+Pot:li V-ODD-POT ;

LEXICON V-ODD-PRET
+Sg1:v K ;
+Sg2: K ;
+Sg3:j K ;
+Du1:jma K ;
+Du2:jda K ;
+Du3:jga K ;
+Pl1:jma K ;
+Pl2:jda K ;
+Pl3:n K ;

LEXICON V-ODD-POT
+Prs+Sg1:lav K ;
+Prs+Sg2:la K ;
+Prs+Sg3:la K ;
+Prs+Du1:lin K ;
+Prs+Du2:lihppe K ;
+Prs+Du3:libá K ;
+Prs+Pl1:lip K ;
+Prs+Pl2:lihpit K ;
+Prs+Pl3:li K ;
K ;







! Sublexica for possessive suffixes
! =================================

LEXICON PxV      ! This intermezzo of a lexicon due to
 :X8 PxV12 ;	 ! the stem alternation in the possessive
     PxV3 ;      ! suffixes.

LEXICON PxV12    ! Due to incoming Acc/Gen 12 vs 3 alternation
 PxV1 ;
 PxV2 ;

LEXICON PxV23	! Due to incoming Refl pron 1 vs 23 alternation
 PxV2 ;
 PxV3 ;

LEXICON PxV1
 +PxSg1:m K ;
 +PxDu1:ma K ;
 +PxPl1:ma K ;

LEXICON PxV2
 +PxSg2:t K ;
 +PxDu2:da K ;
 +PxPl2:da K ;

LEXICON PxV3
 +PxSg3:s K ;
 +PxDu3:ska K ;
 +PxPl3:sa K ;

! Lexicon for the deviating -im, -it suffixes
! Non-deviating form pointed to common PxCc
LEXICON PxCi
 +PxSg1:im K ;
 +PxSg2:it K ;
 PxCc ;

! Lexicon for regular PxC nom,acc, gen
LEXICON PxC
 +PxSg1:am K ;
 +PxSg2:at K ;
 PxCc ;
 
! Lexicon for regular PxC ine, ela, com and pl
LEXICON PxCs
 +PxSg1:ám K ;
 +PxSg2:át K ;
 +PxSg3:es K ;
 +PxDu1:imme K ;
 +PxDu2:ihtte K ;
 +PxDu3:eska K ;
 +PxPl1:imme K ;
 +PxPl2:ihtte K ;
 +PxPl3:isá K ;
 

!Commom lexicon for all PxC
LEXICON PxCc
 +PxSg3:is K ;
 +PxDu1:imme K ;
 +PxDu2:ihtte K ;
 +PxDu3:iská K ;
 +PxPl1:imme K ;
 +PxPl2:ihtte K ;
 +PxPl3:isá K ;



LEXICON K
 ENDLEX ;
! +Clt:clitic ENDLEX ;

LEXICON ENDLEX
 @D.NeedNoun.ON@ # ;
! This ENDLEX business in order to remove illegal compounds.

