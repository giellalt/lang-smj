Multichar_Symbols
 +N +A +Adv +V
 +Pron +CS +CC +Adp +Po +Pr +Interj +Pcle
 +Pers +Dem +Interr +Refl +Recipr +Rel +Indef
 +Sg +Pl +Nom +Gen +Acc +Ill +Ine +Ela +Com +Ess 
 +PxSg1 +PxSg2 +PxSg3 +PxDu1 +PxDu2 +PxDu3 +PxPl1 +PxPl2 +PxPl3
 +Comp +Superl +Actor
 +Clt +Attr
 +Pass +Ind +Prs +Prt +Pot +Cond +Imprt
 +Sg1 +Sg2 +Sg3 +Du1 +Du2 +Du3 +Pl1 +Pl2 +Pl3
 +Inf +Act +Ger +ConNeg +Neg +ImprtII +PrsPrc +PrfPrc +VGen +VAbess
 X1 X2 X3 X4 X5 X6 X7 X8 X9
 Q1 Q2 Q3 Q4 Q5 Q6 Q7 Q8 Q9
 Y1 Y2 Y3 Y4 Y5 Y6 Y7 Y8



LEXICON Root
 NounRoot ;
 ProperNoun ;
 AdjectiveRoot ;
 VerbRoot ;
 Copula ;
 Negativeverb ;
 Adverb ;
 Particles ;
 Subjunction ;
 Conjunction ;
 Adposition ;
 Postposition ;
 Preposition ;
 Interjection ;
 Pronoun ;

! Sublexica for NounRoot
!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON MUORRA
 +N: N-EVENSTRONGSTEM ;
 +N:X1 N-EVENWEAKSTEM ;

LEXICON N-EVENSTRONGSTEM
 NE-EVENCASE ;
 ILL-EVENCASE ;

LEXICON NE-EVENCASE
 +Sg+Nom: K ;
 +Sg+Nom: PxV ;
 +Ess:n K ;

LEXICON ILL-EVENCASE
 +Sg+Ill:j K ;
 +Sg+Ill:s PxC ;

LEXICON N-EVENWEAKSTEM
 +Sg+Gen: K ;
 +Sg+Gen: PxV ;
 +Sg+Acc:v K ;
 +Sg+Acc: PxV ;
 +Sg+Ine:n K ;
 +Sg+Ine:n PxCi ;
 +Sg+Ela:s K ;
 +Sg+Ela:st PxCi ;
 +Sg+Com:jn K ;
 +Sg+Com:jn PxCi ;
 +Pl+Nom: K ;
 +Pl+Gen:j K ;
 +Pl+Acc:jt K ;
 +Pl+Acc:jd PxC ;
 +Pl+Ine:jn K ;
 +Pl+Ine:jn PxC ;
 +Pl+Ela:ij K ;
 +Pl+Ela:jst PxC ;
 +Pl+Com:js K ;


! Contracted stems
!!!!!!!!!!!!!!!!!!

LEXICON SARVES
 :e N-CONTRWEAKSTEM ;
 :á N-CONTRSTRONGSTEM ;

LEXICON N-CONTRWEAKSTEM
 +Sg+Nom:s K ;
 +Sg+Nom:s PxC ;
 +Ess:n ;
 
LEXICON N-CONTRSTRONGSTEM
 ILL-EVENCASE ;
 N-EVENWEAKSTEM ;


! Odd-syllable stems
!!!!!!!!!!!!!!!!!!!!


LEXICON ÅRES
 NE-ODDCASE ;
 OTH-ODDCASE ;

LEXICON GÁMAS ! Odd-syllable C-final noun with CG but no VChange
 NE-ODDCASE ;
 OTH-ODDCASE ;

LEXICON GAHPER
 +Sg+Nom: K ;
 +Sg+Nom: PxC ; 
 OTH-ODDCASE ;

LEXICON NE-ODDCASE
 +Sg+Nom:X1 K ;
 +Sg+Nom: PxC ;
 +Ess:n K ;

LEXICON OTH-ODDCASE
 +Sg+Gen:a K ;
 +Sg+Gen: PxC ;
 +Sg+Acc:av K ;
 +Sg+Acc: PxC ;
 +Sg+Ill:ij K ;
 +Sg+Ine:in K ;
 +Sg+Ine:in PxC ;
 +Sg+Ela:is K ;
 +Sg+Ela:ist PxC ;
 +Sg+Com:ijn K ;
 +Sg+Com:ijn PxC ;
 +Ess:in K ;
 +Pl+Nom:a K ;
 +Pl+Gen:ij K ;
 +Pl+Acc:ijt K ;
 +Pl+Acc:ijd PxC ;
 +Pl+Ill:ijda K ;
 +Pl+Ine:ijn K ;
 +Pl+Ine:ijn PxC ;
 +Pl+Ela:ijs K ;
 +Pl+Ela:ijst PxC ;
 +Pl+Com:ij K ;

LEXICON PxV
 +PxSg1:m K ;
 +PxSg2:t K ;
 +PxSg3:s K ;
 +PxDu1:ma K ;
 +PxDu2:da K ;
 +PxDu3:ska K ;
 +PxPl1:ma K ;
 +PxPl2:da K ;
 +PxPl3:sa K ;

! Lexicon for the deviating -im, -it suffixes
! Non-deviating form pointed to common PxCc
LEXICON PxCi
 +PxSg1:im K ;
 +PxSg2:it K ;
 PxCc ;

! Lexicon for regular PxC
LEXICON PxC
 +PxSg1:am K ;
 +PxSg2:at K ;
 PxCc ;

!Commom lexicon for all PxC
LEXICON PxCc
 +PxSg3:is K ;
 +PxDu1:imme K ;
 +PxDu2:ihttea K ;
 +PxDu3:iskká K ;
 +PxPl1:imme K ;
 +PxPl2:ihtte K ;
 +PxPl3:isá K ;



LEXICON K
 # ;
! +Clt:clitic # ;
