Multichar_Symbols
 +N +A +Adv +V
 +Pron +CS +CC +Adp +Po +Pr +Interj +Pcle
 +Pers +Dem +Interr +Refl +Recipr +Rel +Indef
 +Sg +Pl +Nom +Gen +Acc +Ill +Ine +Ela +Com +Ess +Par +Abe
 +PxSg1 +PxSg2 +PxSg3 +PxDu1 +PxDu2 +PxDu3 +PxPl1 +PxPl2 +PxPl3
 +Comp +Superl +Actor
 +Clt +Attr
 +Pass +Ind +Prs +Prt +Pot +Cond +Imprt +Sup
 +Sg1 +Sg2 +Sg3 +Du1 +Du2 +Du3 +Pl1 +Pl2 +Pl3
 +Inf +Act +Ger +ConNeg +Neg +ImprtII +PrsPrc +PrfPrc +VGen +VAbess
 X1 X2 X3 X4 X5 X6 X7 X8 X9
 Q1 Q2 Q3 Q4 Q5 Q6 Q7 Q8 Q9
 Y1 Y2 Y3 Y4 Y5 Y6 Y7 Y8

! Basic lexica, pointing to the other lexicon files
! ===============================================
  
LEXICON Root
 NounRoot ;
! ProperNoun ;
 AdjectiveRoot ;
 VerbRoot ;
! Copula ;
! Negativeverb ;
! Adverb ;
! Particles ;
 Subjunction ;
 Conjunction ;
! Adposition ;
! Postposition ;
! Preposition ;
! Interjection ;
 Pronoun ;

! Sublexica for NounRoot
! ======================


! Even-syllable stems
! -------------------

! 2syll stem with cg (note X1)
LEXICON MUORRA
 +N: N-EVENSTRONGSTEM ;
 +N:X1 N-EVENWEAKSTEM ;

! 2syll stem without cg (note missing X1)
LEXICON LÅHKKE
 +N: N-EVENSTRONGSTEM ;
 +N: N-EVENWEAKSTEM ;

LEXICON GÅNÅGIS
 +N: GÅNÅGIS-C ;
 +N:a GÅNÅGIS-EVEN ;

LEXICON GÅNÅGIS-C
 +Sg+Nom: K ;

LEXICON GÅNÅGIS-EVEN
 +Ess:n K ;
 +Ess:n PxC ;
 ILL-EVENCASE ;
 :X1 N-EVENWEAKSTEM ;

LEXICON N-EVENSTRONGSTEM
 NE-EVENCASE ;
 ILL-EVENCASE ;

LEXICON NE-EVENCASE
 +Sg+Nom: K ;
 +Sg+Nom: PxV ;
 +Ess:n K ;

LEXICON ILL-EVENCASE
 +Sg+Ill:j K ;
 +Sg+Ill:s PxC ;

LEXICON N-EVENWEAKSTEM
 +Sg+Gen: K ;
 +Sg+Gen: PxV ;
 +Sg+Acc:v K ;
 +Sg+Acc: PxV ;
 +Sg+Ine:n K ;
 +Sg+Ine:n PxCi ;
 +Sg+Ela:s K ;
 +Sg+Ela:st PxCi ;
 +Sg+Com:jn K ;
 +Sg+Com:jn PxCi ;
 +Pl+Nom: K ;
 +Pl+Gen:j K ;
 +Pl+Acc:jt K ;
 +Pl+Acc:jd PxC ;
 +Pl+Ill:jda K ;
 +Pl+Ine:jn K ;
 +Pl+Ine:jn PxC ;
 +Pl+Ela:ij K ;
 +Pl+Ela:jst PxC ;
 +Pl+Com:js K ;
! ess & ill pl do not use Px that often, left out.

! Contracted stems
! ----------------


LEXICON SARVES
 :e N-CONTRWEAKSTEM ;
 :á N-CONTRSTRONGSTEM ;

LEXICON N-CONTRWEAKSTEM
 +Sg+Nom:s K ;
 +Sg+Nom:s PxC ;
 +Ess:n K ;
 
LEXICON N-CONTRSTRONGSTEM
 ILL-EVENCASE ;
 N-EVENWEAKSTEM ;


! Odd-syllable stems
! ------------------

! oddsyll, cg, C-final, 2ndsyll vowchange
LEXICON ÅRES
 NE-ODDCASE ;
 OTH-ODDCASE ;

! oddsyll, cg, C-final, no 2ndsyll vowchange
LEXICON GÁMAS ! Odd-syllable C-final noun with CG but no VChange
 NE-ODDCASE ;
 OTH-ODDCASE ;

! oddsyll, no cg, no vowchange
LEXICON GAHPER
 +Sg+Nom: K ;
 +Sg+Nom: PxC ; 
 OTH-ODDCASE ;

LEXICON NE-ODDCASE
 +Sg+Nom:X1 K ;
 +Sg+Nom:X1 PxC ;
 +Ess:X1n K ;

LEXICON OTH-ODDCASE
 +Sg+Gen:a K ;
 +Sg+Gen: PxC ;
 +Sg+Acc:av K ;
 +Sg+Acc: PxC ;
 +Sg+Ill:ij K ;
 +Sg+Ine:in K ;
 +Sg+Ine:in PxC ;
 +Sg+Ela:is K ;
 +Sg+Ela:ist PxC ;
 +Sg+Com:ijn K ;
 +Sg+Com:ijn PxC ;
 +Ess:in K ;
 +Pl+Nom:a K ;
 +Pl+Gen:ij K ;
 +Pl+Acc:ijt K ;
 +Pl+Acc:ijd PxC ;
 +Pl+Ill:ijda K ;
 +Pl+Ine:ijn K ;
 +Pl+Ine:ijn PxC ;
 +Pl+Ela:ijs K ;
 +Pl+Ela:ijst PxC ;
 +Pl+Com:ij K ;


! Sublexica for AdjectiveRoot
! ===========================

! Even-syllable stems
! -------------------

LEXICON GIEVRRA
 +A: K ;



! Sublexica for VerbRoot
! ======================

! Even-syllable stems
! -------------------

LEXICON BASSAT
+V: FINITE-EVEN ;

LEXICON FINITE-EVEN
+Ind+Prs: I-even-V ;
+Ind+Prt: II-even-V ;
+Pot:X1tj I-odd-V ;
+Imprt: K ;
+ImprtII: K ; 
+Inf:t K ;
+PrfPrc:m K ;
+Ger:min K ;
+Sup:X1tjit K ;
+VAbess:X1k K ;
+VGen:X1 K ;
+Ind+Prs+ConNeg: K ;

! Odd-syllable stems
! ------------------

LEXICON JÅLLÅRIT
 +V: FINITE-UNEVEN ;

LEXICON FINITE-UNEVEN
 +Ind+Prs: I-odd-V ;
 +Ind+Prt: II-odd-V ;
 +Pot:ittja I-even-V ;

+Imprt: K ; ! TODO: Imperatives
+ImprtII: K ; 
+Inf:it K ;
+PrfPrc:am K ;
+Ger:ime K ;
+Sup:ittjat K ;
+VAbess:ik K ;
+VGen:a K ;
+Ind+Prs+ConNeg:a K ;

! Contracted stems
! ----------------

LEXICON OADDÁT
 +V: FINITE-CONTRACTED ;

LEXICON FINITE-CONTRACTED
 +Ind+Prs: I-contr-V ;
 +Ind+Prt: II-contr-V ;
 +Imprt: K ;
 +ImprtII: K ; 
 +Inf:t K ;
 +PrfPrc:m K ;
 +Ger:min K ;
 +Sup:tjit K ;
 +VAbess:k K ;
 +VGen: K ;
 +Ind+Prs+ConNeg: K ;

! Intermediate suffix lexica
! --------------------------

LEXICON I-even-V
 :X1 I-sg12 ;
 :Q1 I-sg3 ;
     I-du23pl12 ;
 :Y1 I-du1pl3 ;

LEXICON II-even-V
 :Y1 II-sg12pl3 ;
 :X2 II-other ;


LEXICON II-odd-V
 II-sg12pl3 ;
 :i II-other ;

LEXICON I-contr-V
 I-sg12 ;
 I-sg3 ;
 I-du23pl12 ;
 :j I-du1pl3 ;

LEXICON II-contr-V
 :j II-sg12pl3 ;
 :j II-other ;
 


! Final suffix lexica
! -------------------

! Suffix series I
! - - - - - - - - 

LEXICON I-sg12
+Sg1:v K ;
+Sg2: K ;

LEXICON I-sg3
+Sg3: K ;

LEXICON I-du23pl12
+Du2:bihtte K ;
+Du3:ba K ;
+Pl1:p K ;
+Pl2:bihtit K ;

LEXICON I-du1pl3
+Du1:in K ;
+Pl3:i K ;

LEXICON I-odd-V
+Sg1:av K ;
+Sg2:a K ;
+Sg3: K ;
+Du1:in K ;
+Du2:ihppe K ;
+Du3:ibá K ;
+Pl1:ip K ;
+Pl2:ihpit K ;
+Pl3: K ;


! Suffix series II
! - - - - - - - - 

LEXICON II-sg12pl3
+Sg1:iv K ;
+Sg2:i K ;
+Pl3:in K ;

LEXICON II-other
+Sg3:j K ;
+Du1:jma K ;
+Du2:jda K ;
+Du3:jga K ;
+Pl1:jma K ;
+Pl2:jda K ;

! Auxiliary verbs
! ---------------

LEXICON NEG
+Prs+Sg1:iv K ;
+Prs+Sg2:i K ;
+Prs+Sg3:ij K ;
+Prs+Du1:en K ;
+Prs+Du2:ähppe K ;
+Prs+Du3:äbá K ;
+Prs+Pl1:ep K ;
+Prs+Pl2:ehpit K ;
+Prs+Pl3:e K ;

+Prt+Sg1:ittjiv K ;
+Prt+Sg2:ittji K ;
+Prt+Sg3:ittjij K ;
+Prt+Du1:ejma K ;
+Prt+Du2:ejda K ;
+Prt+Du3:ejga K ;
+Prt+Pl1:ejma K ;
+Prt+Pl2:ejda K ;
+Prt+Pl3:ettjin K ;
 
+Imprt+Sg2:ale K ;
+Imprt+Sg3:allis K ;
+Imprt+Du1:allon K ;
+Imprt+Du2:al'le K ;
+Imprt+Du3:alliska K ;
+Imprt+Pl1:allop K ;
+Imprt+Pl2:allit K ;
+Imprt+Pl3:allisa K ;

+ImprtII+Sg2:allu K ;
+ImprtII+Sg3:allus K ;
+ImprtII+Du1:allun K ;
+ImprtII+Du2:alluda K ;
+ImprtII+Du3:alluska K ;
+ImprtII+Pl1:allup K ;
+ImprtII+Pl2:allut K ;
+ImprtII+Pl3:allusa K ;

LEXICON ÅRROT
+Imprt+Sg2:o K ;
+Imprt+Sg3:rus K ;
+Imprt+Du1:ron K ;
+Imprt+Du2:'ro K ;
+Imprt+Du3:ruska K ;
+Imprt+Pl1:rup K ;
+Imprt+Pl2:rut K ;
+Imprt+Pl3:rusa K ;

LEXICON LIEHKET

! Present
+Prs+Sg1:lav K ;
+Prs+Sg2:la K ;
+Prs+Sg3:la K ;
+Prs+Du1:lin K ;
+Prs+Du2:lihppe K ;
+Prs+Du3:libá K ;
+Prs+Pl1:lip K ;
+Prs+Pl2:lihpit K ;
+Prs+Pl3:li K ;

! Past
+Prt+Sg1:lidjiv K ;
+Prt+Sg2:lidji K ;
+Prt+Sg3:lij K ;
+Prt+Du1:lijma K ;
+Prt+Du2:lijda K ;
+Prt+Du3:lijga K ;
+Prt+Pl1:lijma K ;
+Prt+Pl2:lijda K ;
+Prt+Pl3:lidjin K ;

! Contracted Neg + Cop forms
+Neg+Prs+Sg2:il'la K ;
+Neg+Prs+Pl3:äl'la K ;
+Neg+Prt+Sg2:il'lim K ;
+Neg+Prt+Pl3:äl'lim K ;
+Neg+Pot+Pl3:äl'littja K ;

! Imperative
+Imprt+Sg2:ale K ;
+Imprt+Sg3:allis K ;
+Imprt+Du1:allon K ;
+Imprt+Du2:al'le K ;
+Imprt+Du3:alliska K ;
+Imprt+Pl1:allop K ;
+Imprt+Pl2:allit K ;
+Imprt+Pl3:allisa K ;

! Conditional and Potentional
LULU ;
LITTJA ;

LEXICON LULU
+Cond:lulu V-UNEVEN-PRET ;

LEXICON LITTJA
+Pot:li V-UNEVEN-POT ;

LEXICON V-UNEVEN-PRET
+Sg1:v K ;
+Sg2: K ;
+Sg3:j K ;
+Du1:jma K ;
+Du2:jda K ;
+Du3:jga K ;
+Pl1:jma K ;
+Pl2:jda K ;
+Pl3:n K ;

LEXICON V-UNEVEN-POT
+Prs+Sg1:lav K ;
+Prs+Sg2:la K ;
+Prs+Sg3:la K ;
+Prs+Du1:lin K ;
+Prs+Du2:lihppe K ;
+Prs+Du3:libá K ;
+Prs+Pl1:lip K ;
+Prs+Pl2:lihpit K ;
+Prs+Pl3:li K ;
K ;







! Sublexica for possessive suffixes
! =================================

LEXICON PxV ! This intermezzo of a lexicon due to
 PxV1 ;	    ! the stem alternation in the possessive
 PxV23 ;    ! suffixes.

LEXICON PxV1
 +PxSg1:m K ;
 +PxDu1:ma K ;
 +PxPl1:ma K ;

LEXICON PxV23
 +PxSg2:t K ;
 +PxSg3:s K ;
 +PxDu2:da K ;
 +PxDu3:ska K ;
 +PxPl2:da K ;
 +PxPl3:sa K ;

! Lexicon for the deviating -im, -it suffixes
! Non-deviating form pointed to common PxCc
LEXICON PxCi
 +PxSg1:im K ;
 +PxSg2:it K ;
 PxCc ;

! Lexicon for regular PxC
LEXICON PxC
 +PxSg1:am K ;
 +PxSg2:at K ;
 PxCc ;

!Commom lexicon for all PxC
LEXICON PxCc
 +PxSg3:is K ;
 +PxDu1:imme K ;
 +PxDu2:ihtte K ;
 +PxDu3:iskká K ;
 +PxPl1:imme K ;
 +PxPl2:ihtte K ;
 +PxPl3:isá K ;



LEXICON K
 # ;
! +Clt:clitic # ;
