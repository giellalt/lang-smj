Multichar_Symbols
 +N +A +Adv +V
 +Pron +CS +CC +Adp +Po +Pr +Interj +Pcle
 +Pers +Dem +Interr +Refl +Recipr +Rel +Indef
 +Sg +Pl +Nom +Gen +Acc +Ill +Ine +Ela +Com +Ess +Par +Abe
 +PxSg1 +PxSg2 +PxSg3 +PxDu1 +PxDu2 +PxDu3 +PxPl1 +PxPl2 +PxPl3
 +Comp +Superl +Actor
 +Clt +Attr +Dim 
 +Pass +Ind +Prs +Prt +Pot +Cond +Imprt +ImprtII +Sup
 +Sg1 +Sg2 +Sg3 +Du1 +Du2 +Du3 +Pl1 +Pl2 +Pl3
 +Inf +Act +Ger +GerII +ConNeg +Neg +PrsPrc +PrfPrc +VGen +VAbess
 +CLB +PUNCT +HYPH +PAR
 +Der/d +Der/l +Der/st +Der/n +Der/h +Der/s +Der/dahtte +Der/dasste
 +Der/ladda +Der/stalla +Der/tj +Der/ár
 g2 m2 n2 h2
 b3 d3 g3 m3 h3
 X1 X2 X3 X4 X5 X6 X7 X8 X9
 Q1 Q2 Q3 Q4 Q5 Q6 Q7 Q8 Q9
 Y1 Y2 Y3 Y4 Y5 Y6 Y7 Y8 Y9


! =================================================
! Basic lexica, pointing to the other lexicon files
! =================================================
  
LEXICON Root
 NounRoot ;
! ProperNoun ;
 AdjectiveRoot ;
 VerbRoot ;
 Adverb ;
 Particle ;
 Subjunction ;
 Conjunction ;
 Adposition ;
! Postposition ;
! Preposition ;
 Interjection ;
 Pronoun ;
Numeral ;
 Punctuation ;



! ======================
! Sublexica for NounRoot
! ======================


! Even-syllable stems
! -------------------

! 2syll stems
! - - - - - -

! 2syll stem with cg (note Q1)
LEXICON MUORRA
 +N: N-EVENSTRONGSTEM ;
 +N:Q1 N-EVENWEAKSTEM ; ! Weak grade Q1
 +N: DenominalNounsMUORRA ;

! 2syll stem without cg (note missing Q1) 
! Many of these are  pres ptc, they should be derived from
! the verbs, and taken from there
LEXICON NOADE
 +N: N-EVENSTRONGSTEM ;
 +N: N-EVENWEAKSTEM ;

! 2syll stems witout any morphophonology
LEXICON KINO ! no diacritics whatsoever
 +N: NE-EVENCASE ;
 +N: ILL-EVENCASE ;
 +N: N-EVENWEAKSTEM-OTH ;
 +N: N-EVENWEAKSTEM-SG-COM ;


! 4syll stems
! - - - - - - 
LEXICON GÅNÅGIS
 +N: GÅNÅGIS-C ;
 +N:a GÅNÅGIS-EVEN ;

LEXICON GÅNÅGIS-C
 +Sg+Nom: K ;

LEXICON GÅNÅGIS-EVEN
 +Ess:n K ;
 +Ess:n PxC ;
 ILL-EVENCASE ;
 :Q1 N-EVENWEAKSTEM ;

LEXICON CELSIUS
 +N: GÅNÅGIS-C ; ! just as gånågis
 +N:a CELSIUS-EVEN ; ! as gånågis, but no Q1 (WeG)

LEXICON CELSIUS-EVEN
 +Ess:n K ;
 +Ess:n PxC ;
 ILL-EVENCASE ;
 N-EVENWEAKSTEM ;


! Nominal substems
! - - - - - - - -  

LEXICON N-EVENSTRONGSTEM
 NE-EVENCASE ;
 :X4 ILL-EVENCASE ; ! X4 for the e:á and e:å alternation

LEXICON NE-EVENCASE
 +Sg+Nom: K ;
 +Sg+Nom: PxV ;
 +Ess:n K ;
 +Ess:n PxC ; ! parallel with CELCIUS above


LEXICON ILL-EVENCASE
 +Sg+Ill:j K ;
 +Sg+Ill:s PxC ;

LEXICON N-EVENWEAKSTEM
 N-EVENWEAKSTEM-OTH ;
 N-EVENWEAKSTEM-SG-COM ; ! separate bec of X5
! :X5 N-EVENWEAKSTEM-SG-COM ; ! separate bec of X5
! Not X5 twice!

LEXICON N-EVENWEAKSTEM-OTH
 +Sg+Gen: K ;
 +Sg+Gen: PxV ;
 +Sg+Acc:v K ;
 +Sg+Acc: PxV ;
 +Sg+Ine:n K ;
 +Sg+Ine:n PxCi ;
 +Sg+Ela:s K ;
 +Sg+Ela:st PxCi ;
 +Pl+Nom: K ;
 +Pl+Gen:j K ;
 +Pl+Acc:jt K ;
 +Pl+Acc:jd PxC ;
 +Pl+Ill:jda K ;
 +Pl+Ine:jn K ;
 +Pl+Ine:jn PxC ;
 +Pl+Ela:js K ;
 +Pl+Ela:jst PxC ;
 +Pl+Com:j K ;
! ess & ill pl do not use Px that often, left out.
! But what about nom pl, gen pl?


LEXICON N-EVENWEAKSTEM-SG-COM
 +Sg+Com:X5jn K ; ! X5 for o:u in ComSg. Note two dummies in a row!
 +Sg+Com:X5jn PxCi ;


! Odd-syllable stems
! ------------------

! oddsyll, cg, C-final, 2ndsyll vowchange
LEXICON ÅRES
 +N:Q1 NE-ODDCASE ; ! Q1 is general WeG
 +N:X5 OTH-ODDCASE ; ! X5 is e:å and o:u

! oddsyll, cg, C-final, no 2ndsyll vowchange
LEXICON GÁMAS ! Odd-syllable C-final noun with CG but no VChange
 +N:Q1 NE-ODDCASE ;
 +N: OTH-ODDCASE ;

! oddsyll, no cg, no vowchange
LEXICON GAHPER
 +N: N-ODDCASE ; ! no pointer to E-ODDCASE, as gahper has no short Ess
 +N: OTH-ODDCASE ;

! oddsyll, cg, vowchange, V-final nominative
LEXICON BENA
 +N:X3 BENA-NE ;
 +N:X5 OTH-ODDCASE ;
! X5 for vow change

! Odd-syllable stems, sublexica
! - - - - - - - - - - - - - - - 

LEXICON NE-ODDCASE ! The lexicon is split bec. of gahper's missing short Ess
 N-ODDCASE ;
 E-ODDCASE ;

LEXICON N-ODDCASE
 +Sg+Nom: K ;
 +Sg+Nom: PxC ;

LEXICON E-ODDCASE
 +Ess:X1n K ; ! X1 to delete cns before short ess
 +Ess:X1n PxC ; ! Parallel to CELCIUS !!

LEXICON OTH-ODDCASE
 DenominalNounsODD ;
 +Sg+Gen:a K ;
 +Sg+Gen: PxC ;
 +Sg+Acc:av K ;
 +Sg+Acc: PxC ;
 +Sg+Ill:ij K ;  ! Px ?? !!
 +Sg+Ine:in K ;
 +Sg+Ine:in PxC ;
 +Sg+Ela:is K ;
 +Sg+Ela:ist PxC ;
 +Sg+Com:ijn K ;
 +Sg+Com:ijn PxC ;
 +Ess:in K ;
 +Pl+Nom:a K ;
 +Pl+Gen:ij K ;
 +Pl+Acc:ijt K ;
 +Pl+Acc:ijd PxC ;
 +Pl+Ill:ijda K ;
 +Pl+Ine:ijn K ;
 +Pl+Ine:ijn PxC ;
 +Pl+Ela:ijs K ;
 +Pl+Ela:ijst PxC ;
 +Pl+Com:ij K ;

LEXICON BENA-NE
 +Sg+Nom: K ;
 +Ess:n K ;

!LEXICON BENA-OBL
! +Sg+Nom:00 PxC ;
! +Sg+Gen:a K ;
! +Sg+Gen: PxC ;
! +Sg+Acc:a K ;
! +Sg+Acc: K ;
! +Sg+Acc: PxC ;
! +Pl+Nom:at K ;
! +Sg+Ill:asa PxV ;
! :i MALLASI-/NUORABU- ;
! :i MALLASI- ;


! Contracted stems
! ----------------


LEXICON SARVES
 +N:Q1 SARVES-NE ;
 +N:X4 CONTR-OTH ;

LEXICON SUOLOJ
 +N:Q3 SUOLOJ-NE ;
 +N:X4 CONTR-OTH ;

LEXICON GUOMOJ
 +N:X6 SUOLOJ-NE ; ! III-I CG
 +N:X4 CONTR-OTH ;


LEXICON SARVES-NE
 +Sg+Nom:s K ;
 +Sg+Nom:s PxC ;
 +Ess:n K ;

LEXICON SUOLOJ-NE
 +Sg+Nom:j K ;
 +Sg+Nom:j PxC ;
 +Ess:n K ;
 
LEXICON CONTR-OTH
 ILL-EVENCASE ;
 N-EVENWEAKSTEM ;
! ComSg may cause trouble, since there is a dummy for it



! Denominal nouns
! ===============

LEXICON DenominalNounsMUORRA
 +Dim:X7tj GAHPER ;

LEXICON DenominalNounsODD
 +Dim+N+Sg+Nom:asj K ; ! added a
 +Dim+N:attja GÅNÅGIS-EVEN ; ! added a

! LEXICON DenominalNounsODD-OBL
 



! ===========================
! Sublexica for AdjectiveRoot
! ===========================

! Even-syllable stems
! -------------------

LEXICON GIEVRRA ! Attr WeG, -s
 +A+Attr:Q1s K ;
 +A: N-EVENSTRONGSTEM ;
 +A:Q1 N-EVENWEAKSTEM ;
 +A: BU/MUS ;
 +Adv:Q1t K ;

LEXICON NUORRA ! Attr =
 +A+Attr: K ;
 +A: N-EVENSTRONGSTEM ;
 +A:Q1 N-EVENWEAKSTEM ; ! cg in nominal paradigm
 +A: BU/MUS ;
 +Adv:Q1t K ; ! Presumably WeG as adverb as well

LEXICON HÁVSSKE ! Attr w/o WeG, -s
 +A+Attr:s K ;
 +A: N-EVENSTRONGSTEM ;
 +A:Q1 N-EVENWEAKSTEM ; ! Presumably WeG in nominal paradigm
 +A: BU/MUS ;
 +Adv:Q1t K ;

LEXICON UNNE ! Attr e > a~e, WeG
 +A+Attr:aQ1 K ; ! a-attr.
 +A+Attr:eQ1 K ; ! e-attr.
 +A:e N-EVENSTRONGSTEM ; ! presumably normal adjective
 +A:eQ1 N-EVENWEAKSTEM ; ! behaviour, with cg, and with
 +A:e BU/MUS ;           ! the e-form as a starting
 +Adv:eQ1t K ;           ! point.


! Fix the pres participle

LEXICON ÁRMMOGIS ! Attr = Pred. Nom. Sg.
 +A+Attr: K ; ! twol fixes the ss > s
 +A+Sg+Nom: K ;
 +A:a GÅNÅGIS-EVEN ; ! Declined as four-syllable noun
 +A:a BU/MUS ;
 +Adv:aQ1t K ; 

LEXICON ASIDASJ
 +A+Attr:is K ; ! Special Attr form
 +A+Sg+Nom:asj K ;
 +A:atja OTH-ODDCASE ; ! Declined as four-syllable noun
 +A:atja BU/MUS ;
 +Adv:atjaQ1t K ; 

LEXICON TJALMEDIBME
 +A+Attr:is K ; ! Special Attr form
 +A:ibme N-EVENSTRONGSTEM ;
 +A:ibmeQ1 N-EVENWEAKSTEM ;
 +A:ibme BU/MUS ;
 +Adv:ibmeQ1t K ; 

! Odd-syllable stems
! ------------------

LEXICON TJIEGOS ! a. Adjs on -k, negating adjs on -dahkes, sm adjs on -os
 +A+Attr: K ;
 +A: N-ODDCASE ; ! no pointer to E-ODDCASE, as adjs have no short Ess
 +A: OTH-ODDCASE ;
 +A: ABBO/AMOS ;
 
LEXICON BASSTEL ! b. Adjs on -et, -l, -r, sm om -k, -sj with -is-attr
 +A+Attr:is K ;

LEXICON MUTTÁK ! c. Adjs on -ák, Attr -is or =
 +A+Attr: K ;
 +A+Attr:is K ;

LEXICON ALLAK ! d. Adjs on -ak, attr. on -a
 +A+Attr: K ;
 +A:k K ;
 +A:ga K ; 

LEXICON GALMAS
 +A+Attr: K ;
 +A:s K ;

LEXICON SUOHKAT
 +A+Attr:is K ;

LEXICON DIMES
 +A+Attr:a K ;
 +A+Nom+Sg:es K ;

LEXICON OAMES
 +A+Attr: K ;
 +A+Nom+Sg:s K ;

! Contracted stems
! ----------------

! Comparation
! -----------

LEXICON BU/MUS ! even-syll adjectives
 +Comp:b BUStem ;
 +Superl:muss BUOREMUS ;

LEXICON ABBO/AMOS ! odd-syll adjectives
 +Comp+N+Sg:p K ;
 +Comp:bbo ABBO ;
 +Superl:amos AMOS ; 


! Comparative
! -----------

LEXICON BUStem
 +Sg+Nom: K ;
 +Sg+Gen:u K ;
 +Sg+Acc:uv K ;
 +Sg+Ill:uj K ;
 +Sg+Ine:un K ;
 +Sg+Ela:us K ;
 +Sg+Com:ujn K ;
 +Ess:un K ;
 +Pl+Nom:u K ;
 +Pl+Gen:uj K ;
 +Pl+Acc:ujt K ;
 +Pl+Ill:ujda K ;
 +Pl+Ine:ujn K ;
 +Pl+Ela:ujs K ;
 +Pl+Com:uj K ;

LEXICON ABBO
 # ;
LEXICON AMOS
 # ;
! THESE TWO LEXICA ARE DUMMY ENTRIES, 
! I DO NOT UNDERSTAND WHY THEY ARE NOT
! HERE, THEY SHOULD HAVE BEEN HERE.

! Superlative
! -----------

LEXICON BUOREMUS
 +Sg+Nom: K ; ! the short form, twol shortens ss#
 +Sg+Nom: PxV ; ! separate nom bec of the short form
 :a BUOREMUS-OBL ; ! Add -a, directly to the obliques

LEXICON BUOREMUS-OBL
 +Ess:n K ; ! essive has strong stem
 +Ill:j K ; ! so has illative
 +Ill:s PxC ; ! this Px form not mentioned in Spiik
 :Q1 N-EVENWEAKSTEM ; ! These words go like muorra
 

! Derivation of adjectives
! ------------------------

! tbw.



! ======================
! Sublexica for VerbRoot
! ======================

! Even-syllable stems
! -------------------

LEXICON BASSAT

+V: BASSATStem ;
+V: DeverbalVerbsBASSAT ;


LEXICON BASSATStem
 EVEN-PASSIVE ;
 BASSATINCH ;

LEXICON EVEN-PASSIVE
 +Pass+V:Q1duvva INFL-EVEN ;
 +Pass+V:Q1dalla INFL-EVEN ;

LEXICON BASSATINCH
 +Inch+V:goahte INFL-EVEN ;
 INFL-EVEN ;
 DeverbalNounsBASSAT ;

LEXICON DeverbalVerbsBASSAT
 +Der/d+V:Q1d INFL-ODD ;
 +Der/dahtte+V:Q1dahtte INFL-EVEN ;
 +Der/dasste+V:Q1dasste INFL-EVEN ;
 +Der/l+V:l INFL-ODD ;
 +Der/ladda+V:ladda INFL-EVEN ;
 +Der/s+V:Y4s INFL-ODD ;
 +Der/st+V:Q1st INFL-ODD ;
 +Der/stalla+V:Q1stalla INFL-EVEN ;
 +Der/tj+V:Y5tj INFL-ODD ;
! +Der/d+V:Q1d DeverbalVerbsJÅLLÅRIT ;
! double causatives?

LEXICON DeverbalNounsBASSAT
 # ;



LEXICON GALGGAT
+V: INFL-EVEN ;
! This is for auxiliaries, they do not have passive.
! Thus, when passive is added, it will include BASSAT, but not GALGGAT

LEXICON INFL-EVEN
+Ind+Prs: I-even-V ;
+Ind+Prt: II-even-V ;
+Pot:Q1tj I-odd-V ;
+Imprt+Sg2:Q1 K ; ! Basic Imperative, like ConNeg
+Imprt+Du2: K ; ! Ad hoc solution. We need a II => III operation here !! (todo)
+Imprt:Y1 IMP1-EVEN ;
+ImprtII:Y1u IMP2-EVEN ; 
+Inf:t K ;
+PrfPrc:Y3m K ;
+PrsPrc:Y6 NOADE ; ! Noun, not correct, III grade!
+Ger:Q1dijn K ;
+GerII:min K ;
+Sup:Q1tjit K ;
+VAbess:Q1k K ;
+VGen:Q1 K ;
+Ind+Prs+ConNeg:Q1 K ;
+Ind+Prt+ConNeg:Y3m K ;
+VNoun:m VSUBST ;

LEXICON VSUBST
 NE-ODDCASE ;
 OTH-ODDCASE ;

! Odd-syllable stems
! ------------------

LEXICON JÅLLÅRIT

 +V: JÅLLÅRITStem ;
 +V: DeverbalVerbsJÅLLÅRIT ;

LEXICON JÅLLÅRITStem
 ODD-PASSIVE ;
 JÅLLÅRITINCH ;

LEXICON ODD-PASSIVE
 +Pass+V:uvva INFL-EVEN ;

LEXICON JÅLLÅRITINCH
 +Inch+V:goahte INFL-EVEN ;
 INFL-ODD ;
 DeverbalNounsJÅLLÅRIT ;

LEXICON DeverbalVerbsJÅLLÅRIT
 +Der/d+V:ahtte INFL-EVEN ;

LEXICON DeverbalNounsJÅLLÅRIT
 # ;

LEXICON INFL-ODD
 +Ind+Prs: I-odd-V ;
 +Ind+Prt: II-odd-V ;
 +Pot:ittja I-even-V ;
 +Imprt+Sg2:a K ;
 +Imprt: IMP1-ODD ; 
 +ImprtII:u IMP2-ODD ; 
 +Inf:it K ;
 +PrfPrc:am K ;
 +PrsPrs:iddje NOADE ; ! as nouns
 +Ger:attjin K ;
 +GerII:ime K ;
 +Sup:ittjat K ;
 +VAbess:ik K ;
 +VGen:a K ;
 +Ind+Prs+ConNeg:a K ;

! Contracted stems
! ----------------

LEXICON OADDÁT
 +V: OADDÁTStem ;
 +V: DeverbalVerbsOADDÁT ;

LEXICON OADDÁTStem
 OADDÁTINCH ;
 CONTR-PASSIVE ;

LEXICON OADDÁTINCH
 +Inch+V:goahte INFL-CONTRACTED ;
 INFL-CONTRACTED ;
 DeverbalNounsOADDÁT ;

LEXICON CONTR-PASSIVE
 +Pass+V:duvva INFL-EVEN ;

LEXICON DeverbalNounsOADDÁT
 # ;

LEXICON DeverbalVerbsOADDÁT
 # ;

LEXICON INFL-CONTRACTED
 +Ind+Prs: I-contr-V ;
 +Ind+Prt: II-contr-V ;
 +Imprt+Du2:jihtte K ;
 +Imprt+Pl2:jip K ;
 +Imprt:ji IMP-CONTRACTED ;
 +ImprtII+Du2:juhtu K ;
 +ImprtII+Pl2:juhtut K ;
 +ImprtII:ju IMP-CONTRACTED ; 
 +Inf:t K ;
 +PrfPrc:m K ;
 +PrsPrc: NOADE ;
 +Der/ár:ár GAHPER ;
 +GerII:min K ;
 +Ger:dijn K ;
 +Sup:tjit K ;
 +VAbess:k K ;
 +VGen: K ;
 +Ind+Prs+ConNeg: K ;




! Intermediate suffix lexica
! --------------------------

LEXICON I-even-V
 :Q2 I-sg12 ; ! Q2 governs WeG and e:á
 :Y2 I-sg3 ;  ! Y2 gives á in sg3 of verbs
     I-du23pl12 ; ! No diacritic, no stem changes
 :Y1 I-du1pl3 ; ! Y1 deletes stem vowels in front of j

LEXICON II-even-V
 :Y1 II-sg12pl3 ; ! Y1 deletes stem vowels in front of j
 :Q1 II-other ; ! ! Q1, WeG. Q1 deletes stem vow bef. j
! Y1 does not give WeG as Q1 does, therefore the split

LEXICON II-odd-V
 II-sg12pl3 ;
 :i II-other ;

LEXICON I-contr-V
 I-sg12 ;
 I-sg3 ;
 I-du23pl12 ;
 :j I-du1pl3 ;

LEXICON II-contr-V
 :j II-sg12pl3 ;
 :j II-other ;
 


! Final suffix lexica
! -------------------

! Suffix series I
! - - - - - - - - 

LEXICON I-sg12
+Sg1:v K ;
+Sg2: K ;

LEXICON I-sg3
+Sg3: K ;

LEXICON I-du23pl12
+Du2:bihtte K ;
+Du3:ba K ;
+Pl1:p K ;
+Pl2:bihtit K ;

LEXICON I-du1pl3
+Du1:in K ;
+Pl3:i K ;

LEXICON I-odd-V
+Sg1:av K ;
+Sg2:a K ;
+Sg3: K ;
+Du1:in K ;
+Du2:ihppe K ;
+Du3:ibá K ;
+Pl1:ip K ;
+Pl2:ihpit K ;
+Pl3: K ;


! Suffix series II
! - - - - - - - - 

LEXICON II-sg12pl3
+Sg1:iv K ;
+Sg2:i K ;
+Pl3:in K ;

LEXICON II-other
+Sg3:j K ;
+Du1:jma K ;
+Du2:jda K ;
+Du3:jga K ;
+Pl1:jma K ;
+Pl2:jda K ;


! Other series
! - - - - - - 


LEXICON IMP1-EVEN
 +Sg3:is K ;
 +Du1:on K ;
 +Du3:iska K ;
 +Pl1:op K ;
 +Pl2:it K ;
 +Pl3:isa K ;

LEXICON IMP2-EVEN
 +Sg2: K ;
 +Sg3:s K ;
 +Du1:n K ;
 +Du2:da K ;
 +Du3:ska K ;
 +Pl1:p K ;
 +Pl2:t K ;
 +Pl3:sa K ;



LEXICON IMP1-ODD
 +Sg3:is K ;
 +Du1:in'no K ;
 +Du2:ihtte K ;
 +Du3:isá K ;
 +Pl1:ip K ;
 +Pl2:it K ;
 +Pl3:isá K ;

LEXICON IMP2-ODD
 +Sg2: K ;
 +Sg3:s K ;
 +Du1:n'no K ;
 +Du2:htu K ;
 +Du3:ska K ;
 +Pl1:p K ;
 +Pl2:htut K ;
 +Pl3:sá K ;


LEXICON IMP-CONTRACTED !Imp1 & Imp2 have common suffs
 +Sg2: K ;
 +Sg3:s K ;
 +Du1:n'no K ;
 +Du3:ska K ;
 +Pl1:p K ;
 +Pl3:sá K ;








! Auxiliary verbs
! ---------------

LEXICON NEG
+Prs+Sg1:iv K ;
+Prs+Sg2:i K ;
+Prs+Sg3:ij K ;
+Prs+Du1:en K ;
+Prs+Du2:æhppe K ;
+Prs+Du3:æbá K ;
+Prs+Pl1:ep K ;
+Prs+Pl2:ehpit K ;
+Prs+Pl3:e K ;

+Prt+Sg1:ittjiv K ;
+Prt+Sg2:ittji K ;
+Prt+Sg3:ittjij K ;
+Prt+Du1:ejma K ;
+Prt+Du2:ejda K ;
+Prt+Du3:ejga K ;
+Prt+Pl1:ejma K ;
+Prt+Pl2:ejda K ;
+Prt+Pl3:ettjin K ;
 
+Imprt+Sg2:ale K ;
+Imprt+Sg3:allis K ;
+Imprt+Du1:allon K ;
+Imprt+Du2:al'le K ;
+Imprt+Du3:alliska K ;
+Imprt+Pl1:allop K ;
+Imprt+Pl2:allit K ;
+Imprt+Pl3:allisa K ;

+ImprtII+Sg2:allu K ;
+ImprtII+Sg3:allus K ;
+ImprtII+Du1:allun K ;
+ImprtII+Du2:alluda K ;
+ImprtII+Du3:alluska K ;
+ImprtII+Pl1:allup K ;
+ImprtII+Pl2:allut K ;
+ImprtII+Pl3:allusa K ;

LEXICON ÅRROT
+Inf:rot K ;

+Imprt+Sg2:o K ;
+Imprt+Sg3:rus K ;
+Imprt+Du1:ron K ;
+Imprt+Du2:'ro K ;
+Imprt+Du3:ruska K ;
+Imprt+Pl1:rup K ;
+Imprt+Pl2:rut K ;
+Imprt+Pl3:rusa K ;

LEXICON LIEHKET
+Inf:liehket K ; ! should I use årrot?

! Present
+Prs+Sg1:lav K ;
+Prs+Sg2:la K ;
+Prs+Sg3:la K ;
+Prs+Sg3:l K ;
+Prs+Du1:lin K ;
+Prs+Du2:lihppe K ;
+Prs+Du3:libá K ;
+Prs+Pl1:lip K ;
+Prs+Pl2:lihpit K ;
+Prs+Pl3:li K ;
+Prs+Pl3:l K ;

! Past
+Prt+Sg1:lidjiv K ;
+Prt+Sg2:lidji K ;
+Prt+Sg3:lij K ;
+Prt+Du1:lijma K ;
+Prt+Du2:lijda K ;
+Prt+Du3:lijga K ;
+Prt+Pl1:lijma K ;
+Prt+Pl2:lijda K ;
+Prt+Pl3:lidjin K ;


! Present in questions
+Prs+Sg1:lev K ;
+Prs+Sg2:le K ;
+Prs+Sg3:le K ;
+Prs+Du1:len K ;
+Prs+Du2:lähppe K ;
+Prs+Du3:läbá K ;
+Prs+Pl1:lep K ;
+Prs+Pl2:lehpit K ;
+Prs+Pl3:le K ;

! Past in questions
+Prt+Sg1:ledjiv K ;
+Prt+Sg2:ledji K ;
+Prt+Sg3:lej K ;
+Prt+Du1:lejma K ;
+Prt+Du2:lejda K ;
+Prt+Du3:lejga K ;
+Prt+Pl1:lejma K ;
+Prt+Pl2:lejda K ;
+Prt+Pl3:ledjin K ;

! Contracted Neg + Cop forms
+Neg+Prs+Sg2:il'la K ;
+Neg+Prs+Pl3:æl'la K ;
+Neg+Prt+Sg2:il'lim K ;
+Neg+Prt+Pl3:æl'lim K ;
+Neg+Pot+Pl3:æl'littja K ;

! Imperative
+Imprt+Sg2:ale K ;
+Imprt+Sg3:allis K ;
+Imprt+Du1:allon K ;
+Imprt+Du2:al'le K ;
+Imprt+Du3:alliska K ;
+Imprt+Pl1:allop K ;
+Imprt+Pl2:allit K ;
+Imprt+Pl3:allisa K ;

! Negative forms
+Ind+Prs+ConNeg:la K ;
+Ind+Prt+ConNeg:lim K ;
+Cond+ConNeg:lulu K ;
+Pot+ConNeg:littja K ;

! Conditional and Potentional
LULU ;
LITTJA ;

LEXICON LULU
+Cond:lulu V-ODD-PRET ;

LEXICON LITTJA
+Pot:li V-ODD-POT ;

LEXICON V-ODD-PRET
+Sg1:v K ;
+Sg2: K ;
+Sg3:j K ;
+Du1:jma K ;
+Du2:jda K ;
+Du3:jga K ;
+Pl1:jma K ;
+Pl2:jda K ;
+Pl3:n K ;

LEXICON V-ODD-POT
+Prs+Sg1:lav K ;
+Prs+Sg2:la K ;
+Prs+Sg3:la K ;
+Prs+Du1:lin K ;
+Prs+Du2:lihppe K ;
+Prs+Du3:libá K ;
+Prs+Pl1:lip K ;
+Prs+Pl2:lihpit K ;
+Prs+Pl3:li K ;
K ;







! Sublexica for possessive suffixes
! =================================

LEXICON PxV ! This intermezzo of a lexicon due to
 PxV1 ;	    ! the stem alternation in the possessive
 PxV23 ;    ! suffixes.

LEXICON PxV1
 +PxSg1:m K ;
 +PxDu1:ma K ;
 +PxPl1:ma K ;

LEXICON PxV23
 +PxSg2:t K ;
 +PxSg3:s K ;
 +PxDu2:da K ;
 +PxDu3:ska K ;
 +PxPl2:da K ;
 +PxPl3:sa K ;

! Lexicon for the deviating -im, -it suffixes
! Non-deviating form pointed to common PxCc
LEXICON PxCi
 +PxSg1:im K ;
 +PxSg2:it K ;
 PxCc ;

! Lexicon for regular PxC
LEXICON PxC
 +PxSg1:am K ;
 +PxSg2:at K ;
 PxCc ;

!Commom lexicon for all PxC
LEXICON PxCc
 +PxSg3:is K ;
 +PxDu1:imme K ;
 +PxDu2:ihtte K ;
 +PxDu3:iskká K ;
 +PxPl1:imme K ;
 +PxPl2:ihtte K ;
 +PxPl3:isá K ;



LEXICON K
 # ;
! +Clt:clitic # ;
