! ======================== !
! Hyphenator for Lule Sámi !
! ======================== !

echo <<defining Cns and Vow...>>

define Vow       [ a | á | e | i | o | u | y | æ | ø | å | ä | ö | 
                   A | Á | E | I | O | U | Y | Æ | Ø | Å | Ä | Ö | 
é | ó | ú | í | à | è | ò | ù | ì | ë | ü | ï | â | ê | ô | û | î | ã | ý 
É | Ó | Ú | Í | À | È | Ò | Ù | Ì | Ë | Ü | Ï | Â | Ê | Ô | Û | Î | Ã | Ý ] ;

define 2ndVow [ e | a | o | u | E | A | O | U ] ;

define VowNot2ndVow [ Vow - 2ndVow ] ;

define vU [ u | U ] ; define vO [ o | O ]  ; define vA [ a | A ]  ;
define vE [ e | E ] ; define vI [ i | I ]  ;

define VowNotU [ Vow - vU ] ;
define VowNotO [ Vow - vO ] ;
define VowNotA [ Vow - vA ] ;
define VowNotE [ Vow - vE ] ;
define VowNotI [ Vow - vI ] ;

define IEdipht [ i e | I e | I E ] ;
define UOdipht [ u o | U o | U O ] ;
define OAdipht [ o a | O a | O A ] ;
define OUdipht [ o u | O u | O U ] ; ! jour-na-lis-sta

define dipht [ IEdipht | UOdipht | OAdipht | OUdipht ] ;

define Cns [ b | c | č | d | đ | f | g | h | j | k | l | m | n | ŋ | ñ | ń | 
             B | C | Č | D | Đ | F | G | H | J | K | L | M | N | Ŋ | Ñ | Ń |
             p | r | s | š | t | ŧ | v | w | x | z | ž |   
             P | R | S | Š | T | Ŧ | V | W | X | Z | Ž ] ; 

!define Gem [ b b | d d | f f | g g | k k | l l | m m | n n | ŋ ŋ | ñ ñ | ń ń | 
!             B B | D D | F F | G G | K K | L L | M M | N N | Ŋ Ŋ | Ñ Ñ | Ń Ń |
!             p p | r r | s s | š š | t t | ŧ ŧ | v v |    
!             P P | R R | S S | Š Š | T T | Ŧ Ŧ | V V ]  ;
             
define cS [ S | s ] ; define Cnss [ Cns - cS ] ;

!define Sgm [ Cns | Vow ] ;

!define Cns [ Cnssymb | Vow i ] ;

echo << Rules>>

define befdipht [..] -> %- ||     Vow           _ dipht ;
define nodipht  [..] -> %- || Cns Vow           _ VowNot2ndVow , Cns VowNotU _ vO , 
                              Cns VowNotO       _ [vA|vU]      , Cns VowNotI _ vE ;
define rdnj4cns [..] -> %- || Vow (Cns) r       _ d n j ;
define unvoiced [..] -> %- || Vow (Cns)         _ h [ n | m | l | j | r ] Vow ;
define palatal  [..] -> %- || Vow (Cns) d       _ d j , Vow (Cns) l _ l j , Vow (Cns) n _ n j , 
                              Vow ([Cns - d]) d _ n j , Vow ([Cns - t]) t _ n j ;
                              
define dissnas  [..] -> %- || Vow [ b | d | j | l | r | v ] _ [ b m | d n | g ŋ ] Vow ;

define gem1cns  [..] -> %- || Vow b       _       b m Vow ,
                              Vow d       _       d (n) j Vow , 
                              Vow g       _       g [ŋ|ń|ñ] Vow , 
                              Vow k       _       k n Vow , 
                              Vow l       _       l [d|j|n|t(j)] Vow ,
                              Vow m       _       m [b|p] Vow ,
                              Vow n       _       n [ d | g | t ([j|s]) ]  Vow ,
                              Vow [ŋ|ń|ñ] _ [ŋ|ń|ñ] [g|k] (Cns) Vow ,
                              Vow p       _       p [k|t([j|s])|s(j)] Vow ,
                              Vow s       _       s [[j[k|m|v]] | k|m|n|t ]  Vow ;
define s2cns    [..] -> %- || Vow Cns s   _ s (j) (Cnss) Vow ;
define s2cnswg  [..] -> %- || Vow Cns     _ s (j) Cnss Vow ;  
                                                         ! máj-stet,  (if commented out: máis-tit), 
                                                         ! " Vow (Cns)" as LC gives må-rå-stit, 
                                                         ! fá-sto, now rås-tit, fás-to.

!define st       [..] -> %- || Vow               _ s t Vow ;   ! a-sta                          
define j4cns    [..] -> %- || Vow  Cns Cns Cns  _ [ [s|t|n] j | t s ]  Vow ;
define j3cns    [..] -> %- || Vow  Cns Cns      _ [ [s|t|n] j | t s ] Vow ;
define j2cns    [..] -> %- || Vow  Cns          _ [ [s|t|n] j | t s ] Vow ;
define j1cns    [..] -> %- || Vow               _ [ [s|t|n] j | t s ] Vow ;
define 4cns     [..] -> %- || Vow  Cns Cns  Cns _  Cns Vow ;
define 3cns     [..] -> %- || Vow  Cns Cns      _  Cns Vow ;
define 2cns     [..] -> %- || Vow  Cns          _  Cns Vow ;
define 1cns     [..] -> %- || Vow               _  Cns Vow ;

                     
echo <<Combining...>>

read regex [
befdipht
.o.
nodipht
.o.
rdnj4cns 
.o.
unvoiced
.o.
palatal 
.o.
dissnas 
.o.
gem1cns
.o.
s2cns
.o.
s2cnswg
.o.
j4cns
.o.
j3cns
.o.
j2cns
.o.
j1cns
.o.
4cns
.o.
3cns
.o.
2cns
.o.
1cns ] ;

invert net ;



!define dissnas  [..] -> %- || Vow [ b | d | j | l | r | v ] _ [ b m | d n | g ŋ ] Vow ;
