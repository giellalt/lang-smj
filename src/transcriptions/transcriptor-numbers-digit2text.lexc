! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@uit.no or feedback@divvun.no

Multichar_Symbols

+Use/NG

LEXICON NUMBERSECTION
           HUNDREDSM     ; ! 200M
 1:tjuohte HUNDREDM      ; ! 100M
           TENSM         ; ! 20-99M
           TEENSM        ; ! 10-19M
           1MILJON       ; ! 1M
           ONESM         ; ! 1-9M
           1GIGA         ; ! 1G
           ONESG         ; ! 1-9G
           HUNDREDST     ; ! 200000-999999
 1:tjuohte HUNDREDT      ; ! 100000-100999
           TENST         ; ! 20000-99999,10000-10999
           TEENST        ; ! 11000-19999
           ONEST         ; ! 2000-9999
  1:tuvsán THOUSAND      ; ! 1000-1999
   1:duhát THOUSAND      ; ! 1000-1999
1% :tuvsán THOUSAND      ; ! 1 000-1 999
 1% :duhát THOUSAND      ; ! 1 000-1 999
           UNDERTHOUSAND ; ! 100-999
           TENS          ; ! 20-99
           TEENS         ; ! 10-19
           ONES          ; ! 1-9

LEXICON ONESG
   :akta%   1GIGA ;
  2:guokta  GIGA  ;
  3:gålmmå  GIGA  ;
  4:niellja GIGA  ;
! 4:niellje GIGA  ; !marg/dial
  5:vihtta  GIGA  ;
  6:guhtta  GIGA  ;
  7:gietjav GIGA  ;
  8:gáktsa  GIGA  ;
  9:aktse   GIGA  ;

LEXICON 1GIGA
 1:millijárdda%  OVERMILLIONS ;

LEXICON GIGA
 :% millijárdda%  OVERMILLIONS ;

LEXICON HUNDREDSM
  2:guokta  CUODIM ;
  3:gålmmå  CUODIM ;
  4:niellja CUODIM ;
! 4:niellje CUODIM ; !marg/dial
  5:vihtta  CUODIM ;
  6:guhtta  CUODIM ;
  7:gietjav CUODIM ;
  8:gáktsa  CUODIM ;
  9:aktse   CUODIM ;

LEXICON CUODIM
 :tjuode HUNDREDM ; !200, not 100

LEXICON HUNDREDM
       TENSM  ;
       TEENSM ;
   %0: ONESM  ;
 %0%0: MILJON ;

LEXICON TEENSM
 1: TEENM ;

LEXICON TEENM
  1:akta    LOHKAIM ;
  2:guokta  LOHKAIM ;
  3:gålmmå  LOHKAIM ;
  4:niellja LOHKAIM ;
! 4:niellje LOHKAIM ; !marg/dial
  5:vihtta  LOHKAIM ;
  6:guhtta  LOHKAIM ;
  7:gietjav LOHKAIM ;
  8:gáktsa  LOHKAIM ;
  9:aktse   LOHKAIM ;

LEXICON LOHKAIM
 :lågenan MILJON ;

LEXICON TENSM
  1%0:låhke   MILJON ;
    2:guokta  LÅGEVM ;
    3:gålmmå  LÅGEVM ;
    4:niellja LÅGEVM ;
!   4:niellje LÅGEVM ; !marg/dial
    5:vihtta  LÅGEVM ;
    6:guhtta  LÅGEVM ;
    7:gietjav LÅGEVM ;
    8:gáktsa  LÅGEVM ;
    9:aktse   LÅGEVM ;

LEXICON LÅGEVM
 %0:låk MILJON ;
   :låk ONESM  ;

LEXICON ONESM
   :akta%   1MILJON ;
  2:guokta  MILJON  ;
  3:gålmmå  MILJON  ;
  4:niellja MILJON  ;
! 4:niellje MILJON  ; !marg/dial
  5:vihtta  MILJON  ;
  6:guhtta  MILJON  ;
  7:gietjav MILJON  ;
  8:gáktsa  MILJON  ;
  9:aktse   MILJON  ;

LEXICON 1MILJON
 1:millijåvnnå%  OVERTHOUSANDS ;
 1:millijåvnnå%  OVERTHOUSANDS ;

LEXICON MILJON
 :% millijåvnnå%  OVERTHOUSANDS ;

! =================
! Under the million
! =================

LEXICON OVERMILLIONS
                            HUNDREDSM     ;
                  1:tjuohte HUNDREDM      ;
                 %0:        TENSM         ;
                 %0:        TEENSM        ;
               %0%0:        1MILJON       ;
               %0%0:        ONESM         ;
             %0%0%0:        HUNDREDST     ; ! x00.000.000
            %0%0%01:tjuohte HUNDREDT      ; ! 100.000.000
           %0%0%0%0:        TENST         ; !  20.000.000
           %0%0%0%0:        TEENST        ; !  10.000.000
         %0%0%0%0%0:        ONEST         ; !   2.000.000
        %0%0%0%0%01:tuvsán  THOUSAND      ; !   1.000.000
       %0%0%0%0%0%0:        UNDERTHOUSAND ;
     %0%0%0%0%0%0%0:        TENS          ;
     %0%0%0%0%0%0%0:        TEENS         ;
   %0%0%0%0%0%0%0%0:        ONES          ;
 %0%0%0%0%0%0%0%0%0:        COMMA         ; !  x000000

LEXICON OVERTHOUSANDS
                         HUNDREDST     ; !    xx00 000
              % :        HUNDREDST     ; !   x x00 000
               1:tjuohte HUNDREDT      ; !    x100 000
             % 1:tjuohte HUNDREDT      ; !   x 100 000
              %0:        TENST         ; !    x0xx 000
            % %0:        TENST         ; !   x 0xx 000
              %0:        TEENST        ; !    x01x 000
            % %0:        TEENST        ; !   x 01x 000
            %0%0:        ONEST         ; !    20x0 000
          % %0%0:        ONEST         ; !   2 0x0 000
           %0%01:tuvsán  THOUSAND      ; !     1001xxx
         %0%01% :tuvsán  THOUSAND      ; !    1001 xxx
       % %0%01% :tuvsán  THOUSAND      ; !   1 001 xxx
          %0%0%0:        UNDERTHOUSAND ; !     x000xxx
        %0%0%0% :        UNDERTHOUSAND ; !    x000 xxx
      % %0%0%0% :        UNDERTHOUSAND ; !   x 000 xxx
        %0%0%0%0:        TENS          ; !     x0000xx
      %0%0%0% %0:        TENS          ; !    x000 0xx
    % %0%0%0% %0:        TENS          ; !   x 000 0xx
        %0%0%0%0:        TEENS         ; !     x00001x
      %0%0%0% %0:        TEENS         ; !    x000 01x
    % %0%0%0% %0:        TEENS         ; !   x 000 01x
      %0%0%0%0%0:        ONES          ; !     x00000x
    %0%0%0% %0%0:        ONES          ; !    x000 00x
  % %0%0%0% %0%0:        ONES          ; !   x 000 00x
    %0%0%0%0%0%0:        COMMA         ; !     x000000
  %0%0%0% %0%0%0:        COMMA         ; !    x000 000
% %0%0%0% %0%0%0:        COMMA         ; !   x 000 000

LEXICON HUNDREDST
  2:guokta  CUODIT ;
  3:gålmmå  CUODIT ;
  4:niellja CUODIT ;
! 4:niellje CUODIT ; !marg/dial
  5:vihtta  CUODIT ;
  6:guhtta  CUODIT ;
  7:gietjav CUODIT ;
  8:gáktsa  CUODIT ;
  9:aktse   CUODIT ;

LEXICON CUODIT
 :tjuode HUNDREDT ; ! 200000-199999

LEXICON HUNDREDT	     ! X = 1-9, Y = 0-9
           TENST     ; ! X2XYYY, X10YYY
           TEENST    ; ! X1XYYY
  %01:akta THOUSANDS ;
   %0:     ONEST     ; ! XX0YYY
 %0%0:     THOUSANDS ; ! X00YYY

LEXICON TEENST
 1: TEENT ;

LEXICON TEENT
 %0:lågev   THOUSANDS ;
  1:akta    LOHKAIT   ;
  2:guokta  LOHKAIT   ;
  3:gålmmå  LOHKAIT   ;
  4:niellja LOHKAIT   ;
  4:niellje LOHKAIT   ;
  5:vihtta  LOHKAIT   ;
  6:guhtta  LOHKAIT   ;
  7:gietjav LOHKAIT   ;
  8:gáktsa  LOHKAIT   ;
  9:aktse   LOHKAIT   ;

LEXICON LOHKAIT
 :lågenan THOUSANDS ;

LEXICON TENST
  2:guokta  LÅGEVT ;
  3:gålmmå  LÅGEVT ;
  4:niellja LÅGEVT ;
! 4:niellje LÅGEVT ; !marg/dial
  5:vihtta  LÅGEVT ;
  6:guhtta  LÅGEVT ;
  7:gietjav LÅGEVT ;
  8:gáktsa  LÅGEVT ;
  9:aktse   LÅGEVT ;

LEXICON LÅGEVT
 %0:låhke             THOUSANDS ;
  1:låhke#akta#tuvsán THOUSAND  ; !OBS
   :låk               ONEST     ;

LEXICON ONEST
  1:        THOUSANDS ; ! Rett! 
  2:guokta  THOUSANDS ;
  3:gålmmå  THOUSANDS ;
  4:niellja THOUSANDS ;
! 4:niellje THOUSANDS ; !marg/dial
  5:vihtta  THOUSANDS ;
  6:guhtta  THOUSANDS ;
  7:gietjav THOUSANDS ;
  8:gáktsa  THOUSANDS ;
  9:aktse   THOUSANDS ;

LEXICON THOUSANDS      ! x > 1
       :tuvsán   THOUSAND ;    ! Denne for tekst-til-tale og andre normative verkty
+Use/NG:duhát    THOUSAND ;    ! 
%0%0%0.:tuvsánat #        ;

LEXICON THOUSAND
          UNDERTHOUSAND ; !   1100-1999
     % :  UNDERTHOUSAND ; ! 1 100-1 999
      %0: TENS          ; !   1020-1099
    % %0: TENS          ; ! 1 020-1 099
      %0: TEENS         ; !   1010-1019
    % %0: TEENS         ; ! 1 010-1 019
    %0%0: ONES          ; !   1001-1009
  % %0%0: ONES          ; ! 1 001-1 009
  %0%0%0: COMMA         ; !  1000
% %0%0%0: COMMA         ; ! 1 000

!===========================
!Here starts the 999 numbers
!===========================

LEXICON UNDERTHOUSAND
           HUNDREDS ;
 1:tjuohte HUNDRED  ;
 1:tjuot   HUNDRED  ; ! XXX - Only in compounds? Sjekk med Inga. Bare i cmp med noe forann, og bare som siste ledd når tallordet er attr til noe. Feks "gålmmåtjuot bijla". Eller når det er ledd etter også. Gålmmåtjuotguoktalåkgålmmå.
 1:tjuodát TJUODAAT ;

LEXICON HUNDREDS
  2:guokta  CUODI ;
  3:gålmmå  CUODI ;
  4:niellja CUODI ;
! 4:niellje CUODI ; !marg/dial
  5:vihtta  CUODI ;
  6:guhtta  CUODI ;
  7:gietjav CUODI ;
  8:gáktsa  CUODI ;
  9:aktse   CUODI ;

LEXICON CUODI
 :tjuode  HUNDRED  ;
 :tjuodát TJUODAAT ;
 
LEXICON HUNDRED
       TENS  ;
       TEENS ;
   %0: ONES  ;
 %0%0: COMMA ;

LEXICON TJUODAAT
 %0%0.: COMMA ;

LEXICON TEENS
 1+Use/NG:        TEEN  ;
        1:lågenan ONES ;

LEXICON TEEN
  1:akta    LOHKAI ;
  2:guokta  LOHKAI ;
  3:gålmmå  LOHKAI ;
  4:niellja LOHKAI ;
! 4:niellje LOHKAI ; !marg/dial
  5:vihtta  LOHKAI ;
  6:guhtta  LOHKAI ;
  7:gietjav LOHKAI ;
  8:gáktsa  LOHKAI ;
  9:aktse   LOHKAI ;

LEXICON LOHKAI
 :lågenan COMMA ;

LEXICON TENS
  1%0:lågev   COMMA ;
1%0%.:lågåt   COMMA ;
    2:guokta  LÅGEV ;
    3:gålmmå  LÅGEV ;
    4:niellja LÅGEV ;
!   4:niellje LÅGEV ; !marg/dial
    5:vihtta  LÅGEV ;
    6:guhtta  LÅGEV ;
    7:gietjav LÅGEV ;
    8:gáktsa  LÅGEV ;
    9:aktse   LÅGEV ;

LEXICON LÅGEV
  %0:låhke ENDLEX        ;
  %0:låk   COMMASECTION ;
    :låk   ONES         ;
 %0.:lågåt COMMA        ;

LEXICON ONES
  CARDINAL ;
  ORDINAL  ;

LEXICON CARDINAL
  1:akta    COMMA ;
  2:guokta  COMMA ;
  3:gålmmå  COMMA ;
  4:niellja COMMA ;
! 4:niellje COMMA ; !marg/dial
  5:vihtta  COMMA ;
  6:guhtta  COMMA ;
  7:gietjav COMMA ;
  8:gáktsa  COMMA ;
  9:aktse   COMMA ;

LEXICON ORDINAL
 1%.:vuostasj  COMMA ;
 2%.:nuppát    COMMA ;
 3%.:goalmát   COMMA ;
 4%.:nælját    COMMA ;
 5%.:vidát     COMMA ;
 6%.:gudát     COMMA ;
 7%.:giehttjit COMMA ;
 8%.:gávtsát   COMMA ;
 9%.:avtsát    COMMA ;
 
 
 
! !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
! !!!!!!!!!!                    !!!!!!!!!!!!!!!!!!!
! !!!!!!!!!!   Comma section    !!!!!!!!!!!!!!!!!!!
! !!!!!!!!!!                    !!!!!!!!!!!!!!!!!!!
! !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


LEXICON COMMA
  ENDLEX       ;
  COMMASECTION ;

LEXICON COMMASECTION
  ,:% tsiehkes%        NUMBERSECTION ;
 %.:% tjuogga%         NUMBERSECTION ; ! these will overlap with dates
 %::% gærddotjuogga%   NUMBERSECTION ;
 %-:% tjanássáhtso%    NUMBERSECTION ;
 %=:% la%              NUMBERSECTION ;
 %°:% gráda%           NUMBERSECTION ;
  §:% paragráffa%      NUMBERSECTION ;
 %*:% násste%          NUMBERSECTION ;
  &:% ja%              NUMBERSECTION ;
  
  
LEXICON ENDLEX
  # ;
