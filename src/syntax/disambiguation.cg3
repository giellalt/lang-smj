# Divvun & Giellatekno - open source grammars for Sámi and other languages
# Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
# http://giellatekno.uit.no & http://divvun.no
#
# This program is free software; you can redistribute and/or modify
# this file under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version. The GNU General Public License
# is found at http://www.gnu.org/licenses/gpl.html. It is
# also available in the file $GTHOME/LICENSE.txt.
#
# Other licensing options are available upon request, please contact
# giellatekno@hum.uit.no or divvun@samediggi.no

# =================================================================== #
# =================================================================== #
#              L U L E   S Á M I   D I S A M B I G U A T O R          #
# =================================================================== #
# =================================================================== #

            # =========================================== #
            #            Short table of contents          #
            # =========================================== #

            # Delimiters
            # Tags and sets
	        # Mapping rules
            # Disambiguation rules
              # One-cohort disambiguation - cycle 0
              # Local disambiguation - cycles 1 and 2
              # Cycle 2: Other local disambiguation         
              # Cycle 3: Global disambiguation 
	          # Cycle 4: Syntactic disambiguation 
	          # Cycle 5: Post-syntactic morphological disambiguation


# ========== #
# Delimiters #
# ========== #


DELIMITERS = "<.>" "<!>" "<?>" "<...>" "<¶>";


# ============= #
# Tags and sets #
# ============= #


# ========
   SETS
# ========

LIST BOS = (>>>) (<s>); 
LIST EOS = (<<<) (</s>); # vislcg and CG-2 together.

# Tags declared as single-membered LISTs 
# ======================================


# Parts of speech
# ---------------

LIST N = N ; 
LIST A = A ; 
LIST Adv = Adv ; 
LIST V = V ; 
LIST Pron = Pron ;
LIST CS = CS ; 
LIST CC = CC ; 
LIST Po = Po ; 
LIST Pr = Pr ;
LIST Pcle = Pcle ; 
LIST Qst = Qst ; 
LIST Num = Num ; 
LIST Prop = Prop ;

# POS sub-categories
# ------------------

LIST Pers = Pers ; 
LIST Dem = Dem ; 
LIST Interr = Interr ;
LIST Indef = Indef ;
LIST Recipr = Recipr ; 
LIST Refl = Refl ; 
LIST Rel = Rel ;
LIST Interj = Interj ; 
LIST Adp = Adp ;


# Morphosyntactic properties
# --------------------------

LIST Sg = Sg ; 
LIST Pl = Pl ;

LIST Nom = Nom ; 
LIST Gen = Gen ; 
LIST Acc = Acc ;
LIST Ill = Ill ; 
LIST Ine = Ine ; 
LIST Ela = Ela ;
LIST Com = Com ; 
LIST Ess = Ess ; 
LIST Par = Par ; 
LIST Abe = Abe ;

LIST SgCmp = SgCmp ; 
LIST SgNomCmp = SgNomCmp ;
LIST SgGenCmp = SgGenCmp ; 
LIST PlGenCmp = PlGenCmp ;
LIST RCmpnd = RCmpnd ;

LIST PxSg1 = PxSg1 ; 
LIST PxSg2 = PxSg2 ; 
LIST PxSg3 = PxSg3 ;
LIST PxDu1 = PxDu1 ; 
LIST PxDu2 = PxDu2 ; 
LIST PxDu3 = PxDu3 ;
LIST PxPl1 = PxPl1 ; 
LIST PxPl2 = PxPl2 ; 
LIST PxPl3 = PxPl3 ;

LIST Comp = Comp ; 
LIST Superl = Superl ; 
LIST Actor = Actor ;
LIST Actio = Actio ; 
LIST Clt = Clt ; 
LIST Foc = Foc ;
LIST Attr = Attr ;
LIST Card = Card ; 
LIST Ord = Ord ;
LIST Date = Date ; 
LIST Range = Range ;
LIST Dim = Dim ; 
LIST Inch = Inch ; 

LIST Prs = Prs ; 
LIST Prt = Prt ;
LIST Ind = Ind ; 
LIST Pot = Pot ; 
LIST Cond = Cond ;
LIST Imprt = Imprt ; 
LIST ImprtII = ImprtII ; 
LIST Sup = Sup ;
LIST IV = IV ; 
LIST TV = TV ;

LIST Sg1 = Sg1 ; 
LIST Sg2 = Sg2 ; 
LIST Sg3 = Sg3 ;
LIST Du1 = Du1 ; 
LIST Du2 = Du2 ; 
LIST Du3 = Du3 ;
LIST Pl1 = Pl1 ; 
LIST Pl2 = Pl2 ; 
LIST Pl3 = Pl3 ;

LIST Inf = Inf ; 
LIST Ger = Ger ; 
LIST GerII = GerII ;
LIST ConNeg = ConNeg ; 
LIST Neg = Neg ;
LIST PrsPrc = PrsPrc ; 
LIST PrfPrc = PrfPrc ;
LIST VGen = VGen ; 
LIST VAbess = VAbess ;

LIST CLB = CLB ; 
LIST PUNCT = PUNCT ;
LIST LEFT = LEFT ; 
LIST RIGHT = RIGHT ;
LIST HYPH = HYPH ; 
LIST PAR = PAR ;


# Derivation
# ----------

LIST Der/agák = Der/agák ; 
LIST Der/ahkes = Der/ahkes ;
LIST Der/ahtes = Der/ahtes ; 
LIST Der/ahtte = Der/ahtte ;
LIST Der/alla = Der/alla ; 
LIST Der/asste = Der/asste ;
LIST Der/belak = Der/belak ; 
LIST Der/bieljak = Der/bieljak ;
LIST Der/báguk = Der/báguk ; 
LIST Der/bájnuk = Der/bájnuk ;
LIST Der/bának = Der/bának ; 
LIST Der/d = Der/d ;
LIST Der/dahka = Der/dahka ; 
LIST Der/dahtte = Der/dahtte ;
LIST Der/dalla = Der/dalla ; 
LIST Der/dasste = Der/dasste ;
LIST Der/dibme = Der/dibme ; 
LIST Der/duhtte = Der/duhtte ;
LIST Der/duvva = Der/duvva ; 
LIST Der/dábak = Der/dábak ;
LIST Der/dáfuk = Der/dáfuk ; 
LIST Der/dávdak = Der/dávdak ;
LIST Der/dávtak = Der/dávtak ; 
LIST Der/ferjak = Der/ferjak ;
LIST Der/garák = Der/garák ; 
LIST Der/gattsak = Der/gattsak ;
LIST Der/gerdak = Der/gerdak ; 
LIST Der/giedak = Der/giedak ;
LIST Der/gielak = Der/gielak ; 
LIST Der/giesjkak = Der/giesjkak ;
LIST Der/gietjak = Der/gietjak ; 
LIST Der/girjak = Der/girjak ;
LIST Der/guovluk = Der/guovluk ; 
LIST Der/h = Der/h ;
LIST Der/habsak = Der/habsak ; 
LIST Der/hekkak = Der/hekkak ;
LIST Der/hábjak = Der/hábjak ; 
LIST Der/hájak = Der/hájak ;
LIST Der/hámak = Der/hámak ; 
LIST Der/hápsak = Der/hápsak ;
LIST Der/ivnak = Der/ivnak ; 
LIST Der/jagák = Der/jagák ;
LIST Der/jergak = Der/jergak ; 
LIST Der/jienak = Der/jienak ;
LIST Der/jiermak = Der/jiermak ; 
LIST Der/juolgak = Der/juolgak ;
LIST Der/l = Der/l ; 
LIST Der/ladda = Der/ladda ;
LIST Der/lahkak = Der/lahkak ; 
LIST Der/lahtte = Der/lahtte ;
LIST Der/lanják = Der/lanják ; 
LIST Der/lasj = Der/lasj ;
LIST Der/lasste = Der/lasste ; 
LIST Der/lijkak = Der/lijkak ;
LIST Der/linnjak = Der/linnjak ; 
LIST Der/lis = Der/lis ;
LIST Der/luonduk = Der/luonduk ; 
LIST Der/luottak = Der/luottak ;
LIST Der/lágán = Der/lágán ; 
LIST Der/lágásj = Der/lágásj ;
LIST Der/merkak = Der/merkak ; 
LIST Der/mielak = Der/mielak ;
LIST Der/muoduk = Der/muoduk ; 
LIST Der/mus = Der/mus ;
LIST Der/n = Der/n ; 
LIST Der/namák = Der/namák ;
LIST Der/nierak = Der/nierak ; 
LIST Der/njunjak = Der/njunjak ;
LIST Der/njálmak = Der/njálmak ; 
LIST Der/nálak = Der/nálak ;
LIST Der/Pass = Der/Pass ; 
LIST Der/r = Der/r ; 
LIST Der/rabdak = Der/rabdak ;
LIST Der/rattak = Der/rattak ; 
LIST Der/rávak = Der/rávak ;
LIST Der/segak = Der/segak ; 
LIST Der/siejbak = Der/siejbak ;
LIST Der/siejnak = Der/siejnak ; 
LIST Der/sijduk = Der/sijduk ;
LIST Der/st = Der/st ; 
LIST Der/stahtte = Der/stahtte ;
LIST Der/stalla = Der/stalla ;
LIST Der/stasste = Der/stasste ; 
LIST Der/stával = Der/stával ;
LIST Der/suorak = Der/suorak ; 
LIST Der/suorgak = Der/suorgak ;
LIST Der/suttak = Der/suttak ; 
LIST Der/sáv'vak = Der/sáv'vak ;
LIST Der/tjalmak = Der/tjalmak ; 
LIST Der/tjiegak = Der/tjiegak ;
LIST Der/tjuolmak = Der/tjuolmak ; 
LIST Der/tjuvdak = Der/tjuvdak ;
LIST Der/tjålak = Der/tjålak ; 
LIST Der/tjårvak = Der/tjårvak ;
LIST Der/u/a/åd = Der/u/a/åd ; 
LIST Der/us = Der/us ;
LIST Der/uvsak = Der/uvsak ; 
LIST Der/uvsuk = Der/uvsuk ;
LIST Der/varák = Der/varák ; 
LIST Der/vigák = Der/vigák ;
LIST Der/vuobdak = Der/vuobdak ; 
LIST Der/vuoduk = Der/vuoduk ;
LIST Der/vuohta = Der/vuohta ; 
LIST Der/vájmuk = Der/vájmuk ;
LIST Der/ájvuk = Der/ájvuk ;
LIST Der/såjak = Der/såjak ;
LIST Der/åjvak = Der/åjvak ; 
LIST Der/åsak = Der/åsak ;

LIST DERIVED =
  Der/agák Der/ahkes Der/ahtes Der/ahtte Der/alla Der/asste Der/belak
  Der/bieljak Der/báguk Der/bájnuk Der/bának Der/d Der/dahka Der/dahtte
  Der/dalla Der/dasste Der/dibme Der/duhtte Der/duvva Der/dábak
  Der/dáfuk Der/dávdak Der/dávtak Der/ferjak Der/garák Der/gattsak
  Der/gerdak Der/giedak Der/gielak Der/giesjkak Der/gietjak Der/girjak
  Der/guovluk Der/h Der/habsak Der/hekkak Der/hábjak Der/hájak Der/hámak
  Der/hápsak Der/ivnak Der/jagák Der/jergak Der/jienak Der/jiermak
  Der/juolgak Der/l Der/ladda Der/lahkak Der/lahtte Der/lanják Der/lasj
  Der/lasste Der/lijkak Der/linnjak Der/lis Der/luonduk Der/luottak
  Der/lágán Der/lágásj Der/merkak Der/mielak Der/muoduk Der/mus Der/n
  Der/namák Der/nierak Der/njunjak Der/njálmak Der/nálak Der/r
  Der/rabdak Der/rattak Der/rávak Der/segak Der/siejbak Der/siejnak
  Der/sijduk Der/st Der/stahtte Der/stalla Der/stasste Der/stával
  Der/suorak Der/suorgak Der/suttak Der/sáv'vak Der/tjalmak Der/tjiegak
  Der/tjuolmak Der/tjuvdak Der/tjålak Der/tjårvak Der/u/a/åd Der/us
  Der/uvsak Der/uvsuk Der/varák Der/vigák Der/vuobdak Der/vuoduk
  Der/vuohta Der/vájmuk Der/ájvuk Der/såjak Der/åjvak Der/åsak ;
  


# Noun tags

LIST LOC = Ine Ela ; # This set since we imported sme rules, all LOCs must be revised.


# Syntactic tags
# --------------

LIST @+FAUXV = @+FAUXV ; 
LIST @+FMAINV = @+FMAINV ; 
LIST @-FAUXV = @-FAUXV ;
LIST @-FMAINV = @-FMAINV ; 
LIST @-FSUBJ = @-FSUBJ ;

SET FMAINV = @-FMAINV OR @+FMAINV ;

LIST @ADVL = @ADVL ;
LIST @>ADVL = @>ADVL ;
LIST @ADVL< = @ADVL< ;
LIST @<ADVL = @<ADVL ;
LIST @ADVL> = @ADVL> ;
LIST @-FADVL = @-FADVL ;
LIST @A< = @A< ;
LIST @>A = @>A ;
LIST @ActioN> = @ActioN> ; 
LIST @APP = @APP ;
LIST @APP-N< = @APP-N< ;
LIST @APP-Pron< = @APP-Pron< ;
LIST @APP>Pron = @APP>Pron ;
LIST @APP-Num< = @APP-Num< ;
LIST @APP-ADVL< = @APP-ADVL< ;
LIST @CVP = @CVP ;
LIST @CNP = @CNP ;
LIST @CMPND = @CMPND ; 
LIST @COMP-CS< = @COMP-CS< ;
LIST @HAB = @HAB ;
LIST @HNOUN = @HNOUN ; 
LIST @INTERJ = @INTERJ ; 
LIST @MEASURE = @MEASURE ;
LIST @>N = @>N ;
LIST @N< = @N< ;
LIST @NNum> = @NNum>; 
LIST @NumN< = @NumN<;
LIST @>Num = @>Num;
LIST @Num< = @Num< ;
LIST @NPron< = @NPron< ;
LIST @NQ< = @NQ< ; 
LIST @NUM-PRON = @NUM-PRON ; 
#LIST @NUMBER = @NUMBER ;
LIST @OBJ = @OBJ ;
LIST @<OBJ = @<OBJ ;
LIST @OBJ> = @OBJ> ;
LIST @OPRED = @OPRED ; 
LIST @<OPRED = @<OPRED ;
LIST @OPRED> = @OPRED> ;
LIST @PCLE = @PCLE ; 
LIST @PrcN> = @PrcN> ; 
LIST @PronN< = @PronN< ; 
LIST @PronN> = @PronN> ; 
LIST @Pron< = @Pron< ;
LIST @>Pron = @>Pron ;
LIST @P< = @P< ;
LIST @>P = @>P ;
LIST @SPRED = @SPRED ; 
LIST @<SPRED = @<SPRED ;
LIST @SPRED> = @SPRED> ;
LIST @SUBJ = @SUBJ ; 
LIST @<SUBJ = @<SUBJ ;
LIST @SUBJ> = @SUBJ> ;
LIST @SUBJ-QH = @SUBJ-QH ;


LIST @TITLE = @TITLE ; 
LIST @VOC = @VOC ; 
LIST @X = @X ;


# Semantic tags
# -------------

LIST Ani = Ani ; 
LIST Fem = Fem ; 
LIST Mal = Mal ;
LIST Obj = Obj ; 
List Org = Org ; 
LIST Plc = Plc ;
LIST Sur = Sur ; 
LIST Tit = Tit ;

LIST HUM-PROP = Fem Mal Sur Tit ;

# Other tags
# ----------

LIST ABBR = ABBR ; 
LIST ACR = ACR ;

# Single-word sets
# ----------------
 
LIST COMMA = "," ; 
LIST ¶ = ¶;

#LIST OKTA = "okta"; LIST gå = "gå" ;

# Initials
# --------

LIST INITIAL =  "a" "b" "c" "d" "e" "f" "g" "h" "i" "j" "k" "l" "m"
                "n" "o" "p" "q" "r" "s" "t" "u" "v" "w" "x" "y" "z"
                "á" "æ" "ø" "å" "ö" "ä" ;

LIST CAP-INITIAL = "A" "B" "C" "D" "E" "F" "G" "H" "I" "J" "K" "L" "M"
        "N" "O" "P" "Q" "R" "S" "T" "U" "V" "W" "X" "Y" "Z"
        "Á" "Æ" "Ø" "Å" "Ö" "Ä" ;




# ======== #
# Tag sets #
# ======== #
   SETS    #
# ======== #


SECTION


LIST WORD = N A Adv V Pron CS CC Adp Po Pr Interj Pcle Pers Dem Interr Refl Recipr Rel Indef ; # any word
LIST ALL  = N A Adv V Pron CS CC Adp Po Pr Interj Pcle Pers Dem Interr Refl Recipr Rel Indef PUNCT ; # any token

# Derivational sets
# -----------------
# (Check these sets)

LIST N-DER = Der/vuohta Der/lis Der/mus Der/dahka Der/us Der/ahkes Der/ibme Der/dibme Der/Dimin Der/r Der/mielak Actor Der/m;

LIST V-DER = Der/r Der/d Der/dalla Der/l Der/st Der/h
Der/dahtte Der/dasste Der/stalla Der/stahtte Der/stasste
Der/ladda  Der/lahtte Der/lasste Der/u/a/år  Der/ahtte Der/alla Der/adda
Der/asste  Der/duvva  Der/duhtte Der/u/a/åd Der/Pass Der/ahtes  ;

LIST A-DER = Der/dibme Der/lasj Der/lágásj Der/lágán Der/ahtes
Der/juolgak Der/luottak Der/suttak Der/uvsuk Der/jienak
Der/rattak Der/hekkak Der/åjvak Der/belak Der/sijduk Der/giedak
Der/gielak Der/muoduk Der/varák Der/báguk Der/nálak Der/rabdak
Der/rávak Der/ájvuk Der/såjak Der/giesjkak Der/bának Der/njálmak
Der/nierak Der/merkak Der/suorgak Der/suorak Der/gattsak
Der/siejbak Der/siejnak Der/hájak Der/habsak Der/hápsak
Der/hábjak Der/sáv'vak Der/namák Der/lanják Der/vuoduk Der/hámak
Der/linnjak Der/lijkak Der/dávtak Der/tjårvak Der/gietjak
Der/bieljak Der/tjuvdak Der/dábak Der/tjiegak Der/gerdak
Der/vuobdak Der/uvsak Der/ivnak Der/ferjak Der/bájnuk
Der/luonduk Der/dávdak Der/jergak Der/mielak Der/jiermak
Der/njunjak Der/guovluk Der/dáfuk Der/vájmuk Der/lahkak Der/åsak
Der/garák Der/tjuolmak Der/tjålak Der/segak Der/tjalmak
Der/vigák Der/girjak Der/stával Der/agák Der/jagák Der/jahkásasj Der/lis (Actor A);

SET NOT-DER = WORD - DERIVED ;

# Case sets
# ---------
LIST ADVLCASE =             Ill Ine Ela Com Ess     ;
LIST OBLCASE  =     Acc Gen Ill Ine Ela Com Ess Par ;
LIST CASE     = Nom Acc Gen Ill Ine Ela Com Ess Par ;

SET NOT-NOM = CASE - Nom ;
SET NOT-GEN = CASE - Gen ;
SET NOT-ACC = CASE - Acc ;


# Adjective sets
# --------------

SET LEX-A = A - A-DER;

# Verb sets
# ---------
SET VERB = V - (V N);   # V is all readings with a V tag in them, VERB should
                        # be the ones without an N tag following the V

#SET LEX-V = VERB - V-DER;             # VERB but not V-DER

SET V-NOT-CMPND = V - RCmpnd; # Not in use.

SET LEX-V = V - V-DER;

# Finiteness and mood
# - - - - - - - - - -

SET V-IND-FIN = Prs OR Prt ; # Problem: "In boahtán" is an invisible indicative
SET V-MOOD = Prs OR Prt OR Pot OR Imprt OR ImprtII OR Cond ;
SET VFIN = V-MOOD - ConNeg ;
SET VFIN-POS = V-MOOD - ConNeg - Neg ;
SET VFIN-NOT-IMP = VFIN - Imprt - ImprtII ;
SET NOT-VFIN = VERB - VFIN ;

# Person
# - - - - 

LIST V-SG1 = (V  Ind Prs Sg1) (V Ind Prt Sg1) (V Cond Prs Sg1) 
             (V Cond Prt Sg1) (V Pot Prs Sg1) (V  Neg Ind Sg1);
LIST V-SG2 = (V  Ind Prs Sg2) (V Ind Prt Sg2) (V Cond Prs Sg2) 
             (V Cond Prt Sg2) (V Pot Prs Sg2) (V  Neg Ind Sg2);
LIST V-SG3 = (V  Ind Prs Sg3) (V Ind Prt Sg3) (V Cond Prs Sg3) 
             (V Cond Prt Sg3) (V Pot Prs Sg3) (V  Neg Ind Sg3);
LIST V-DU1 = (V  Ind Prs Du1) (V Ind Prt Du1) (V Cond Prs Du1) 
	     (V Cond Prt Du1) (V Pot Prs Du1) (V  Neg Ind Du1);
LIST V-DU2 = (V  Ind Prs Du2) (V Ind Prt Du2) (V Cond Prs Du2) 
	     (V Cond Prt Du2) (V Pot Prs Du2) (V  Neg Ind Du2);
LIST V-DU3 = (V  Ind Prs Du3) (V Ind Prt Du3) (V Cond Prs Du3) 
	     (V Cond Prt Du3) (V Pot Prs Du3) (V  Neg Ind Du3);
LIST V-PL1 = (V  Ind Prs Pl1) (V Ind Prt Pl1) (V Cond Prs Pl1) 
             (V Cond Prt Pl1) (V Pot Prs Pl1) (V  Neg Ind Pl1);
LIST V-PL2 = (V  Ind Prs Pl2) (V Ind Prt Pl2) (V Cond Prs Pl2) 
             (V Cond Prt Pl2) (V Pot Prs Pl2) (V  Neg Ind Pl2);
LIST V-PL3 = (V  Ind Prs Pl3) (V Ind Prt Pl3) (V Cond Prs Pl3) 
             (V Cond Prt Pl3) (V Pot Prs Pl3) (V  Neg Ind Pl3);

 # Note that imperative verbs are not included in these sets!
LIST HABEO-SG3 = ("liehket" Sg3) (V Neg Sg3) ("sjaddat" Sg3) ;

LIST HABEO-PL3 = ("liehket" Pl3) (V Neg Pl3) ("sjadda" Pl3) ;

SET HABEO-3 = HABEO-SG3 OR HABEO-PL3 ;


# Some subsets of the VFIN sets
# - - - - - - - - - - - - - - -

SET V-DU = V-DU1 OR V-DU2 OR V-DU3 ;
SET V-PL = V-PL1 OR V-PL2 OR V-PL3 ;

SET V-DU-PL = V-DU1 OR V-DU2 OR V-DU3 OR V-PL1 OR V-PL2 OR V-PL3 ;

SET V-NOT-SG1 = VFIN-NOT-IMP - V-SG1 ;

SET V-NOT-SG3 = V-SG1 OR V-SG2 OR V-DU1 OR V-DU2 OR V-DU3 OR
                V-PL1 OR V-PL2 OR V-PL3 ;

SET V-1-2 = V-SG1 OR V-SG2 OR V-DU1 OR V-DU2 OR V-PL1 OR V-PL2 ;
SET V-3   = V-SG3 OR V-DU3 OR V-PL3 ;

# Sets consisting of forms of "liehket" (these ones need to be rewritten)
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

LIST LIEHKET-FIN-NOT-IMP = ("liehket" V Ind) ("liehket" V Cond) ("liehket" V Pot); 


# Pronoun sets
# ------------

SETS


LIST MUN = (Pron Pers Sg1 Nom) ;
LIST DON = (Pron Pers Sg2 Nom) ;
LIST SON = ("son" Pron Pers Sg3 Nom) ;
LIST MÅJ = (Pron Pers Du1 Nom) ;
LIST DÅJ = (Pron Pers Du2 Nom) ;
LIST SÅJ = (Pron Pers Du3 Nom) ;
LIST MIJPERS = (Pron Pers Pl1 Nom) ;
LIST DIJ = (Pron Pers Pl2 Nom) ;
LIST SIJ = ("son" Pron Pers Pl3 Nom) ;

SET PPRON-NOM-NOT-DAT = MUN OR DON OR SON OR MÅJ OR DÅJ OR SÅJ OR MIJPERS OR DIJ OR SIJ ;

SET PPRON-DU-PL = MÅJ OR DÅJ OR SÅJ OR MIJPERS OR DIJ OR SIJ ;

SET PPRON-NOT-SIJ = MUN OR DON OR SON OR MÅJ OR DÅJ OR SÅJ OR MIJPERS OR DIJ ;

LIST PPRON-GEN = (Sg1 Gen) (Sg2 Gen) (Sg3 Gen) (Du1 Gen) (Du2 Gen) (Du3 Gen) 
	(Pl1 Gen) (Pl2 Gen) (Pl3 Gen);

SET PPRON-NOT-GEN = (Pron Pers) - PPRON-GEN ;

LIST DEM-SG = (Pron Dem Sg Nom);
LIST DEM-PL = (Pron Dem Pl Nom);
SET NOT-DEM = WORD - Dem ;


# Adjectival sets and their complements
# -------------------------------------

LIST A-ATTR = (A Attr) ; # Used in one rule. Not needed? 

#SET LEX-A = A - A-DER ;

SET A-CASE = A - Attr - Adv ;

LIST A-CC = A CC ;

SET NOT-ADJ = WORD - A ;
#SET NOT-ADJ-NUM-GEN = WORD - A - Num - Gen ;
SET NOT-Attr = WORD - Attr ;
SET NOT-AA = WORD - A - Adv ;
SET NOT-AAPCLE = WORD - A - Adv - Pcle ;


# Adverbial sets and their complements
# ------------------------------------

SET NOT-ADJ-PCLE = WORD - A - Pcle ;
SET NOT-ADJ-CC = WORD - A - CC ;
SET NOT-ADV = WORD - Adv ;
SET NOT-ADV-PCLE = WORD - Adv - Pcle ;
SET NOT-ADV-PCLE-VFIN = WORD - Adv - Pcle - VFIN ;

SET LEX-ADV = Adv - (A*) ;

SET ADVERBIAL = Adv OR Ill OR Ine OR Ela OR Com OR Ess ;

SET NOT-ADV-N = WORD - Adv - N;
SET NOT-ADV-INDEF = WORD - Adv - Indef ;
SET NOT-ADV-PCLE-ILL = WORD - Adv - Pcle - Ill ;
SET NOT-ADV-PCLE-INDEF = WORD - Adv - Pcle - Indef ;
SET NOT-ADV-PCLE-NEG = WORD - Adv - Pcle - Neg ;
SET NOT-ADVL-PCLE-NEG = WORD - @ADVL - @P< - Pcle - Neg ; #Ask Eckhardt.


LIST MANNE = "gåsi" "goasse" "goassa" "goassis" "gåkku" "gånne" "gåste" "makkir" "makkár" "manne" "manen" "mannas" ;

LIST ADV-NOT-NOUN = "duodaj";


# Coordinators
# - - - - - - -

LIST NEGFOC = (Neg Foc) ;

LIST XGU = "dagu" "degu" ;
  # compounds

SET CRD = COMMA OR CC OR ("/") OR XGU ; 
	# coordinators
	
SET LCRD = (@CNP) ;
    # local coordinator
SET GCRD = (@CVP) ;
    # global coordinator


# Sets for adverbs that have lookalikes
# - - - - - - - - - - - - - - - - - - -
 # Here come some adverbs that have identical twins in other POS. 
 # If these are found in Adv contexts, we treat them as adverbs.

#LIST ADV-NOT-VERB = "dušše" ;

#LIST ADV-NOT-NOUN = "easka" "várra" ;

#LIST ADV-NOT-NAMES = "Ain" "Anne" "Diego" "Dieppe" "Enge" "Galle" "Haga"
                     # "Joba" "Johan" "Liikka" "Mai" "Mannes" "Mo" "Mot" "Naba"
                     # "Nan" "Oktan" "Sierra" "Sokka" "Villa" ;

#SET ADV-NOT-OTHER-POS = ADV-NOT-VERB OR ADV-NOT-NOUN OR ADV-NOT-NAMES ;


# Sets of elements with common syntactic behaviour
# ================================================

LIST COPULAS = "bissot" "liehket" "sjaddat" "soajttet" "sjaddat" "viedjet" "årrot";
#"dáidit" "gártat"  ;
 # 'Dáidit' can appear without 'leat'.

SET V-NOT-COP = V - COPULAS ;

LIST MOD-ASP = "bierrit" "viedjet"  "viddnit"  ;
#                "berret" "dáidit" "fertet" "galgat" "gillet" "lávet" "sáhttit" 
#		"seahtit" "soaitit" "viggat" ;
 # Verbs that never have arguments of their own - so we leave out "áigut", for ex.

LIST AUX-OR-MAIN = "ájggot" "bessat" "bierrit" "buktet" "galggat" "hálijdit" "hæhttut" "máhttet" "nagádit" "sihtat" "vierttit" "årrot" ;
 # These verbs can take arguments, so they do not belong in the AUX group, 
 # but they are nevertheless mapped to (@+FAUXV). 

SET AUX = COPULAS OR Neg OR MOD-ASP OR AUX-OR-MAIN ;

SET V-NOT-AUX = V - AUX ;  # The set of verbs that cannot be auxiliaries. 
			   # Does not include AUX-OR-MAIN.

SET VFIN-NOT-AUX = VFIN - AUX ;

LIST GerII-V = "ájttsat" "gullat" "gullát" "mannat" "tjuodtjot" "tjåhkkåhit" "vuojnnet" "vællahit" ;

#SET V-MAIN = V - COPULAS - Neg - MOD-ASP OR ("orrut") ;  # The set of verbs that can be 
#							# main verbs. Includes 
#							# AUX-OR-MAIN.
#
#SET V-DER-PASS = V + V-DER + Pass;    # + is intersection, this should work
#				      # The set is used in one rule with no hits.
#	
#SET V-PASS = (V TV Pass);
#
#SET V-NOT-AUX-PASS = V - AUX - V-PASS ; # No longer in use.
#
#SET V-ACT = V - Pass; # No longer in use.
#
#SET ACT-PRFP = PrfPrc - V-PASS; # Appears in one rule.

SET V-TRANS-ACT = TV - Der/Pass ;
 # All active verbs with a TV tag, including AUX-OR-MAIN.
 
SET V-TRANS-ACT-NOT-AUX = V-TRANS-ACT - AUX ;

SET V-TRANS-ACT-NOT-ACT = V-TRANS-ACT - Actio - Actor - PrsPrc ;

SET V-TRANS-ACT-NOT-ACT-NOT-AUX = V-TRANS-ACT - Actio - Actor - AUX;


# Sets for verbs choosing oblique objects or adverbials
# - - - - - - - - - - - - - - - - - - - - - - - - - - - 

#LIST INEV =
LIST COMV = "aktidit" "bierggit" "buohtastahttet" "gávnadit" "oassálasstet" "sjiehtadallat" ;
LIST ELAV = "ballat" "bessat" "biehttot" "buorggot" "gáhtot" "gærggat" "gåhttsåt" "gåvvat"  "hiejttet" "luojttádit" "mássit" "nádot" "tsaggat" "várrodit" "viessot" "vuornnot" "ållit" ;

LIST ILLV = "bajkket" "báddnut" "báhtset" "biedjat" "bielkket" "boadádallat" "bæhtádallat" "bælkkut" "bårrut" "dabrijdit" "dehpudallat" "dihpput" "duhtat" "duobbmidallat" "dædnjot" "dåbdijdit" "dårvustallat" "ganudit" "ganugit" "gierrot" "givnjedit" "guhkahit" "gullut" "guodet" "guoddet" "guorrasit" "guosskat" "guottedallat" "gåddudallat" "gådtjåt" "gåttådallat" "hárjjánit" "javllat" "jáhkket" "jánndedit" "joavddut" "ládadallat" "láhpput" "lávgájdallat" "lijkkut" "madtjut" "mielastuvvat" "njoammot" "oahpásmuvvat" "oassálasstet" "rájadallat" "riegádit" "råhtot" "sehkanit" "sjiehttat" "sjærggodit" "skulldit" "sledut" "snårrudit" "snårrut" "suhttat" "sæbrrat" "tjágŋalit" "tjágŋat" "tjajmadallat" "tjárggat" "tjuokkudallat" "tsábbmudallat" ;

#LIST ACC-LOCV
#LIST ACC-ILLV

LIST OPREDV = "adnet" "gåhttjot" "válldet" ;

LIST ESSV = "aneduvvat" "boahtet" "sjaddat";
    # ESSV + A = Ess instead of Ine.

# Other verb sets
# - - - - - - - -

LIST INFV = "boahtet" "buktet" "galggat" "gåhttjot" "liehket" "máhttet" "oadtjot" "sihtat" "suovvat";
#LIST ACC-INFV = 


 # These are verbs taking accusative objects and infinitives;
 # a subset of INF-VERB.
	## Ii suova nieiddas náitalit.


LIST TOGETHER-V =  "tjadnat" "tjåhkanit" "viellidit" "viessot" ;
#"goallostuvvot" "beaškkehat" "bidjat" "ô ­atnit" "suddat"  "heivet" "biddjojuvvot" "ô ­askit" "geassit" "gullat" "rehkenastit" "ordnet" "soahpat" "gávnnadit" "heivehit" "ô ­atnat" "doaibmat" "iskat" "laktit" "seaguhit";
 # Danger: This list could be to long, in certain respects. 
 # TODO: Think of counter examples, i.e. sentences where we have these verbs, but
 # where oktii is not an adverbial.

LIST MOVEMENT-V = "boahtet" "mannat" "suhkat" "vádtsát" "vádtset" "vuoddját" "vuodjet" "vuolgget";
 
 
LIST ACTIVITY-V = "duobbmit" "gástadit" "lasedit" "mihttit" "åhpadit";

LIST BODY-ACTIVITY-V = "tjiektjat" ; 
LIST WRITING-ACTIVITY-V = "tjállet" ; 
LIST VERBAL-ACTIVITY-V = "hållat" "tjuorvvot" ; 
LIST ABSTR-ACTIVITY-V = "buoredit" "loabedit" "vilpástallat" ;  
LIST INTELLECTUAL-ACTIVITY-V = "usjudit" ; 

# More to come:
LIST VEHICLE-PLACE-V = "ánodit" "gahtjat" "nisskot" "oadtjot" "suoládit" "valldet" ;   
LIST VEHICLE-TOOL-V = "buktet" "doalvvot" ;    
SET HUMAN-SOURCE-V = VEHICLE-PLACE-V OR VEHICLE-TOOL-V ;
# The two subsets differ in their choice of case for members of VEHICLE, but both choose locative for members of the set HUMAN (with the thematic role source)

#LIST ABSTR-ENTITY-COM-V 
#LIST ONLY-PLACE-LOC-V 
#LIST HUMAN-LOC-V 

LIST PLACE-V = "tjuodtjot" "tjællot" "tsæggahit" "válldut" "vuojnnet" "vuojnnut" "årrot"; 
# not so sure about where to put "vuojnnet" "vuojnnut" 
# Ine vs. Com: Only ANIMATE, TOOLS, ABSTR-TOOLS and CONCEPT take Com.

#LIST ABSTR-PLACE-V
#LIST EVENT-V = "dáhpáhuvvat" ;  # TIME/AIGODAT are locative
#LIST STATE-V = "eallit" ;
#LIST REM-WITH-PARTS = "gaikkihit" ; # Belongs to the DYNAMIC-V. They also take PARTS in comitative case.   

SET DYNAMIC-V = ACTIVITY-V OR MOVEMENT-V OR VERBAL-ACTIVITY-V OR ABSTR-ACTIVITY-V OR BODY-ACTIVITY-V OR INTELLECTUAL-ACTIVITY-V OR HUMAN-SOURCE-V ;

#SET DYNAMIC-V = ACTIVITY-V OR MOVEMENT-V OR VERBAL-ACTIVITY-V OR ABSTR-ACTIVITY-V OR BODY-ACTIVITY-V OR INTELLECTUAL-ACTIVITY-V OR ABSTR-ENTITY-COM-V OR HUMAN-SOURCE-V OR REM-WITH-PARTS ;
# According to the rules choosing LOC or Com, dynamic-verbs are not connected to TOOLS, ABSTR-TOOLS or CONCEPT in (Pl LOC).
# Human becomes either LOC or Com depending on its subset membership.





# NOUNS
# -----
# NP sets defined according to their morphosyntactic features
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 

LIST N-SG-NOM = (N Sg Nom); LIST N-PL-NOM = (N Pl Nom);

SET LEX-N = N - N-DER;
SET NOT-ACTOR = N - Actor ;

LIST PROP = (N Prop);
SET CNOUN = N - Prop;
SET CNOUN-PL-NOM = (N Pl Nom) - Prop ;
SET HNOUN = N - RCmpnd ;
SET HNOUN-NOM = (N Nom) - RCmpnd ;
SET N-NOT-GEN = N - (N Gen) ;

SET NP-HEAD        = Pron OR HNOUN ;
SET NP-HEAD-SG     = (Pron Sg1) OR (Pron Sg2) OR (Pron Sg3) OR (N Sg) - RCmpnd;
SET NP-HEAD-PL     = (Pron Pl1) OR (Pron Pl2) OR (Pron Pl3) OR (N Pl) - RCmpnd;
SET NP-HEAD-SG-NOM = (Pron Sg1 Nom) OR (Pron Sg2 Nom) OR (Pron Sg3 Nom) OR (N Sg Nom) - RCmpnd;
SET NP-HEAD-PL-NOM = (Pron Pl1 Nom) OR (Pron Pl2 Nom) OR (Pron Pl3 Nom) OR (N Pl Nom) - RCmpnd;
SET NP-HEAD-NOM    = NP-HEAD-SG-NOM OR NP-HEAD-PL-NOM;
SET NP-HEAD-ACC    = (Pron Acc) OR (N Acc) - RCmpnd;
SET NP-HEAD-GEN    = (Pron Gen) OR (N Gen) - RCmpnd;



# The PRE-NP-HEAD family of sets
# - - - - - - - - - - - - - - - -

SET PRE-NP-HEAD = (Prop Attr) OR (Prop @>N) OR (A Attr) OR ("buorre") OR (Pron Pers Gen) OR (N Gen) OR 
         Num OR RCmpnd OR CC OR (Pron Dem) OR (Pron Refl Gen) OR (Pron Indef) OR 
         (PrfPrc @>N) OR (PrfPrc @PrcN>) OR PrsPrc OR (A Ord) ;
         # OR (Ill LINK 1 (PrfPrc @AN>)) OR (Com LINK 1 (PrfPrc @AN>)) OR (LOC LINK 1 (PrfPrc @AN>))
         # The strict version of items that can only be premodifiers, not parts of the predicate
         
SET V-PRE-NP = PrfPrc OR PrsPrc OR Actor OR Actio OR (V A) OR (Ind Prs Sg3) OR (Ind Prs Sg1) OR (Imprt Prs Du2) OR (ImprtII Prs Du2) ;
    # to be used together with PRE-NP-HEAD before @PrcN> is disambiguated
         
SET NP-MEMBER  = PRE-NP-HEAD OR N ;
SET PRE-A-N   = (Pron Pers Gen) OR (Pron Indef) OR Num OR (A Ord) OR (Pron Dem) OR (Pron Refl Gen) ;
SET NOT-PRE-A-N = WORD - PRE-A-N ;
LIST PUNCT-LEFT = (PUNCT LEFT) ;
LIST PUNCT-RIGHT = (PUNCT RIGHT) ;

SET PRE-APP   = COMMA OR PUNCT-LEFT OR PRE-NP-HEAD ; # This set ist not only for what can
                            # stand in front of appositions but also
                            # postmodifiers.

# V NPNHAIIP... N
# BOS ... N V

# There must be a sensible naming convention for these sets...
# Most strict BARRIERs...
SET NPNH     = WORD - PRE-NP-HEAD OR ABBR ;       # NPNH  = "NOT-PRE-NP-HEAD" 
SET NPNHV    = WORD - PRE-NP-HEAD - V-PRE-NP ;    # including possible verbal premodifiers 
SET NPNHA    = WORD - PRE-NP-HEAD - Adv ;        # NPNHA = "NOT-PRE-NP-HEAD-OR-ADV"
SET NPNHAV   = WORD - PRE-NP-HEAD - Adv - V-PRE-NP ; # including possible verbal premodifiers
SET NPNHAI   = WORD - PRE-NP-HEAD - Adv - Indef ;    # NPNHAI = "     ...-OR-INDEF"
SET NPNHAII  = WORD - PRE-NP-HEAD - Adv - Indef - Ill ; # Illative indir.obj.
SET NPNHAIIP = WORD - PRE-NP-HEAD - Adv - Indef - Ill - Pcle ; # <== is this our NPNH set?
SET NPNHC    = WORD - PRE-NP-HEAD - Com ; # "njaŋggofirpmiiguin bivdin"
SET NPNHCCC  = WORD - PRE-NP-HEAD - COMMA ;
SET NAPP     = WORD - PRE-APP ;
# SET TOLERANT-PRENPP = V ; (everything non-NP-head!)
# ... Most sloppy BARRIERs

# Set for NP with participle premodifier
# ... IF *-1 X BARRIER NPNH OR 
# LocComIll are ok, but only with a PrfPcc to the right


# Other negatively defined sets
# - - - - - - - - - - - - - - -

SET NOT-N = WORD - N ;
SET NOT-NA = WORD - N - A ;
SET NOT-NP = ConNeg OR Inf OR Pcle OR Interj OR CS ; # The set NP-BOUNDARY is bigger. 
SET NPROP = N - PROP ;
SET NOT-COMMA = CLB - COMMA OR PUNCT-LEFT OR PUNCT-RIGHT ;




# Nominal sets defined according to their morphophonological properties
# ---------------------------------------------------------------------

LIST ILL-ADJ = "amás" "nákkes" "oasálasj" "siválasj" ;
# There are synomyms to these words which probably also take ill.


# LIST NOM-GEN-NOUN = ... ;

SET N-NOT-ACTIO = N - Actio ;



# Nominal sets defined according to their semantical properties
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -

# inessive vs. comitative:
LIST SG-WORD = "mieda" "miella" "miehttse#mieda" "vijsesvuohta";

# inessive vs. essive:
LIST INE-NOUN = "ednam";
LIST ESS-NOUN = "sáluk" "vuodo" "vuornes";
LIST COM-NOUN = "aktijvuohta" ;

LIST TITLE =  "gånågis" "profehta" "profehtta" ;
LIST ESS-ADVL-N = "bäjvádahka" "biegga" "dálkke" "dulvve" "njáhsto" "rássjo" "sjievnnjet"; 



# SETS FROM SME TO COME IN HERE.

# Time sets
# - - - - -


LIST AIGODAT = "ájge" "gávdda" "ájge#gávdda" "válgga#gávdda" "sámedikke#gávdda" ;

LIST VAHKKU = "biejvve" "b" "gidá" "giesse" "tijmma" "jánndur" "båddå" "tjaktja" "tjaktja#giesse" "tjaktja#sjievnnjet" "tjuohte#jahke" "dálvve"	"kloahkka" "kloahkko" "kl" "doajmma#jahke" "gierdde" "giesse" "gidá" "bálle" "idja" "jahke" "jahke#tjuohte" "mánno" "mihttsamárra" "riegádim#biejve" "vahkko" "stuorradigge#gávdda" "1700-#låhko" "1700-#jahke#låhko" ;
 # The quoteless jahki here is since the preprocessor is not fine-tuned
 # yet, it still gives '"1870-" # jahki N Sg Com'. # lets see if "dbm" works
 
LIST AJGGI = "ájgge" "bivddo#ájgge" "bæssásj-#ájgge" "tjaktja#ájgge" "dálvve#ájgge" "giesse#ájgge" "javlla#ájgge";
  # Maybe it could be added to VAHKKU?


LIST MANNU = "ådå-jak-#mánno" "ådåjakmánno" "guovvamánno"  "snjuktjamánno"  "vuoratjismánno"  "moarmesmánno" "biehtsemánno"  "snjilltjamánno"  "bårggemánno"  "ragátmánno"  "gålgådismánno"  "basádismánno"  "javllamánno" ;

 
LIST BEAIVI = "mánnodak" "dijstak" "gasskavak" "duorastak" "bierjjedak" "lávvodak" "ájllek" "basse";


SET TIME = MANNU OR VAHKKU OR BEAIVI OR AJGGI ;
  # Do not add AIGODAT, it has some other characteristics in some cases.

SET NOTIME = N - TIME ;

LIST GIESSEK = "dálvvek" "gidak" "giessek" ("giessek" Adv) "tjaktjak" ; 


# Amount sets
# - - - - - -


LIST MEASURE = "mijlla" "kilo#mehter" "mehtar" "mehter" "senti#mehter" "sm" "milli#mehter" "stihkko" "salla" "lihttar" "kilo" "unnánasj" ;

LIST CURRENCY = "kråvnnå" "kr" "¢" "€" "$";

LIST CURRENCY-GEN = ("kråvnnå" Gen) ("kr" Gen) ("¢" Gen) ("€" Gen) ("$" Gen);
SET NOT-CURRENCY-GEN = N - CURRENCY-GEN ; 	

LIST NUMUNIT = "tjuohte" "tuvsán" "millijåvnnå" "milijåvnnå" "mill" "mil" "milliárdda" "mrd";

LIST KIND = "sládja" "válgga-#šládja" ;

LIST COUNTABLE = "oasse" "kapihtal" "paragráffa" ;

LIST NUMMAR = "nr" "nummar" "tabælla#nummar" ;

LIST KLASS = "ássje" "bálkká#tsiehkke" "s" "sijddo" "§" "§§" "páragráffa" "oasse" "kap" "kapihtal" ;

LIST AKTA = "akta" ;
SET NUMERALS = Num - AKTA ;
# this set does not contain ordinals, I am not sure if that is necessary.

SET CARDINALS = Num - Ord ;

LIST ALU = ("allu" Gen)("assu" Gen)("čiekŋu" Gen)("gassu" Gen)("govdu" Gen)
	  ("guhkku" Gen)("lossu" Gen)("stuorru" Gen)("viidu" Gen);
	  # Those are words that only appear in one form, which Nickel calls A/G.
    # They are only in a predicative position.
    # needs to be translated . Are they translatable, do these forms exist in Julev (TO)?

# Semantic noun-sets
# -------------------

LIST HUMAN-INDIVIDUAL = "áddjá" "áhkká" "áhkko" "áhttje" "áhttje#bielle" "ájttek" "bárne#bárnne" "bárne#niejdda" "bárnne" "bárnne#bielle" "biebbmo#bárnne" "biddno#moarsse" "boaddnje" "gájmme" "gállasj#guojmme"  "gállasj#ålmåj" "gálojinitja" "goasske" "goasske"  "gieruk" "ibme" "ieddne" "ieddne#bielle" "iemet" "irgge" "iset" "násste#dåbdde" "niejdda" "næjttso" "næjttso#riehko" "oarbben#bielle" "oarbben" "siessá" "siessal" "sirram#nissun" "svilli" "mannje" "moarsse" "muohtá" "muohtal" "niehpe" "oabbá" "oabbá#bielle" "tjiehtje" "tjiehtjev" "vieljasj" "viellja" "viellja#gieres" "viellja#bielle" "vilpástalle" "æddnul" "ænoj" ; 

LIST HUMAN-GROUP = "sijdda" "vehka" ;

LIST PROFESSION = "guolle#bivdde" ;

LIST OFFICE = "stáhta#ministar" ;
LIST ROLE = "årro" ;

SET HUMAN = HUMAN-INDIVIDUAL OR HUMAN-GROUP OR PROFESSION OR ROLE OR OFFICE ;

LIST BODY = "giehta" "juolgge" "tjalmme" ;
LIST ANIMAL = "guokkardiddje" "judos" "sávttsa" "vierttsa" ;
LIST HUNT-ANIMAL = "rievsak" ;
LIST BOAZU = "boatsoj" ;
LIST FISH = "guolle" ;

SET ALL-ANIMALS = ANIMAL OR HUNT-ANIMAL OR BOAZU OR FISH ;

SET ANIMATE = HUMAN OR ALL-ANIMALS ;

#PLANT

LIST TOOLS = "dållå" "jiedna" "tjáhtje";

LIST WRITING-TOOLS = "pænna" ;

LIST ABSTR-TOOLS = "ássje" "duobbmo" "musihkka" "varra" "vuornes"; 
 
#PARTS ruohtas
#SHOE
#BEDCLOTHES
#FOOD
#CONTAINTERS

LIST ROOM-VEHICLE = "bijlla" "hiergge" "vanás";
LIST SMALL-VEHICLE = "syhkal" ; 

SET VEHICLE = ROOM-VEHICLE OR SMALL-VEHICLE ;

LIST TEXT = "girjje" ;
LIST ABSTR-TEXT = "álmmuk#lávla" "álmmuk#subtsas" "lávla" "subtsas" ; 
# eaktu, gažaldat, mearrádus

#WATER
#OBJECT = PARTS OR SHOE OR FOOD OR TEXT
#ENTITY = TOOLS OR OBJECT OR VEHICLE

LIST BUILDING = "dåhpe" "fáŋŋga#goahte" "gábmá" "goahte" "låvda#goahte" "oases" "synagoga" ;

LIST ROOM = "ladnja" "oadem#ladnja" ;

LIST GEOGRAPHICAL-PLACE = "dáfo" "ednam" "gádde" "guovllo" "jávrre" "várre" "åjdå#miehttse";  
LIST POLITICAL-PLACE = "rijkka" "stáda"; 
LIST GENERAL-PLACE = "gahttun#suorre" "sijdda" "stáda";  
LIST SPEC-PLACE = "värro#áltár" ; # for Ill disamb
LIST CONCRETE-ROUTE = "gahttun" "njálmme#rájgge" "uksa#rájgge" "væddja";
LIST ABSTR-ROUTE = "mijlla" ;

SET ROUTE = CONCRETE-ROUTE OR ABSTR-ROUTE ; 

SET PLACE = GEOGRAPHICAL-PLACE OR POLITICAL-PLACE OR GENERAL-PLACE OR ROUTE OR BUILDING OR ROOM ;

LIST ORGANIZATION = (Prop Org) "giddagas" "girku" ;

#FYLKA
#NATION = NATION-NOT-NORGA OR NORGA

#STATE ("dearvvas" Der/vuohta) "gaskavuohta" "miella#dearvvašvuohta" "ráfi" "váivi" "veahkki";

#ABSTR-ENTITY  "ášši" "bálká" "bušeahtta" "doaibma#plána" "fuolahan#bálká" "giella#ruhta" "hehttehus" "plána" "prošeakta" "ruhta" "sosiála#plána" "stáhta#doaibma" "váttisvuohta" ;

LIST CONCEPT = "gieresvuohta" "guojmme#gieresvuohta" "mujtto" "måråj" "oame#dåbddo" ;

#LIST ACTIVITY = "čađaheapmi" "dearvvašvuođa#dutkan" "fuolaheapmi" "geahčastat" "guorahallan" "šiehtadallan" ;

#ABSTRACT = ABSTR-ENTITY OR CONCEPT OR ACTIVITY 

LIST SITUATION = "dille" ;

LIST INSTITUTION = "instituhtta" ;
LIST INDUSTRY = "æládus#tjærdda" ;
LIST EDUCATION = "skåvlla" "vuodo#skåvllå" ;
LIST EVENT = "aktisasj#tjåhkanibme" "tjåhkanibme" ; 

SET ABSTR-PLACE = ORGANIZATION OR INSTITUTION OR EVENT OR EDUCATION OR INDUSTRY OR SITUATION ;

SET ACTOR = ANIMATE OR INSTITUTION OR ORGANIZATION ;
SET HAB-ACTOR = HUMAN-INDIVIDUAL OR ACTOR OR HUMAN-GROUP OR INDUSTRY OR EDUCATION OR ("guhti") OR ("guhtis") OR ("guhtimusj") OR ("galles") OR Pers  ;
# PEOPLE-NUM OR ("eará") 

LIST LANGUAGE = "sáme#giella" ;

#ILLNESS
#POLITICAL-ABSTR-N  "boazodoallo#politihkka" "boras#pirepolitihka" "kultur#árbi" "sosiála#politihkka" 

#OASSI

LIST HEADOFPARTS = "oassi" "oajvve#oasse" "bielle" "guhtimusj" "goappátjagá" "proseanta" "%" ;
  # This may be a better partitive set.

# Miscellaneous sets
# ------------------


LIST Px = PxSg1 PxSg2 PxSg3 PxDu1 PxDu2 PxDu3 PxPl1 PxPl2 PxPl3 ;
LIST PX1 = PxSg1 PxDu1 PxPl1 ;
LIST PX2 = PxSg2 PxDu2 PxPl2 ;
LIST PX3 = PxSg3 PxDu3 PxPl3 ;

SET PXNOTSG1 = Px - PxSg1 ; SET PXNOTSG2 = Px - PxSg2 ; SET PXNOTSG3 = Px - PxSg3 ;
SET PXNOTDU1 = Px - PxDu1 ; SET PXNOTDU2 = Px - PxDu2 ; SET PXNOTDU3 = Px - PxDu3 ;
SET PXNOTPL1 = Px - PxPl1 ; SET PXNOTPL2 = Px - PxPl2 ; SET PXNOTPL3 = Px - PxPl3 ;

LIST GASKAL = "gaskal" "gaskii" "gaska" "gaskas" ; 
 # p-positions that like coordination

SET REALCLB = CLB - COMMA ;
SET REALWORD = WORD - Num - Ord ;

LIST CONTRA = "valla" ;

SET NOT-CC = WORD - CC ;

SET NOT-PCLE = WORD - Pcle ;

LIST COMPAR = "dájvvábut" "ienep" "ietjá#lágásj" "ietjá" "ietján gå" "danga båttån" "sæmmi#lágásj" "sæmmi#láhkáj" "sæbbmi#láhkáj "; 
 # These combine with "gå" Pcle, but COMPAR stands for 'comparison' rather than
 # 'comparative'.

SET NOT-PUNCT-RIGHT = ALL - RIGHT ;

# Border sets and their complements
# ---------------------------------

SET CP = (Pron Interr) OR (Pron Rel) ;

LIST MO-MANge = "gåktu" "gåktus" "gånnå" "gåsstå" "gåsstås" "manen" (V Qst);
 
SET MO = MO-MANge - ("man" Foc/ge) ; 

 
LIST SEMICOL = "\;" ;
LIST COL = ":" ;


SET S-BOUNDARY = (Pron Interr) OR (Pron Rel) OR MO OR ("muhto") OR ("de" CC) OR CS OR ("\;") OR (":") OR ("-") OR ("–") OR (".") ;
	# includes CP
	# remember that (",") and CC are potential sentence boundaries, too
	
SET S-BOUNDARY2 = (Pron Interr) OR (Pron Rel) OR ("muhto") OR CS OR ("de" CC) OR ("\;") OR (":") OR ("-") OR ("–") OR (@CVP) ; 
	# includes CP
	# this one includes @CVP, the conjunction, which actually connects two sentences (each with a finite verb) to each other, and not @CNP, which coordinates internal NP-/AdvP-/AP ... coordination
	# To be used only AFTER the disambiguation of @CVP and @CNP taking place in the chapter right before "Disambiguatig pronouns"

SET BOC = S-BOUNDARY OR BOS ;
SET EOC = S-BOUNDARY OR EOS ;
SET BOC2 = S-BOUNDARY2 OR BOS ;
SET EOC2 = S-BOUNDARY2 OR EOS ;

SET NP-BOUNDARY = BOS OR EOS OR REALCLB OR VFIN OR Inf OR GerII OR ConNeg OR VGen OR Sup OR PPRON-NOT-GEN OR Recipr OR Po OR Pr OR Pcle OR ("jed") OR Interj OR CS OR CP ;

SET SV-BOUNDARY = S-BOUNDARY OR FMAINV ; # VFIN-NOT-AUX ; # should be V-MAIN linked to VFIN-aux to the left. (cg-3)
# This set is ment to use in rules for disambiguating due to verbs or verbsets. 
SET SV-BOUNDARY2 = S-BOUNDARY2 OR FMAINV ;

SET SVF-BOUNDARY = S-BOUNDARY OR VFIN ;
# This set is ment to use in rules for disambiguating due to verbs or verbsets. 
# Here we search for either an S-BOUNDARY or a finite verb, either aux or main.

SET CCCOMMA = CC OR COMMA ;
	# remember that those are potential sentence boundaries, too
#SET BOUNDARY = S-BOUNDARY OR NP-BOUNDARY ; # not in use

#SET INTR = Rel OR PUNCT-LEFT ; 
	# interrupters - not in use



# Syntactic sets
# --------------


LIST ALLSYNTAG = (@+FAUXV) (@+FMAINV) (@-FAUXV) (@-FMAINV) (@-FSUBJ)
                 (@ADV-A) (@ADV-ADV) (@ADVL) (@ActioN>) (@CC)
                 (@CMPND) (@CS) (@P<) (@>P)
                 (@HNOUN) (@INTERJ) (@MEASURE) (@NPron<) (@NQ<) 
                 (@NUM-PRON) (@OBJ) (@OPRED) (@PCLE) 
                 (@PCLE-COMPL) (@PrcN>) (@PronN<) (@PronN>) 
                 (@N<) (@>N) (@SPRED)
                 (@SUBJ) (@SUBJ-QH) (@VOC) (@X); 





# ==================== #
# ==================== #
# Disambiguation rules #
# ==================== #
# ==================== #


# Rough outline of the plan.
# 0 same cohort
# 1 neighbour, early (1-2)
# subj-vfin in that order
# neg-conneg, in that order
# 
# 2 close, later
# the bulk here, ordered according to POS
# 3 everywhere, late

 
# =================================== #
# One-cohort disambiguation - cycle 0 #
# =================================== #
             SECTION              #
# =================================== #

# This is for cases using the same cohort only.

    # ----------------- #
    # Table of Contents #
    # ----------------- #

#SELECT (@TITLE) IF (1 Prop LINK 0 Mal OR Fem OR Sur);
    ## Suv birra profehta Jesaja le hållam.
    
SELECT Ill IF (0 ("gártjes" Ill));
    ## gárttjásij boahtet     


# Lexicalised names
# -----------------

REMOVE ACR IF (0 Prop) ;

REMOVE (Prop Pl) IF (0 (Prop Sg));
	## sijáv Betlehemaj rájaj ja javlaj...	



# Lexicalised derivations
# -----------------------
# SELECT instead of REMOVE if one wants to investigate word structure.

REMOVE (Der/lasj A Attr) IF (0 NOT-DER LINK 0 (A Attr));
REMOVE (Der/lasj A Attr) IF (0 NOT-DER LINK 0 (N));
REMOVE N-DER IF (0 LEX-N LINK NOT 0 Ess OR (Der3 Actor));
REMOVE (V* Actor N Sg Nom) IF (0 NOT-ACTOR);
REMOVE (V* Actor N) IF (0 NOT-ACTOR);  # A try

REMOVE (Actio N Sg Nom) IF (0 (N-NOT-ACTIO Sg Nom));
REMOVE (Actio N Sg Acc) IF (0 (N-NOT-ACTIO Sg Acc));
REMOVE (Actio N Sg Gen) IF (0 (N-NOT-ACTIO Sg Gen));
REMOVE (Actio N Sg Ill) IF (0 (N-NOT-ACTIO Sg Ill));
REMOVE (Actio N Sg Ine) IF (0 (N-NOT-ACTIO Sg Ine));
REMOVE (Actio N Sg Ela) IF (0 (N-NOT-ACTIO Sg Ela));
REMOVE (Actio N Sg Com) IF (0 (N-NOT-ACTIO Sg Com));
REMOVE (Actio N Ess) IF (0 (N-NOT-ACTIO Ess));
REMOVE (Actio N Pl Nom) IF (0 (N-NOT-ACTIO Pl Nom));
REMOVE (Actio N Pl Acc) IF (0 (N-NOT-ACTIO Pl Acc));
REMOVE (Actio N Pl Gen) IF (0 (N-NOT-ACTIO Pl Gen));
REMOVE (Actio N Pl Ill) IF (0 (N-NOT-ACTIO Pl Ill));
REMOVE (Actio N Pl Ine) IF (0 (N-NOT-ACTIO Pl Ine));
REMOVE (Actio N Pl Ela) IF (0 (N-NOT-ACTIO Pl Ela));
REMOVE (Actio N Pl Com) IF (0 (N-NOT-ACTIO Pl Com));

REMOVE (Der1 Der/dibme) IF (0 (Der1 Der/Pass)) ;
# várroduvvin

REMOVE (Der2 Der/dahtte) IF (0 (Der1 Der/ahtte)) ;

REMOVE (Der1 Der/Dimin) IF (0 Sup) ;

#REMOVE (N Ess) IF (NEGATE *-1 OPREDV BARRIER VERB)(0 (V GerII));
    # Do we need this? Taken from sme.    



REMOVE N-DER IF (0 N-DER LINK 0 Ess)(0 LEX-N LINK 0 Ess)(NEGATE 0 Actio);
 # Removes N-DER if O is also a lexical noun, and both nouns are essive.
 # Check!!!

REMOVE A-DER IF (0 LEX-A);
REMOVE (A* Adv) IF (0 LEX-ADV) ;
REMOVE (V-DER Inf) IF (0 (NOT-DER Inf));
REMOVE V-DER IF (0 LEX-V) ;

REMOVE (Der1) IF (0C (Der3)) ; 
 
REMOVE Imprt (0 Der/vuohta OR (".*#.*t"r)) ; 
REMOVE Imprt (-1 ("nåv_gåk")) ; 
REMOVE (Foc/ga) (0 ("dalága") OR ("liehket" Du3)); 
REMOVE (Foc/ge) (0 ("dasik")); 
SELECT Po (-1 Gen)(0 ("diehti"));
 
# End of SELECT/REMOVE switch area

# Choosing between particular lemmas
# -----------------------------------

LIST LEMMA1 = "ah" "almatja#bárnne" "Alme#rijkka" "áltár" "birrusij" "denára" "giládit" ("gåhttjot" Der/Pass)  "herlukvuohta" "iehkedis" "Jubmel" "oajvemus" "oajvve#oasse" ("oanedit" Der/Pass) "talennta" "tjáledit" "tjuoladit" "värro#áltár" "ådå" ;
    # first dictionary form

LIST LEMMA2 = "aj" "almatja#bárnep" "alme#rijkka" "álltár" ("biros" Ill) "denárra" ("gillat" Der1) "gåhtjo#duvvá"  ("herluk" A* Der4 Der/vuohta) "iehket" "jubmel" ("oajvve" N Superl) "oajvve#oases" "oane#duvvá" "talænnta" ("tjuollat" Der1 Der/Pass) ("tjállet" V* TV Der1 Der/Pass) "värro#álltár" "ådås";

REMOVE LEMMA2 IF (0 LEMMA1);

LIST AMB-LEMMA1 = "identitehtta" "profehtta" ;

LIST AMB-LEMMA2 = "identitehta" "identitæhtta" "profehta"  ;

REMOVE:r614 AMB-LEMMA2 IF (0 AMB-LEMMA1 LINK NEGATE 0 (Sg Nom) LINK NOT 0 Px);

SELECT LANGUAGE (0 LANGUAGE) ;
	# avoid "sáme#gielas" "Sáme#giella"

SELECT ("riegádahttet") ;	

LIST VERB-BASE = "bassat" "gillat" "mannat" "njammat" "njuolggit" "njuovvat" "råhkåt" ;

LIST VERD-DER = "basádit" "giládit" "mannát" "njamádit" "njuolggidit" "njuovadit" "råhkålit" ;

REMOVE VERD-DER IF (0 VERB-BASE);


# These are no fixed in the smj.fst:
#LIST NOT-ACR = "ah" "aj" "ájgge" "ja" "dat" "vas" "vaj" "ga" "ájn" "ij" "liehket" "mij" "man" "jahke" "na" "nav"  ;
# "<ga>" ??

#LIST NOT-ABBR = "guhti" "ij" "dat" ;

#REMOVE ACR IF (0 NOT-ACR) ; 
#REMOVE ABBR IF (0 NOT-ABBR) ; 


SELECT VFIN IF (*-1 S-BOUNDARY OR BOS OR (":") BARRIER VERB) (*1 S-BOUNDARY OR BOS OR (":") BARRIER VFIN) ;   

SELECT VFIN IF (*-1 ("de") BARRIER VERB LINK -1 COMMA) (*1 S-BOUNDARY OR BOS BARRIER VFIN) ;    
 
#SELECT VFIN IF (*-1 S-BOUNDARY OR BOS)(0 V) (*1 COMMA BARRIER VFIN LINK 1 ("ja") OR ("de")) ; 
# errouneously removed ConNeg

SELECT VFIN IF (*-1 S-BOUNDARY OR BOS)(0 Px) (*1 COMMA BARRIER VFIN LINK 1 ("ja") OR ("de")) ;    
   
SELECT VFIN IF (*-1 Ger BARRIER NOT-ADV-PCLE) ;    

SELECT (Der/goahte PrfPrc) IF (0 N) ;    

REMOVE VFIN (0 ("vilpástalle" Nom) LINK *-1 BOC BARRIER NP-HEAD-NOM)(*1 VFIN BARRIER S-BOUNDARY) ; 

#REMOVE VFIN (*-1 MO)(1C VFIN);

#SELECT VFIN IF (*-1 COMMA) (*1 S-BOUNDARY OR COMMA ) ; # BARRIER VFIN) ;    

             SECTION              #

# ===================================== #
# Local disambiguation - cycles 1 and 2 #
# ===================================== #
# --------------------------------------------------------- #
# Cycle 1a: cases that will be needed in careful mode later #
# --------------------------------------------------------- #
                 SECTION
# --------------------------------------------------------- #

REMOVE ABBR IF (NEGATE -1 BOS)(0 ("liehket"));
	## Duodje l vuodon ådå ájádusájda.


# Possessive suffix

REMOVE Px (0 Actio OR Actor) ;

REMOVE (A PxSg2) (0 Adv) ;
REMOVE Px (-1 ("juohkka"));

# PxSg1
#   V* TV Pass V PrfPrc          = V* TV Pass V* Actor N Sg Gen PxSg1
# = V* TV Pass V* Actio N Sg Nom = V* TV Pass V* Actor N Sg Acc PxSg1 
# = V* TV Pass V* Actor N Sg Nom PxSg1

# PxSg2
# V TV Inf = V* TV Actor N Sg Gen PxSg2 = V* TV Actor N Sg Nom PxSg2
#          = V* TV Actor N Sg Acc PxSg2 

REMOVE PxSg2 (*-1 VFIN BARRIER S-BOUNDARY LINK NOT 0 Sg2) ;  
        
# PxSg3
# N Sg Ela = N Sg Acc PxSg3 = N Sg Gen PxSg3 ( = N Sg Nom PxSg3)

# PxDu1

REMOVE PxDu1 IF (*-1 (Pron Pl1) BARRIER NPNH)(0 PxPl1);

# PxDu2
# PxDu3
 
# PxPl1
# PxPl2
# PxPl3
        
# ---------------------------------------------------------------- #
# Cycle 1b: removing the readings that were left from the 1a rules #
# ---------------------------------------------------------------- #
#              SECTION
# ---------------------------------------------------------------- #


# 1b is for finishing off the 1a leftovers.



# -------------------------------------------- #
# Cycle 2: Other cases of local disambiguation #
# -------------------------------------------- #
             SECTION
# -------------------------------------------- #


# Neg forms
# ---------

SELECT Neg IF (*1 ConNeg BARRIER VERB OR S-BOUNDARY); 
    ## I galga Härrav

SELECT Neg IF (-1 ("jali" CC));
    ## Le jali ij!


# ImprtII
# -------

REMOVE ImprtII (1 VFIN LINK NOT 0 Inf) ;
REMOVE ImprtII (-1 Gen)(0 N) ;

SELECT (ImprtII ConNeg Sg2) IF (*-1 (Neg ImprtII Sg2));
    ## Allu då dån tjállu!
SELECT (ImprtII ConNeg Sg3) IF (*-1 (Neg ImprtII Sg3));
    ## Allus tjále!

SELECT (ImprtII ConNeg Du1) IF (*-1 (Neg ImprtII Du1));
    ## Allun   tjále!                          
SELECT (ImprtII ConNeg Du2) IF (*-1 (Neg ImprtII Du2));
    ## Alluda då dåj tjállu!                          
SELECT (ImprtII ConNeg Du3) IF (*-1 (Neg ImprtII Du3));
    ## Alluska   tjále!                          
                                                      
SELECT (ImprtII ConNeg Pl1) IF (*-1 (Neg ImprtII Pl1));    
    ## Allup  tjále!                              
SELECT (ImprtII ConNeg Pl2) IF (*-1 (Neg ImprtII Pl2));    
    ## Allut dij tjállu!                              
SELECT (ImprtII ConNeg Pl3) IF (*-1 (Neg ImprtII Pl3));    
    ## Allusa  tjále!

# In case of poetry; Make sure that no Neg ImprtII is found to the right (!)
SELECT (ImprtII ConNeg Sg2) IF (*1 (Neg ImprtII Sg2) LINK *1 EOS BARRIER (ImprtII ConNeg));
SELECT (ImprtII ConNeg Sg3) IF (*1 (Neg ImprtII Sg3) LINK *1 EOS BARRIER (ImprtII ConNeg));
SELECT (ImprtII ConNeg Du1) IF (*1 (Neg ImprtII Du1) LINK *1 EOS BARRIER (ImprtII ConNeg));
SELECT (ImprtII ConNeg Du2) IF (*1 (Neg ImprtII Du2) LINK *1 EOS BARRIER (ImprtII ConNeg));
SELECT (ImprtII ConNeg Du3) IF (*1 (Neg ImprtII Du3) LINK *1 EOS BARRIER (ImprtII ConNeg));
SELECT (ImprtII ConNeg Pl1) IF (*1 (Neg ImprtII Pl1) LINK *1 EOS BARRIER (ImprtII ConNeg));    
SELECT (ImprtII ConNeg Pl2) IF (*1 (Neg ImprtII Pl2) LINK *1 EOS BARRIER (ImprtII ConNeg));    
SELECT (ImprtII ConNeg Pl3) IF (*1 (Neg ImprtII Pl3) LINK *1 EOS BARRIER (ImprtII ConNeg));    

SECTION

REMOVE (ImprtII ConNeg);




# ConNeg forms
# ------------

# V Ind ConNeg = V Imprt ConNeg = V VGen = V Imprt Sg2 = V Ind Prs Sg2
 
SELECT (Imprt ConNeg) IF (*-1 (Neg Imprt) BARRIER VFIN OR ConNeg OR CS);


SELECT ConNeg IF (*-1 Neg BARRIER ConNeg);
	## Iv jållåra.
    ## Jubmel le aktidam, ij almasj galga tjuolldet.

# SELECT (Ind ConNeg) IF (*-1 (Neg Ind) BARRIER VFIN OR ConNeg OR PrfPrc OR CS OR CP)
# 	(NEGATE 0 N LINK *1 ConNeg BARRIER NOT-ADV-PCLE) <= noun lookalike
# 	(NEGATE 0 LOC LINK -1 (Neg Foc) LINK -1 LOC);    <= locative lookalike
	
SELECT (Imprt ConNeg) IF (-1 (Neg Imprt));
SELECT (Ind ConNeg) IF (-1 (Neg Prt) OR (Neg Prs));
SELECT (Neg Imprt) IF (1 (Imprt ConNeg));

#REMOVE ConNeg IF (NEGATE *-1 Neg BARRIER CLB)(NEGATE *1 Neg BARRIER CLB);      

#bos clb    ij   conneg  clb  ... *conneg .. clb eos         
#clb obj , neg (kooridinerer) obj conneg   => conneg ok



# 1b is for finishing off the 1a leftovers.



# -------------------------------------------- #
# Cycle 2: Other cases of local disambiguation #
# -------------------------------------------- #
             SECTION
# -------------------------------------------- #

# Verb vs. Actio/Actor
# - - - - - - - - - -
SELECT VERB IF (-2 BOS)(-1C Adv)(1 CLB OR Adv OR Pr OR CC); #OBS
    ## Oanegav ájádallam, de huomahiv: 
    ## Dåppe åroj dasik Herodes lij jábmám.


# ImprtII
# -------

SELECT (ImprtII ConNeg Sg2) IF (*-1 (Neg ImprtII Sg2));
    ## Allu då dån tjállu!
SELECT (ImprtII ConNeg Sg3) IF (*-1 (Neg ImprtII Sg3));
    ## Allus tjále!

SELECT (ImprtII ConNeg Du1) IF (*-1 (Neg ImprtII Du1));
    ## Allun   tjále!                          
SELECT (ImprtII ConNeg Du2) IF (*-1 (Neg ImprtII Du2));
    ## Alluda då dåj tjállu!                          
SELECT (ImprtII ConNeg Du3) IF (*-1 (Neg ImprtII Du3));
    ## Alluska   tjále!                          
                                                      
SELECT (ImprtII ConNeg Pl1) IF (*-1 (Neg ImprtII Pl1));    
    ## Allup  tjále!                              
SELECT (ImprtII ConNeg Pl2) IF (*-1 (Neg ImprtII Pl2));    
    ## Allut dij tjállu!                              
SELECT (ImprtII ConNeg Pl3) IF (*-1 (Neg ImprtII Pl3));    
    ## Allusa  tjále!

# In case of poetry; Make sure that no Neg ImprtII is found to the right (!)
SELECT (ImprtII ConNeg Sg2) IF (*1 (Neg ImprtII Sg2) LINK *1 EOS BARRIER (ImprtII ConNeg));
SELECT (ImprtII ConNeg Sg3) IF (*1 (Neg ImprtII Sg3) LINK *1 EOS BARRIER (ImprtII ConNeg));
SELECT (ImprtII ConNeg Du1) IF (*1 (Neg ImprtII Du1) LINK *1 EOS BARRIER (ImprtII ConNeg));
SELECT (ImprtII ConNeg Du2) IF (*1 (Neg ImprtII Du2) LINK *1 EOS BARRIER (ImprtII ConNeg));
SELECT (ImprtII ConNeg Du3) IF (*1 (Neg ImprtII Du3) LINK *1 EOS BARRIER (ImprtII ConNeg));
SELECT (ImprtII ConNeg Pl1) IF (*1 (Neg ImprtII Pl1) LINK *1 EOS BARRIER (ImprtII ConNeg));    
SELECT (ImprtII ConNeg Pl2) IF (*1 (Neg ImprtII Pl2) LINK *1 EOS BARRIER (ImprtII ConNeg));    
SELECT (ImprtII ConNeg Pl3) IF (*1 (Neg ImprtII Pl3) LINK *1 EOS BARRIER (ImprtII ConNeg));    

SECTION

REMOVE (ImprtII ConNeg);


# Neg forms
# ---------

SELECT Neg IF (1 ConNeg);
    ## I galga Härrav



	# Verbgenitive - copied from sme-dis.rle
	# ------------
	
# Background: 	
# VGen is a bad lookalike for various finite forms. It is rare, and occurs in
# special contexts.
# Philosophy:
# 1. We make rules selecting VGen where we feel safe to say we have found it.
# 2. We remove all remaining VGen readings.

# 1. Looking for VGen.


#LIST VGENV = "bassit" "bivdit" "boahtit" "čierrut" "čuoigat" "čuoiggadit" "čuovvut" "fárret" "fitnat" "garrudit" "gálašit" "gállit" "gilljut" "girdit" "johtit" "jorgut" "luoibmat" "mannat" "mátkkoštit" "njahkalit" "njoammut" "riidet" "ruohttat" "sihkkelastit" "soabbut" "šloahtat" "váccašit" "vádjolit" "vájaldit" "vájuldit" "vánddardit" "vázzit" "viehkat" "viežžat" "vuodjat" "vuodjit" "vuojadit" "vuojašit" "vuolgit" "čilget" "čuorvut" "čurvet" "dadjat" "dárostit" "sámástit" "suomastit" "digáštallat" "hállat" "hoallat" "huikit" "hupmat" "juoigat" "lávlut" "logaldallat" "máidnut" "máinnašit" "muitalit" "savkalit" "skierbmut" "vástidit" "njurgut" "veahkehit" "vuoitit" "vuoššat" ;

LIST VGENV = "tsusskahit" "vádtset" ;

SELECT VGen IF (-1 ("gaskan"))(NEGATE 0 Der/Pass OR Ine OR Inf);
	## Gaskan viessobasa bohte guossit.
	## Gaskan bora viehkalin olggos.

SELECT VGen IF (NEGATE *-2 Neg BARRIER VERB OR SV-BOUNDARY)(-1 ("lahka"))(NEGATE 0 Der/Pass OR N OR Inf);	
	## Son lei lahka jámi.
	## Dál mun lean lahka geargga.

SELECT VGen (0 VGENV LINK *1 VFIN BARRIER SV-BOUNDARY LINK NOT 0 AUX) ;
	## Valla stádaj årro dav gullájin ja váttse suv maŋelt vuolggin .
    ## Allit fásstudattijn hådjen vuojnnu nåv gåk vilpástalle, gudi tsusskaha fásstudi almatjij vuojnedijn. 

#SELECT VGen IF (*-1 VGENV BARRIER Neg OR SV-BOUNDARY LINK 0 V-MAIN)(0 VGENV LINK NOT 0 Der/Pass OR Loc OR Inf OR ("mannat"));	
#SELECT:r1836 VGen IF (0 VGENV LINK NOT 0 Inf OR Loc OR Der/Pass OR ("mannat") OR ("fitnat"))(1 VGENV LINK 0 V-MAIN);
#	## Máhtte vulggii njurggu čáppa nuohta.
#	## Dat bođii čuoigga.
#	## Mánná viegai ovtta čieru ruoktot.
#	## Juoigga bođii ja garrut manai.
#
#SELECT:r1837 VGen IF (-1 COPULAS BARRIER NOT-ADV-PCLE)(0 VGENV LINK NOT 0 Der/Pass OR Loc OR Inf);  
#	## Muhtumat ledje vácci, ja muhtumat ledje čuoigga. 
#	
#SELECT:r1838 VGen IF (-2 COPULAS)(-1 Indef OR Adv)(0 VGENV LINK NOT 0 Der/Pass OR Loc OR Inf);  	
#	## Dát ađa lea uhccán vuošša. 
#	
#SELECT:r1839 VGen IF (NEGATE *-2 Neg BARRIER VERB OR SV-BOUNDARY)(0 VGENV LINK NOT 0 Der/Pass OR Loc OR Inf)(*1 COPULAS BARRIER SV-BOUNDARY OR Acc LINK 0 V-MAIN)(NEGATE *1 NPNH BARRIER S-BOUNDARY2)(NEGATE -1 BOS LINK 2 COMMA OR Acc);
#	## Min vuoitti dat goitge lea.
#	
#SELECT:r1840 VGen IF (-1 CC LINK -1C VGen);
#SELECT:r1841 VGen IF (1 CC LINK 1C VGen);
#	## Olbmot bohte sihke vuoji ja vácci.
#	## Sihke vácci ja vuoji bohte olbmot.

# 2. Done that, now we remove whatever VGen was left.

REMOVE VGen IF (0 (VGen));





SECTION


# Subject-verb
# Note that there is a later chapter for person disambiguation, they should be unified.

# Sg1
# ---

# V Sg1 = Acc

SELECT (V Sg1) IF (*-1 (Pron Pers Sg1) BARRIER V);
	## Mån didjij javlav:
REMOVE (V Sg1) (-1 Refl LINK NOT 0 Sg1) ;


# Sg2
# ---

SELECT (V Sg2) IF (*-1 (Pron Sg2) BARRIER NOT-ADV) ;
	## Dån jållåra. Ahte dån farga jållårå.
		
REMOVE Dem IF (0 (Pron Pers Sg2))(1 (V Sg2));
	## Dån jållåra.
	
REMOVE (Imprt Sg2) IF (-1 (Pron Pers Sg2))(0 (Ind Prs Sg2));
	## Dån jållåra.

# Sg3
# ---

SELECT (V Sg3) (*-1 (Sg Nom) BARRIER VERB OR S-BOUNDARY)(0C V)(NOT *1 VFIN BARRIER S-BOUNDARY OR CC) ; 

SELECT (V Ind Sg3) (1 CC LINK *1 Sg3 BARRIER VERB OR Nom) ;

SELECT (V Sg3) (*-1 CC BARRIER Nom OR VERB LINK *-1 Sg3 BARRIER VERB OR Nom)(NOT 0 Adv) ;

LIST DUOLLA = "duolla" "miella" ;

SELECT (V Sg3) (0 ("liehket"))(*1 DUOLLA BARRIER NOT-PCLE);
    ## Le gus duolla tjæjssárij värov mákset vaj ij?


SELECT (V Sg3)(*-1 NP-HEAD-SG-NOM OR (Pron Sg) BARRIER Nom OR S-BOUNDARY OR VFIN LINK NOT 0 (Nom Pl))(0C V) ;
SELECT (V Sg3)(-1 (Dem Sg) LINK -1 BOS) ;


# Du1
# ---

SELECT Du1 IF (-1 (Pron Pers Du1 Nom))(0 (V Prt));
	## Måj båråjma ;

# Du2
# ---

SELECT Du2 IF (-1 (Pron Pers Du2 Nom))(0 (V Prt));
	## Dåj båråjda ;

SELECT Du2 IF (0 (Pron Pers Du2 Nom))(1 (V Du2));

# Pl1
# ---

SELECT Pl1 IF (-1 (Pron Pers Pl1 Nom))(0 (V Prt));
	## Mij båråjma ;

SELECT Pl1 IF (0 (Pron Pers Pl1 Nom))(1 (V Pl1));


# Pl2
# ---

SELECT Pl2 IF (-1 (Pron Pers Pl2 Nom))(0 (V Prt));
	## Dij båråjda ;

SELECT Pl2 IF (0 (Pron Pers Pl2 Nom))(1 (V Pl2))

# Dan vs. dat

REMOVE ("Dan") (0 ("dat")) ; # NT

# =========================== #
# Disambiguating pronouns #
# =========================== #
# =========================== #

SELECT Dem (0 Pers + $$CASE LINK 1 N + $$CASE) ;

SELECT (Pers Gen) (0 Dem)(1 ("diehti")) ;

SELECT (Pers Gen)(0 Dem)(1 (N Sg) LINK NOT 0 Gen) ;

SELECT Pers (0 Dem) ;

# =========================== #
# Disambiguating subjunctions #
# =========================== #
# =========================== #

REMOVE Pcle IF (-1 BOS OR CLB)(0 CS);
	##, de allit sunji åsko!

	# de
	# - -
REMOVE ("de" @CNP) ;

SELECT ("de" CC)(-1 COMMA OR VFIN) ;
    ## Valla åvddål gå aktan lijga viessogoahtám, de bihkusij bådij Maria lij ájlis Vuojŋŋanisás sahkanam.
    ## Jesus jårgijdij ja gå suv ájtsaj de javlaj.
SELECT ("de" Adv)(*-1 BOC BARRIER VERB OR COMMA) ; 
#SELECT ("de" Adv)((-1 Pcle OR BOS) OR (1 COMMA)) ;
#SELECT ("de" Adv)((-1 NP-HEAD-NOM LINK -1 Pcle OR BOS) OR (1 COMMA)) ; #XX
    ## Bärggal de alla várráj suv doalvoj ja vuosedij sunji gájkka värálda rijkajt ja daj herlukvuodav ja javlaj.



	# gå
	# - -

	# Yes/no ( = fi. -ko/-kö, no. V1))

#SELECT Pcle IF (-3 BOS OR CLB)(-2 CC)(-1 V)(0 ("gå"));
#SELECT Pcle IF (-2 BOS OR REALCLB OR PUNCT OR COMMA)(0 ("gå"))(*1 ("?"));
#	##                       
#	
#SELECT Pcle IF (-2 ("vaj"))(0 ("gå"));
#	##  
#
#SELECT Pcle IF (-2 VERB)(-1C VFIN)(0 ("gå"));
#SELECT Pcle IF (*-2 VFIN BARRIER S-BOUNDARY LINK NOT 0 Inf)(-1C VFIN)(0 ("gå")) (NEGATE 1 VFIN); 
#	## 
#
#SELECT Pcle IF (-1 (V Neg) OR MOD-ASP)(0 ("gå"));        
#	## 
#
#SELECT Pcle IF (*-3 ("gå" Pcle) BARRIER S-BOUNDARY OR VFIN LINK -1 VFIN) (-2 CC)(-1 VFIN)(0 ("gå"));
#	## 
#
##SELECT Pcle IF (-2 Inf)(-1 VFIN)(NEGATE -1 PrfPrc)(0 ("gå"))(NEGATE *1 (@CS-COMPL) BARRIER S-BOUNDARY); #OBS
#	## 
#
#SELECT Pcle IF (-3 N OR Pron)(-2 ("ahte"))(-1 VFIN)(0 ("gå"));
#	## 
#
#SELECT Pcle IF (-1 N LINK *-1 BOS BARRIER NOT-NP)(NEGATE -1 ("hávvi"))(0 ("gå")) (*1 VFIN BARRIER S-BOUNDARY)(*1 ("?"));
#	## 
#
#SELECT Pcle IF (-2 (N Gen) LINK *-1 BOS BARRIER NOT-NP)(-1 Po)(0 ("gå")) (*1 VFIN BARRIER S-BOUNDARY);
	##  

	# Something else ( = fi. kuin, no. (så) som) (are all these CS?)
	# Testing: 
#SELECT CS IF (*-1 A OR Adv LINK *-1 ("nu") OR ("mihá") BARRIER S-BOUNDARY)(0 ("gå"));
#SELECT CS IF (*-1 ("seammá"))(NEGATE -1 VFIN LINK NOT *-1 VFIN BARRIER S-BOUNDARY)(0 ("gå")); 
	## 

#SELECT CS IF (*-1 COMPAR BARRIER NOT-ADJ OR CLB)(0 ("gå")); 
#SELECT CS IF (*-1 Comp BARRIER S-BOUNDARY OR PUNCT LINK NOT 0 VERB)(0 ("gå")) (NEGATE *1 VFIN BARRIER S-BOUNDARY);
	## 

#SELECT CS IF (-1 Comp LINK *-1 COPULAS BARRIER V-NOT-AUX OR CS) (NEGATE *-1 V-PL1 OR V-PL2 OR V-PL3 BARRIER V-NOT-AUX)(0 ("gå"));
	## .

#SELECT CS IF (*-2 Comp BARRIER NPNH LINK NOT 0 Inf)(-1 A OR N)(0 ("gå"));
	## 

#SELECT CS IF (-1 A OR Adv)(NEGATE -1 Po)(0 ("gå"))(*1 Interr BARRIER VFIN);
	## 

#SELECT CS IF (0 ("gå"))(1 ("ahte")); # 
	##  

SELECT CS IF (0 ("danen"))(1 ("gå"));




##############################
##############################

    MAPPINGS    #

##############################
##############################


# Mappings of minor categories
# ----------------------------

MAP (@CMPND) TARGET RCmpnd;
MAP (@>N) TARGET (Prop Attr);

MAP (@INTERJ) TARGET Interj ;
	## Maid, iigo leat boahtán?

MAP (@INTERJ) TARGET ("surgat") IF (-1 ("vuoi") OR ("hei"));
	## Vuoi surgat dan gándda!

MAP (@INTERJ) TARGET ("beargalat") IF (*-1 Nom BARRIER S-BOUNDARY LINK NOT 0 (Dem Nom));
	## Gosa don leat beargalat mu reaga dahkan?

MAP (@PCLE) TARGET Pcle ;
	## Amma mii eat leat máksán? 
MAP (@Num<) TARGET ABBR IF (-1 Num)(0 INITIAL LINK NOT 1 NP-MEMBER);
  	## 98 B.
  	
MAP (@>N) TARGET ABBR IF (-1 (Prop Mal) OR (Prop Fem))(1 (Prop Sur));
	## Aslak A. Guttorm.  

# Mapping vocatives - taken from sme-dis.rle
# - - - - - - - - - - - - - - - - - - - - - 

MAP (@VOC) TARGET Nom IF (*-1 BOS OR REALCLB BARRIER WORD)(1 COMMA LINK NOT 1C Nom OR Rel)(NEGATE 3 COMMA OR CC LINK *1 Nom BARRIER NPNH);
MAP (@VOC) TARGET Nom IF (*-1 BOS OR REALCLB BARRIER WORD)(1 COMMA LINK 1 Interr LINK *1 ("?") BARRIER COMMA);
    ## Herodes, guhti lij tetrárkka, .. # Not VOC  
MAP (@VOC) TARGET (N Sg Nom) IF (-1 BOS OR REALCLB)(1 COMMA) (*2 (Pron Sg2))(NEGATE 2 Interr);
    ## Duohta, ja didjij javlav:
MAP (@VOC) TARGET Nom IF (-1 BOS OR REALCLB)(1 COMMA)(2 VFIN);
    ## Vilpástalle, gádoda vuostatjin bielkáv tjalmestat!
MAP (@VOC) TARGET Nom IF (-1 COMMA LINK *-1 (V Sg2) OR (V Du2) OR (V Pl2) OR (Pron Sg2) OR (Pron Du2) OR (Pron Pl2) LINK NOT 0 ("liehkit"))(1 EOS) (NEGATE 0 Sg LINK *-1 V-SG3 BARRIER VERB);
	## Vuoj dunji, Betsaida!
	## Gåjda dássta, Sáhtán!
MAP (@VOC) TARGET Nom IF (-1 COMMA)(0 Pers)(0 Sg2 OR Du2 Or Pl2) (*1 Nom BARRIER NPNH LINK 1 EOS);
	## Gáidet eret mu luhtte, dii verrošeaddjit! 

MAP (@VOC) TARGET (N Nom) IF (-2 BOS)(-1 (Imprt Sg2))(NEGATE -1 Adv OR Qst)(1 EOS);
	## Gula eadni!

MAP (@VOC) TARGET (N Nom) IF (*-1 VFIN)(-1 COMMA LINK NOT -1 Nom)(1 COMMA LINK NOT 1 Nom);
	## Buvttes, Máhtte, goikemuoraid!
	## Tjuovvolav duv, Härrá, valla suova muv vuostak vuolgget fuolkám hivástahtátjit.
MAP (@VOC) TARGET (Prop Nom) IF (0 Mal Or Fem LINK -1 COMMA LINK *-1 (Pers Sg2))(1 COMMA) ;
    ## Man huohppelasj la dån, Tore, javlaj Ámmá munji. 
    
MAP (@VOC) TARGET (N Nom) IF (-4 BOS)(-3 (V Sg2))(-2 ("gå"))(-1 COMMA)(1 COMMA);
	## It go, bártnážan, boađáše fargga ruoktot?

SECTION

##############################
# Mapping of COMP-CS<
##############################

MAP (@COMP-CS<) TARGET WORD (*-1 ("gå" CS) OR ("nåv_gåk") BARRIER VFIN OR S-BOUNDARY)(NOT 0 VFIN OR CC OR CS)((1 EOC) OR (1 COMMA LINK 1 CC OR CS OR Rel)) ;
    ## Ij gus diján le ienep árvvo gå dajn ?
MAP (@COMP-CS<) TARGET WORD (*-1 ("vájk") BARRIER VFIN LINK -1 ("gå" CS) LINK *-1 Comp OR ("ienep") OR ("ietjá") OR ("ienebut") BARRIER VERB)(1 EOS) ;
    ## Diján le ienep árvvo gå vájk man edna tsitsátjin.	


MAP (@COMP-CS<) TARGET Adv (-1 ("gå" CS) LINK -1 Comp)(1 COMMA LINK 1 ("de")) ;
	## Dál go ruhtadilli lea buoret go ovdal, de leat gávttis eanet čiŋat.

MAP (@COMP-CS<) TARGET $$CASE (-1 ("gå" CS) LINK -1 $$CASE)(1 COMMA LINK 1 VFIN) ;
    ## Guhti áhkástis sirrá ietjá oares gå skiegesvuodas , dahká vaj gállasjvuohta máhttá sujsta doajeduvvat.
 
MAP (@COMP-CS<) TARGET (Pers Acc) (-1 ("gå" CS) LINK *-1 Comp OR ("ienep") OR ("ietjá") OR ("ienebut") BARRIER VERB)(1 COMMA LINK 1 VFIN) ;    
    ## Guhti áhtjes jali iednes ähttsá ienebut gå muv , ij le munji árvvogis.

MAP (@COMP-CS<) TARGET WORD (*-1 ("gå" CS) BARRIER VFIN OR S-BOUNDARY LINK *-1 Neg)(1 CC OR CS) ;
    ## Ja aktak ij Bárnev dåbdå ietján gå Áhttjes , ja aktak ij Áhtjev dåbdå ietján gå Bárnnes ja gesi Bárnne sihtá suv bigodit .


SELECT (@COMP-CS<) ;


##############################
##### CC - CS - MAPPING ######
##############################

	# vaj
	# - -
	# This one may be CS (vaj = 'in order to') or CC (vaj = 'or').
	


MAP (@CNP @CVP) TARGET CC ;
	## Bárnev sån riegádahttá ja dån galga suv Jesusin gåhttjot, dan diehti gå álmmugav suttojstisá lånes.
# meaning: @CC-G @CC-L (global, local, i.e., inside and outside NP)

MAP (@CNP @CVP) TARGET CS IF (0 ("gå") OR ("nåv_gåk") OR XGU);
    ##

MAP (@CVP) TARGET CS ;
	## 
	
# =========================	
# CC- and CS-Disambiguation
# =========================

#XX - the rule is effective

REMOVE ("de" @CNP) ; #XX

SELECT (CC @CVP) (0 ("vaj"))(1 Neg LINK 1 EOS OR Pcle); #XX
SELECT (CC @CVP)(0 ("vaj"))(1 Qst); # ???
	## Le gus duolla tjæjssárij värov mákset vaj ij?

SELECT (CC @CVP) (0 ("vaj"))(2 ("gå")); #XX
	##  

SELECT CC IF (0 ("vaj"))(NEGATE *1 VFIN BARRIER S-BOUNDARY);  #XX
	## .

SELECT CC IF (*-1 Inf BARRIER VERB OR CLB)(0 ("vaj"))(*1 Inf BARRIER VERB); #XX
	## 

SELECT @CVP IF (-1 COMMA)(0 CC); #XX
    ## Ieddnes Maria lij Josefijn giláduvvam, valla åvddål gå aktan lijga viessogoahtám, de bihkusij bådij Maria lij ájlis Vuoiŋŋanisás sahkanam.
    
SELECT @CVP IF (-1 BOS OR (@CVP))(NOT 0 ("de")); #XX
    ## Jesus jårgijdij ja gå suv ájtsaj de javlaj:

SELECT @CVP IF (-2 BOS LINK 1 Num)(NEGATE *1 Num BARRIER NPNH); #XX
    # We need this rule until we get a better file format without line numbers.
    ## Valla dáv ájádaládijn Härrá ieŋŋgil Josefij niegon bihkusij ja javlaj...

SELECT @CVP IF (NEGATE -2 Gen)(-1 ("åvddål") LINK NOT *-1 VFIN BARRIER SV-BOUNDARY2)(0 ("gå"));
    ## Ieddnes Maria lij Josefijn giláduvvam, valla åvddål gå aktan lijga viessogoahtám, de bihkusij bådij Maria lij ájlis Vuoiŋŋanisás sahkanam.
    
SELECT @CVP IF (-1 ("diehti") LINK -1 Gen)(0 ("gå")); #XX
    ## Bárnev riegádahttá ja dån galga suv Jesusin gåhttjot, dan diehti gå álmmugav suttojstisá lånes.

    
SELECT @CNP IF (-1 RCmpnd); #XX
    ## Sávtsa- ja gájtsanáhkkegárvoj vádtsin, vánev gierddin, doarrádaláduvvin ja illastuvvin.
    
#SELECT @CNP IF (NEGATE *-1 VFIN OR AUX BARRIER S-BOUNDARY)(*-1 V OR N OR Adv OR A OR Pron); 
    ## Betleheman, Judean, dan diehti gå profehta le náv tjállám..
    # Not supposed to select "Ja de mun lean boahtán." 

#SELECT @CNP IF (-1C $$CASE)(1C $$CASE); #XX
SELECT @CNP IF (-1 $$CASE LINK 0 N)(1 $$CASE LINK 0 N LINK NEGATE 0 Gen LINK 1 N); #XX
    ## Jesus synagogajn åhpadij , evangeliumav rijka birra sárnnedij ja almatjijt gájklágásj dávdajs ja vigijs buoredij.

SELECT @CNP IF (-1C NP-HEAD-NOM)(*1C NP-HEAD-NOM BARRIER NPNHA); #XX

SELECT @CNP IF (-1C NP-HEAD-ACC)(*1C NP-HEAD-ACC BARRIER NPNHA); #XX
    ## Bärggal de alla várráj suv doalvoj ja vuosedij sunji gájkka värálda rijkajt ja daj herlukvuodav ja javlaj.

SELECT @CNP IF (0 ("gå"))(1 (@COMP-CS<)); 

SELECT @CVP IF (-1 (@COMP-CS<))(1 Rel LINK *1 VFIN BARRIER S-BOUNDARY); #XX
    ## Ja aktak ij Bárnev dåbdå ietján gå Áhttjes , ja aktak ij Áhtjev dåbdå ietján gå Bárnnes ja gesi Bárnne sihtá suv bigodit .

#SELECT @CNP IF (-1 (@X) OR N)(NOT 0 ACR)(*1 N BARRIER NPNHA LINK NOT 1 VFIN);
    ## Vuolgij Nasarets ja årruj Kafarnaumij, jávrregáddáj, Sebulona ja Naftali guovlojn.

SELECT @CNP IF (NEGATE -1 BOS)(NEGATE 1 CS OR Rel OR Interr)(NEGATE *1 VFIN BARRIER S-BOUNDARY OR PUNCT-RIGHT); #XX
    # No potential finite verb following
    ## Judaj riegádijga Peres ja Sera, gej ieddne lij Tamar, Peresij riegádij Hesron,...
    ## Jesus jårgijdij ja gå suv ájtsaj de javlaj:


SELECT @CVP IF (1 CS); #XX
    ## Álbmotriekti gierdá dákkár doaimmaid duššefal heahtediliin ja go daidda lea dievaslaš ipmárdus dákkár álbmogiid ovddasvástideaddji eiseválddiid bealis.
    # Does 1 have to be strict?

#SELECT @CVP IF (*-1 VFIN BARRIER S-BOUNDARY OR CC)(*1 VFIN BARRIER S-BOUNDARY OR CC LINK NOT 0 ("diehti")); #XX
    ## Valla dáv ájádaládijn Härrá ieŋŋgil Josefij niegon bihkusij ja javlaj.

SELECT @CVP IF (0 CC)(1C VFIN);  #XX
    # A secure finite verb after it.
    ## Valla dáv ájádaládijn Härrá ieŋŋgil Josefij niegon bihkusij ja javlaj: "Josef, Davida bárnne, ala balá...

SELECT @CVP IF (*-1C VFIN BARRIER S-BOUNDARY)(*1C NP-HEAD-NOM BARRIER NPNHA); #XX
    ## 
SELECT @CVP IF (NEGATE -1 NP-HEAD-NOM)(1 NP-HEAD-NOM LINK 1 VFIN); #XX
    ## Bárnev sån riegádahttá ja dån galga suv Jesusin gåhttjot.

SELECT @CVP IF (-1 NP-HEAD LINK *-1 VFIN BARRIER S-BOUNDARY2)(1 VFIN); #XX
    ## Bärggal de alla várráj suv doalvoj ja vuosedij sunji gájkka värálda rijkajt ja daj herlukvuodav ja javlaj.

SELECT @CVP IF (*-1C VFIN BARRIER S-BOUNDARY)(*1C AUX LINK 0 Prs BARRIER S-BOUNDARY);  #XX
    ## Gåhtjo dan diehti hávdev gålmmå biejve váttugit gŋahttjat vaj åhpadisålmmå e besa suv suoládit ja álmmugij javllat sån le jábbmegij lussta tjuodtjelam.

SELECT @CVP IF (*-1 COMMA LINK *-1C VFIN BARRIER S-BOUNDARY)(*1C AUX LINK 0 Prs BARRIER S-BOUNDARY);  #XX
    # The comma version
    ## Guhti tiempelav gajko ja gålmån biejven vas tsieggi, gájo dal ietjat, ja luojttáda ruossas.

SELECT @CVP IF (-2 BOS)(-1 PUNCT)(0 CC);  #XX
    ## Ja bárnne daŋga båttå buorránij.

SELECT @CVP IF (*-1C VFIN BARRIER S-BOUNDARY2)(*1C VFIN BARRIER S-BOUNDARY2); #XX
    ## Josiaj riegádij Jojakin vieljatjij dan ájge gå álmmugav vuolgadin Babylonaj.

SELECT @CVP IF (*1 AUX LINK *1C NOT-VFIN);  #XX

SELECT @CVP IF (*1 AUX LINK *1 Rel OR MANNE LINK *1 AUX LINK *1 NOT-VFIN LINK 0 AUX LINK *1 NOT-VFIN); #XX
    # The relative sentence version
    ## 

SELECT @CVP IF (NEGATE -1 ADVLCASE OR Adv)(*1 ADVLCASE OR Adv BARRIER NPNH LINK 1 VFIN);  #XX
    ## Elihudaj riegádij Elasar, Elasaraj Mattan, Mattanij Jakob, ja Jakobij riegádij Josef, Maria ålmåj.

SELECT CS IF (0 ("vaj")); #XX
	## Divna dát sjattaj vaj luluj ålliduvvat mav Härrá lij profehta baktu javllam.
	## Muv niejdda le jur jábmám, valla boade bieja giedat suv nali vaj ällá.

SELECT @CVP IF (*-1 VFIN BARRIER S-BOUNDARY OR @CVP)(*1 VFIN BARRIER S-BOUNDARY OR CC LINK NOT 0 ("diehti")); #XX


SELECT @CNP ;
             SECTION              #
	

    

# Adjectives
# ==========

# Attr pre-N
# ----------

SELECT (Prop Attr) IF (0 ("Johannes"))(1 ("gástadiddje")) ;
REMOVE Attr IF (-1 ("Johannes"))(0 ("gástadiddje")) ;


SELECT (Prop Attr) IF (1 Mal OR Fem OR Sur);
	## Jesus Kristus

SELECT (A Attr) IF (-1 V-NOT-AUX OR N)(1 N LINK NOT 0 VFIN);
	## Æjgádijda vaddá buorep diedojt.

REMOVE Attr IF (0 (Prop Nom) OR (Prop Gen))(NEGATE 1 N);
#REMOVE Attr IF (0 (Prop Nom) OR (Prop Gen))(NEGATE 1 A LINK *1 N BARRIER NOT-ADJ);

REMOVE Attr IF (*1 VFIN BARRIER N);
	## Josef lij rievtesmielak, ittjij sidá suv hehpaduttet.

REMOVE Adv IF (-1 (Dem Com))(0 (A Attr))(1 (N Com));
	## dajna gjielgga rájájn.

REMOVE (A Sg Nom) IF (*-1 BOS OR LEFT OR CLB BARRIER NPNH)(1 N)(2 BOS OR RIGHT OR CLB);
	## (vijdes sisanujn)




# Only Attr if something to lean on
# - - - - - - - - - - - - - - - - -

REMOVE Attr IF (NEGATE *1 N BARRIER NPNHCCC)(NEGATE 1 PUNCT-LEFT LINK *1 PUNCT-RIGHT LINK *1 N BARRIER NPNHCCC);



# Rules for Attr between Dem and N, one for each case combination
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
SELECT (A Attr) IF (*-1 (Dem Sg Nom) BARRIER NPNH)(*1 (N Sg Nom) BARRIER NPNH); 
SELECT (A Attr) IF (*-1 (Dem Pl Nom) BARRIER NPNH)(*1 (N Pl Nom) BARRIER NPNH); 

SELECT (A Attr) IF (*-1 (Dem Sg Acc) BARRIER NPNH)(*1 (N Sg Acc) BARRIER NPNH); 
SELECT (A Attr) IF (*-1 (Dem Pl Acc) BARRIER NPNH)(*1 (N Pl Acc) BARRIER NPNH); 

SELECT (A Attr) IF (*-1 (Dem Sg Gen) BARRIER NPNH)(*1 (N Sg Gen) BARRIER NPNH); 
SELECT (A Attr) IF (*-1 (Dem Pl Gen) BARRIER NPNH)(*1 (N Pl Gen) BARRIER NPNH); 

SELECT (A Attr) IF (*-1 (Dem Sg Ill) BARRIER NPNH)(*1 (N Sg Ill) BARRIER NPNH); 
SELECT (A Attr) IF (*-1 (Dem Pl Ill) BARRIER NPNH)(*1 (N Pl Ill) BARRIER NPNH); 

SELECT (A Attr) IF (*-1 (Dem Sg Ine) BARRIER NPNH)(*1 (N Sg Ine) BARRIER NPNH);
SELECT (A Attr) IF (*-1 (Dem Pl Ine) BARRIER NPNH)(*1 (N Pl Ine) BARRIER NPNH);

SELECT (A Attr) IF (*-1 (Dem Sg Ela) BARRIER NPNH)(*1 (N Sg Ela) BARRIER NPNH);
SELECT (A Attr) IF (*-1 (Dem Pl Ela) BARRIER NPNH)(*1 (N Pl Ela) BARRIER NPNH);

SELECT (A Attr) IF (*-1 (Dem Sg Com) BARRIER NPNH)(*1 (N Sg Com) BARRIER NPNH);
SELECT (A Attr) IF (*-1 (Dem Pl Com) BARRIER NPNH)(*1 (N Pl Com) BARRIER NPNH);

SELECT (A Attr) IF (*-1 (Dem Ess) BARRIER NPNH)(*1 (N Ess) BARRIER NPNH);





	# Other attribute rules
	# ---------------------

# This whole chapter is taken from sme, and should be revised.
# The rules in question have sme examples.

#SELECT Attr IF (0 Prop LINK 1 ("guovttos")); # guovttos in Lule Sámi?
  	## Gaskaija áiggi Bávlos guovttos Silasiin rohkadalaiga ja lávlluiga.
  # a. also without the LINK to Com? b. larger set than only "guovttos"?
  
REMOVE (Actor Nom)(-1C Nom)(0 Attr LINK 1 N) ; 

REMOVE PrfPrc (0 Actio LINK NOT *-1 COPULAS BARRIER VERB)(1 N);
    ## Dåppe buktin suv lusi gállnam ålmmåv guoddemlátjo nanna vällahime.

REMOVE (A Attr) IF (1 CC)(2 N)(NEGATE 2 (A Attr)) (NEGATE 2 PrfPrc LINK *1 N BARRIER NPNH);
	## ...jus doaimma ovttadagat sihke fágalaččat ja lágideami bokte leat...

SELECT (N Attr) IF (NEGATE 0 Prop OR Pron)(1 N);
	## Máttimus sápmelaččat eahpidedje dáža dutkiid eanet go davit sápmelaččat.
	
SELECT CAP-INITIAL IF (-1 Mal OR Fem)(0 INITIAL)(1 Sur); 
    ## Mikkel A. Eira.
    
SELECT Adv IF (NEGATE -1 Gen)(0 ("åvddål") LINK NOT *-1 VFIN BARRIER SV-BOUNDARY OR CCCOMMA)(1 ("gå"));
        ## Ieddnes Maria lij Josefijn giláduvvam, valla åvddål gå aktan lijga viessogoahtám, de bihkusij bådij Maria lij ájlis Vuoiŋŋanisás sahkanam.

#REMOVE Adv IF (NEGATE *-1 VERB BARRIER S-BOUNDARY2)(NEGATE 0 ("gosa") OR ("mielde"))(NEGATE 1C A)(NEGATE *1 VERB BARRIER S-BOUNDARY2);
	## Diehtojuohkin divššohasa lagamuš oapmahažžii.
	## Stuorát guolit mat eai bisán čalmmiide, mannet čađa.
 
REMOVE (A Attr)(0 HUMAN + N LINK *1 PLACE BARRIER NPNH) ;

SELECT (A Attr) IF (*-1 BOS OR CS OR (":" CLB) BARRIER NPNHA)(NEGATE 0 VERB)(NEGATE 1 Dem OR VERB) (NEGATE 0 Prop LINK -1 (Prop Attr) LINK 2 CC) (NEGATE 0 Prop LINK 1 CC LINK 1 Prop LINK NOT 1 Prop)(*1 N BARRIER NPNH LINK NOT 0 Prop LINK NOT 1 Po)(NEGATE 1 COMMA LINK 1 N);
	## Golbma čáppa, doaimmalaš nieidda livžo áhčiset riegádanbeaivve.

SELECT (A Attr) IF (-1 COPULAS)(1 A OR N LINK NOT *1 Po OR Ine OR Ela OR Inf BARRIER NP-BOUNDARY) (NEGATE 1 Adv OR VERB)(*2 ("gus") BARRIER NOT-NP);
	## Sis-Finnmárkkus lea vuolit gaskamearálaš bruttoboahtu go fylkkas...

SELECT (A Attr) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1C NP-HEAD-NOM BARRIER NOT-ADV-PCLE)(NEGATE 1 CLB LINK 1 N)(*1 N BARRIER NPNH LINK NOT 1 Po OR Ine OR Ela);  
	## Dieđuid diehtogieđahallama ovddasvástideaddjin leat guovlluguovdasaš dearvvašvuođadoaimmat.

SELECT (A Attr) IF (*-1 Num BARRIER NPNH)(*1 N BARRIER NPNH)(NEGATE 0 Prop LINK 1 CC)(NEGATE 0 ("kr") LINK -1 CARDINALS);
	## Dan dihte ohcat guokte lassi stipendiáhttavirggi sámi/eamiálbmot-
	## guoskevaš dutkamiidda.

SELECT (A Attr) IF (-1 Adv)(NEGATE 0 Inf)(*1C N BARRIER NPNH)(NEGATE 1 CLB LINK 1 N) (NEGATE 1 Prop);
	## Mánná viehkalii olggos álás julggiid.
	## Universitehtas lea maiddái čielga nášunála ovddasvástádus...
	
SELECT (A Attr) IF (*-1 V-NOT-AUX BARRIER SV-BOUNDARY2)(0 Adv)(1 Acc)(2 ("gus"));
    ## Mun čállen romit reivve go don.	

SELECT (A Attr) IF (NEGATE *-3 COPULAS BARRIER NOT-ADV-PCLE)(-2 Attr OR PrfPrc OR PrsPrc) (-1 CC)(1 N LINK NOT 0 Nom)(NEGATE 0 (Prop Gen) LINK NOT 1 Prop);
	## Soahpamušas váilot njuolggadusat smávit vahágahtti ja geavatlaččat bággolonistemiide.
 
REMOVE (A Nom) IF (0 (A Attr))(NEGATE 0 (Pl Nom) LINK *-1 (V Pl3) BARRIER NOT-ADV-PCLE OR CS LINK NOT *-1 (Pl Nom) BARRIER S-BOUNDARY2 OR CC) (NEGATE 0 (Pl Nom) LINK *-1 COPULAS BARRIER NOT-ADV-PCLE LINK 0 Pl3) (*1C N OR RCmpnd BARRIER NOT-ADJ OR (PUNCT RIGHT) LINK NOT 1 Po) (NEGATE 1 Prop OR Ill)(NEGATE 1 COMMA LINK 1 N) (NEGATE 1 Gen LINK *1 Po BARRIER NP-BOUNDARY);
	## Dat lea čuovvovaš sajis.

REMOVE (A Gen) IF (0 (A Attr))(*1 N BARRIER NOT-ADJ LINK NOT 1 Po);
REMOVE (A Acc) IF (0 (A Attr))(*1 N BARRIER NOT-ADJ LINK NOT 1 Po);
    	## Sámegiella ja dárogiella leat ovttaárvosaš gielat.
	## Lea deaŧalaš movttiidahttit studenttaide gazzat doavttergrádaoahpu. 
 # The careful mode (*1C N) was removed because of the second example.
 # But reintroduced because of the third example.

 
REMOVE V IF (NEGATE -1 (N Nom))(0 (A Attr))(*1 N BARRIER NOT-ADJ LINK NOT 1 Po) (NEGATE 0 Inf)(*2 VFIN BARRIER S-BOUNDARY2);
    	## Olles doaibmaviessu lea sullii 2300 njealjehasmetera stuoru. 

REMOVE N IF (0 (A Attr))(NEGATE 1 VFIN OR PrfPrc OR CLB)(NEGATE 0 (Sg Nom) LINK *-1 (V Sg3) BARRIER NOT-ADV-PCLE LINK NOT *-1 Nom BARRIER S-BOUNDARY2 OR CC) (NEGATE 0 (Pl Nom) LINK *-1 (V Pl3) BARRIER NOT-ADV-PCLE LINK NOT *-1 Nom BARRIER S-BOUNDARY2 OR CC) (*1 N BARRIER NOT-ADJ LINK NOT 0 ADVLCASE LINK NOT 1 Po);
	## ...muhto dakkár radikála rievdadus eahpitkeahttá buktá garra reakšuvnnaid,...

SELECT (A Attr) IF (NEGATE *-1 COPULAS BARRIER NP-HEAD-NOM OR V-NOT-AUX)(NEGATE 0 Num OR Inf OR Comp OR ("nubbi") OR Gen)(NEGATE 0 Adv LINK 1 Gen LINK 1 N)(1 N)(NEGATE 1 VFIN OR Inf) (NEGATE 0 (N Sg Nom) LINK *-1 V-SG3 BARRIER NPNHA LINK NOT *-1 NP-HEAD-NOM BARRIER S-BOUNDARY2) (NEGATE *-1 ("gus") BARRIER NOT-NP LINK NOT -1 VFIN LINK NOT 1 EOS); 
	## Gonagas sáhttá mearridit sierranaš njuolggadusaid báikkalaš dilálašvuođaid ektui.

SELECT (A Attr) IF (NEGATE *-1 LIEHKET-FIN-NOT-IMP BARRIER NOT-ADV LINK *-1 BOS BARRIER NOT-ADV) (NEGATE 0 Adv OR Inf)(1 A-CASE LINK 1 CLB LINK 1 NP-BOUNDARY); 
	## ...main leat mielde dušše priváhta oasálaččat, ii leat...
	## Lea dehálaš deattuhit, ahte IKT ii doaimmá.
	## Lea erenoamáš dehálaš movttiidahttit sámegielat doaktáriid bargat gielddain.

REMOVE Adv IF (-2 A OR N)(-1 ("gus" Pcle))(*1 N BARRIER NPNH);
	## No longer needed after the introduction of the previous rule?

REMOVE PrsPrc IF (0 (A Attr))(1 N);
    	## Sidjiide maid lei guollerikkis Ruija geasuheaddji guovlu.
 
REMOVE (A Ine) IF (0 Attr)(1 N);
	## Olggosaddin sáhttá liikká dáhpáhuvvat jos gávdnojit dasa beaktilis sivat.


SELECT (A Attr) IF (*-1 BOS OR CS OR (":" CLB) BARRIER NPNHA)(NEGATE 0 VFIN OR Inf) (*1 HNOUN BARRIER NPNH LINK NOT 0 Prop)(NEGATE 1 VFIN)(NEGATE 1 COMMA LINK 1 N);
	## Sámedikki vuosttamuš dábálaš dievasčoahkkima maŋŋa... 

#REMOVE N IF (-1 Gen OR Dem OR Indef)(0 Attr)(1 N)(NEGATE 1 VFIN);
	## Oktasaš vuoigatvuođat ledje šaddan dološ áiggi rájes...
	# This doesn´t function. It hits "násstedåbdde" in: De lullet muhtem násstedåbdde Betlehemaj båhtin ja gahtjin.
 

REMOVE VFIN IF (-1 Gen OR Dem OR Indef)(0 Attr)(1 N);
	## Lávži goallostuvvo dan ovddit noađđehearggi čeabehii.

REMOVE Adv IF (NEGATE -2 V-NOT-AUX)(-1 Gen OR Dem OR Indef)(NEGATE -1 PrfPrc) (0 Attr)(1 N);
	## ...galget divššohasa lagamus oapmahaččat oažžut dieđu...

SELECT (A Attr) IF (1 (PUNCT LEFT) LINK *1 (PUNCT RIGHT) LINK 1 N);
	## Dábálaččat earuha váldolaččat gaskal dábálaš (totala) geavahan-
	## vuoigatvuođaid ja sierranas (partiála) geavahanvuoigatvuođaid.

#SELECT (A Attr) IF (-1 COMPAR)(1 ("gus" Pcle))(2 (A Attr))(*3 N BARRIER NOT-ADJ);
	## Sámedikki ásaheapmi lea eanet ideologalaš go čielga mearrádus.

SELECT (A Attr) IF (NEGATE 0 Adv OR Num)(1 TIME);
	## Váilui guhkit áiggi lotnolaš oaivila. 

SELECT (A Attr) IF (*-1 ("lassi" Ess) BARRIER NPNH)(*1 Ill BARRIER NPNH);
	## Vuoigatvuohta leat Sámedikkiid jienastusloguin lea lassin dábálaš 
	## jienastanvuoigatvuođa eavttuide.

 
	# Coordinated attributes
	# ----------------------
SELECT (A Attr) IF (NEGATE 0 Prop)(1 COMMA)(2 Attr)(3 CC LINK *1 HNOUN BARRIER NPNH);
	## ...go viidáset gaskkusta oppalaččat álbmoga historjjálaš,
	## kultuvrralaš ja politihkalaš dieđuid.

SELECT (A Attr) IF (NEGATE 0 Prop)(1 COMMA)(2 Attr OR PrsPrc)(NEGATE 3 N LINK 1 COMMA) (*3 HNOUN BARRIER NPNH);
	## Ferten maid imaštallat go dehálaš, sámepolitihkalaš birrasat
	## barget nu áŋgirit dán ovdii.

SELECT (A Attr) IF (-2 Attr)(NEGATE -2 Prop)(-1 CC)(1 N OR Actio);
	## Divššohasain lea vuoigatvuohta oažžut čilgejuvvot álkit ja
	## oanehaččat journálas geavahuvvon fágadoahpagiid.

SELECT PrfPrc IF (*-1 CC BARRIER NOT-ADV LINK -1 A);
	## Dilálašvuođaid álgu sáhttá leat dovdameahttun dahje jávkan historjjá seavdnjatvuhtii.

 # The last rule does not hit only prenominal PrfPrc. It is nevertheless useful,
 # since PrfPrc but not Actio coordinates with adjectives in predicate position. 

SELECT (A Attr) IF (1 CC)(2C (A Attr));
SELECT (A Attr) IF (1 CC)(2 (A Attr))(NEGATE 2 VERB)(3 N OR RCmpnd)(NEGATE 4 Po); 
	## ...go galgá válljejuvvot gaskal vejolaš ja dohkkalaš iskkadan-
	## ja dikšunvugiid. 
	## Olbmot geat ellet náitoslágan dahje guimmešvuođalágan gaskavuođain.

SELECT (A Attr) IF (NEGATE -1 ("leat"))(1 CC)(*2 PrfPrc BARRIER NOT-ADV) (*3 N BARRIER NPNH);
	## Johtalus rabas dahje galbmon mearas lea muddejuvvon.

SELECT (A Attr) IF (*-1 Neg BARRIER S-BOUNDARY2 OR VFIN) (*1 CONTRA BARRIER WORD LINK 1C Attr);
	## ILO-konvenšuvdna nr 169 ii vuođđuduva formála, muhto funktionála
	## oamastanvuoigatvuođa doahpagii.


	# Special rules for 'buorre' (the only adjective showing case agreement)
	# ----------------------------------------------------------------------
SELECT (Pl Nom) IF (0 ("buorre"))(*1 (N Pl Nom) BARRIER NOT-ADJ-CC);
	## Leatgo buorit guolit?

SELECT Acc IF (0 ("buorre"))(NEGATE 0 Comp OR Superl) (*1C (N Acc) BARRIER NOT-ADJ-CC);
	## Doaibmaplána lea bidjan buori vuođu.
	## Doaivvutgo oažžut buriid guliid?

SELECT Gen IF (0 ("buorre"))(NEGATE 0 Comp OR Superl)(*1C (N Gen) BARRIER NOT-ADJ-CC LINK 0 HNOUN);
	## ...galgá journála čállojuvvot buore fidnodábi mielde.


	# Comparatives
	# ------------
 # This block of rules is there to ensure case agreement for attributive
 # comparatives.

REMOVE Gen IF (0 Comp)(*1 HNOUN BARRIER NPNH LINK 0 NOT-GEN); 
REMOVE Acc IF (0 Comp)(*1 HNOUN BARRIER NPNH LINK 0 NOT-ACC);
	## Oppalaččat daid gažaldagaid birra mat gieđahallojuvvojit... 
	## ...oktiibuot 3.000.000 ruvnno jahkásaččat 5 jagi badjel.

REMOVE Nom IF (0 Comp)(*1 HNOUN BARRIER NPNH OR PUNCT LINK 0 NOT-NOM LINK NOT 0 Inf)(NEGATE 1 COMMA OR CC)(NEGATE 0 (Pl Nom) LINK *-1 (V Pl3) BARRIER NPNHA)(NEGATE 0 (Sg Nom) LINK *-1 V-SG3 BARRIER NPNHA) (NEGATE 1 Gen LINK *1 Po BARRIER NP-BOUNDARY);
	## Álggahit eará doaibmabijuid gáibida baicca lassi árjja...
  

	# Adjectives as predicates
	# ========================

SELECT (A Pl Nom) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 (N Pl Nom) OR (Pron Pl Nom) OR (Pl1 Nom) OR (Pl2 Nom) OR (Pl3 Nom) BARRIER S-BOUNDARY2 OR (Sg Nom))(NEGATE 0 N)(NEGATE 1 A)(NEGATE 1 Adv LINK 1 A);
	## ...ja dan golut leat govttolaččat veahki ektui.

SELECT (A Pl Nom) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 (Du1 Nom) OR (Du2 Nom) OR (Du3 Nom) BARRIER S-BOUNDARY2)(NEGATE 0 N)(NEGATE 1 VERB OR A);
	## I have found no examples, but the rule should work! 

SELECT (A Pl Nom) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 Nom BARRIER S-BOUNDARY2 LINK *-1 CC BARRIER NPNH LINK -1 Nom)(NEGATE 0 N)(NEGATE 1 VERB OR A);
	## Sámegiella ja dárogiella leat ovttaárvvosaččat. 

SELECT (A Sg Nom) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 (Sg Nom) BARRIER NOT-ADV-PCLE LINK *-1 BOS OR CS OR (":" CLB) OR CP BARRIER NOT-ADV-PCLE) (1 CLB OR EOS);
	## Jos viessu lea dohkálaš, de ráfi orru dan badjel.
	## Vuoigŋa gal lea geargat, muhto oažži lea headju.

SELECT (A Sg Nom) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 (Sg Nom) BARRIER NOT-ADV-PCLE LINK *-1 BOS OR CS OR (":" CLB) OR CP BARRIER NOT-ADV-PCLE) (*1 COPULAS BARRIER NOT-ADV-PCLE LINK *1 CLB OR EOS BARRIER NOT-ADV-PCLE);
	## ...dan dihte go geassegollebivdu lei áibbas heittot leamaš,...

SELECT (A Sg Nom) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK *-1 (N Sg Nom) OR (Pron Sg Nom) OR (Sg1 Nom) OR (Sg2 Nom) OR (Sg3 Nom) BARRIER S-BOUNDARY2 LINK NOT 0 Ess)(NEGATE 0 N OR Inf OR Adv)(NEGATE 1 VERB)(NEGATE 1 CC LINK 1 Adv) (NEGATE *1 N BARRIER NPNH);	## Mun in leat dohkálaš ahte don boađášit mu vissui.
    ## Dån la jalla.
    
SELECT (A Sg Nom) IF (*1 COPULAS BARRIER NOT-ADV-PCLE LINK 1 (N Sg Nom) OR (Pron Sg Nom) OR (Sg1 Nom) OR (Sg2 Nom) OR (Sg3 Nom) LINK NOT 0 Ess)(NEGATE 0 N OR Inf OR Adv)(NEGATE *1 N BARRIER NPNH);    
    ## Jiesstánam lijka man jalla la dån.

SELECT (A Sg Nom) IF (*-1 COPULAS BARRIER Nom OR PrfPrc OR CS OR MANNE LINK 0 Sg1 OR Sg3)(NEGATE 0 Adv OR N OR Inf)(NEGATE 1 VERB OR Num OR A) (NEGATE 1 ("nu") OR ("hui") LINK 1 A)(NEGATE 1 CC LINK 1 Adv) (NEGATE *1 N BARRIER NPNH); 
	## Lea ártet go it gallet fulkkiin.

SELECT (A Sg Nom) IF (*-1 COPULAS BARRIER SV-BOUNDARY2 LINK *-1 (Neg Sg1) OR (Neg Sg2) OR (Neg Sg3) BARRIER NOT-ADV-PCLE)(NEGATE 0 N)(NEGATE 1 N OR A) (NEGATE 1 ("nu") OR ("hui") LINK 1 A);
	## Ii leat vuogas geavahit hálduiváldima dálá fásta opmodagaide.

SELECT (A Sg Nom) IF (*-1 COPULAS BARRIER NOT-AA LINK 0 Sg3)(NEGATE 0 Inf) (0 (N Sg Ine) OR (Actor) OR (N* A))(*1 EOS OR CLB BARRIER NOT-ADV-PCLE); 
	## Lea veahá eahpidahtti, ahte eanaš oassi ođđa govvadáidagis,... 
	## Dávjá lea lunddolaš, ahte doaktáris lea vástádusrolla.

SELECT (A Sg Nom) IF (-1 NP-HEAD-SG-NOM)(*-2 COPULAS BARRIER NOT-ADV-PCLE) (NEGATE 0 Attr LINK *1 (N Sg Nom) BARRIER CLB OR NPNH);
	## Dábálaččat lea čuollu guhkit, golbma, njellja firbmebaji guhku.


SELECT (A Pl Nom) IF (*-1 (N Pl Nom) BARRIER NOT-ADV-PCLE OR CLB LINK *-1 COPULAS BARRIER SV-BOUNDARY2);
	## Mánnáguoddi-spagáin leat spahká-oaivvit sakka guhkibut.
	## Dán oktavuođas leat dološ geavahusa njuolggadusat miellagiddevaččat.

SELECT (A Pl Nom) IF (*-1 COPULAS BARRIER SV-BOUNDARY2 LINK *-1 (V Neg) BARRIER NOT-ADV-PCLE LINK 0 V-DU-PL)(NEGATE 0 N)(NEGATE 1 VERB OR A); #(NEGATE 1 N);
	## Sis-Finnmárkku dikkit maŋŋilgo Norga badjelasas válddii sorjjas-
	## meahttunvuođa dán guovllus, eai šat lean seamma dehálaččat 
	## sápmelaččaid gaskkas go ovdal.

SELECT (A Sg Nom) IF (*-1 BOS OR REALCLB BARRIER NOT-CC) (1 (Dem Sg Nom) OR MUN OR DON OR SON); 
	## Ja ávdugas son guhte oskkui.
 
SELECT (A Pl Nom) IF (*-1 BOS OR REALCLB BARRIER NOT-CC) (*1 NP-HEAD-PL-NOM BARRIER NPNH);
	## Ávdugasat sii geat moraštit.

REMOVE VERB IF (-1 COPULAS)(0 Nom)(NEGATE 0 PrfPrc)(1C (Pron Interr));
REMOVE (A Attr) IF (-1 COPULAS)(0 Nom)(1C (Pron Interr)); 
	## Dat geaidnu lea govdat mii doalvu gáđohussii.
	## Dát dáidá leahkit duohta máid boazodoalli lohká.

REMOVE VFIN IF (*-1 COPULAS BARRIER NOT-ADV-INDEF)(0 Attr)(1 N OR EOS);
	## Joddu suhppejuvvo go doaris lea gárvvis.	

SELECT A IF (*-1 COPULAS BARRIER NOT-ADV-INDEF)(NEGATE 0 VERB OR Ord OR N) (1 N OR EOS);
	## ...de dilli orru leamen earalágan.

SELECT A IF (-2 (N Sg Nom))(-1 ("leat" Sg3))(NEGATE 0 N OR PrfPrc OR Inf) (NEGATE 0 Ill LINK 1 PrfPrc)(NEGATE 0 Adv LINK 1 A); 
	## Sin váilevaš oahppu ja giellamáhttu lea guhkit áiggi leamaš
	## heađušteaddjin. 	

REMOVE (A Attr) IF (0 A)(1 COMMA)(NEGATE 2 A OR PrsPrc)(NEGATE 2 CC LINK 1 Attr);
	## Jos dat ii leat vejolaš, de mii fertet mannat.

REMOVE (A Attr) IF (0 A)(NEGATE 0 Indef)(NEGATE 1 A OR N OR Num OR Actio OR PrfPrc OR PrsPrc OR CC OR COMMA); 
	## Dat galgá čuovvoluvvot nu guhkás go vejolaš.

REMOVE (A Attr) IF (0 A-CASE OR Adv)(1 CC)(NEGATE *2 N BARRIER NP-BOUNDARY);
	## ...berre mearridanváldi leat nu viiddis go vejolaš ja nu
	## čadni go vejolaš.

REMOVE (A Gen) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)(1 Inf); 
	## Lea hávski oažžut reivve.

SELECT (A Sg Nom) IF (*-1 COPULAS BARRIER NOT-ADV LINK 0 V-SG3 LINK NOT *-1 Ine BARRIER N)(NEGATE -1 Gen)(NEGATE 0 Inf OR Attr) (NEGATE 0 Adv LINK *1 VERB BARRIER NOT-ADV)(NEGATE 1 PrfPrc) (NEGATE 1 CC LINK 1 Adv);
	## Buođđu lea dál gárvvis.
	## Dalle lea lunddolaš, ahte...

SELECT (A Pl Nom) IF (*-1 COPULAS BARRIER NOT-ADV-PCLE) (*-1 V-DU-PL BARRIER V-NOT-AUX LINK NOT *-1 Neg BARRIER NOT-ADV-PCLE);
	## Dán máilmmi mánát leat čeahpibut soabadit gaskaneaset.

SELECT (A Pl Nom) IF (1 (Pron Pl2))(*2 COPULAS BARRIER VERB) (*2 V-PL2 BARRIER NOT-ADV-PCLE);
	## Man čeahpit dii lehpet hilgut Ipmila báhkkomiid! 

SELECT (A Pl Nom) IF (-1 (N Pl Nom))(*-2 COPULAS BARRIER NOT-ADV-PCLE LINK 0 Pl3) (NEGATE 1 PrfPrc);
	## ...vai leago eanavuovdinorgánat geatnegasat luobahit eatnamiid...

SELECT (A Pl Nom) IF (*-1 ("orrut") BARRIER NOT-ADV-PCLE) (*-1 V-PL3 BARRIER SV-BOUNDARY2);
	## Riekteháhkama eavttut geavahusa njuolggadusaid mielde eai oro absoluhtat.

SELECT (A Pl Nom) IF (-2 (A Pl Nom) LINK *-1 COPULAS BARRIER NOT-ADV-PCLE)(-1 CC);
	## ...jus dat leat čuožžovaččat ja dárbbašlaččat.

REMOVE (A Pl Nom) IF (*-1 COPULAS BARRIER VERB)(*-1 V-SG3 BARRIER V-NOT-AUX) (*1 (A Sg NOM) BARRIER SV-BOUNDARY2 OR Nom);
	## No hits?

REMOVE A (-1 MOVEMENT-V)(0 (N Ill));

	# Coordination
	# ------------
SELECT (A Sg Nom) IF (-2 (A Sg Nom))(-1 CC OR COMMA)(1 CC OR COMMA)(2 (A Sg Nom));
SELECT (A Sg Nom) IF (-2 (A Sg Nom))(-1 CC)(1 CS OR EOS)(NEGATE 0 Inf LINK -2 Inf);
	## e. e. man bissovaš, dovddus ja bindil dat lea.
	## Vare livččet jogo galmmas dahje liekkas!

REMOVE (A Attr) IF (1 COMMA OR CC)(2 A)(NEGATE *1 N BARRIER NP-BOUNDARY);
	## ...man bissovaš, dovddus ja bindil dat lea.

SELECT (A Pl Nom) IF (-2 (A Pl Nom))(-1 CC OR COMMA)(1 CC OR COMMA)(2 (A Pl Nom));
	## Lehket álo ustitlaččat, vuollegaččat ja guhkesmielalaččat.

SELECT (A Pl Nom) IF (-2 (Pl Nom))(NEGATE -2 Adv)(-1 CC)(1C NP-BOUNDARY);
	## Ollu profehtat ja vanhurskásat leat áibbašan oaidnit dan.

SELECT (Pl Nom) IF (-2 (Pl Nom))(NEGATE -2 Adv OR Inf)(-1 CC)(1 CC OR CLB) (2 NP-BOUNDARY);
	## De bohte ollu tuollárat ja suttolaččat ja čohkkájedje beavddis. 

SELECT (Pl Nom) IF (1 CC)(2C (Pl Nom))(NEGATE 2 Interr);
	## Dušše inuihtat ja sápmelaččat sáhttet adnojuvvot Davviriikkaid
	## eamiálbmogin.

 
	# And now some rules for adverbs that modify adjectives
	# =====================================================

SELECT Adv IF (0 A)(NEGATE 0 Inf)(1C A)(NEGATE *1 N BARRIER NPNH);
	## ...jus ii leat čielgasit dárbbašmeahttun dahje geavatlaččat
	## veadjemeahttun.
	## ...ahte váikkuhusat divššohasa lagamuččaide leat mearkka-
	## šahtti stuorrát. 
	
SELECT Adv IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)(1 A);
	## Sámi riekteáddejupmi ii lean doarvái miellagiddevaš 
	## suokkarduvvot ovttas dainna gažaldagain.

SELECT Adv IF (-2C Adv)(-1 CC)(0 A);
	## Maŋŋá soađi lea sámi álbmot rahčan ollu vai dat adnojuvvošii
	## seammá dássásažžan, sihke duođalaččat ja rievttálaččat.

SELECT Adv IF (0 A)(1 CC)(2C Adv);
	## ...dan muddui go dat lea geavatlaččat ja ruđalaččat vejolaš.

# End of adjective copy from sme

SELECT Adv (0 ("man"))(1 A);	
	## Jiesstánam lijka man jalla la dån.


# Particles
# =========

# galla
# - - - 
SELECT Pcle IF (*-1 BOS BARRIER NOT-PCLE)(1 ("dal"));
    ## Galla dal mån diedáv manen li muson.


# Case
# ====

# Genitive
# ========

# Gen as modifier
REMOVE V (NOT *-1 VFIN BARRIER S-BOUNDARY2)(0 Gen LINK NEGATE 0 VFIN LINK *-1 NP-HEAD-NOM BARRIER NOT-ADV)(*1 (N Nom) BARRIER NPNH OR CC OR COMMA LINK *1 VFIN BARRIER V OR S-BOUNDARY2) ;
	## Giela árvo le bájnnám sámij giellamáhtov. 
#REMOVE Nom (NOT *-1 VFIN BARRIER S-BOUNDARY)(0 Gen)(*1 (N Nom) BARRIER NPNH OR CC OR COMMA LINK *1 VFIN BARRIER V OR S-BOUNDARY) ;
	

# Gen after Num

SELECT (Sg Gen) IF (*-1 Num BARRIER NPNH LINK 0 Gen OR Nom)(NEGATE 0 Po OR CC OR PxSg3 OR Ill);
	## Aktat ij buvte guokta härrä dievnnut.

SELECT (Sg Gen) IF (*-1 Num BARRIER NPNH LINK 0 Acc)(NEGATE 0 Po OR CC OR PxSg3);

# Gen in Spec
REMOVE (Pl Nom) IF (0 Prop)(*1 N BARRIER NOT-ADJ);

# Gen before Po

SELECT Gen IF (1C Po);
	## Profehta baktu.

SELECT Gen (0 POLITICAL-PLACE LINK 1 ("årro")) ; 
REMOVE Nom (0 (Pron Gen) LINK *-1 FMAINV BARRIER SV-BOUNDARY2 LINK 0 IV)(1 N) ;
	
# Coordination:	
SELECT Gen IF (1 (@CNP))(*2 Gen BARRIER NPNHAI LINK 1C Po);
    ## Gåktus dijá åhpadiddje buktá tullárij ja suddárij siegen bårråt?	


# Specific words
# ==============


# Adverbs

REMOVE Ess IF (0 Adv)(NEGATE 0 ("akta" Ess))(0 Ess)(NEGATE 0 ("mij" Foc) OR Ine);
    ## Árvvediv ilá tjoaskes lij ålggon vádtset.

SELECT Adv (0 ("maŋemussaj"));
# The unwanted analyses are
# maŋemussaj	maŋŋe+N+Superl+Sg+Ill
# maŋemussaj	maŋep+A+Superl+Sg+Ill


SELECT Adv IF (0  ("riegádim#rájes") OR ("álggo#rájes") OR ("garrasit")) ;

SELECT Adv (-1 VFIN)(0 ("ålgus"))(1 Sup OR Inf) ;

# aktan
SELECT Adv IF (NEGATE -1 VERB)(0 ("aktan"))(NEGATE 1 (N Ess));
    ## Valla åvddål gå aktan lijga viessogoahtám.
SELECT Ess IF (0 ("aktan"))(1 (N Ess));


SELECT Adv IF (0 ("åvddål"))(1 CS) ;
	## åvddål gå lij bárnes.
	
SELECT Adv IF (0 ADV-NOT-NOUN)(1 COPULAS);	
SELECT Adv IF (*-1 COPULAS BARRIER NOT-ADV-PCLE)(1 A);	
    ## Valla duodaj li bårre stálpe.	
    
# desik - dasik
SELECT CS IF (0 ("desik") OR ("dasik"))(*1 VFIN BARRIER S-BOUNDARY2);
    ## Åro sjávot desik lav tjielggim.    
    ## Ja de rájddiv dasik dudálattjan iehtjam dåbddiv.

# ietján
SELECT ("ietján")(1 ("gå") LINK *1 @COMP-CS< BARRIER SV-BOUNDARY2 OR CC) ;

# rájes

SELECT Po IF (0 ("rájes"))(*1 ("rádjáj" Po) BARRIER VERB);
	## Fáŋŋgavuoda rájes Kristusa rádjáj.
	
# sáddnán
SELECT Adv IF (-1 BOS OR PUNCT OR CLB)(0 ("sáddnán"))(1 COMMA OR Nom);
    ## Sáddnán, lágas ij bokstávva, ...
    ## Sáddnán sån ij bálkás masse.

# åvddåj
SELECT Po (-1 NP-HEAD + Gen)(0 ("åvddåj"));

# ållu
SELECT Adv IF (0 ("ållu"))(1 (A Superl)) ;
    ## Iv la gåjt mån ållu værámus.


# Proper nouns
# - - - - - - 
SELECT Mal OR Fem ((0 Nom LINK -1 ("riegádit")) OR (0 Ill LINK 1 ("riegádit") LINK 1 Nom)) ;
SELECT Mal OR Fem (0 Nom LINK -1 COPULAS LINK -1 HUMAN + Nom) ;

# Johannes
SELECT ("Johannes") IF (1 ("gástadiddje"));
	## Johannes gástadiddje

# Valla
REMOVE ("Valla") IF (-1 BOS OR PUNCT OR ("." CLB))(NEGATE 1 V-SG3 LINK 1 (Pron Pers Sg3 Nom));
	## Valla Jesus sunji javlaj:

# Nouns

# bigos
SELECT ("bigos" Ill) IF (*0 ("boahtet") OR ("sjaddat") OR ("buktet") BARRIER NOT-ADV);
SELECT V IF (0 ("boahtet"))(*0 ("bigos" Ill) BARRIER NOT-ADV);

# iellet vs. ællát
SELECT ("ællát")(-1 ("vaj"))(0 ("iellet") LINK 1 EOC);

# gástadiddje
SELECT ("gástadiddje") IF (-1 ("Johannes"));

# tjállát vs. tjállet
SELECT ("tjállet" PrfPrc) ;

# miella
SELECT Ela IF (-1 Gen)(0 ("miella" N Sg Ela));
	## Valla suv mielas la na.

# jages
SELECT Ela IF (0 ("jahke" N Sg Ela))(1 ("jahke" N Sg Ill));
	## Jages jahkáj.

# 


# boahtte
SELECT Attr (0 ("boahtte") LINK 1 N);


# bådij
# "båhte" N Pl Com  = "boahtet" V TV Ind Prt Sg3 = "båhte" N Pl Gen

# riegádij

SELECT (Ill) IF (1 ("riegádit" V)) ;
	## Serubbabelij riegádij Avihud.

# juoga
REMOVE Adv (0 ("juoga"));
	
# ============ #
# Noun or not? #
# ============ #

REMOVE V IF (-1C Attr)(0 N);
	## Álmmuk mij sjievnnejdin årru, le stuorra tjuovgav vuojnnám.
	
SELECT N IF (-1 Gen)(0 A)(1 CLB OR EOS);
    ## Gen iednne lij Uriasa áhkká?	



# ============ #
# Verb or not? #
# ============ #


# Ind Prt Sg3 vs N Pl Gen/Com

SELECT (Ind Sg3) IF (*-1 CC BARRIER NOT-ADV LINK -1 (Ind Sg3));
    ##Sálugin gudi rievtesferdukvuodav nälggu ja gåjkki. 
    ## Ja de sijáv Betlehemaj rájaj ja javlaj:

SELECT (Ind Sg3) IF (NEGATE -1 V)(1 CC LINK *1 (Ind Sg3) BARRIER NOT-ADV);
    ## Valla dáv ájádaládijn Härrá ieŋŋgil Josefij niegon bihkusij ja javlaj:
    ## Valla Johannes nádoj ja javlaj:

SELECT (N Pl Nom) IF (NEGATE *-1 (Pl Nom) BARRIER S-BOUNDARY2)(NOT 0 CC)(NEGATE 0 Gen LINK 1 Po)(*1 (Ind Pl3) BARRIER NP-BOUNDARY OR (Pl Nom) OR COMMA);
    ## Valla stádaj årro dav gullájin.


# ====================== #
# Adjectives and adverbs #
# ====================== #

REMOVE Attr IF (1C V OR Pr OR Po OR Adv OR A OR EOS);
REMOVE Attr IF (1 COMMA)(NEGATE 2 (A Attr));
	## li ållo sáme, gudi mánnán sáme ságastin.
	
REMOVE A (*-1 BOS OR S-BOUNDARY2 BARRIER VERB OR S-BOUNDARY2)(0 Adv)(1 VFIN + IV LINK NOT 0 AUX) ;	
    ## Gå Jesus duoppebuj váttsij, ....


# ========================== #
# Disambiguating clitics     #
# ========================== #
# ========================== #
# Disambiguating numerals    #
# ========================== #

REMOVE Acc IF (NEGATE *-1 V-TRANS-ACT-NOT-ACT BARRIER SV-BOUNDARY2 OR Acc)(0 Num)(1 (Sg Gen))(NEGATE *1 V-TRANS-ACT-NOT-ACT BARRIER SV-BOUNDARY2 OR Acc);
    ## Gå Jesus dåppelt vuolgij, de guokta tjalmedime suv tjuorvo tjuovvolijga:

SELECT (Num Ine) IF (*1C (N Ine) BARRIER NOT-ADJ);
    ## Gålmån biejven mån manniv dåhku.
    
SELECT (Num Ess) IF (*1C (N Ess) BARRIER NOT-ADJ);  
    ## gålmån guolen # It´s not in Spiik.

SELECT (Num Gen) IF (*1 (N Ill) BARRIER NOT-ADJ);  
    ## gålmå guolláj

SELECT (Num Com) IF (*1 (N Com) BARRIER NOT-ADJ);  
    ## gålmåjn guolijn
  

# ========================== #
# Disambiguating adpositions #
# ========================== #

SELECT Gen (1 ("nubbe") LINK 1 ("bielle"));
SELECT ("nubbe" Indef Gen) (1 ("bielle"));
REMOVE Po (0 ("bielle") LINK -1 ("nubbe")) ;
REMOVE Gen (0 ("bielle") LINK -1 ("nubbe"))(NOT 1 N) ;
SELECT ("bielle" Ill) (0 Com LINK -1 ("nubbe") LINK -1 PLACE) ;

SELECT Po IF (-1 Gen)(NEGATE *1 Gen BARRIER Attr OR CLB OR V OR CC OR CS);
    ## Nåv li buolva Abrahama fájes Davida rádjáj.

SELECT Po IF (NEGATE *-2 Pr BARRIER N OR SV-BOUNDARY)(-1C Gen)(NEGATE *1C Gen BARRIER S-BOUNDARY2);

    

	# ============== #
      SECTION    # 
	# ============== #

	
	
# ====================== #
# Disambiguating adverbs #
# ====================== #
# ====================== #


REMOVE Po IF (0 Adv)(NEGATE -1 Gen);
    ## Ja vádtse álmmuk suv åvdålt maŋelt tjuorvoj:
    
SELECT Adv IF (-1 V OR (@CVP) OR BOS)(0 ("aktan"))(1 Com);    
    ## ..viellidin aktan sujna ja åhpadisålmmåj. 
    ## Jesus de tjuodtjelij ja aktan åhpadisålmmåj suv tjuovvolij.

SELECT Adv (*-1 VFIN BARRIER VERB)(1 Inf) ;
	## Allit jáhke máhttebihtit allasihte dåssjå javllat: Mijá áhttje le ham Abraham.

SELECT Adv (-1 Neg)(*1 ConNeg BARRIER S-BOUNDARY2) ;
	## Almasj ij dåssjå lájbes vieso, ájnat juohkka bágos mij Jubmela njálmes boahtá.

SELECT Adv (0 ("miellusit") OR ("nievres#láhkáj") OR ("ieme#láhkáj") OR ("rievtes#láhkáj") OR ("alvos#láhkáj") OR ("vuogas#láhkáj") OR ("valjes#láhkáj") OR ("árvvogis#láhkáj") OR ("oanegis#láhkáj") OR ("mælggadis")) ;

# ======================= #
# Disambiguating pronouns #
# ======================= #


# Personal pronouns

# Pl1
SELECT (Pron Pers Pl1) (*1 VFIN + Pl1 BARRIER NOT-ADV-PCLE) ; 
SELECT (Pron Pers Pl1) (1 Interj LINK 1* VFIN + Pl1 BARRIER NOT-ADV-PCLE) ; 

# Sg2
SELECT (Pron Pers Sg2 Nom) IF (*-1 (V Sg2) BARRIER NOT-ADV-PCLE);
	## Le gus dån oahpes dán bájkkáj? 
	## Man huohppelasj la dån Tore, javlaj Ámmá munji. 

SELECT (Sg2) IF (0 (Pron Pers))(*1 (V Sg2) BARRIER N);
	## Ja dån dal muv luse boadá.

SELECT (Pron Interr) IF (0 (Pl1 Nom) LINK -1 S-BOUNDARY2 OR BOS)(1 (N Pl Acc));
    ## Mij dahkamusájt le dujna munnujn?
    
REMOVE Adv (-1 ("oasse"))(0 (Pron Ela)) ;    

# Genitive Sg = Accusative Sg for personal pronouns.
# Accusative vs. Genitive
# =======================

# Sg Acc = Prs Sg1
SELECT Acc (*-1 S-BOUNDARY2 BARRIER NPNH)(0 Sg1 LINK 1 VFIN LINK 0 V-TRANS-ACT-NOT-ACT LINK NOT *1 Acc BARRIER S-BOUNDARY2 OR COMMA);

# Accusative is unique (-v), with one notable exception:
# Accusative = genitive for personal pronouns (big issue)

SELECT Gen IF (*-1 BOS OR CLB BARRIER NOT-ADV)(0 (Pron Pers))(*1 N BARRIER NOT-ADJ);
    ## Muv niejdda le jur jábmám.
    
SELECT Gen IF (NEGATE *-1 V-TRANS-ACT-NOT-ACT BARRIER S-BOUNDARY2)(0 (Pron Pers))(1 Po OR N LINK NOT 0 Prop OR V-TRANS-ACT-NOT-ACT)(NEGATE 1 Ess);
SELECT Gen IF (NEGATE *-1 V-TRANS-ACT-NOT-ACT BARRIER S-BOUNDARY2)(0 (Pron Pers))(1 A LINK *1 N BARRIER NPNH);
    ## Go sijddasis jåvsådij, di bådijga tjalmedime suv åvddåj.     

SELECT Acc IF (-1 ("liehket"))(0 (Pron Pers))(1 V-TRANS-ACT-NOT-ACT);
    ## , ja boahtám lip suv guddnedittjat.

SELECT Acc IF (-1 V-MOOD)(0 (Pron Pers))(1 V-TRANS-ACT-NOT-ACT + Inf);
    ## , ittjij sidá suv hehpaduhttet.

SELECT Acc IF (-1 Neg)(0 (Pron Pers))(1 V-TRANS-ACT-NOT-ACT);
   ## Valla ittjij suv duohtada åvddål gå Maria lij bárnes giegádahttám.

REMOVE Gen IF (*-1 OPREDV BARRIER NPNH LINK NOT 0 N LINK NOT *-1 NP-HEAD-ACC BARRIER SV-BOUNDARY2)(1 Ess);
	## Sån gåhttju suv Jesusin.

REMOVE Gen IF (1 Ess)(2 OPREDV);
    ## Dån galga suv Jesusin gåhttjot.

REMOVE (Pers Acc) IF (*1C (N Acc) BARRIER S-BOUNDARY2 OR Po OR Actio OR ("degu") OR COMMA LINK NOT 0 TIME);
    ## Vuojnijma suv nástev ihtemin.

SELECT (Pers Acc) IF (-1 V-TRANS-ACT-NOT-ACT LINK *-1 BOS OR PUNCT OR CLB)(1 CLB);
    ## ”Tjuovvolav duv, Härrá, ...

SELECT Acc IF (0 Pers OR Refl)(*0 V-TRANS-ACT-NOT-ACT BARRIER SV-BOUNDARY2 LINK NOT 0 N);
    ## Ja de rájddiv dasik dudálattjan iehtjam dåbddiv.  

# =================================== #
# Reflexive pronouns #
# =================================== #
 
SELECT (Refl Pl Nom)(-1 Pl2 OR Pl1 OR Pl3);    
SELECT (Refl Pl Nom)(1 (V Pl2) OR (V Pl1) OR (V Pl3));    
SELECT (Refl PxPl2)(-1 Pl2);  

SELECT (V Pl3)(-1 (Refl PxPl3));  

# =================================== #
# Interrogative and relative pronouns #
# =================================== #

# Interrogative

SELECT Interr IF (*-1 BOS OR (":") BARRIER NOT-PCLE)(0 Rel)(*1 ("?"));
SELECT Interr IF (-1 CC LINK *-1C Interr)(0 Rel)(*1 ("?"));
    ## - Na majt hærrájt sjokoládajn?
    ## "Guhtis le muv ieddne ja gudi li muv vielja?"

# Relative

SELECT Rel IF (0 Interr)(NEGATE 0 Pers OR Adv);
    ## Ittjij rat gulá majt javlliv.        
    ## Valla usjudalliv manen oastestiddje lij nav imálasj láhkáj munji gæhttjam gå sjokoládav ádnuv. 
    ## Guhti ádnu oadtju, ja guhti áhtsá gávnna, ja guhti goalkkal, sunji uksa rabáduvvá. 

SELECT Gen IF (0 Rel)(*1 N BARRIER NOT-ADJ LINK *1 V-SG3);

# Rel Sg Nom = Pers Pl1 Nom

SELECT (Pron Rel Sg Nom) IF (*-1 (N Sg) OR Indef)(NEGATE *1 V-PL1);

SELECT (Pron Rel Sg Nom) IF (*-1 COMMA OR CLB BARRIER NOT-PUNCT-RIGHT LINK -1 (N Sg))(NEGATE *1 V-PL1);

SELECT Rel (-1 Dem OR N)(0 Nom)(1 V-3);


# ====================== #
# Demonstrative pronouns
# ====================== #

REMOVE Par IF (0 (Dem Sg Nom))(1 V-SG3);
    ## Divna dát sjattaj vaj lulu ålliduvvat mav Härrá lij profehta baktu javllam.
    



# Indefinite pronouns

# Number disambiguation

SELECT Sg IF (0 (Pron Indef Sg Nom))(*1 (N Sg Nom) BARRIER NPNH);
SELECT Sg IF (0 (Pron Indef Sg Gen))(*1 (N Sg Gen) BARRIER NPNH);
SELECT Sg IF (0 (Pron Indef Sg Acc))(*1 (N Sg Acc) BARRIER NPNH);
SELECT Sg IF (0 (Pron Indef Sg Ill))(*1 (N Sg Ill) BARRIER NPNH);
SELECT Sg IF (0 (Pron Indef Sg Ine))(*1 (N Sg Ine) BARRIER NPNH);
SELECT Sg IF (0 (Pron Indef Sg Com))(*1 (N Sg Com) BARRIER NPNH);
SELECT Sg IF (0 (Pron Indef Sg Ela))(*1 (N Sg Ela) BARRIER NPNH);
SELECT Pl IF (0 (Pron Indef Pl Nom))(*1 (N Pl Nom) BARRIER NPNH);
SELECT Pl IF (0 (Pron Indef Pl Gen))(*1 (N Pl Gen) BARRIER NPNH);
SELECT Pl IF (0 (Pron Indef Pl Acc))(*1 (N Pl Acc) BARRIER NPNH);
SELECT Pl IF (0 (Pron Indef Pl Ill))(*1 (N Pl Ill) BARRIER NPNH);
SELECT Pl IF (0 (Pron Indef Pl Ine))(*1 (N Pl Ine) BARRIER NPNH);
SELECT Pl IF (0 (Pron Indef Pl Com))(*1 (N Pl Com) BARRIER NPNH);
SELECT Pl IF (0 (Pron Indef Pl Ela))(*1 (N Pl Ela) BARRIER NPNH);

# Case disambiguation

SELECT Nom IF (0 (Pron Indef Sg Nom))(*1C (N Sg Nom) BARRIER NPNH);
SELECT Gen IF (0 (Pron Indef Sg Gen))(*1C (N Sg Gen) BARRIER NPNH);
SELECT Acc IF (0 (Pron Indef Sg Acc))(*1C (N Sg Acc) BARRIER NPNH);
# unfinished, return to this.

#Sme:
#SELECT (Pron Indef Gen) IF (0 ("nubbi")) (1 ("dáfus" Po) OR ("beali" Po) OR ("hávvi") OR ("beallái" Po) OR ("bealli") OR ("sohka#bealli"));

SELECT (Pron Indef Gen) IF (0 ("nubbe")) (1 ("bieles"));
    ## De gåhtjoj åhpadisålmmåjt sujna jávre nuppe bälláj vuolgget.
    # More indef-pronouns should be added to this.

SELECT (Pron Attr) IF (0C Pron)(*1C N BARRIER NOT-ADJ-PCLE OR ("gå"));
    	## Dan muitalii muhtin mátkkošteaddji gean namma lei Keilhau.
    	## Njuolggadusain ledje eará ge mearrádusat.
    	## Usjudallagåhtiv makkár sjokoládav dal oasstet.

SELECT Indef (0 ("divna"))(1 Num) ;

# =========== #
# Numerals
# =========== #
SELECT Acc (0 Num LINK 1 (N Sg Gen))(*-0 V-TRANS-ACT-NOT-ACT BARRIER SV-BOUNDARY2 LINK 0 FMAINV) ;

    ## Aktat ij buvte guokta härrá dievnnut.


# ========================= #
# Disambiguating adjectives #
# ========================= #
# ========================= #
# Rules related to specific adjectives
# ====================================

# aluk vs. allat Comp Attr
# - - - - - - - - - - - - 
#LIST ALLAT = "boazo#lohku" "dáidda#oahpahus" "dássi" "divišuvdna" "dutkan" "eallin#dássi" "gealdda" "gráda"  "ieš#mássa" "julev#sáme#giel#oahppu"  "lohku" "oahppa"  "oahppo#lágádus" "oahppu" "oahpahus"  "oahppo#ásahus" "oahppo#dássi" "oahppo#institušuvdna"  "oahppo#fálaldat" "oahppat" "skuvla" "duodje#oahppu" "sápmi" "lávda#oahppo#fálaldat" "virgi" ;
#
#
#SELECT:r1761 ("allat") IF (0 ("alit"))(1 ALLAT OR MONEY OR EDUCATION OR ("gus"));
#SELECT:r1762 ("allat") IF (-3 ("badjel"))(-2 Num)(-1 MEASURE)(0 ("alit"));
#SELECT:r1763 ("allat") IF (0 ("alit"))(1 CC)(2 ("gaska#dássi"));
#	
#SELECT:r1764 ("alit") IF (0 ("allat"))(1 COMMA OR (@CNP) OR PUNCT)(2 COLOUR);
#SELECT:r1765 ("alit") IF (0 ("allat"))(-1 COMMA OR (@CNP) OR PUNCT)(-2 COLOUR);	
#SELECT:r1766 ("alit") IF (0 ("allat"))(1 ("Volvo") OR ("biila") OR ("čalbmi") OR ("dielku") OR ("russa") OR ("čuovga") OR ("albmi") OR ("ivdni") OR ("fearga") OR CLOTHES OR BEDCLOTHES OR BUILDING);
#
#SELECT:r3573 ("alit") IF (-1 go OR Qst)(0 ("allat" Nom))(1 EOS);

LIST ALLAT-NOUN = "Artemisa" "apostel" "ámmát#ålmåj" "árvvo" "hærrá" "jalggadis" "jubmel" "máddo" "muvrra" "ráde" "tråvnnå" "várre" ;

SELECT ("allak" Attr) (1 ALLAT-NOUN) ;
SELECT ("allak") IF (0 Der/vuohta)(1 ("tråvnnå")) ;

REMOVE A (0 ("áhkká") LINK *0 ("válldet")) ;

# ============================= #
# Disambiguating verbs - part 1 #
# ============================= #
# ============================= #
# In this cycle we apply some safe verb rules.
# ConNeg forms
# ------------
# Need more advanced rule to select ConNeg when there is more
# material between Neg and ConNeg.
# No Neg, no ConNeg.
REMOVE ConNeg IF (*-1 BOC2 LINK *1 EOC2 BARRIER Neg);



# Indicative
# ----------
SELECT (V Sg2) (NOT *-1 NP-HEAD-NOM OR Ine)(*1 @CVP BARRIER NP-HEAD-NOM OR S-BOUNDARY2 LINK *1 (V Sg2) BARRIER VERB) ;
    ## Jus värroálltárij le vattáldagáv guoddemin ja dalloj huomaha vieljanat le juoga duv vuosstij , de álltára åvddåj vattáldagát guode ja maná suv lusi såbadittjat.

# Imperative
# ----------

    #  V TV Ind ConNeg=V TV Imprt ConNeg=V TV VGen=V TV Imprt Sg2
    # = V TV Ind Prs Sg2
    
SELECT Imprt (-1 BOS)(0 ("gæhttjat")) ;
REMOVE Imprt (-1 VFIN) ;


SELECT (Imprt Sg2) IF (*-1 BOS OR (":") BARRIER WORD)(NEGATE 0 ("duohta") LINK 1 COMMA)(NOT *1 (Pers Sg2) BARRIER SV-BOUNDARY2); 
    ## Åro sjávot desik lav tjielggim.

SELECT (Imprt Sg2) IF (*1 Prop BARRIER WORD LINK 0 Mal OR Fem LINK 1 COMMA);

LIST IMPRTV = "suovvat" ;

SELECT (Imprt Sg2) IF (0 IMPRTV LINK NOT *-1 (Sg2 Nom));
    ## Tjuovvolav duv, Härrá, valla suova muv vuostak vuolgget fuolkám hivástahtátjit. 

SELECT Imprt IF (*-1 BOS OR (":") BARRIER WORD)(*1 ("excl") BARRIER VFIN); 
	## Ja ålmmåj javlaj: ”Gálggi giedat!”

REMOVE (Imprt ConNeg) IF (*-1 Neg LINK NOT 0 Imprt);
	## ... beassat juska ij nav vuojga duodjuhit máhte, ...
 
    # V TV Ind ConNeg = V TV Imprt ConNeg = V TV VGen = V TV Imprt Sg2


    # V Imprt Du1
REMOVE (Imprt Du1) IF (-1 Ind) ;
	## Duodje l vuodon ådå ájádusájda.

REMOVE (Imprt Du1) IF (-2 Pl3)(-1 CC)(0 Pl3);
	## Ålggolij ganugin ja gåhttjun suv boahtet.

    
    # V* TV Actor N Sg Gen = V TV Imprt Du2 = V* TV Actor N Pl Nom
    #= V* TV Actor N Sg Nom = V* TV Actor A Attr
  
    # V TV Inf = V TV Imprt Pl2

REMOVE (Imprt Pl2) IF (*-1 VFIN BARRIER S-BOUNDARY2)(0 Inf);
	## Jáhkáv barggoiellemin sjaddá vuojnnut.

  
    # V TV Ind ConNeg = V TV Imprt ConNeg
 
    # V* TV Pass V VGen = V* TV Pass V Ind Prs Sg3 = V* TV Pass V Ind ConNeg
    #= V* TV Pass V Imprt Sg2 = V* TV Pass V Ind Prs Sg2 = V TV Ind Prs Sg3

REMOVE Imprt IF (0C Der/Pass);
    ## Valla sunji ij goassak ándagi luojteduvá, guhti ájlis Vuoiô ëô ëanisáv álbet, ájnat siválattjan ihkeven sudduj bissu.
    
REMOVE Imprt IF (*-1 CS OR CP BARRIER S-BOUNDARY2 LINK NOT 0 ("de"));
    ## Juohkka dåssjedis bágo åvdås, mav almasj tsuvkká, hähttu duobbmobiejven vásstedit.

REMOVE Imprt IF (*-1C (Neg Ind) BARRIER CLB OR V-IND-FIN);
    #Does not work yet but might work later
    
REMOVE Imprt IF (*-1 (Neg ImprtII) BARRIER CLB OR V-IND-FIN);
    
REMOVE Imprt IF (*-1C ConNeg BARRIER VFIN OR COMMA OR CS OR CP);
	## I galga Härráv, Jubmelat, gähttjalit. 



REMOVE Imprt IF (*1C VFIN BARRIER NOT-ADV-PCLE OR CLB);
	## Tjalmme le rubmaha tjuovgga.
	## Dåppe buktin suv lusi gállnam ålmmåv guoddemlqtjo nanna vällahime.

SELECT Imprt IF (-1 BOS OR PUNCT OR REALCLB)(0 ("liehket"))(1 A);


# Pl3
REMOVE (Imprt Pl3) IF (-1 Gen)(0 N);
	## Joarkkaskåvlå åhpadusa.

SELECT Imprt (0 Ind)(1 ("dal"));

#REMOVE Imprt IF (-1 N OR A OR Po OR Num);
	#Imperative after N is possible 
SELECT (Imprt Sg2) (*-1C (Imprt Sg2) BARRIER VFIN)(0 Ind);
REMOVE (Imprt Sg2) (*-1 VFIN LINK NOT 0C (Imprt Sg2))(0 Ind)(NOT 1 ("dal"));


# Imperative II, ImprtII
# ----------------------

REMOVE (ImprtII Du1) IF (*-1 (Pl Nom))(0 (V Pl3));


# Infinitive
# ----------
# Inf or Imprt Pl2 or Der of different sorts.

SELECT Inf IF (*-1 INFV);#BARRIER

# Infinitive in coordination

SELECT Inf IF (1 CC)(2C Inf);

SELECT Inf IF (-1 CC)(-2C Inf);

# Gerund II
# ---------
# Gerund II = Actio N Ess

SELECT GerII (*0 AUX OR GerII-V BARRIER SV-BOUNDARY2 OR VERB)(0 Actio);
    ## Viehka dåbelin lij stuorra svijnneällo guohtomin.
    ## Vuojnijma suv nástev ihtemin.
    # Select because it´s the only way to get rid of (Actio Ine) when the word is lexicalized. If it makes trouble, we have to make a rule for PLACE and other possible Ine. 

SELECT GerII (0 Actio OR Ess LINK *-1 BOS BARRIER WORD)(*1 COMMA BARRIER VERB OR S-BOUNDARY2 LINK 1 Nom);


# ==================== #
# Disambiguating nouns #
# ==================== #
       SECTION     #
# ==================== #

# Special nouns:
REMOVE ("giellat") (0 N LINK -1 Inf OR VFIN OR Gen OR Sup) ;	
REMOVE ("giellat") (-1 CC LINK -1 LANGUAGE) ;	
REMOVE ("giellat") (1 CC LINK 1 LANGUAGE) ;	


#POS homonymy:
# Pron Interr = Pron Rel
# Adv Po
# N Po

REMOVE Pr IF (0 Po OR Adv)(*1 EOS OR CLB OR VFIN BARRIER (N Gen) OR (Pron Gen));
    ## Almerijkka le lahka.

REMOVE Po IF (0 Pr OR Adv)(*-1 BOS OR CLB OR VFIN BARRIER (N Gen) OR (Pron Gen));
    ## Almerijkka le lahka.

SELECT Po (-1 (N Gen))(0 ("rádje") LINK 0 Ela OR Ill) ;

# ============ #
# Proper nouns #
# ============ #

SET NOT-ABBR = WORD - ABBR ;

# Prop or not
# -----------

# Capitalised nouns in the middle of a sentence are names.
SELECT Prop IF (NEGATE *-1 BOS OR (":") OR (PUNCT LEFT) OR (Num Nom) BARRIER REALWORD);

# Prop Sg Nom or Gen Sg or Pl Nom
SELECT (N Prop Sg Nom) IF (-1 CS OR BOS)(*1 V-SG3 BARRIER NOT-AAPCLE);
    ## Maria lij bárnes riegádahttám.

SELECT HUM-PROP (*-1 BOS OR (":") BARRIER WORD)(0 A)(1 COMMA LINK NOT *1 A BARRIER NOT-ADV) ;

# Prop Sg Gen or Pl Nom
#REMOVE (N Prop Sg Nom) IF (-1 BOS)(1 N)(2 (V Sg3));
	## Nordlánda fulkkasuohkanin gullu gålmmå sámegiela.
# This must be too strong...

# Which Prop
# ----------

# Johannes or Johanne
SELECT ("Johannes" Nom) (NOT *-1 Nom BARRIER S-BOUNDARY2)(0 ("Johanne"))(*1 Sg3 BARRIER Nom OR S-BOUNDARY2 LINK NOT 0 ("liehket")) ;

SELECT (Prop Nom) (-1 ("riegádit") LINK -1 (Prop Ill)) ;
SELECT (Prop Nom) (1 COMMA LINK *1 Nom BARRIER NPNH LINK 1 COMMA LINK 1 VFIN) ;
SELECT (Prop Nom) (-1 ("liehket") LINK -1 ("namma"));
SELECT (Prop Ela) (-1 ("liehket") LINK -1 Mal OR Fem)(0 Nom);
SELECT (Prop Plc Ela) (1 CC LINK 1 (Prop Plc Ela) LINK 1 VFIN) ;
SELECT (Prop Mal) (-1 ("ieŋŋgil"))(0 ("Gabriel")) ;


# Prop Plc or not
# - - - - - - - -
REMOVE Plc IF (*1 ("guhti") OR ("bárnne") BARRIER WORD);  
REMOVE Plc IF (-1 ("jubmel") OR ("allak"))(0 Mal OR Fem);  


# Attr
# ----
IFF Attr IF (0 (Prop Mal Nom) OR (Prop Fem Nom))(*1 (N Prop Sur) OR (N Prop Fem) OR (N Prop Mal) BARRIER NOT-ABBR); # (NEGATE 0 (@OBJ) LINK *1 (@OPRED) BARRIER NOT-NP); 
#REMOVE Attr IF (0 (Prop Mal) OR (Prop Fem))(0 Nom)(NEGATE *1 (N Prop Sur) OR (N Prop Fem) OR (N Prop Mal) BARRIER NOT-ABBR); # (NEGATE 0 (@OBJ) LINK *1 (@OPRED) BARRIER NOT-NP); 

REMOVE (A Attr) (0 ("duolla" Adv) LINK *-1 ("liehket") BARRIER NOT-PCLE)(1 Gen LINK 1 N) ;

# Accusative or genitive
# ----------------------

#SELECT Acc IF (*-1 V-TRANS-ACT-NOT-ACT BARRIER SV-BOUNDARY2 OR Acc LINK NOT 0 ELAV LINK NOT *-1 Acc BARRIER SV-BOUNDARY2)(0 (N Gen) OR (N Nom) LINK NOT 0 PrfPrc)(NEGATE 1 (N Acc));
SELECT Acc IF (0 (N Gen) LINK NOT 0 V)(*1 V-TRANS-ACT-NOT-ACT BARRIER SV-BOUNDARY2 OR COMMA LINK NOT *1 Acc BARRIER SV-BOUNDARY2);
#SELECT Acc IF (0 (N Gen) OR (N Acc))(*1 V-TRANS-ACT BARRIER SV-BOUNDARY2 LINK 0 (Actio Ela) LINK NOT *1 Acc BARRIER SV-BOUNDARY2);
#    ## Ale balá Mariav áhkkán válldemis.

SELECT Gen (-1 Indef OR Adv OR V-TRANS-ACT-NOT-ACT)(0 Nom LINK *1C Acc BARRIER NPNH) ;
SELECT Gen (-1 Indef OR Adv OR V-TRANS-ACT-NOT-ACT)(0 Nom LINK 1 CC LINK 1 Gen LINK *1 Acc BARRIER NPNH) ;
    ## Áhttje , hievedav alme ja ednama härráv.

#SELECT Gen (-1 COMMA LINK -1C Acc)(0 Nom LINK 1 CC LINK 1 Gen LINK *1 Acc BARRIER NPNH) ;
SELECT Gen (0 Nom LINK 1 (N Acc) LINK 1 COMMA OR EOC);
SELECT Gen (1 @CNP LINK 1C Gen)(0 Nom);
    ## Áhttje , hievedav duv , alme ja ednama härráv.

#SELECT Gen (*-1 Imprt BARRIER SV-BOUNDARY2)(0 Nom LINK *1C Acc BARRIER NPNH LINK 1 V-TRANS-ACT + Sup) ;

# Verb mappings - taken from sme-dis.rle. Those which have a #, have to be considered..
# -------------

MAP (@+FAUXV) TARGET Neg; #XX

MAP (@+FMAINV) TARGET VFIN IF (0 ("årrot") LINK NOT *1 GerII BARRIER NOT-ADV) ; #XX
        ## Dåppe åroj dasik Herodes lij jábmám vaj luluj ålliduvvat.

MAP (@-FAUXV) TARGET AUX IF (*-1 Neg BARRIER S-BOUNDARY2 OR VERB)(0 ConNeg OR PrfPrc)(*1 Inf OR Sup BARRIER VERB OR S-BOUNDARY2); #XX
    ## .., iv ga le árvvogis sujsta gábmagijt nuolatjit,..
 
MAP (@+FAUXV) TARGET AUX (0 VFIN LINK NOT 0 ("liehket"))(*1 Inf OR Sup OR ConNeg OR PrfPrc BARRIER S-BOUNDARY2 OR VFIN); #XX
MAP (@+FAUXV) TARGET ("liehket") (0 VFIN)(*1 PrfPrc OR GerII BARRIER S-BOUNDARY2 OR VFIN); #XX
    ## ... , ma uddni li tsäggot ja idet vuobnnáj bálkestuvvi, 

MAP (@+FAUXV) TARGET ("liehket" Cond) (0 VFIN)(*1 Inf BARRIER S-BOUNDARY2 OR VFIN); #XX


MAP (@+FAUXV) TARGET COPULAS IF (0 VFIN LINK NOT 0 ("liehket"))(*1 Inf OR Sup OR PrfPrc OR GerII BARRIER S-BOUNDARY2 OR VFIN);	#XX
    ## Härrá ieŋŋgil lij gåhttjum.
    
MAP (@+FAUXV) TARGET COPULAS IF (*-1 Inf OR Sup OR PrfPrc OR GerII BARRIER NOT-ADV-PCLE)(0 VFIN); #XX
    ## Ja boahtám lip suv guddnedittjat.
	    
MAP (@-FAUXV) TARGET AUX IF (0 PrfPrc)(*1 Inf OR Sup OR GerII BARRIER S-BOUNDARY2);	#XX
   ## In leat sáhttán gáhku borrat dan rájes. 
   ## In leat leamaš mielde gáhku láibume ovdal.    
        
MAP (@-FMAINV) TARGET ConNeg ; #XX
    ## Ittjij sidá suv hehpaduhttet.

MAP (@+FMAINV) TARGET COPULAS OR AUX (NEGATE *-1 Inf OR Sup OR PrfPrc OR GerII BARRIER S-BOUNDARY2 OR VFIN OR COMMA)(0 VFIN)(NEGATE 0 Actor)(NEGATE *1 Inf OR Sup OR PrfPrc OR GerII BARRIER S-BOUNDARY2 OR VFIN OR DUOLLA OR Acc); #XX
    ## ... , gej iessne lij Tamar.

MAP (@+FMAINV) TARGET ("liehket") (*-1 (A Nom) BARRIER NOT-ADV)(0 VFIN)(*1 Inf BARRIER NOT-ADV); #XX


MAP (@+FMAINV) TARGET VFIN (*-1 ("javllat") BARRIER S-BOUNDARY2 OR VFIN OR COMMA)(NEGATE 0 Actor)(NEGATE *1 Inf OR Sup OR PrfPrc OR GerII BARRIER S-BOUNDARY2 OR VFIN OR DUOLLA); #XX
    ## Sälggagärmmahatjivga , guhtis le javllam dij boahtte moares bessabihtit ?

MAP (@+FMAINV) TARGET VFIN-NOT-AUX ; #XX
    ## Peresij riegádij Hesron.

MAP (@-FMAINV) TARGET PrfPrc IF (*-1 ("liehket") BARRIER PrfPrc OR GerII)(NEGATE 0 AUX LINK *1 Inf OR Sup BARRIER S-BOUNDARY2)(1 NP-BOUNDARY); #XX
    ## Maria lij ájlis Vuojŋŋanisás sahkanam.
    ## Ieddnes Maria lij Josefijn giláduvvam.
    
MAP (@-FMAINV) TARGET PrfPrc IF (*-1 COPULAS OR Neg BARRIER S-BOUNDARY2 LINK 0 VFIN)(NEGATE *-1C PrfPrc BARRIER VERB OR N); #XX
	## Ii dal láhppán gal leat.
	## Ii son liikon dohko.
	## Ii okta ge geahččalan eará sivaid ohcat.
    
MAP (@-FMAINV) TARGET PrfPrc IF (*1 COPULAS BARRIER NOT-ADV-PCLE LINK 0 VFIN); #XX    
    ## Ja boahtám lip suv guddnedittjat.

MAP (@A<) TARGET Inf (*-1 DUOLLA + A BARRIER VERB OR S-BOUNDARY); #XX

MAP (@<SUBJ) TARGET Inf (*-1 COPULAS BARRIER NOT-ADV-PCLE LINK -1 (A Nom)); 

    
MAP (@-FMAINV) TARGET Inf; #XX
    ## Ittjij sidá suv hehpaduhttet.

MAP (@-FMAINV) TARGET Sup (*-1 AUX BARRIER VFIN); #XX
    ## Ja boahtám lip suv guddnedittjat.

MAP (@<ADVL) TARGET Sup (NOT *-1 AUX BARRIER VFIN); #XX



MAP (@-FMAINV) TARGET GerII;  #XX

MAP (@<ADVL) TARGET VAbess (*-1 VFIN BARRIER S-BOUNDARY2) ; #XX
MAP (@ADVL>) TARGET VAbess (*1 VFIN BARRIER S-BOUNDARY2) ; #XX
	## Mus báhce moadde girjji logakeahttá.

MAP (@<ADVL) TARGET VGen (*-1 VFIN BARRIER S-BOUNDARY2) ; #XX
MAP (@ADVL>) TARGET VGen (*1 VFIN BARRIER S-BOUNDARY2) ; #XX
	## Máhtte manai čuoigga skuvlii.

MAP (@<ADVL) TARGET Ger OR (V Actio Ela) (*-1 FMAINV BARRIER S-BOUNDARY2) ;
MAP (@ADVL>) TARGET Ger OR (V Actio Ela) (*1 FMAINV BARRIER S-BOUNDARY2);
	## Biigá lei bassaladdame báhpa boađedettiin.
MAP (@-FMAINV) TARGET PrfPrc ; #XX

MAP (@>N) TARGET Actio (1 N) ;



MAP (@X) TARGET VERB ; #XX



# Nouns versus adjectives
# =======================
# Nouns vs. verbs
# ======================


# ===== #
# Nouns #
# ===== #


# Case disambiguation
# ===================

# barrier
SET COOR-BOUNDARY = S-BOUNDARY2 OR HNOUN OR VERB ;

# Nominative
# ==========

# Nom Sg = Gen Sg if no CG
# Nom Pl = Gen Sg systematically, cf. under Genitive below.

#REMOVE Gen IF (1 COMMA OR CC)(NEGATE 2 Gen);

# Nominative
# ==========

REMOVE Gen IF (-1 (Pron Pers Nom) OR Imprt)(0 (Prop Nom) LINK 0 Mal OR Fem)(1 CLB);
    ## Man huohppelasj la dån Tore, javlaj Ámmá munji. 
    ## - Gulá Tore, tjielggij.

REMOVE Gen IF (-1 BOS OR PUNCT OR CLB)(1 COMMA);
    ## Härrá, suova muv vuolgget vuostak áhtjám hávddádittjat.

SELECT (Sg Nom) IF (NEGATE *-1 Sg3 BARRIER SV-BOUNDARY2 OR Nom)(0 HUMAN)(1 (V Sg3));
    ## Fáŋŋgagoaden Johannes guláj Kristusa dagoj birra.
    ## Johannes bådij ja sån ittjij bårå.

REMOVE (N Pl Nom) IF (*-1 BOS OR CLB OR CS BARRIER NPNH)(0 (N Sg Nom))(1 (V Sg3)) ;
REMOVE (N Sg Gen) IF (*-1 BOS OR CLB OR CS BARRIER NPNH)(0 (N Sg Nom))(1 (V Sg3)) ;
	## gå profehta le náv tjállám.
    
SELECT Pl IF (0 SG-WORD)(-1 (Pron Gen) LINK -1 Num OR Ord BARRIER NPNH);
   	## Dát lea okta min ovttasbargguin.
    # This rule is necessary before the next one as it chooses plural even if the genitive pronoun modifier after the numeral and in front of the singular noun is not disambiguated yet (Acc-Gen disambiguation comes later).

SELECT Sg IF (0 SG-WORD)(NEGATE *-1 Num OR Ord BARRIER NPNH)(*-1 BOS LINK NOT *1 ("goabbák") OR ("moattelágásj") OR ("gáiklágásj") OR ("guovtelágásj") OR ("ietjálágásj"));
    ## Bårråmussan lij rássegahpana ja miehttsemieda.
    # The words in the latter rule have to be checked.

REMOVE (N Sg Nom) IF (-1C (Dem Pl Nom))(0 (N Pl Nom));
    ## Just oavddodago dunnu lunna bus Tyrosin ja Sidonin dagáduvvat lulun, de da stáda juo ådno lulujga buorádusáv hårsåj ja gunáj sinna dahkat.
    
SELECT (N Sg Nom) IF (*-1 COPULAS BARRIER NOT-AAPCLE LINK 0 Sg3 LINK -1 Ine LINK 0 N OR (Pron Pers))(NEGATE 0 PrfPrc)(NEGATE *1 Nom BARRIER NPNH);
    ## Sujna le goajvvo giedan.  
    ## Sujna le bahás vuojŋŋanis. 
     
REMOVE (Pl Nom) IF (0 (Sg Nom) LINK 0 N OR (Pron Pers))(1 VFIN LINK 0 Sg3);  
    ## ... mav profehta Jeremia lij javllam. 
    ## Härrá lij profehta baktu javllam. 
    
REMOVE (Sg Nom) IF (NEGATE *-1 ("ja") OR COMMA BARRIER NOT-AA LINK -1 Nom)(0 (Pl Nom) LINK 0 N OR (Pron Pers))(*1 VFIN BARRIER S-BOUNDARY2 OR Nom OR CLB LINK 0 Pl3 OR Du3);    
    ## Biekka garrasit huodnahis vuosstij båssun ja huonaj alvvásláhkáj vieraj.
    ## De lullet muhtem násstedåbdde Betlehemaj båhtin ja gahtjin.
    ## Guollebivdde vuojn lijga.

SELECT Pl (0 Sg LINK 0 N OR (Pron Pers))(*1C (Pron Rel Pl) BARRIER WORD);
    ## Allit fásstudattijn hådjen vuojnnu nåv gåk vilpástalle, gudi tsusskaha fásstudi almatjij vuojnedijn. 
    
SELECT Sg (0 Pl LINK 0 N OR (Pron Pers))(*1C (Pron Rel Sg) BARRIER WORD);  
    ## Ja dåppe årruj stádaj man namma le Nasaret.  
    
REMOVE Nom (0 Acc)(*0 V-TRANS-ACT-NOT-ACT-NOT-AUX + Imprt BARRIER SV-BOUNDARY2) ;    

REMOVE Nom (0 Acc)(*-1 V-TRANS-ACT-NOT-ACT-NOT-AUX BARRIER SV-BOUNDARY2 OR Acc LINK NOT *-1 Acc BARRIER SV-BOUNDARY2) ;    

    
REMOVE Ine IF (-1 N LINK 0 ADVLCASE)(0 Prop LINK 0 Mal OR Fem OR Sur)(NEGATE *1 COPULAS BARRIER NOT-ADV-PCLE);
    ## ., Elasaraj Mattan, ....       

# These are not safe:    
SELECT Nom IF (-1 HUMAN-INDIVIDUAL)(0 (Prop Nom) LINK 0 Mal OR Fem)(NEGATE 1 N);
    ## vielljas Andreas
SELECT Nom IF (-2 Nom LINK NOT *-1 Nom)(-1 (@CNP))(0 N)(NEGATE 1 N);
    ## vieljatja Andreas ja Johannes
SELECT Nom IF (NOT *-1 Nom)(0 N)(1 (@CNP))(2 Nom)(NEGATE 3 N);    
    ## Tomas ja Matteus        
SELECT Nom IF (0 N)(1 (@CNP))(2 TITLE)(3 Nom)(NEGATE 4 N);   
    ## Tomas ja profehtta Matteus

# N Sg Gen = N Pl Nom
# ------------------- 

SELECT (Plc Gen) (0 (Prop Nom) OR Refl LINK 1 GEOGRAPHICAL-PLACE) ;
SELECT (Plc Gen) (0 (Prop Nom) LINK 1 CC LINK 1 Gen LINK 1 GEOGRAPHICAL-PLACE) ;

REMOVE Gen IF (NEGATE 0 TIME OR ROUTE)(0 N)(NEGATE 1 N OR A OR Gen OR (Pron Indef) OR Num OR Po OR CC OR COMMA OR Ger) ;
    ## Nåv li divna buolva.
    
REMOVE (N Sg Gen) IF (0 (N Pl Nom) LINK NOT 0 TIME OR ROUTE LINK *-1 COPULAS BARRIER Nom OR S-BOUNDARY2 OR VERB)(1 CC OR COMMA LINK NOT 1 Gen) ;
    ## Nåv li buolva, lågenanniellja buolva Abrahama rájes Davida rádjáj.
    
REMOVE (N Sg Gen) IF (-2 Ine)(-1 COPULAS LINK 0 Pl3)(0 (N Pl Nom))(NEGATE 1 (Pl Nom));
    ## Johannesin lidjin biktasa kamelaguolgajs. 
    
REMOVE (N Sg Gen) IF (-2 Ess)(-1 COPULAS)(0 (N Pl Nom))(NEGATE 1 (Pl Nom));
    ## .. ja bårråmussan lij rássegahpana ja miehttsemieda.    

REMOVE (N Sg Gen) IF (-2 ADVLCASE)(-1 VFIN)(0 (N Sg Nom))(NEGATE 1 Nom);
	## Hiskiaj riegádij Manasse.
REMOVE Gen IF (-1 ADVLCASE)(0 (N Nom))(1 CLB LINK NOT 1 Gen);
    ## ..., Joramij Ussia, ...    
REMOVE Gen IF (-1 ADVLCASE)(0 (N Nom))(1 Com)(2 CLB);  
    ## ..., Jakobij Juda vieljatjij, ...   
   
SELECT Gen IF (-1 CC)(0 Nom)(*1 ADVLCASE BARRIER NOT-ADJ LINK *1 (N Nom) BARRIER NOT-ADJ LINK 1 CLB OR CC);
    ## Riehpihijn li lieso ja alme låttijn biese, ..


    # Taken from sme-dis.rle:
SELECT (N Nom) IF (-2C (N Nom))(-1 CC)(NEGATE 0 VERB OR Pron)(NEGATE 0 (A Ess) LINK -2 Ess)(NEGATE 1 N OR Pron OR Num OR A OR Actio); 
	## Mánát ja nuorat leat dál.  
	##  ... , Judaj riegádijga Peres ja Sera, ...    

SELECT (N Sg Nom) IF (*-1 Rel BARRIER NPNH)(*1 V-SG3 BARRIER NOT-ADV-PCLE LINK *1 CLB OR EOS BARRIER (N Sg Nom));
	## Dát sjattaj vaj luluj ålliduvvat mav Härrá lij profehta baktu javllam.

#REMOVE (N Sg Gen) IF (*-1 BOS OR CS BARRIER NPNH)(1 (V Ind Pl3) LINK NOT 0 Imprt OR ImprtII);
	## dan dihti go profehta le náv tjállám.

REMOVE (N Sg Gen) IF (*-1 BOS OR CS BARRIER (Pron Pers Du1))(0 (N Pl Nom))(*1 (V Ind Pl3)  BARRIER S-BOUNDARY2 LINK NOT 0 Imprt OR ImprtII);

# Oppramsing
# - - - - - - 
#SELECT Nom IF (*-1 COMMA BARRIER NPNH OR CC LINK -1 Nom)(0 N OR Num LINK NOT 0 Gen OR V)(NEGATE *1 VFIN BARRIER COMMA);
    ## Nåv li buolva, lågenanniellja buolva Abrahama rájes Davida rádjáj.  
    
SELECT Nom IF (-1 COMMA OR CC LINK -1 Po LINK -1 Gen LINK *-1 Po BARRIER NPNH LINK -1 Gen LINK *-1 Nom BARRIER NPNH LINK 0 N OR Num)(0 N OR Num)(NEGATE *1 VFIN BARRIER COMMA) ;    
    ## Nåv li buolva, lågenanniellja buolva Abrahama rájes Davida rádjáj, lågenanniellja buolva Davida rájes Babylona fáŋŋgavuoda rádjáj ja lågenanniellja buolva fáŋŋgavuoda rájes Kristusa rádjáj.
    

# Nominative in sentence fragments
# --------------------------------

SELECT Nom IF (NEGATE *-1 Pr OR V)(0 Gen LINK NOT 0 Num)(NEGATE *1 Po OR V);
	## Sjokoláda ja næjtso




# Genitive
# ========

# N Sg Gen = Prs Sg2 = VGen


# N Sg Gen = N Pl Nom
# -------------------

SELECT Gen (0 N)(1C Ger) ;
SELECT Gen (0 N)(1 CC LINK *1 Gen BARRIER NPNH LINK 1C Ger) ;
	## adnet fáhkajguoskavasj terminologijav giela hámij ja struktuvra gåvådattijn.

SELECT Gen (-1 ILLV)(0 Nom LINK 1 Ill);

SELECT Gen IF (NEGATE -1 COPULAS LINK 0 Pl)(-1 ("soames"))(0 TIME)(NEGATE 1 COPULAS  LINK 0 Pl);
    ## .., javlaj Ámmá munji soames biejve.

SELECT Gen IF (*-1 V-TRANS-ACT-NOT-ACT BARRIER NPNH)(0 (N Sg))(NEGATE 0 VFIN)(*1 N BARRIER NOT-ADJ OR COMMA LINK 1 NP-BOUNDARY);
    ## Gullam gånågisá bágojt, vuolggin.
    ## Áhttje , hievedav alme ja ednama härráv.
    
    
SELECT (Sg Gen) IF (*-1C (Dem Sg Gen) BARRIER NOT-ADJ)(NEGATE 0 VFIN)(0 N);
    
SELECT (Sg Gen) IF (0 Prop)(1 (N Nom) LINK NOT 0 Prop);
    ## Josef la Maria ålmåj.
    
REMOVE Nom IF (NEGATE 0 TITLE)(0 N)(1C Nom);
    ## Valla dáv ájádaládijn Härrá ieŋŋgil Josefij niegon bihkusij ja javlaj:
        
SELECT (Sg Gen) IF (-1C Gen)(0C N)(1 N);
SELECT (Sg Gen) IF (-1C Gen)(0C N)(1 COMMA)(*2 N BARRIER NOT-ADJ);
    ## Davida bárne, Abrahama bárne máddo.

REMOVE (Pl Nom) IF (*-1 Num BARRIER NOT-ADJ)(0 N);
	## gålmmå jage.
	
REMOVE Gen IF (1 (Pron Pers Gen));
    ## De bådijga tjalmedime suv åvddåj.	
    
REMOVE Nom IF (*-2 COPULAS BARRIER NOT-ADJ)(-1 Nom OR Ess)(1 N LINK 0 ADVLCASE);    
    ## , ja liehket koordinerimorgádna julevsáme giellabargguj.



# N Sg Nom = N Sg Gen = N Pl Nom (cases without CG)
# -------------------------------------------------



# N Pl Gen = N Pl Com
# -------------------

REMOVE (N Pl Com) IF (0 TIME LINK 0 (N Pl Gen));
    ## Daj biejvij Johannes gástadiddje båij ja Judea åjdåmiehtsen sårnnedatjáj..

# N Pl Gen = N Pl Com = N Sg Ill
# ------------------------------

SELECT (Gen) IF (*-1 VFIN)(0C N LINK 0 Com OR Ill)(1C N);
    ## Gånnå le juvdáj riegádimgånågis?



# Illative
# ========

# Sg Ill = Pl Com =  Pl Gen (if no CG)

SELECT Ill IF (*-1 ILL-ADJ BARRIER NOT-ADJ)(0 N);
SELECT Ill IF (0 N)(*1 ILL-ADJ BARRIER NOT-ADJ-PCLE);

SELECT (N Ill) IF (*-1C (Num Gen))(NEGATE 0 Gen);

SELECT Ill IF (*-1 ILLV BARRIER SV-BOUNDARY2 OR Ill LINK NOT -1 Ill);
SELECT Ill IF (*1 ILLV BARRIER SV-BOUNDARY2 OR Ill)(NOT -1 Ill);
    ## Habssa dabrijdij njunnjáj.
SELECT Ill IF (*-1 CC BARRIER NPNH LINK -1 Ill LINK -1* ILLV BARRIER SV-BOUNDARY2);
	## Åhpadus sámegielan galggá viehkedit mánájt ja nuorajt oassálastátjit sáme kultuvrraj ja sebrudakiellemij.


SELECT Ill IF (*-1 (V* TV Der/Pass) BARRIER SV-BOUNDARY2);   
    ## Mån bårådalliv bednagij.
    

# Coordination:
SELECT Ill IF (1 (@CNP))(*2C Ill BARRIER NOT-ADJ);
    ## Guovtij guovtij sijáv ietjas åvdån rájadij juohkka stádaj ja bájkkáj gåsi iesj ájgoj.


# Comitative
# ==========
    
SELECT Com IF (*-1C (Num Com));
    ## avtajn guolijn

SELECT Com IF (-2 IV)(-1 ("akta" Ess))(0 Ine);
    ## Bievddegáddáj viellidin aktan sujna ja åhpadisålmmåj.

SELECT Com IF (-2 IV OR (@CVP) OR BOS OR AUX)(-1 ("aktan" Adv))(0 Ine OR Ill);

    ## Bievddegáddáj viellidin aktan sujna ja åhpadisålmmåj.
    ## Jesus de tjuodtjelij ja aktan åhpadisålmmåj suv tjuovvolij.

SELECT Com IF (*-2 Ill BARRIER NOT-ADV-PCLE-VFIN)(-1 N)(0 Gen LINK NOT 0 V)(NEGATE 1 N);    
     ## ..., Jakobij Juda vieljatjij, ...    
     ## ... , Jakobij riegádij Juda vieljatjij dan ájge.

SELECT (Sg Com) IF (-1 (Dem Sg Com))(0 N);
    ## Dájna vuogijn profehta Jesaja bágo sidjij ålliduvvi.
    
SELECT (Sg Com) IF (*-1 DYNAMIC-V BARRIER SV-BOUNDARY2 LINK 0 FMAINV LINK NOT 0 HUMAN-SOURCE-V)(0 (Pl Ine) LINK 0 TOOLS OR ABSTR-TOOLS OR WRITING-TOOLS OR CONCEPT);
SELECT (Sg Com) IF (0 (Pl Ine) LINK 0 TOOLS OR ABSTR-TOOLS OR WRITING-TOOLS OR CONCEPT)(*1 DYNAMIC-V BARRIER SV-BOUNDARY2 LINK 0 FMAINV LINK NOT 0 HUMAN-SOURCE-V);
    ## Mån dijáv tjátjijn buorádussaj gástadav.
    ## Ájlis Vuojŋŋanisájn ja dålåjn sån dijáv gástat. 
    ## Guhtis dijás mårråhijnes ga máhttá stihko guhkev viessomájggásis lasedit?
    
SELECT (Sg Com) IF (*-1 PLACE-V BARRIER SV-BOUNDARY2)(0 (Pl Ine) OR Ill LINK 0 ANIMATE OR CONCEPT OR TOOLS OR ABSTR-TOOLS OR (Pron Pers));
SELECT (Sg Com) IF (0 (Pl Ine) LINK 0 ANIMATE OR CONCEPT OR TOOLS OR ABSTR-TOOLS OR (Pron Pers))(*1 PLACE-V BARRIER SV-BOUNDARY2);    
    
SELECT Com IF (-2 ("mij" Acc))(-1 Pers OR N OR VFIN OR Pcle)(1 ("?"));
SELECT Com IF (-1 ("mij" Acc))(1 ("?")); 
    ## - Na majt hærrájt sjokoládajn? 
    ## Na majt sjokoládajn?   
        
SELECT (Sg Com) IF (*1C Sg BARRIER WORD LINK 0 Interr OR Rel);
    ## Mån jugáv ja gástadusáijn, majna gástaduváv, dåj aj gástaduvvabihtte.
    ## Dåppe gávnadij álmmåjn, gen namma lij Aineas.


	# Movement-verbs 
	# - - - - - - -
# Actio-event is not included yet.

REMOVE Com IF (*1 MOVEMENT-V BARRIER SV-BOUNDARY2 LINK 0 FMAINV)(0 Ine OR Ill LINK 0 PLACE OR ABSTR-PLACE);
REMOVE Com IF (0 Ine OR Ill LINK 0 PLACE OR ABSTR-PLACE)(*-1 MOVEMENT-V BARRIER SV-BOUNDARY2 LINK 0 FMAINV);
    ## Maŋep biejve de oassásij vádtsájiv.

SELECT Com IF (0 VEHICLE OR ANIMATE OR (Pron Pers))(*1 MOVEMENT-V BARRIER SV-BOUNDARY2 LINK 0 FMAINV); 
SELECT Com IF (*-1 MOVEMENT-V BARRIER SV-BOUNDARY2 LINK 0 FMAINV)(0 VEHICLE OR ANIMATE OR (Pron Pers) BARRIER S-BOUNDARY2);  	
    ## De gåhtjoj åhpadisålmmåjt sujna jávre nuppe bälláj vuolgget.
    ## Sån ájggu vuolgget sujna jávre nuppe bälláj.
    ## Sån vuolggá sujna jávre nuppe bälláj.
 
SELECT Com IF (*-1 ("oasstet") BARRIER SV-BOUNDARY2 LINK 0 VERB)(0 ("kråvnnå") OR Num);  
SELECT Com IF (0 ("kråvnnå") OR Num)(*1 ("oasstet") BARRIER SV-BOUNDARY2 LINK 0 VERB); 
    ## Gålmåjn låk kråvnåjn la sån oasstám biebmojt.  
    
SELECT Com IF (*-1 COMV BARRIER SV-BOUNDARY2 LINK 0 VERB)(NEGATE 0 PLACE OR SITUATION);
SELECT Com IF (NEGATE 0 PLACE OR SITUATION)(*1 COMV BARRIER SV-BOUNDARY2 LINK 0 VERB);
    ## Duola dagu subsastin man gássjel la uddni bierggit árbbedábálasj æládusáj.

SELECT Com IF (*-1 COMV BARRIER SV-BOUNDARY2 LINK 0 VERB)(0 POLITICAL-PLACE);
SELECT Com IF (0 POLITICAL-PLACE)(*1 COMV BARRIER SV-BOUNDARY2 LINK 0 VERB);
    ##  
	
SELECT Com IF (*-2 TOGETHER-V LINK 1 ("aktan") BARRIER NOT-ADV LINK *1 N OR Pers BARRIER NOT-AAPCLE);
  	## Mii heive bures oktii ránes datneláiggiin.
  
SELECT Com IF (*1 TOGETHER-V BARRIER NPNH OR SV-BOUNDARY2 LINK 1 ("aktan"));
  	##  
  	
SELECT Com IF (*-1 NP-HEAD-NOM BARRIER S-BOUNDARY2 LINK 0 ACTOR)(0 ABSTR-TOOLS)(*1 ABSTR-ACTIVITY-V);
	## Geahččoeiseválddit galget bearráigeahčuin váikkuhit dasa ahte dearvvašvuođa- ja sosiálabálvalusain váldet vuhtii geavaheaddjiid vuoigatvuođa oažžut áddehahtti informašuvnna ja vuoigatvuođa informerejuvvon miehtamii, ja fágalaččat dohkálaš doaimmaid giellagáibádusaid.

SELECT Com (0 HUMAN-INDIVIDUAL + Ill LINK -1 HUMAN-INDIVIDUAL OR Pers OR Mal OR Fem) ;  	

 # Body-activity-verb
# - - - - - - - - - -
SELECT Com IF (*-1 BODY-ACTIVITY-V OR VERBAL-ACTIVITY-V BARRIER SV-BOUNDARY2 LINK 0 FMAINV)(0 BODY);
SELECT Com IF (0 BODY)(*1 BODY-ACTIVITY-V OR VERBAL-ACTIVITY-V BARRIER SV-BOUNDARY2 LINK 0 FMAINV);
    ## Muhto ieža eai hálit daid lihkastahttit suorbmagežiin ge.
    # They belong to the dynamic-verbs, but they can also have BODY as a tool.   	 
 
    

# ------------ Rules from sme -------- Start ----------- 
# These rules have to be adjusted to smj - be aware of Ill homon.:


    # ANIMATE + Noun
    # - - - - - - - - - - - - - -
#SELECT (Sg Com) IF (*-1 ANIMATE BARRIER NOT-ADJ LINK 0 Nom OR Gen)(0 (Pl LOC) LINK NOT 0 ABSTR-TEXT OR TEXT OR PLACE OR INDUSTRY OR EDUCATION OR INSTITUTION)(1 CLB OR EOS);   
    ## Sii vástidedje: Johanasa gásttain.  
    # Does it really need (1 CLB OR EOS)?
   	 

	# Disambiguation based upon verb valency
	# --------------------------------------

	
# Verbal-activity-verbs
# - - - - - - - - - - -  

#SELECT (Sg Com) IF (*-1 VERBAL-ACTIVITY-V BARRIER SV-BOUNDARY2 LINK 0 FMAINV)(0 (Pl LOC) LINK 0 ("sátni") OR ("namma"));   
    ## Son ii daja njuolga rievttes namain. 
    
#SELECT (Pl LOC) IF (*-1 VERBAL-ACTIVITY-V BARRIER SV-BOUNDARY2 LINK 0 FMAINV)(0 Com LINK 0 EVENT OR EVENT-TOOL-ACTIO);
  	## Dan geažil lea dehálaš ávžžuhit bargiid sámástit ráđđádallamiin.
   

# FROM-HUMAN-LOC-VEH-V
# - - - - - - - - - - -
SELECT (Sg Com) IF (*-1 VEHICLE-TOOL-V BARRIER SV-BOUNDARY2 LINK 0 FMAINV)(0 Ine LINK 0 VEHICLE);
SELECT (Sg Com) IF (0 Ine LINK 0 VEHICLE)(*1 VEHICLE-TOOL-V BARRIER SV-BOUNDARY2 LINK 0 FMAINV);
    ## Sii bukte girjjiid biillain.


# Abstract-entity-com-verbs
# - - - - - - - - - - - -

#SELECT (Sg Com) IF (*-1 ABSTR-ENTITY-COM-V BARRIER SV-BOUNDARY2)(0 (Pl LOC) LINK 0 ABSTR-ENTITY OR ACTOR);
#SELECT (Sg Com) IF (0 (Pl LOC) LINK 0 ABSTR-ENTITY OR ACTOR)(*1 ABSTR-ENTITY-COM-V BARRIER SV-BOUNDARY2);
    ## Muhto mii diehtit ahte fágagirjjit leat divrasat ja diibmá jagi bušeahtain ii sáhttán investeret.
    ## Mii leat lihkostuvvan bures prošeavttain.

# Only-place-loc-verb 
# - - - - - - - - - - 
#SELECT (Sg Com) IF (0 (Pl LOC) LINK NOT 0 PLACE OR ORGANIZATION OR HUMAN)(*1 ONLY-PLACE-LOC-V BARRIER SV-BOUNDARY2  LINK 0 FMAINV);
#SELECT (Sg Com) IF (*-1 ONLY-PLACE-LOC-V BARRIER SV-BOUNDARY2 LINK 0 FMAINV)(0 (Pl LOC) LINK NOT 0 PLACE OR ORGANIZATION OR HUMAN);
    ## Giellakantuvrras ii leat formála váldi álggahit gielladoaimmaid etáhtain, muhto veahkehit.
    # It can be ambiguity with ORGANIZATION.

# HUMAN-LOC-Verb 
# - - - - - - - 
#SELECT (Sg Com) IF (0 (Pl LOC) LINK 0 CONCEPT OR HUMAN OR ("sátni"))(*1 HUMAN-LOC-V BARRIER SV-BOUNDARY2 LINK 0 FMAINV);
#SELECT (Sg Com) IF (*-1 HUMAN-LOC-V BARRIER SV-BOUNDARY2 LINK 0 FMAINV)(0 (Pl LOC) LINK 0 CONCEPT OR HUMAN OR ("sátni"));
    ## Ollu ásahusat geavahit dan sin doaimmain.
    ## Geavat mu biilla ráhkisvuođain.
    ## Sámediggeráđđi šálloša ja imaštalla go departementa ii namut sámi skuvlla sániin ge skuvlla IKT-áŋgiruššanplánain.     

# Rem-with-parts-verb
# - - - - - - - - - -
#SELECT (Sg Com) IF (0 (Pl LOC) LINK 0 PARTS)(*1 REM-WITH-PARTS BARRIER SV-BOUNDARY2 LINK 0 FMAINV);
#SELECT (Sg Com) IF (*-1 REM-WITH-PARTS BARRIER SV-BOUNDARY2 LINK 0 FMAINV)(0 (Pl LOC) LINK 0 PARTS);
    ## Juohke šaddu man mu almmalaš Áhčči ii leat gilván, gaikkihuvvo ruohttasiin.

   
	# COPULAS 
	# - - - - 
#SELECT (Sg Com) IF (*-1 COPULAS LINK 0 PrfPrc OR Inf OR ConNeg OR VFIN BARRIER SV-BOUNDARY2)(0 (Pl LOC) LINK 0 ANIMATE OR CONCEPT LINK *-1 A BARRIER NP-BOUNDARY);
#SELECT (Sg Com) IF (0 (Pl LOC) LINK 0 ANIMATE OR CONCEPT LINK *-1 A BARRIER NP-BOUNDARY)(*1 COPULAS LINK 0 Inf OR ConNeg OR VFIN BARRIER S-BOUNDARY2 LINK NOT *1 VERB LINK NOT 0 VFIN LINK NOT 0 COPULAS);
  	## Go dii ehpet leat šiega leamaš ealibiin.
  	## Go dii lehpet šiega leamaš ealibiin.
  	## Go dii lehpet šiega ealibiin.
  	## Son rávvii buohkaid bissut Hearrá luhtte duođalaš váimmuin.
  	## Son lea šaddan beakkán su joavkkuinis.




# ---------------------- 



# Coordination
# - - - - - - 
SELECT Com IF (-2 Com)(-1 (@CNP))(0 Ill OR Ine)(1 CLB);
    ## Bievddegáddáj viellidin aktan sujna ja åhpadisålmmåj.
    ## Valla ettjin rijbada vijsesvuodajn ja vuojŋŋanisájn, majna hålaj.


# Inessive
# ========
# N Sg Ine = N Ess if no CG
# Dem Sg Ine = Dem Sg Com

SELECT Ine IF (0 Ess LINK 0 INE-NOUN);
    ## Náv aj ednamin.
    
SELECT Ine IF (-1 (Indef Ine))(0 N);
    ## Jordanäno nuppen bielen.
    
REMOVE Ess IF (-1 Indef LINK NOT 0 ("sæbbmi") OR ("nubbe") OR ("ietjá"))(0 N);
    ## Juohkka biejven li ietjas vájve. 
    # Are there more Indef šhich can be followed by Ess? 

SELECT Ine IF (0 (N Plc Ess));
	## Jesus lij riegádam Betleheman.
         # Needs further condition!!
         
SELECT (N Ine) IF (0 VAHKKU OR MANNU)(*1 VFIN BARRIER NP-BOUNDARY);
    ## Gidán máhttá oaggut.         

SELECT Ine IF (1 COPULAS)(*2 (N Nom) BARRIER VERB);
    ## Sujna le goajvvo giedan.
    ## Juohkka biejven li ietjas vájve.
    
SELECT Ine IF (0 Com)(1 (@CNP))(*2 Ine BARRIER NPNH LINK 1 COPULAS);    
    ## Gå mijájn ja fariseajn li fásto.  

SELECT Ine IF (0 N OR (Pron Pers) LINK 0 Com)(1 COPULAS LINK NOT 1 FMAINV LINK NOT 0 COPULAS);    
    ## Sujsta váldeduvvá aj mij sujna le.
   
    
SELECT Ine IF (-1 COPULAS LINK -1 (N Pl Acc) LINK -1 ("mij" Pron Interr Sg Nom));  
    ## Mij dahkamusájt le dujna munnujn?  
    
SELECT Ine IF (0 TEXT OR ABSTR-TEXT)(*1 VERBAL-ACTIVITY-V LINK 0 Der/Pass BARRIER NPNH);
  	## Dálá sámiid máidnasiin ja muitalusain muitaluvvo dološ oskui gullan hálddiid birra .

SELECT Ine IF (*-1 VERBAL-ACTIVITY-V LINK 0 Der/Pass BARRIER NPNH)(0 TEXT OR ABSTR-TEXT);
  	## Sii máinnašuvvojedje vearrologahallamiin earáid maŋŋá. 
  	
SELECT Ine IF (0 PLACE)(*1 PLACE-V BARRIER SV-BOUNDARY2);  	

SELECT Ine IF (*-1 (Num Ine) BARRIER NOT-ADJ)(0 VAHKKU)(*1 VFIN BARRIER S-BOUNDARY2);
    ## Gålmån biejven mån manniv dåhku.
    
    
    
# Coordination:
# - - - - - - -
SELECT Ine IF (*-1 COPULAS BARRIER SV-BOUNDARY2 LINK 0 FMAINV LINK *1 Nom BARRIER NPNH LINK 1 CC)(0 N OR Pers)(*1 (N Nom) BARRIER NPNH);
    ## Riehpihijn li lieso ja alme låttijn biese. 

REMOVE Ess IF (NEGATE *-1 OPREDV BARRIER SV-BOUNDARY2)(0 (Prop Ine))(NEGATE *1 OPREDV BARRIER SV-BOUNDARY2);   
    ## Gå Jesus lij riegádam Betleheman, Judean, ...

REMOVE Ess IF (*-1 PLACE-V OR COPULAS BARRIER SV-BOUNDARY2)(0 (Sg Ine) LINK 0 A);
REMOVE Ess IF (0 (Sg Ine) LINK 0 A)(*1 PLACE-V OR COPULAS BARRIER SV-BOUNDARY2);
    ## Dahpa uvsav ja råhkådalá Áhttjásit guhti le tjiehkusin. 
    ## De Áhttját guhti tjiehkusin vuojnná vaddá dunji bálkáv.

REMOVE Ine IF (-1C (Dem Sg Com))(0 (Dem Sg Com));
    ## Duobbmobiejven Nineve almatja tjuodtjeli aktan dájna vierregijn ja sijá duobbmon sjaddá, dajn gå buorádusáv Jona sárnnedattijn dahkin...
 
# Too strong:    
REMOVE Ess IF (-1 ("liehket") OR ("årrut") LINK 0 FMAINV)(0 (A Ine) OR (N Ine) LINK NOT 0 HUMAN OR Actor LINK NOT *1 Ill BARRIER NPNH); 
REMOVE Ess IF (0 (A Ine) OR (N Ine) LINK NOT 0 HUMAN OR Actor)(1 ("liehket") OR ("årrut") LINK 0 FMAINV);  
    ## Ålles rumáj le sjievnnjedin.
    ## Álmmuk mij sjievnnjedin årru. 
    ## Ja sidjij gudi jábmema rijkan ja sähkásin årru. 
    

SELECT (Pl Ine) IF (NEGATE *-1 COMV BARRIER SV-BOUNDARY2)(0 (Sg Com) LINK 0 PLACE)(NEGATE *1 COMV BARRIER SV-BOUNDARY2);
    ## Ale ga vattástagájt jieddnát gálggi nåv gåk vilpástalle synagogajn ja gahttunijn rámmpoma diehti dahki. 
    ## Jesus synagogajn åhpadij.
    ## Sij lijkkuji synagogajn ja gahttunsuorijn vuojnnuma diehti råhkådallat. 
    
   

# Elative
# ======= 
SELECT (N Ela) IF (*-1C (Num Par));
    ## avtat guoles 

SELECT Ela IF (1 (IV PrfPrc));
    ## Maria lij ájlis Vuojŋŋanisás sahkanam, ..

SELECT Ela IF (*0 ELAV BARRIER Ela OR SV-BOUNDARY2);
SELECT (V Actio Ela) IF (*0 ELAV BARRIER Ela OR SV-BOUNDARY2);
    ## Ale balá Mariav áhkkán válldemis.
    
REMOVE Nom IF (*-1 Nom BARRIER NOT-AAPCLE)(0 Ela LINK NOT 0 Prop);    
    ## Gå Josef oademis gåhttsåj, ...    
    
REMOVE Acc IF (NEGATE *-1 V-TRANS-ACT-NOT-ACT BARRIER SV-BOUNDARY2)(NEGATE -1 COMMA LINK -1 Acc)(0 N)(NEGATE *1 V-TRANS-ACT-NOT-ACT BARRIER SV-BOUNDARY2);  
    ## Gå Josef oademis gåhttsåj, ... # oademis - Ela
    ## De ålliduváj mav profehta Jeremia lij javllam. # javllam - PrfPrc
    ## Nåv li divna buolva, .. # nåv - Adv   
    
SELECT Ela IF (*-1 COPULAS BARRIER VERB)(0 N OR Pers)(1 Comp OR ("ienep" Ess))(2 S-BOUNDARY2 OR CLB);    
    ## Valla dánna le juoga Jonas ienebun.
    ## Oahppe ij le åhpadiddjes bárep.
    ## Nissunis riegádam ij le Johannes gástadiddjes stuoráp.
    
    # HUMAN-SOURCE-V
    # - - - - - - - - 
SELECT Ela IF (0 PxSg3 LINK NOT 0 TOOLS OR ABSTR-TOOLS OR WRITING-TOOLS OR CONCEPT LINK *-1 HUMAN-SOURCE-V BARRIER SV-BOUNDARY2 LINK 0 FMAINV);
SELECT Ela IF (0 PxSg3 LINK NOT 0 TOOLS OR ABSTR-TOOLS OR WRITING-TOOLS OR CONCEPT)(*1 HUMAN-SOURCE-V BARRIER SV-BOUNDARY2 LINK 0 FMAINV);
 
SELECT Ela (*-1 @CNP OR COMMA BARRIER NPNH LINK *-1C Ela BARRIER COOR-BOUNDARY) ;
 
# Essive
# ======

SELECT Ess (-1 ESSV)(NOT 0 Acc) ;
	## Fáhka sámegiella nubbegiellan máhttá aj aneduvvat ræjddon ietjá fágajda vaj giellaåhpadus máhttá nanniduvvat.

SELECT ESS-NOUN + Ess IF (0 Ine LINK 0 ESS-NOUN);
    ## Sálugin gudi vuojŋŋanisán li häjo.
    
SELECT ("viehkke" Ess) IF (-1 Ill)(*-2 ("boahtet") BARRIER NOT-AAPCLE);   
    ## Boade munji viehkken.

REMOVE Ine IF (*-1 OPREDV BARRIER Ess)(0 Ess);
REMOVE Ine IF (0 Ess)(*1 OPREDV LINK NOT 1 Ess);
	## Suv gåhttju Immanuelin.
	## , gev Kristusin gåhttju.



# Late demonstrative prononoun rules
# ----------------------------------

REMOVE Com IF (0 (Dem Pl Gen))(1C (N Pl Gen));
    ## Daj ájgij Jesus javlaj.

REMOVE Par IF (0 (Dem Sg Nom))(1C (N Sg Nom));
    ## Beelsebula, bahás vuojŋŋanisáj oajvemusá fámujn, duot ålmåj bahás vuoiŋŋanisájt vuodjel.


# ============================= #
# Disambiguating verbs - part 2 #
# ============================= #
             SECTION            #
# ============================= #


# Person
# ======
SELECT (Ind Pl1) IF (*-1 (Pers Pl1 Nom) BARRIER NOT-ADV-PCLE);
    ## Giesen mij vuojadip.

# Copula
SELECT HABEO-SG3 (-1 HAB-ACTOR + Ine)(*1 (Sg Nom) BARRIER NPNH) ; ;
SELECT HABEO-PL3 (-1 HAB-ACTOR + Ine)(*1 (Pl Nom) BARRIER NPNH) ; ;

SELECT Sg2 IF ((*-1 (Pron Pers Sg2 Nom) BARRIER (N Nom) OR S-BOUNDARY2) OR (*-1 Sg2 BARRIER Pron OR VFIN OR S-BOUNDARY2)) ;
SELECT Sg3 IF (*-1 (Pron Pers Sg3 Nom) BARRIER (N Nom));
SELECT Pl3 IF (*-1 (Pron Pers Pl3 Nom) BARRIER (N Nom));
    ## Sij le jåhtemin.

SELECT Sg2 IF (*1 (Pron Pers Sg2 Nom) BARRIER N);
SELECT Sg3 IF (*1 (Pron Pers Sg3 Nom) BARRIER N);
SELECT Pl3 IF (*1 (Pron Pers Pl3 Nom) BARRIER N);
    ## -Man huohppelasj la dån Tore, javlaj Ámmá munji. 
    
SELECT Pl3 IF (*-1C (N Pl Nom) BARRIER (Pron Pers Sg2 Nom) OR (Pron Pers Sg3 Nom) OR (N Sg Nom));  
SELECT Pl3 IF (*-1 (N Sg Nom) BARRIER (Pron Pers Sg2 Nom) OR  (Pron Pers Sg3 Nom) OR (N Sg Nom) LINK *-1 CC BARRIER NPNH LINK -1 (N Sg Nom));
    ## Anne ja Berit le jåhtemin.
    
REMOVE Sg2 IF (*-1 (N Sg Nom) OR (Rel Sg Nom) BARRIER (Pron Pers Sg2 Nom) OR COMMA OR VFIN)(NEGATE 0 Neg);
REMOVE Pl3 IF (NEGATE *-1 (N Pl Nom))(*-1 (N Sg Nom) LINK NOT *-1 CC BARRIER NPNH LINK -1 (N Sg Nom))(0 ("liehket"));

SELECT (V Du3)(*0 (N Pl Nom) OR (A Pl Nom) BARRIER (Pron Pers Sg2 Nom) OR (Pron Pers Sg3 Nom) OR (N Sg Nom));



 
# PrfPrc
# =======

SELECT PrfPrc IF (*-1 ("liehket" V) BARRIER V);
	## Dát sjattaj vaj luluj ålliduvvat mav Härrá lij profehta baktu javllam.

SELECT PrfPrc IF (-1 BOS)(*1 ("liehket") BARRIER NOT-ADV-PCLE) ;

    
SELECT (V Sg2) IF (-1 BOS)(1 ("gus" Pcle))(2 (Pron Pers Sg2 Nom));
	## Le gus dån oahpes dán bájkkáj?

# Third person singular - Sg3
# ---------------------------
    # Copula le is Sg3 = Sg2 = Pl3.

SELECT (V Sg3) IF (-1 BOS)(1 ("gus" Pcle))(2 (Pron Pers Sg3 Nom));
	## Le gus sån oahpes dán bájkkáj?

REMOVE (V Sg3) IF (-1 (N Pl Nom) OR (Pron Pl Nom) LINK NOT 0 Pr OR Po)(0 (V Pl3));

REMOVE (V Pl3) IF (-1 (Sg Nom) LINK *-1 SV-BOUNDARY BARRIER CC LINK -1 (Sg Nom));
	## Dáhta l navti.
	
SELECT (V Sg3) IF (-1 (N Pl Acc) LINK *-1 ("mij" Pron Interr Sg Nom) BARRIER NOT-ADJ);
    ## Mij dahkamusájt le dujna munnujn?	

# First person dual - Du1
# -----------------------
    # Prs Du1 = Prt Pl3 "bårråt" "juhkat"
REMOVE (Prs Du1) IF (NEGATE *-1 MÅJ)(NEGATE *-1 MUN LINK *-1 (Pron Nom))(0 (Prt Pl3))(NEGATE *1 MUN LINK *1 (Pron Nom))(NEGATE *1 MÅJ);

    # Prt Du1 = Prt Pl1
REMOVE (Prt Du1) IF (NEGATE *-1 MÅJ)(NEGATE *-1 MUN LINK *-1 (Pron Nom))(0 (Prt Pl1))(NEGATE *1 MUN LINK *1 (Pron Nom))(NEGATE *1 MÅJ);

    # Imprt Du1 = N Ess ("mujttet" - "mujtto")

# Second person dual - Du2
# ------------------------
    # Prt Du2 = Prt Pl2
REMOVE (Prt Du2) IF (NEGATE *-1 DÅJ)(NEGATE *-1 DON LINK *-1 (Pron Nom))(0 (Prt Pl2))(NEGATE *1 DON LINK *1 (Pron Nom))(NEGATE *1 DÅJ);
    
# Third person dual - Du3
# -----------------------

# First person plural - Pl1
# -------------------------
    # Prt Pl1 = Prt Du1

# Second person plural - Pl2
# --------------------------
     # Prt Pl2 =  Prt Du2

# Third person plural - Pl3
# -------------------------
    # Prt Pl3 = Prs Du1
    # Prt Sg2 = Prs Pl3

SELECT (V Pl3) IF (-1 BOS)(1 ("gus" Pcle))(2 (Pron Pers Pl3 Nom));
	## Le gus sij oahpes dán bájkkáj?

SELECT (V Pl3) IF (*-2 BOS OR CS BARRIER NPNH)(-1 (N Pl Nom))(0 VFIN);
	## Dan diehti go profehta le náv tjállam.

SELECT (V Pl3) IF (*-1 (Rel Pl Nom) BARRIER VFIN OR S-BOUNDARY2)(0 VFIN);
    ## Hárjjánam lij báhtjajda gudi biebbmosjokoládav ádnu.	

SELECT (V Pl3) IF (*-1C (Pl Nom) OR (Pl3 Nom) BARRIER (V Pl3) OR CC OR S-BOUNDARY2);

SELECT Prs (0 Prt)(*-1C Prs BARRIER VFIN) ;
SELECT Prt (0 Prt)(*-1C Prt BARRIER VFIN) ;

              SECTION            #

# Rules for a special infinitive construction
# ===========================================
SELECT Inf IF (*-1 Acc BARRIER NOT-PCLE LINK -1 ("makkár") LINK -1 FMAINV); 
SELECT Inf IF (*-1 ("makkár" Acc) BARRIER NOT-PCLE LINK -1 FMAINV); 
    ## Usjudallagåhtiv makkár sjokoládav dal oasstet.
    ## Valla ittjiv ájn rat diede makkárav oasstet.


# Without finite verb:
SELECT Inf IF (*-1 BOS LINK NOT *1 VFIN);
    ## Man diehti de sjokoládav oasstet?



# ---- For NT
SELECT ("tjierrot") IF (0 ("tjierrut")) ;
SELECT ("Johannes") IF (0 ("Johanne")) ;
SELECT ("Apollos") IF (0 ("Apollosa")) ;
SELECT ("almme") (0 ("Alme")) ;

            AFTER-SECTIONS             #


#Disse kommenteres inn når analysen skal brukes i functions.cg3   

#SUBSTITUTE (A) (A <smj>) WORD ;
#SUBSTITUTE (N) (N <smj>) WORD ;
#SUBSTITUTE (Adv) (Adv <smj>) WORD ;
#SUBSTITUTE (V) (V <smj>) WORD ;
#SUBSTITUTE (Num) (Num <smj>) WORD ;
#SUBSTITUTE (Interj) (Interj <smj>) WORD ;
#SUBSTITUTE (Po) (Po <smj>) WORD ;
#SUBSTITUTE (Pr) (Pr <smj>) WORD ;
#SUBSTITUTE (Pron) (Pron <smj>) WORD ;
#SUBSTITUTE (CC) (CC <smj>) WORD ;
#SUBSTITUTE (CS) (CS <smj>) WORD ;
#SUBSTITUTE (Pcle) (Pcle <smj>) WORD ;
    

#REMOVE (@SPRED) IF (NEGATE 0 Gen OR Px);

	#=====#
      END #
	#=====#



