!Distributed under the terms of the GNU General Public License version 2
! or any later version.


! ========================================================================== !
!                   Lule Sámi morphophonological rule set                    !
! ========================================================================== !

! twol-smj.txt
! The file contains the rule set for the non-segmental
! Lule Sámi morphphonological rules

! m4 rules for selecting parts of the file to a certain application.
changequote(<<,>>)
changecom(<<!>>,<<
>>)
define(<<FLAGINIT>>,
<<ifdef(<<$1>>,<<define(<<$1>>,1)>>,<<define(<<$1>>,0)>>)>>)dnl
dnl
define(<<DIVNUM>>,0)dnl
define(<<ENDOMIT>>,<<popdef(<<DIVNUM>>)divert(DIVNUM)dnl>>)dnl
define(<<ENDKEEP>>,<<popdef(<<DIVNUM>>)divert(DIVNUM)dnl>>)dnl
define(<<OMITIF>>,
  <<pushdef(<<DIVNUM>>,ifelse(eval($1),0,0,-1))divert(DIVNUM)dnl>>)dnl
define(<<KEEPIF>>,
  <<pushdef(<<DIVNUM>>,ifelse(eval($1),0,-1,0))divert(DIVNUM)dnl>>)dnl
define(<<ELSE>>,
  <<define(<<DIVNUM>>,ifelse(DIVNUM,0,-1,0))divert(DIVNUM)dnl>>)dnl

FLAGINIT(<<HYPHENATE>>)
FLAGINIT(<<SHORTCOMP>>)
FLAGINIT(<<DIPHSIMPL>>)
FLAGINIT(<<HUNSPELL>>)

Alphabet
 a á b c d e f g h i j k l m n ñ ń ŋ o p q			! small
 r s t u v w x y z å ä ø ö å æ:æ æ:ä %-
 é ó ú í à è ò ù ì ë ü ï â ê ô û î ã ý
 ç č đ ð š ŧ þ ß ª

 A Á B C D E F G H I J K L M N Ñ Ń Ŋ O P Q			! capital
 R S T U V W X Y Z Å Ä Ø Ö Å Æ:Æ Æ:Ä
 É Ó Ú Í À È Ò Ù Ì Ë Ü Ï Â Ê Ô Û Î Ã Ý
 Ç Č Đ Ð Š Ŧ þ

! ':0			            ! Gradation mark
 ':0 %/ '7:'            ! From sme.

KEEPIF(HYPHENATE)
 #   %^                     ! Compound boundary and hyphenation mark (hyphenation version)
ELSE
 #:0 %^:0                   ! Compound boundary and hyphenation mark
ENDKEEP

 e7:e i7:i o7:o o9:o u7:u z9:z   ! Morphophonemes  in sme, here temporarily due to common propernoun file

 h8:h g8:g m8:m n8:n		                    ! these are deleted in nom
 b9:b d9:d g9:g l9:l m9:m n9:n h9:h r9:r    	! these can not occur before #
 k9:k t9:t s9:s p9:p                            ! Non-sámi cons clusters
 a9:a e9:e o9:o æ9:æ Æ9:Æ ä9:ä Ä9:Ä                  ! Wd-final foreign vow, and non-fluctuating æ/ä

OMITIF(HUNSPELL)
 X1:0 X2:0 X3:0 X4:0 X5:0 X6:0 X7:0 X8:0 X9:0   ! Diacritics
 Q1:0 Q2:0 Q3:0 Q4:0 Q5:0 Q6:0 Q7:0 Q8:0 Q9:0   ! They trigger morphophono-
 Y1:0 Y2:0 Y3:0 Y4:0 Y5:0 Y6:0 Y7:0 Y8:0 Y9:0   ! logical rules
 %>:0 »:0											! stem-suffix border mark
 ELSE
 X1 X2 X3:0 X4 X5 X6 X7 X8 X9   ! Diacritics
 Q1 Q2 Q3 Q4 Q5:0 Q6:0 Q7:0 Q8 Q9   ! They trigger morphophono-
 Y1 Y2 Y3 Y4 Y5 Y6 Y7 Y8 Y9   ! logical rules
 %> »                         ! stem-suffix border mark, only visible in Hunspell
ENDOMIT

;

Sets
 Vow     = a á e i o u y æ ä ø ö å
           A Á E I O U Y Æ Ä Ø Ä Å
           é ó ú í à è ò ù ì ë ü ï â ê ô û î ã ý
           É Ó Ú Í À È Ò Ù Ì Ë Ü Ï Â Ê Ô Û Î Ã Ý
           a9 e9 o9 æ9 ä9
                 Æ9 Ä9;
 æä      = æ ä                 ;
 Cns     = b b9 c d d9 f g g8 g9 h h8 h9 j k l l9 m m8 m9 n n8 n9 ŋ ñ ń p q r r9 s t v w x z z9 ' ; ! All consonants
 Cns7    =      c      f         h       j   l    m       n       ŋ ñ ń p q r    s t v w x z   ; ! Surface cons excl 1st members of xx-type G3
 Cns8    = b    c d    f g       h       j k l    m       n       ŋ ñ ń p q r    s t v w x z   ; ! All surface consonants
 Cns9    =   b9     d9     g8 g9   h8 h9       l9   m8 m9   n8 n9             r9             ' ; ! Underlying consonants
 Cns4    =             f                     l    m       n       ŋ ñ ń     r        v         ; ! Don't remember
 StemCns = b b9   d d9   g g8 g9 h h8 h9 j   l l9 m m8 m9 n n8 n9 ŋ ñ ń     r r9 s             ; ! Can occur stem-finally
 DelCns  =                 g8      h8               m8      n8                                 ; ! deleted in nom...
 WeG     = X2 X3 X7 Y5 Q1 Q2 Q3 Q6 ;
 Dummy   = X2 X3 X4 X5 X6 X7 X8 Y1 Y2 Y3 Y4 Y5 Y6 Y7 Y8 Y9 Q1 Q2 Q3 Q4 Q5 Q6 Q7 %> » ;
 Hyph = %- ;

Definitions

 LCnsPhon7 = [ :Cns7 | :n :j | :s :j | :t :j | :t :s ] ;     ! Using Cns7, excluding 1st member of two-cns G3
 LCnsPhon  = [ :Cns8 | :n :j | :s :j | :t :j | :t :s ] ;     ! Using Cns8, without the previous restriction

!     G3:G2 Here we document our alternation patterns
! S7 kkn:k0n
! S8 f'f:f0f
! S9 jgg:j0g
! S4 hkk:h0k
! S5  xy:zy (no zeros)
! S6  xx:yy (no zeros)
! S7  xy:zy (no zeros)
! S8        (no cg)

 LowerG2   = [
               [
                  (Cns:0)  LCnsPhon7 (Cns:0) LCnsPhon (Cns:0)            ! xy that cannot be G3, since x cannot form xy G3.
               |                                                         ! nijbe,
                  [           ! This section is for 3-cons G2. and for 2cns G2 that share the initial cns with 2cns G3
                     (Cns:0) [:j|:l|:m|:n|:v] (Cns:0) :s :t                  ! S9, 3cns-G2 bäjstov, etc.
                  |
                     (Cns:0) [ :l | :r | :n ]      (Cns:0) :s :k                   ! S9, 3cns-G2 sválskes, etc.
                  |
                     (Cns:0) [  ! 2cns G2 that share the initial cns wit 2cns G3
                                :b (Cns:0) [ :d | :m | :j | :l | :n | :n :j | :r | :s | :t :j | :t :s  ] ! S9, initial b
                                |                                                                        ! gábdev
                                :d (Cns:0) [ :j | :n | :n :j ]                                           ! S7, initial d
                                |                                                                        ! iednev
                                :g (Cns:0) :ŋ                                                            ! S7, initial g
                                |                                                                        ! låg0ŋot
                                :k (Cns:0) [ :n | :k ]                                                   ! S7, initial k
                                |
                                :g (Cns:0) :n                                                            !däggna:degna                        
                             ]                                                                           !
                             |
                             :r (Cns:0)   :s :j :t                                                    ! S9, rsjt, bårsjtav
                  ]
               ]
                 - [
                     [ d t [s|j] ] |  b b | d d | g g | k [ s | t | t j | t s ]  |
                     f ':0 f | l ':0 l | m ':0 m | n ':0 n | n ':0 n j | ŋ ':0 ŋ | r ':0 r | s ':0 s | s ':0 s j | v ':0 v
                     ]
             ];
 LowerG1   =   (Cns:0)   LCnsPhon ;
 LowerG12  = [ LowerG1 | LowerG2 ] ;


 G32 =  f ':0 f | l ':0 l | m ':0 m | n ':0 n | n ':0 n j | ŋ ':0 ŋ | r ':0 r | s ':0 s | s ':0 s j | v ':0 v ;

 G3 =[Cns Cns Cns Cns|
 [b|:b] b m|
 [d|:d] d [n|j]|
 [g|:g] g ŋ|
 k k n|
 l l [d|j|t]|
 m m [b|p]|
 n n d|
 ŋ ŋ [g|k]|
 p p [s|t]|
 s s[k|m|n|t]|
 b [d d|j j|l l|n n|r r|s s]|
 j [b b|d d|g g|k k|l l|m m|n n|ŋ ŋ|p p|r r|s s|t t|v v]|
 l [b b|f f|g g|k k|m m|ŋ ŋ|p p|s s|v v]|
 m [k k|s s]|
 n s s|
  ŋ [n n |t t]|
  r [b b|[d|:d] d|f f|g g|j j|k k|m m|n n|ŋ ŋ|p p|s s|t t|v v|d d]|
  v [d d|g g|k k|l l|n n|ŋ ŋ|r r|s s|t t]|
  h [[k|:k] k|[p|:p] p|[t|:t] t]|
  d t [j|s]|
  b b|d d|g g|
  k[s|t[j|s]]] ;

!diphtsimpl IF Vow: G32 AND G2 context - [Y6 Y7 Y9];
! oa:å <=> Vow: G32 Vow: WeG ; ! Evt. a subset of WeG, if only some WeG Dummys trigger diphtsimpl

!!1.86, let's see
!
!Definitions
!
!! CnsPhon = [ [ Cns7 | Cns8: ] | n j | s j | t j | t s ] ;
!! G3      = [ [ CnsPhon CnsPhon [ CnsPhon+ | '] ] - [[[j|l|m|n|v] s t ] | [l|r] s k | r s j t ] ] ;
!! G2      = [   CnsPhon CnsPhon |                   [[[j|l|m|n|v] s t ] | [l|r] s k | r s j t ] ] ;
!! G1      =     CnsPhon ;
!! G12     = [ G1 | G2 ] ;
!
! LowerCnsPhon = [ :Cns7 | :n :j | :s :j | :t :j | :t :s ] ;
! LowerG2  = [ (Cns:0)  LowerCnsPhon (Cns:0) LowerCnsPhon (Cns:0) |
!              (Cns:0) [[[:j|:l|:m|:n|:v] :s :t ] |
!              (Cns:0) [:l|:r] :s :k |
!              (Cns:0)  :r :s :j :t ] ] ;
! LowerG1  = (Cns:0)   LowerCnsPhon ;
! LowerG12 = [ LowerG1 | LowerG2 ] ;


Rules


! Consonant alternations in certain pos
! -------------------------------------

! This is a dummy negative test, in order to get the test paradigm work.
!$ a
!$ b


"Word Final Devoicing of Certain Single Consonants d9 etc."
 Cx:Cy <=> Vow: (Cns:) (%>:) (»:) ( Dummy: ) _ (%>:) (»:) ( Dummy: ) (Hyph) [ # | #: ] ;
       where Cx in ( b9 d9 g9 m9 h9 )
             Cy in ( p  t  k  v  j  )
	   matched ;
!€ iemed9#
!€ iemet0

"Word final weakening -tj and -ttj to -sj part 1"
 t:s <= Vow: (%>:) (»:) ( Dummy: ) ( d: ) _ j (%>:) (»:) ( Dummy: ) (Hyph) [ # | #: ] ;


"Word final weakening -tj and -ttj to -sj part 2"
 t:s => Vow: (%>:) (»:) ( Dummy: ) ( d: ) _ j (%>:) (»:) ( Dummy: ) (Hyph) [ # | #: ] ;
 KEEPIF(SHORTCOMP)
        Vow:               h:0 _ [j|s] Vow:0 (%>:) (»:) X9: ; !facultative in first-part compounds, ts too
 ENDKEEP
! t:s <=> Vow: (%>:) (»:) ( Dummy: ) _ ( t: ) j (%>:) (»:) ( Dummy: ) (Hyph) #: ;
!€ jågåtj#
!€ jågåsj0

!"Word final weakening of -ttj to -sj"
! t:0 <=> Vow: t:s _ j (%>:) (»:) (Hyph) #: ;
! oajválattja : oajválasj This is moved to the t:0 rule set.
! Still, this will fail if and when clitics are added to the word.
! d:s changed to d:

"Word Final Deletion of n8 m8 g8 h8"
  Cx:0 <=> Vow _  (%>:) (»:) X3: ;
	where Cx in ( DelCns ) ;
! The twoL-sme had [X4:|X8:|X6: Cons:|X9:], be prepared to
! expand this rule accordingly.

!€ loavddag8X3#
!€ l0åv0da000


"Word Final Neutralization of g8, h8, m8"
  Cx:Cy <=> Vow _  (%>:) (»:) X2: (Hyph) [ # | #: ] ;
			where Cx in (g8 h8 m8)
			      Cy in (t  t  n )
			matched ;
! The twol-sme had [X2: | X7: ], be prepared to expand this rule accordingly.
! Since we use X2, the "2" in g8 etc. may be superfluous...

!"Word-final Devoicing of d9, m9"
! Cx:Cy <=> Vow _  (%>:) (»:) (Hyph) #: ;
!       where Cx in ( d9 m9 )
!	          Cy in ( t  v  )
!	     matched ;
! iemet:iemeda


"Deleting Final h9 in Short Essive of Uneven Syllables"
 h9:0 <=> Vow: _ (%>:) (»:) Q1: X1: n ;

"Deleting Final l9 in Short Essive of Uneven Syllables"
 l9:0 <=> Vow: _ (%>:) (»:) [Q1:|X3:] X1: n ;

"Deleting Final m9 in Short Essive of Uneven Syllables"
 m9:0 <=> Vow: _ (%>:) (»:) Q1: X1: n ;

"Deleting Final n9 in Short Essive of Uneven Syllables"
 n9:0 <=> Vow: _ (%>:) (»:) Q1: X1: n ;

"Deleting Final r9 in Short Essive of Uneven Syllables"
 r9:0 <=> Vow: _ (%>:) (»:) Q1: X1: n ;



!      where Cx in ( s m h ) ;
! originally it was true for s, m, h. Keep an eye on this.

!"Word Final Cluster Simplification"  ! málestit : máles
! t:0 <=> Vow (%>:) (»:) ( Dummy: ) s _ (%>:) (»:) (Hyph) (Hyph) #:  ;
!be prepared to define more rules on cluster simpl.
!moved to Consonant gradation
!€ málest#
!€ máles00


! Vowel lengthening
! -----------------

"Compulsatory lengthening in grade I even-syllables"
! a:á <=>  Cns  [ a|i|u ] [ Cns:0 Cns: | Cns: Cns:0 ] _ (%>:) (»:) WeG: ; ! in grade I
  a:á <=> (Cns) [ a|i|u ] [ Cns:0 Cns: (%>:) (»:) | Cns: Cns:0 (%>:) (»:) ] _ (Cns:0 | Cns:) (%>:) (»:) WeG: ; ! in grade I
          (Cns) [ a|i|u ]   Cns:0 [ n | s | t ] j     _ ( StemCns: ) (%>:) (»:) WeG: ; ! nj, sj, tj in grade I
          (Cns) [ a|i|u ]   Cns:0        t s          _ (%>:) (»:) WeG: ; ! ts in grade I
	                        Cns                       _ (%>:) (»:) Y2:0 ; ! Indicative Present Singular 3rd Final Vowel in verbs
                            Cns:*                     _ (%>:) (»:) Y6:0 X4:0 j ; ! Ill sg of Actor nouns bassat : basse : bas'sáj
                            Cns:*                     _ (%>:) (»:) Y7: ; ! Diminutive of Actor nouns bassat : basse : bassásj
                                                      _ (%>:) (»:) Y6: X4: (X4:0) (X8:0) [ m | t | d | s ] ; ! Compulsary e:á in Px, Actors! sállat : sálle : sállám
! where Vx in ( a i u ) ; ! not all simple vowels ( e o å æ ) are evidently long!
!€ gussaQ1#
!€ gu0sá00
!€ skihpaQ1s#
!€ ski0bá0s0

! the right-context facultative consonant in order to ensure skibás.
!"Compulsatory lengthening in palatal grade I even-syllables"
! a:á <=> Cns [ a|i|u ] [ n | s | t ] j _ WeG: ; ! nj, sj, tj in grade I
! where Vx in ( a i u ) ; ! not all simple vowels ( e o å æ ) are evidently long!
! example?
! delete if preceeding works


! Diphtong simplification
! -----------------------

"oa:å Diphtong Simplification Part I"
 o:0 <=> [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] _ a: LowerG12  [ a: | i: ] (%>:) (»:) WeG: ; !1 WeG of a- and i-stems
         [ 0 | [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] _ a: LowerG12 e: (%>:) (»:) Q1: ( X5: ) j ; !2 ComSg & G-C Pl of e-stems
!        [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] _ a: LowerG12 a: (%>:) (»:) Y6: ( X5: ) j ; !3 ComSg & G-C Pl of Actor nouns from a-verbs, G1
!        [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] _ a: LowerG12 e: (%>:) (»:) Y6: ( X5: ) j ; !4 ComSg & G-C Pl of Actor nouns from e-verbs, G1
         [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] _ a: LowerG12 e: (%>:) (»:) Y1: ; !5 Different verb forms
         [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] _ a: LowerG12 o: (%>:) (»:) X4: j ; !6 Ill
         [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] _ a: LowerG12 o: (Cns:*) (%>:) (»:) [ Y1: | Y2: | Y3: | Y4: | Y5: | X7: | X4: (X8:) | [Q1:|Q4:] X4: X8: ] ; !7 Long pass, Dim and Prs Sg3 etc. Px
!        [ #: | Cns: | (%>:) (»:) Dummy: ] _ a: LowerG12 o: (Cns:*) (%>:) (»:) [ Y1: | Y2: | Y3: | Y4: | Y5: | Y7: | Y9: | X7: ] ; !7 Long pass, Dim and Prs Sg3 etc.
         [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] _ a: LowerG12 o: Cns:+ (%>:) (»:) X5: Vow ; !8 odd-syll, oblique
         [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] _ a: LowerG12 a [g8:|l9:|s] (%>:) (»:) X3: (X1:) [ (Hyph) [ # | #: ] | n ] ; !9 NE låvda
         [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] _ a: LowerG12 e: (%>:) (»:) Q1:0 t: j [ [ # | #: ] | [Vow:[v|n|h|b|p|[ # | #: ]]]]; !10 Potensialis doadjet:dåjitjav
         [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] _ a: LowerG12 e: (%>:) (»:) Q4: ( X5: ) j ; !11 ComSg & G-C Pl of e-stems
         [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] _ a: LowerG12 o:u (%>:) (»:) [ Q1: | Q4: ] X5: j n ([a:|i:]) ; !12Px Com
         [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] _ a: G32 o:u (%>:) (»:) WeG: ;
         [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] _ a: G32 e:i (%>:) (»:) WeG: ;
         [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] _ a: G32 [ a: | i: ] (%>:) (»:) WeG: ;



"oa:å Diphtong Simplification Part II"
 a:å <=> [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] o: _ LowerG12  [ a: | i: ] (%>:) (»:) WeG: ;       !1 WeG of a- and i-stems
         [ 0 | [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] o: _ LowerG12 e: (%>:) (»:) Q1: ( X5: ) j ;       !2 ComSg & G-C Pl of e-stems
!        [ #: | Cns: | (%>:) (»:) Dummy: ] o: _ LowerG12 a: (%>:) (»:) Y6: ( X5: ) j ;       !3 ComSg & G-C Pl of Actor nouns from a-verbs, G1
!        [ #: | Cns: | (%>:) (»:) Dummy: ] o: _ LowerG12 e: (%>:) (»:) Y6: ( X5: ) j ;       !4 ComSg & G-C Pl of Actor nouns from e-verbs, G1
         [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] o: _ LowerG12 e: (%>:) (»:) Y1: ;       !5 Different verb forms
         [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] o: _ LowerG12 o: (%>:) (»:) X4: j ; !6 Ill
         [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] o: _ LowerG12 o: (Cns:*) (%>:) (»:) [ Y1: | Y2: | Y3: | Y4: | Y5: | X7: | X4: (X8:) | [Q1:|Q4:] X4: X8: ] ; !7 Long pass and Prs Sg3 etc.  Px
!        [ #: | Cns: | (%>:) (»:) Dummy: ] o: _ LowerG12 o: (Cns:*) (%>:) (»:) [ Y1: | Y2: | Y3: | Y4: | Y5: | Y7: | Y9: | X7: ] ; !7 Long pass and Prs Sg3 etc.
         [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] o: _ LowerG12 o: Cns:+ (%>:) (»:) X5: Vow ;       !8 odd-syll, oblique
         [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] o: _ LowerG12 a [g8:|l9:|s] (%>:) (»:) X3: (X1:) [ (Hyph) [ # | #: ] | n ] ;       !9 NE låvda
         [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] o: _ LowerG12 e: (%>:) (»:) Q1:0 t: j [ [ # | #: ] | [Vow:[v|n|h|b|p|[ # | #: ]]]]; !10 Potensialis doadjet:dåjitjav
         [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] o: _ LowerG12 e: (%>:) (»:) Q4: ( X5: ) j ;       !11 ComSg & G-C Pl of e-stems.
         [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] o: _ LowerG12 o:u (%>:) (»:) [ Q1: | Q4: ] X5: j n ([a:|i:]) ; !12Px Com
         [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] o: _ G32 o:u (%>:) (»:) WeG: ; !  X2 X3 X7 Y5 Q1 Q2 Q3
         [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] o: _ G32 e:i (%>:) (»:) WeG: ;
         [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] o: _ G32 [ a: | i: ] (%>:) (»:) WeG: ;



!€ toahkkeY6X5jn
!€ toahkki00jn

!$ toahkkeY6X5jn
!$ t0åhkki00jn

!€ boalloX4j
!€ b0ållu0j

! BUG 193:
!€ roavggoX4j
!€ roavggu0j
!$ roavggoX4j
!$ r0åvggu0j

! G3:G2, fake ex
! Here, G2 allows diphtsimpl
!€ toas'soQ1X5jn
!€ t0ås0su00jn

! And vice versa
!$ toas'soQ1X5jn
!$ toas0su00jn

! G3:G3, lexical ex, no CG
! will have to have a different lexicon in noun-smj-lex,
! not pointing to Q1. Cf. GOAHTI/STAHTA sme.
!$ moas'soX5jn
!$ m0ås0su0jn

!€ moas'soX5jn
!€ moas0su0jn

! The next example shows actor noun in G3 (actor nouns are always G3)
!€ goar'roY6X5jn
!€ goar0ru00jn
! This test should hit, but fails, since G3 looks like G2 at the lower level.
! Further clever thinking is needed here.

! this is G2 > G3. Derived G3 from a G2 stem (actor noun)
!€ goarroY6X5jn
!€ goarru00jn
! This example should be like this (no diphsimpl), but at present our analysator gives diphtsimpl
! as it is G2. Whe have two possibilities:
! a. find a way of saying that it is G3 (impossible, I think)
! b. block the diphtsimpl not based upon (invisible) G3, but upon its triggering context.

! The next example shows errouneous dipht simpl for G3.
!$ goar'roY6X5jn
!$ g0år0ru00jn

! Here, G3 blocks diph simpl
!$ goar'roY2
!$ g0år0ru0

! The following example shows correct  diphtong simplification in G2 (3Sg pres)
!€ goarroY2
!€ g0årru0

!€ doa0djeY6
!€ doaddje0

!$ doa0djeY6
!$ d0åddje0

!G3 > G2
!€ goar'roY5d9it
!€ g0år0ru0dit

!$ goar'roY5d9it
!$ goar0ru0dit

! The following tests are using fictive, non-existent verbs, to test the correctnes of the mapping rules in twolc
! the verbs are *toabmot, oadot, etc.
! X4 for o:u in illsg, pres. part

!€ toa0bmoY6X4j
!€ toabbmu00j

!€ toabmoX4j
!€ t0åbmu0j

!$ toa0bmoY6X4j
!$ t0åbbmu00j

!€ toabmoX7dallat
!€ t0å0mu0dallat
!$ toabmoX7dallat
!$ toa0mu0dallat

!€ oaddoY6X4j
!€ oaddu00j

!€ boassjkoQ1X5jn
!€ b0å0sjku00jn

!$ boassjkoQ1X5jn
!$ boas0jku00jn

!€ boajsstoQ1X5jn
!€ b0åj0stu00jn

!$ boajsstoQ1X5jn
!$ boaj0stu00jn

!€ boaggoQ1X5jn
!€ b0åkku00jn

!$ boaggoQ1X5jn
!$ boakku00jn


KEEPIF(SHORTCOMP)
"Facultative oa:å Diphtong Simplification Part I"
 o:0 => _ a: Cns:+ Vow:0 (%>:) (»:) X9: ; !Facultative in first-part of compounds

"Facultative oa:å Diphtong Simplification Part II"
 a:å => o: _ Cns:+ Vow:0 (%>:) (»:) X9: ; !Facultative in first-part of compounds
!	 [ #: | Cns: | (%>:) (»:) Dummy: ] o: _ Cns:* o: (%>:) (»:) ( Dummy:* - Q3: ) j ; ! o-stems, before j. !rm when tested
! The rule complexes are identical, but fix different parts of the monophtongisation.
! Note that the "o-stems before j" part is restricted to Ill & Com Sg, cf. bug #136.
ENDKEEP

! All real sámi æ's are also ä's, but not in these contexts, where other rules will take over.
"æ:ä Blocking"
 æ:ä /<= _ Cns:+ [ o: (%>:) (»:)                                            X4:                            j              |
                   o:    (Cns:*) (%>:) (»:) [ Y1: | Y2: | Y3: | Y4: | Y5: | X7: | X4: (X8:) | [Q1:|Q4:] X4: X8:]          |
                   o:     Cns:+ (%>:) (»:)                                  X5:                                           |
                   a:     (%>:) (»:) WeG:                                                                                 |
                   o:u   (%>:) (»:) (WeG:)                                             ([Q1:|Q4:] X5:      j n ([a:|i:])) |
                   Vow:0 (%>:) (»:)                                                                   X9:                 ] ;
! Rationale:
! Stop change to ä in all Real Sámi contexts. The idea is to keep Swedish Snälla from becoming Lule Snelav etc.


!$mæddoQ1jn
!$mätto0jn

!$mæddoQ1X5jn
!$mättu00jn

!$mæddoX4j
!$mäddu0j


! æ:e diphthong simplificaton


"æ:e Diphthong Simplification 1"
 æ:e <=  [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] _ LowerG12 o: (%>:) (»:) X4: j ; !6 Ill
         [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] _ LowerG12 o: (Cns:*) (%>:) (»:) [ Y1: | Y2: | Y3: | Y4: | Y5: | X7: | X4: (X8:) | [Q1:|Q4:] X4: X8:] ; !7 Long pass, Dim and Prs Sg3 etc. !Px Nom, acc, gen
!        [ #: | Cns: | (%>:) (»:) Dummy: ] _ LowerG12 o: (Cns:*) (%>:) (»:) [ Y1: | Y2: | Y3: | Y4: | Y5: | Y7: | Y9: | X7: ] ; !7 Long pass, Dim and Prs Sg3 etc.
         [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] _ LowerG12 o: Cns:+ (%>:) (»:) X5: Vow ; !8 odd-syll, oblique
         [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] _ LowerG12 a: (%>:) (»:) ; !  WeG of a-stems !Errouneously gives vællahit:vellahit
         [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] _ LowerG12 o:u (%>:) (»:) [ Q1: | Q4: ] X5: j n ([a:|i:]) ; !Px Com
         [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] _ G32      o:u (%>:) (»:) WeG: ;
         [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] _ G32      a: (%>:) (»:) WeG: ;
KEEPIF(SHORTCOMP)
                                _ Cns:+    Vow:0 (%>:) (»:)       X9: ; !Facultative in first-part of compounds
ENDKEEP

"æ:e Diphthong Simplification 2"
 æ:e  => [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] _ LowerG12 o: (%>:) (»:) X4: j ; !6 Ill
         [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] _ LowerG12 o: (Cns:*) (%>:) (»:) [ Y1: | Y2: | Y3: | Y4: | Y5: | X7: | X4: (X8:) | [Q1:|Q4:] X4: X8:] ; !7 Long pass, Dim and Prs Sg3 etc. !Px Nom, acc, gen
!        [ #: | Cns: | (%>:) (»:) Dummy: ] _ LowerG12 o: (Cns:*) (%>:) (»:) [ Y1: | Y2: | Y3: | Y4: | Y5: | Y7: | Y9: | X7: ] ; !7 Long pass, Dim and Prs Sg3 etc.
         [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] _ LowerG12 o: Cns:+ (%>:) (»:) X5: Vow ; !8 odd-syll, oblique
         [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] _ LowerG12 a: (%>:) (»:) ; !  WeG of a-stems
         [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] _ LowerG12 o:u (%>:) (»:) [Q1:|Q4:] X5: j n ([a:|i:]) ; !Px Com
         [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] _ G32      o:u (%>:) (»:) WeG: ; !  X2 X3 X7 Y5 Q1 Q2 Q3
         [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] _ G32      a:  (%>:) (»:) WeG: ;



"ie:æ Diphthong Simplification Part I"
 i:0 <=> [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] _ e: Cns:* e: (%>:) (»:) X4: ;     !1
     	 [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] _ e: Cns:* e: (%>:) (»:) [ Q2: | Y2: | Y3: ] ;     !2 Prs Sg of e-stem verbs
     	 [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] _ e: Cns:* e: Cns: (%>:) (»:) X5: Vow ; !3 odd-syll, oblique
                                _ e: (Cns) Cns:+ e:á (Cns:+) (%>:) (»:) [X5:|Y2:|Q2:|Y3:|X7:|Y4:|X4:(X5:|Q1:)|Y6: X4:|Y7:|[Q1:|Q4:] X4: (X4:) X8:]  ; ! Vovel change in front of stem vowel variation
                                _ e: (Cns) Cns:+ i:á (%>:) (»:) Y5: Cns:+ ;     !Der/r
                                _ e: [[:d|:b|:g] [d|b|g] Cns:+ | Cns:+ | h :Cns Cns:+] e:0 (%>:) (»:) [ Y1: | Y9: ] u ; !ielvvet:ælvvu, ielvvet:ælvvup short passive, ImprtII
                                _ e: Cns:+ e:0 (%>:) (»:) Y8: o ; !ielvvet:ælvvop, Imprt
                                _ e: Cns:+ e:0 (%>:) (»:) Q8: u ; !giesset:gessus, ImprtII


!€ ielvveY9ut
!€ 0ælvv00ut

!€ iehttseY1up
!€ 0æhtts00up

!€ giesseQ8us
!€ g0ess00us


! Note !!! These three rules are supposed to operate in tandem, and shound ALWAYS be kept in synch, except for the operator and operand.
"ie:æ Diphthong Simplification Part IIa" ! Norwegian æ is an option...
 e:æ  =>  [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] i: _ Cns:* e: (%>:) (»:) X4: ;  !1
          [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] i: _ Cns:* e: (%>:) (»:) [ Q2: | Y2: | Y3: ] ;  !2 Prs Sg of e-stem verbs
          [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] i: _ Cns:* e: Cns: (%>:) (»:) X5: Vow ;  !3 odd-syll, oblique
                                 i: _ (Cns) Cns:+ e:á (Cns:+) (%>:) (»:) [X5:|Y2:|Q2:|Y3:|X7:|Y4:|X4:(X5:|Q1:)|Y6: X4:|Y7:|[Q1:|Q4:] X4: (X4:) X8:]  ; ! Vovel change in front of stem vowel variation
                                 i: _ (Cns) Cns:+ i:á (%>:) (»:) Y5: Cns:+  ;                                     !Der/r
                                 i: _ [[:d|:b|:g] [d|b|g] Cns:+ | Cns:+ | h :Cns Cns:+] e:0 (%>:) (»:) [ Y1: | Y9: ] u ; !ielvvet:ælvvu, short passive, ImprtII
                                 i: _ Cns:+ e:0 (%>:) (»:) Y8: o ;                                     !ielvvet:ælvvop, Imprt
          [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ]    _ Cns Cns:+ a:á (%>:) (»:) ;                                     ! Verbs: e:æ only Vowel change in front of stem vowel variation, bessat : bæssá
!         [ #: | Cns: | (%>:) (»:) Dummy: ]    _ Cns:* a: (%>:) (»:) Y6: ( X5: ) ;                                     ! Verbs: e:æ only Actor nouns
          [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ]    _ ( [:d|:b|:g] [d|b|g] Cns:+ | Cns:+ | h :Cns Cns:+) a: (%>:) (»:) Y6: ( X5: ) ([Hyph|(Hyph) [ # | #: ]]) ; !bessat:bæsse, imprt, du2 and Actor

"ie:ä Diphthong Simplification Part IIb" ! Swedish ä is an option...
 e:ä  =>  [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] i: _ Cns:* e: (%>:) (»:) X4: ;  !1
          [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] i: _ Cns:* e: (%>:) (»:) [ Q2: | Y2: | Y3: ] ;  !2 Prs Sg of e-stem verbs
          [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] i: _ Cns:* e: Cns: (%>:) (»:) X5: Vow ;  !3 odd-syll, oblique
                                 i: _ (Cns) Cns:+ e:á (Cns:+) (%>:) (»:) [X5:|Y2:|Q2:|Y3:|X7:|Y4:|X4:(X5:|Q1:)|Y6: X4:|Y7:|[Q1:|Q4:] X4: (X4:) X8:] ; ! Vovel change in front of stem vowel variation
                                 i: _ (Cns) Cns:+ i:á (%>:) (»:) Y5: Cns:+ ;                                     !Der/r
                                 i: _ [[:d|:b|:g] [d|b|g] Cns:+ | Cns:+ | h :Cns Cns:+] e:0 (%>:) (»:) [ Y1: | Y9: ] u ;     !ielvvet:ælvvu, short passive, ImprtII
                                 i: _ Cns:+ e:0 (%>:) (»:) Y8: o ;                                     !ielvvet:ælvvop, Imprt
          [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ]    _ Cns Cns:+ a:á (%>:) (»:) ;                                     ! Verbs: e:æ only Vowel change in front of stem vowel variation, bessat : bæssá
!         [ #: | Cns: | (%>:) (»:) Dummy: ]    _ Cns:* a: (%>:) (»:) Y6: ( X5: ) ;                                     ! Verbs: e:æ only Actor nouns
          [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ]    _ ( [:d|:b|:g] [d|b|g] Cns:+ | Cns:+ | h :Cns Cns:+) a: (%>:) (»:) Y6: ( X5: ) ([Hyph|(Hyph) [ # | #: ]]) ; !bessat:bæsse, imprt, du2 and Actor

"ie:ä Diphthong Simplification Part IIc" ! And there are no other options. This rule to block the e, and allow for the æ and ä.
 e:e  /<= [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] i: _ Cns:* e: (%>:) (»:) X4: ;  !1
          [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] i: _ Cns:* e: (%>:) (»:) [ Q2: | Y2: | Y3: ] ;  !2 Prs Sg of e-stem verbs
          [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] i: _ Cns:* e: Cns: (%>:) (»:) X5: Vow ;  !3 odd-syll, oblique
                                 i: _ (Cns) Cns:+ e:á (Cns:+) (%>:) (»:) [X5:|Y2:|Q2:|Y3:|X7:|Y4:|X4:(X5:|Q1:)|Y6: X4:|Y7:|[Q1:|Q4:] X4: (X4:) X8:] ; ! Vovel change in front of stem vowel variation
                                 i: _ (Cns) Cns:+ i:á (%>:) (»:) Y5: Cns:+ ;                                     !Der/r
                                 i: _ [[:d|:b|:g] [d|b|g] Cns:+ | Cns:+ | h :Cns Cns:+] e:0 (%>:) (»:) [ Y1: | Y9: ] u ;     !ielvvet:ælvvu, short passive, ImprtII
                                 i: _ Cns:+ e:0 (%>:) (»:) Y8: o ;                                     !ielvvet:ælvvop, Imprt
          [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ]    _ Cns Cns:+ a:á (%>:) (»:) ;                                     ! Verbs: e:æ only Vowel change in front of stem vowel variation, bessat : bæssá
!         [ #: | Cns: | (%>:) (»:) Dummy: ]    _ Cns:* a: (%>:) (»:) Y6: ( X5: ) ;                                     ! Verbs: e:æ only Actor nouns
          [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ]    _ ( [:d|:b|:g] [d|b|g] Cns:+ | Cns:+ | h :Cns Cns:+) a: (%>:) (»:) Y6: ( X5: ) ([Hyph|(Hyph) [ # | #: ]]) ; !bessat:bæsse, imprt, du2 and Actor


! Note: Check that Q2 above does not overgenerate to nouns!

!€ jeh0tsaY6
!€ jæhttse0

!€ jeh0tsaY6
!€ jähttse0


!"Diphthong simplification whenever weak grade in a-stems I"
! Vx:0 <=> _ Vy:Vz Cns:* a ( Cns:0 ) (%>:) (»:) WeG: ;
!      where Vx in ( o i )
!	    Vy in ( a e )
!	    Vz in ( å æ )
!	    matched ;

!"Diphtong simplification whenever weak grade in a-stems II"
! Vy:Vz <=> Vx:0 _ Cns:* a ( Cns:0 ) (%>:) (»:) WeG: ;
!      where Vx in ( o i )
!            Vy in ( a e )
!            Vz in ( å æ )
!	    matched ;
!€ boarkkaQ1
!€ b0år0ka0
!€ loavddag8X3#
!€ l0åv0da000



!"Vovel change in front of stem vowel variation"
! æ:e <=> _ Cns Cns:+ o:u (%>:) (»:) ; ! ditto

"Vowel-change oa:å for verbs part I"
 å:o <=> [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] _ 0: [Cns:+|:d d] a:á (%>:) (»:) ; ! hållat:hoallá, presSg3
         [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] _ 0: ( [:d|:b|:g] [d|b|g]  Cns:+ | Cns:+ | h :Cns Cns:+ | :d d ) a: (%>:) (»:) [ Y6: | Y7: | Y9: ] ( X5: ) ([Hyph|(Hyph) [ # | #: ]]) ; ! hållat:hoalle, imprtDu2

"Vowel-change oa:å for verbs part II"
 0:a <=> [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] å: _ [Cns:+|:d d] a:á (%>:) (»:) ;
         [ [ # | #: ] | Cns: | (%>:) (»:) Dummy: ] å: _ ( [:d|:b|:g] [d|b|g] Cns:+ | Cns:+ | h :Cns Cns:+ | :d d ) a: (%>:) (»:) [ Y6: | Y7: | Y9: ] ( X5: ) ([Hyph|(Hyph) [ # | #: ]]) ;

!€ hå0llaY2
!€ hoallá0

!€ gå00dnaY6
!€ goaddne0

!$ hå0llaY2
!$ hållá0

!??? jf jehtsat. TO: jehtsat:jæhttse
!€ gå0h0tsaY6
!€ goahttse0


! Stem vowel alternations
! -----------------------

! a-stem alternations
! - - - - - - - - - -


"a:e in Present Participle of even-syllable verbs"
! a:e <=> Cns: _ (%>:) (»:) Y6: ;
 a:e <=> Cns: _ (%>:) (»:) Y6:0 [Hyph|(Hyph) [ # | #: ]|n:|s:|v:|m:|t:|d:] ;
! bassat : bas'se

!Error message, delete this after testing.
! **** Conflicting pair(s) of contexts:
!  Cns:* _ (%>:) (»:) Y6:0 X4:0 j ;
!  Cns: _ (%>:) (»:) Y6: ;
!Left context example:     b
!Right context example:    Y6:0 X4:0 j


"a:i in Prs Prc of even-syllable verbs"
 a:i <=> Cns: _ (%>:) (»:) Y6:0 ( X5:0 ) j ([Hyph|(Hyph) [ # | #: ]]) ;
!€ bas'saY6jt#
!€ bas0si0jt0

"a-stem vowel deletion"
 a:0 <=> Cns: _ (%>:) (»:) [ Y8: | Y9: ] ; !in even-syllable verbs, imp 1du, 1pl and short passives
 KEEPIF(SHORTCOMP)
         Cns: _ (%>:) (»:) X9: ; !in first-part compounds



! á-stem alternations
! - - - - - - - - - -

"á-stem vowel deletion"
 á:0 <=>  Cns: _ (%>:) (»:) X9: ; !in first-part compounds
ENDKEEP

! e-stem alternations
! - - - - - - - - - -


! e:i
! - -

"e:i in e-stems"
 e:i <=> Cns: _ (%>:) (»:) [ Q1:0 | Q4:0 ] ( X5:0 ) j ; ! 1) WeG or not, e:i, X5 is added to get e:i also in ComSg.
         Cns: _ (%>:) (»:) [ Y6:0 | Q7:0 ] (X5:0) j ([Hyph|(Hyph) [ # | #: ]]) ; ! 2) Actor nouns
         Cns: _ (%>:) (»:) Q1:0 t: j [ [ # | #: ] | [Vow:[v|n|h|b|p|[ # | #: ]]]] ; ! 3) "e:i in potentialis"


! 1) biesse -> biesij
!€ biesseQ1j
!€ bie0si0j

! 2) boahtte -> boahttij
!€ boah0teY6j
!€ boahtti0j

! 3)
!€ gálleQ1tj#
!€ gá0li0sj0

!€ gálleQ1tjav#
!€ gá0li0tjav0

!€ gálleQ1tjin#
!€ gá0li0tjin0

!€ gálleQ1tjihpit#
!€ gá0li0tjihpit0

!€ gálleQ1tjibá#
!€ gá0li0tjibá0

!€ gálleQ1tjip#
!€ gá0li0tjip0

!€ gálleQ1tja#
!€ gá0li0tja0


! e:á
! - -

! The following two rules constitute a <= / => rule pair.

"e:á in certain stem types 1"
 e:á <=  [ Vow: - å ] ([ 0:Cns Cns: | Cns: 0:Cns ]) (Cns:+) _ (%>:) (»:) (Y6:0) X4:0 (X4:0) (X8:0) [m|t|d|s i [m|t|s|m m e|h t t e|s k a|s á]] ; ! 1) Compulsory e:á in Px! bárnne : bárnnám
[ Vow: - å ] Cns:+ _ (%>:) (»:) X4: (X5: | Q1: )  [ [ # | #: ] | v | n | s | j | t | d | g | b | p ] ; ! 2) Ill Sg (context (1) only in part 2)
         [ Vow: - å ] ([ 0:Cns Cns: | Cns: 0:Cns ]) (Cns:+) _ (%>:) (»:) Y6:0 X4:0 j ; ! 3) Ill Sg Actor nouns
         [ Vow: - å ] Cns:+ _ Cns:+ (%>:) (»:) X5: Vow ;     ! 4) Uneven N.
         [ Vow: - å ] Cns:+ _ (%>:) (»:) Y2: ;        ! 5) Indicative Present Singular 3rd Final Vowel in verbs
         [ Vow: - å ] Cns:+ _ (%>:) (»:) Y3: ;              ! 6) PrfPrc, gállet : gállám
         [ Vow: - å ] Cns:+ _ (%>:) (»:) Q2: ;              ! 7) gállet : gáláv
         [ Vow: - å ] Cns:+ _ (%>:) (»:) X7: ;              ! 8) báhko : bágusj, goahte : goadásj
         [ Vow: - å ] ([ 0:Cns Cns: | Cns: 0:Cns ]) (Cns:+) _ (%>:) (»:) Y7: ;              ! 9) PrsPrc dim
	     [ Vow: - å ] Cns:+ _ (%>:) (»:) (Y6:0) [ Q1:0 | Q4:0 ] X4:0 X8:0 ;        ! 10) 1st, 2nd Gen/Acc Px declension
         [ Vow: - å ] Cns:+  _ Cns:* (%>:) (»:) Y4: Vow ;    ! 11)verb der. alla
!         [Cns:|#:] [ a|i|u ] [ Cns:0 Cns: | Cns: Cns:0 ] _ (%>:) (»:) Y5: Cns:+ ; !12) verb Der/(u/a/å)d
         d: i: e: h: t: t: _ (%>:) (»:) Q7: X4: ;
! 2)
!€ bálggeX4v
!€ bálggá0v

! 6)
!€ gálleY3m#
!€ gállá0m0

! 7)
!€ gálleQ2v#
!€ gá0lá0v0

! 8)
!€ báhkoX7tj#
!€ bá0gu0sj0

!€ goahteX7tj#
!€ goa0dá0sj0

!$ goahteX7tj#
!$ go00dá0sj0


! e:á <=> [ Vow: - å ] Cns* _ (%>:) (»:) X4:0 [ j #: | s Vow: ] ;

"e:á in certain stem types 2"
 e:á => [ Vow: - å ] ([ 0:Cns Cns: | Cns: 0:Cns ]) (Cns:+) _ (%>:) (»:) (Y6:0) X4:0 (X4:0)  (X8:0) [m|t|d|s i [m|t|s|m m e|h t t e|s k a|s á]] ; ! 1) Compulsary e:á in Px! bárnne : bárnnám
        [ Vow: - å ] Cns:+ _ (%>:) (»:) X4: (X5:)  [ [ # | #: ] | v | n | s | j | t | d | g | b | p ] ; ! 2) Ill Sg
        [ Vow: - å ] ([ 0:Cns Cns: | Cns: 0:Cns ]) (Cns:+) _ (%>:) (»:) Y6:0 X4:0 j ;               ! 3) Ill Sg Actor Nouns
        [ Vow: - å ] Cns:+ _ Cns:+ (%>:) (»:) X5: Vow ;              ! 4) Uneven N
        [ Vow: - å ] Cns:+ _ (%>:) (»:) Y2: ;                 ! 5) Indicative Present Singular 3rd Final Vowel in verbs
        [ Vow: - å ] Cns:+ _ (%>:) (»:) Y3: ;                       ! 6) PrfPrc, gállet : gállám
        [ Vow: - å ] Cns:+ _ (%>:) (»:) Q2: ;                       ! 7) gállet : gáláv
        [ Vow: - å ] Cns:+ _ (%>:) (»:) X7: ;                       ! 8) báhko : bágusj, goahte : godásj
        [ Vow: - å ] ([ 0:Cns Cns: | Cns: 0:Cns ]) (Cns:+) _ (%>:) (»:) Y7: ;                       ! 9) PrsPrc dim
        [ Vow: - å ] Cns:+ _ (%>:) (»:) (Y6:0) [ Q1:0 | Q4:0 ] X4:0 X8:0 ;                 ! 10)1st, 2nd Gen/Acc Px declension
        [ Vow: - å ] Cns:+  _ Cns:* (%>:) (»:) Y4: Vow ;             ! 11)verb der. alla
!       [Cns:|#:] [ a|i|u ] [ Cns:0 Cns: | Cns: Cns:0 ] _ (%>:) (»:) Y5: Cns:+  ; ! 12) verb Der/(u/a/å)d
        d: i: e: h: t: t: _ (%>:) (»:) Q7: X4: ;

! 1)
!€ bárnneX4m
!€ bárnná0m

!$ bárnneX4m
!$ bárnne0m


! The X7 rule could be replaced by a general vowel changer, combined with Q1 in
! the lexicon entry for the Dim suffix.


!"e:á in 1st, 2nd person Gen/Acc Px declension." ! bárnne : bárnám, bárnes
! e:á <=> _ (%>:) (»:) Q1:0 X8:0 ;
! The rule for optional e:á does not work. Thus, it is commented out, and
! the Px paradigm only gets ám etc. Return to this issue.


! e:å
! - -

"e:å in certain stem types with å as root vowel"
 e:å <=> å Cns:* _ (%>:) (»:) X4:0 [ j [ # | #: ] | s Vow: ] ;  ! 1) [ADD examples here]
         å Cns:* _ Cns:+ (%>:) (»:) X5: Vow ;            ! 2) [ADD examples here]
         å Cns:* _ (%>:) (»:) Q2:0 ;                    ! 3) gådet : gådåv, jåhtet : jådåv
         å Cns:* _ (%>:) (»:) Y2:0 ;                    ! 4) gådet : gådå,  jåhtet : jåhtå
         å Cns:* _ (%>:) (»:) Y3:0 ;                    ! 5) jåhtet : jåhtåm
         å Cns:* _ (%>:) (»:) Y5:0 ;                    ! 6) verb derivation
         å ([ 0:Cns Cns: | Cns: 0:Cns ]) (Cns:+) _ (%>:) (»:) Y7:0 t: j [ (Hyph) [ # | #: ] | Vow: ] ; ! 7) noun derivation låhkke:låhkkåsj
         å Cns:* _ (%>:) (»:) Y7:0 X7:0 l ;             ! 8) noun derivation låhkke:låhkålasj
         å ([ 0:Cns Cns: | Cns: 0:Cns ]) (Cns:+) _ (%>:) (»:) Y6:0 X4:0 j  [ [ # | #: ] | Cns: ] ;             ! 9) noun derivation
         å Cns:+ _ (%>:) (»:) X7: ;                     ! 10) dåhpe : dåbåsj

	 	! The first subrule gives Ill. sg. of even-stem
! No shorter version here, bec. of no å-å effect
! The second subrule gives oblique of uneven stem
! The third one is vowel harmony, e:å after root vowel å

! 3)
!€ gådeQ2v
!€ gådå0v
!€ jåhteQ2v
!€ jå0då0v

! 4)
!€ gådeY2
!€ gådå0
!€ jåhteY2
!€ jåhtå0

! 5)
!€ jåhteY3m
!€ jåhtå0m

! 7)
!€ låhkkeY7tj
!€ låhkkå0sj



! e:u
! - -

!"e:u in Verb Derivations" !This holds only for one word (TO)
! e:u <=> Cns:* _ (%>:) (»:) Y4:0 ;
! árret : árrusit

! e:a
! ----

!"e:a in Verb Derivation" !not productive
! e:a <=> [Cns:|#:] [ (Vow:) Vow: - [å:|a:|i:|u:] ] Cns:* _ (%>:) (»:) Y5:0 ; ! láhppet : láhpadit

! e:0
! ----
 "e-stem vowel deletion"
  e:0 <=> Vow: (%>:) (»:) (Dummy:) [ # | #: ] g:k _ [ # | #: ] ; !clitic ge:k
          Cns: _ (%>:) (»:) [ Y8: | Y9: ] ; !in even-syllable verbs, imp 1du, 1pl and short passives

!€ ielvveY9ut
!€ 0ælvv00ut


KEEPIF(SHORTCOMP)
          Cns: _ (%>:) (»:) X9: ; !in first-part compounds
 ENDKEEP


! i-stem alternations
! - - - - - - - - - -


! i:á
! - -

"i:á in Verb Derivation"
 i:á <=> Cns:* _ (%>:) (»:) Y5:0 ;
! guollit : guollár

KEEPIF(SHORTCOMP)
"i-stem vowel deletion"
 i:0 <=> Cns: _ (%>:) (»:) X9: ; !in first-part compounds
ENDKEEP

! o-stem alternations
! - - - - - - - - - -

! The duplicates of the three lines of the two following rules are
! there to resolve the => conflict between the two rules.

"o:u in certain stem types 1"
o:u  => Cns:  _ (%>:) (»:) X4: j ; !X4 IllSg
        Cns:  _ (%>:) (»:) (Q1:0 | Q4:0) X5:0 j n ; ! Q1 X5 ComSg. !parenthesis (Q1:0) bec of no CG in certain words. elujn
        Cns:  _ Cns:+ (%>:) (»:) X5: Vow ; !Odd-syllable
        Cns:  _ (%>:) (»:) [ Y1: | Y2: | Y3: ] ; ! Y1=  Y2= Y3=PrfPrc
        Cns:* _ (%>:) (»:) Y5: ; !verb der. (u/a/å)d
        Cns:  _ Cns:* (%>:) (»:) Y4: Vow ; !verb der. alla
        Cns:  _ (%>:) (»:) Y6:0 X5:0 j n ; ! Com sg of Actor nouns elujn
        Cns:  _ (%>:) (»:) Y6:0 X4:0 j ; ! IllSg of Actor nouns
        Cns:* _ (%>:) (»:) X7: ; ! báhko : bágusj, goahte : godásj
        Cns:* _ (%>:) (»:) Y7: ; ! PrsPrc dim
              _ (%>:) (»:) (Y6:0) X4:0 (X8:0) ;        ! Px áhkko:áhkkum
              _ (%>:) (»:) [Q1:0|Q4:0] X4:0 X8:0 ;        !  1st, 2nd Gen/Acc Px declension
              _ (%>:) (»:) (Y6:0) ([Q1:0|Q4:0]) X5: j n [a [m|t]|i [s|m m e|h t t e|s k a|s á]] ; !Px Com


"o:u in certain stem types 2"
o:u <= Cns:  _ (%>:) (»:) X4: j ; !X4 IllSg
!       Cns:  _ (%>:) (»:) (Q1:0 | Q4:0) X5:0 j n ; !parenthesis (Q1:0) bec of no CG in certain words. outcommented because rule is facultative:elujn/ælojn
        Cns:  _ Cns:+ (%>:) (»:) X5: Vow ;  !Odd-syllable
        Cns:  _ (%>:) (»:) [ Y1: | Y2: | Y3: ] ; ! Y1=  Y2= Y3=PrfPrc
        Cns:* _ (%>:) (»:) Y5: ; !verb der. (u/a/å)d
        Cns:  _ Cns:* (%>:) (»:) Y4: Vow ; !verb der. alla
        Cns:  _ (%>:) (»:) Y6:0 X5:0 j n ; ! Com sg of Actor nouns elujn
        Cns:  _ (%>:) (»:) Y6:0 X4:0 j ; ! IllSg of Actor nouns
        Cns:* _ (%>:) (»:) X7: ; ! báhko : bágusj, goahte : godásj
        Cns:* _ (%>:) (»:) Y7: ; ! PrsPrc dim
              _ (%>:) (»:) (Y6:0) X4:0 (X8:0) ;        ! Px áhkko:áhkkum
              _ (%>:) (»:) [Q1:0|Q4:0] X4:0 X8:0 ;        ! 1st, 2nd Gen/Acc Px declension
              _ (%>:) (»:) (Y6:0) ([Q1:0|Q4:0]) X5: j n [a [m|t]|i [s|m m e|h t t e|s k a|s á]] ; !Px Com



!Old o:u rules
! o:u <=> Cns: _ (%>:) (»:) [ X4: j | Q1: X5: ] ; !X4 IllSg, Q1 X5 ComSg.
! o:u <=> Cns: _ (%>:) (»:) X4: j ; !X4 IllSg, Q1 X5 ComSg.

"u:o in contracted nouns"
 u:o <=> Cns:* _ (%>:) (»:) [ Q3: | X6: ] ; !æddnu:ænoj, boahttsu:boatsoj

"o-stem vowel deletion"
 o:0 <=> Cns: _ (%>:) (»:) Y9:0 ; !in short passives
 KEEPIF(SHORTCOMP)
          Cns: _ (%>:) (»:) X9: ; !in first-part compounds
ENDKEEP

! å-stem alternations
! - - - - - - - - - -

"å:e in Present Participle of even-syllable verbs"
 å:e <=> Cns: _ (%>:) (»:) Y6: ([Hyph|(Hyph) [ # | #: ]]) ;
         Cns: _ (%>:) (»:) Y6: [
 d a
 |m (a)
 |n
 |n a [m|t]
 |[ s t | n ] i [ h t t e | m m e | s | s [ k a | á ] ]
 |s (k) (a)
 |s t a [m|t]
 |t
 |v
 ] :0 - X4: (:0 - X8:) [:0 - s:|:0 - m: |:0 - t: |:0 - d: (a:) ]  ; !All this to prevent change in Px
! låhkåt : låhkke

"å:i in Actor nouns of even-syllable verbs"
 å:i <=> Cns: _ (%>:) (»:) Y6:0 ( X5:0 ) j ([Hyph|(Hyph) [ # | #: ]]) ;
! låhkåt : låhkkijt

"å-stem vowel deletion"
 å:0 <=> Cns: _ (%>:) (»:) [ Y8: | Y9: ] ; !in even-syllable verbs, imp 1du, 1pl and short passives
 KEEPIF(SHORTCOMP)
         Cns: _ (%>:) (»:) X9: ; !in first-part compounds
ENDKEEP

! alternations valid for several stem types
! - - - - - - - - - - - - - - - - - - - - -

"Stem vowel deletion in even-syllable verbs, imp 3sg, 3du, 2pl, 3pl"
 Vx:0 <=> Cns: _ (%>:) (»:) [Y1:0|Q8:] ;
      where Vx in ( a å e ) ;

!€ ielvveY1up
!€ 0ælvv00up

!€ giessaY1up
!€ giess00up

!€ bårråY1up
!€ bårr00up

!€
!€


! Consonant gradation rules
! -------------------------

! New rules, without conflicts
! The numbering of rules refer to the overview in Spiiks grammar
! Thus !S7-3 means "Series 1, 3-letter patterns", etc.

! Deletion rules
! - - - - - - - -

"Consonant gradation b:0"
b:0 <=> Vow: ( [l|b] )     _ m         Vow: (StemCns:) (%>:) (»:) WeG: ; ! S7-3, S7-2 bbn:b0n, bn:0n
!	    Vow:           _ [d|j|l|n|r|s] Vow: (StemCns:) (%>:) (»:) WeG: ; ! S7-2
	    Vow: [ j|l|r ] _ b             Vow: (StemCns:) (%>:) (»:) WeG: ; ! S9-3
	    Vow:           _ b: m          Vow: (%>:) (»:) X6:             ; ! III-I special gradation ! bbm:m
	    Vow:   b:      _    m          Vow: (%>:) (»:) X6:             ; ! III-I special gradation ! bbm:m
KEEPIF(SHORTCOMP)
        Vow: ( b )     _    m          Vow:0 (%>:) (»:) X9: ; !first part of compounds
	    Vow: [ j|l|r ] _ b             Vow:0 (%>:) (»:) X9: ; ! first part of compounds
        Vow:   b:      _               Vow:0 (%>:) (»:) X9: ; ! first part of compounds
ENDKEEP

"Consonant gradation d:0"
d:0 <=> Vow: ( d )             _ [ n | j ] Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S7-3, S7-2 ddn:d0n, dn:0n
        Vow: ( d )             _   n   j   Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S7-4 and S7, dnj:nj
        Vow: [ b | j | r | v ] _ d         Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9
        Vow:                   _ d: n      Vow: (%>:) (»:) X6: ;             ! III-I special gradation ddn:n
        Vow: d:                _    n      Vow: (%>:) (»:) X6: ;             ! III-I special gradation ddn:n
        Vow:                   _ t:s j (%>:) (»:) (Hyph) [ # | #: ]  ;             ! oajváladtj# : oajvála0sj0
KEEPIF(SHORTCOMP)
        Vow: ( d )             _ [ n | j ] Vow:0 (%>:) (»:) X9: ;             !first part of compounds
        Vow: ( d )             _   n   j   Vow:0 (%>:) (»:) X9: ;             !first part of compounds
        Vow: [ b | j | r | v ] _ d         Vow:0 (%>:) (»:) X9: ;             !first part of compounds
        Vow: d:                _           Vow:0 (%>:) (»:) X9: ;             !first part of compounds
ENDKEEP

"Consonant gradation g:0"
g:0 <=> Vow: ( g )       _    [ñ|ń|ŋ|l|n] Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S7-3, S7-2
    	Vow: [ j|l|r|v ] _ g          Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9
    	Vow:             _ g: [ñ|ń|ŋ] Vow: (%>:) (»:) X6: ; ! III-I special gradation
    	Vow:   g:        _    [ñ|ń|ŋ] Vow: (%>:) (»:) X6: ; ! III-I special gradation
KEEPIF(SHORTCOMP)
        Vow: ( g )       _    [ñ|ń|ŋ] Vow:0 (%>:) (»:) X9: ; !first part of compounds
        Vow: [ j|l|r|v ] _ g          Vow:0 (%>:) (»:) X9: ; !first part of compounds
        Vow:   g:        _            Vow:0 (%>:) (»:) X9: ; !first part of compounds
ENDKEEP

"Consonant gradation k:0"
k:0 <=> Vow:     k         _ n   Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S7-3
    	Vow:     k         _ t j Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S7-4
    	Vow: [ j|l|m|r|v ] _ k   Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9
    	Vow:     h         _ k   Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S4, preasp stops
KEEPIF(SHORTCOMP)
	    Vow:     k         _ n   Vow:0 (%>:) (»:) X9: ; !first part of compounds
		Vow:     k         _ t j Vow:0 (%>:) (»:) X9: ; !first part of compounds
	    Vow: [ j|l|m|r|v ] _ k   Vow:0 (%>:) (»:) X9: ; !first part of compounds
		Vow:     h         _ k   Vow:0 (%>:) (»:) X9: ; !first part of compounds
ENDKEEP

"Consonant gradation l:0"
l:0 <=> Vow:   l       _ [ d|j|t|n ] Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S7-3
		Vow:   l       _ t j         Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S7-4
		Vow:           _ l           Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S8, II-I
		Vow: [ j|b|v ] (h) _ l       Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9
KEEPIF(SHORTCOMP)
	    Vow:   l       _ [ d|j|t|n ] Vow:0 (%>:) (»:) X9: ; !first part of compounds
		Vow:   l       _ t j         Vow:0 (%>:) (»:) X9: ;  !first part of compounds
		Vow:           _ l           Vow:0 (%>:) (»:) X9: ; !first part of compounds
		Vow: [ j|b|v ] (h) _ l       Vow:0 (%>:) (»:) X9: ; !first part of compounds
ENDKEEP

"Consonant gradation m:0"
m:0 <=> Vow: m         _ [ b|p ] Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S7-3
		Vow:           _ m        Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S8, II-I
		Vow: [ j|l|r|v ] _ m       Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9
		Vow:           _ (%>:) (»:) Q1: X1: n ; ! Deleting Final Cns in Short Essive of Uneven Syllables
KEEPIF(SHORTCOMP)
	    Vow: m         _ [ b|p ] Vow:0 (%>:) (»:) X9: ; !first part of compounds
		Vow:           _ m        Vow:0 (%>:) (»:) X9: ; !first part of compounds
		Vow: [ j|l|r|v ] _ m       Vow:0 (%>:) (»:) X9: ; !first part of compounds
ENDKEEP

"Consonant gradation n:0"
n:0 <=> Vow:         n           _ d           Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S7-3
		Vow:         n           _ t ([ j|s ]) Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S7-4
		Vow:                     _ n ( j )       Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S8, II-I, also palat.
		Vow: [ b|j|l|ñ|ń|ŋ|r|v|m|g ] _ n           Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9, III-II
		Vow: [ b|r|v ]           _ n j         Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9-4, III-II
KEEPIF(SHORTCOMP)
	    Vow:         n           _ d           Vow:0 (%>:) X9: ; !first part of compounds
		Vow:         n           _ t [ j|s ]   Vow:0 (%>:) X9: ; !first part of compounds
		Vow:                     _ n ( j )       Vow:0 (%>:) X9: ; !first part of compounds
		Vow: [ b|j|l|ñ|ń|ŋ|r|v ] _ n           Vow:0 (%>:) X9: ; !first part of compounds
		Vow: [ b|r|v ]           _ n j         Vow:0 (%>:) X9: ; !first part of compounds
ENDKEEP

"Consonant gradation p:0"
p:0 <=> Vow:         p   _ (%>:) (»:) ( j ) Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S7-3, S7-4 j
        Vow:         p   _ t s           Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S7-4 ppts:pts
        Vow: [ j|l|r|v ] _ p             Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9
        Vow:   h         _ p             Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S4 hpp:hp
        Vow:         p   _ k             Vow: ( StemCns: ) (%>:) (»:) WeG: ;
KEEPIF(SHORTCOMP)
        Vow:         p   _ [ s|t ] ( j ) Vow:0 (%>:) (»:) X9: ; !first part of compounds
        Vow:         p   _ t s           Vow:0 (%>:) (»:) X9: ; !first part of compounds
        Vow: [ j|l|r|v ] _ p             Vow:0 (%>:) (»:) X9: ; !first part of compounds
        Vow:   h         _ p             Vow:0 (%>:) (»:) X9: ; !first part of compounds
ENDKEEP

"Consonant gradation s:0"
s:0 <=> Vow:                   _ s ( [ j|k|m|n|t|p|v|ń|ŋ|ñ ] ) (%>:) (»:) Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S7-3, S8 II-I
        Vow:                   _ s j [ k|m|v ]       Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S7-4
        Vow: [ b|j|l|m|n|r|v ] _ s                   Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9
        Vow: [ r|l|m ]         _ s j                 Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9-4
        Vow: [ r|l|n|j ]       _ s k                 Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9-4
        Vow: [ j|m|n|v|l ]     _ s t                 Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9-4
        Vow: r                 _ s j t               Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9 rssjt:rsjt
        Vow: s:                _ (%>:) (»:) (Hyph) [ # | #: ] ;                                         ! This is a word fin simpl, ss# -> s#, and not CG
        Vow:                   _ (%>:) (»:) Q1: X1: n ;                                  ! Deleting Final Cns in Short Essive of Uneven Syllables
KEEPIF(SHORTCOMP)
        Vow:                   _ s ( [ j|k|m|n|t ] ) Vow:0 (%>:) (»:) X9: ; !first part of compounds
        Vow:                   _ s j [ k|m|v ]       Vow:0 (%>:) (»:) X9: ; !first part of compounds
        Vow: [ b|j|l|m|n|r|v ] _ s                   Vow:0 (%>:) (»:) X9: ; !first part of compounds
        Vow: [ r|l|m ]         _ s j                 Vow:0 (%>:) (»:) X9: ; !first part of compounds
        Vow: [ r|l|n|j ]       _ s k                 Vow:0 (%>:) (»:) X9: ; !first part of compounds
        Vow: [ j|m|n|v ]       _ s t                 Vow:0 (%>:) (»:) X9: ; !first part of compounds
        Vow: r                 _ s j t               Vow:0 (%>:) (»:) X9: ; ! No real consonant gradation, rather consonant reduction in compounds (X9)
        Vow: h:0 t:s           _                     Vow:0 (%>:) (»:) X9: ; ! No real consonant gradation, rather consonant reduction in compounds (X9)
ENDKEEP

!"Consonant gradation s:0"
!s:0 <=> Vow: s                 _ ( [ j|k|m|n|t ] ) Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S7-3, S8 II-I
!        Vow: s                 _ j [ k|m|v ]       Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S7-4
!        Vow: [ b|j|l|m|n|r|v ] _ s                 Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9
!        Vow: [ r|l|m ]         _ s j               Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9-4
!        Vow: [ r|l|n ]         _ s k               Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9-4
!        Vow: [ j|m|n|v ]       _ s t               Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9-4
!        Vow: r s               _ j t               Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9 rssjt:rsjt
!        Vow: s:                _ (%>:) (»:) (Hyph) #: ;                                       ! This is a word fin simpl, ss# -> s#, and not CG
!        Vow:                   _ (%>:) (»:) Q1: X1: n ;                                ! Deleting Final Cns in Short Essive of Uneven Syllables

!        Vow: s                 _ ( [ j|k|m|n|t ] ) Vow:0 (%>:) (»:) X9: ; !first part of compounds
!        Vow: s                 _ j [ k|m|v ]       Vow:0 (%>:) (»:) X9: ; !first part of compounds
!        Vow: [ b|j|l|m|n|r|v ] _ s                 Vow:0 (%>:) (»:) X9: ; !first part of compounds
!        Vow: [ r|l|m ]         _ s j               Vow:0 (%>:) (»:) X9: ; !first part of compounds
!        Vow: [ r|l|n ]         _ s k               Vow:0 (%>:) (»:) X9: ; !first part of compounds
!        Vow: [ j|m|n|v ]       _ s t               Vow:0 (%>:) (»:) X9: ; !first part of compounds
!        Vow: r s               _ j t               Vow:0 (%>:) (»:) X9: ; ! No real consonant gradation, rather consonant reduction in compounds (X9)
!        Vow: h:0 t:s           _                   Vow:0 (%>:) (»:) X9: ; ! No real consonant gradation, rather consonant reduction in compounds (X9)

"Consonant gradation ŋ:0"
Cx:0 <=> Vow:             _ Cy ( [ g|k ] ) Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S7-3, S8 II-I
         Vow: [ j|l|r|v ] _ Cy          Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9
         Vow:          Cy _ k t         Vow: ( StemCns: ) (%>:) (»:) WeG: ;
KEEPIF(SHORTCOMP)
         Vow:             _ Cy ( [ g|k ] ) Vow:0 (%>:) (»:) X9: ; !first part of compounds
         Vow: [ j|l|r|v ] _ Cy          Vow:0 (%>:) (»:) X9: ; !first part of compounds
ENDKEEP
	where Cx in ( ń ŋ ñ )
		  Cy in ( ń ŋ ñ )
    matched ;



"Consonant gradation f:0"
f:0 <=> Vow: ( [r|l] ) _ f Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S8 ff:f, S9 rff:rf
        Vow:         f _ t Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S8 ff:f, S9 rff:rf

KEEPIF(SHORTCOMP)
        Vow: ( [r|l] ) _ f   Vow:0 (%>:) (»:) X9: ; !first part of compounds
ENDKEEP

"Consonant gradation r:0"
r:0 <=> Vow: ( [ b|j|v ] ) _ r Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S8 rr:r, S9 brr etc.
KEEPIF(SHORTCOMP)
        Vow: ( [ b|j|v ] ) _ r   Vow:0 (%>:) (»:) X9: ; !first part of compounds
ENDKEEP

"Consonant gradation v:0"
v:0 <=> Vow: ( [ j|l|r ] ) _ v Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S8 , S9 jvv etc.
KEEPIF(SHORTCOMP)
        Vow: ( [ j|l|r ] ) _ v   Vow:0 (%>:) (»:) X9: ; !first part of compounds
ENDKEEP

"Consonant gradation j:0"
j:0 <=> Vow: [b|r] _ j Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S9
KEEPIF(SHORTCOMP)
        Vow: [b|r] _ j   Vow:0 (%>:) (»:) X9: ; !first part of compounds
ENDKEEP

"Consonant gradation t:0"
t:0 <=> Vow: [j|ñ|ń|ŋ|r|v] _ t             Vow: ( StemCns: ) (%>:) (»:) WeG: ;             ! S9 jtt:jt etc.
    	Vow: [b|j|r|v]     _ t j           Vow: ( StemCns: ) (%>:) (»:) WeG: ;           ! S9
      	Vow: [b|j|v|r]     _ t s           Vow: ( StemCns: ) (%>:) (»:) WeG: ;           ! S9
    	Vow:       h:      _ t ( [ j|s ] ) Vow: ( StemCns: ) (%>:) (»:) WeG: ; ! S4 htt(j)
        Vow:       t:s     _ j (%>:) (»:) (Hyph) [ # | #: ] ;                                 ! Word final weakening of -ttj to -sj
        Vow (%>:) (»:) (Dummy:) s     _ (%>:) (»:) (Hyph) [ # | #: ]  ;                           ! W-Final Cl. Simpl. málestit : máles
        Vow:       h:      _ t ( [ j|s ] ) Vow: (%>:) (»:) X6: ;               ! III-I special gradation
                                                  ! Second last rule will fail if and when clitics are added to the word.
                                                  ! Note that there is no weak grade clitic here.
KEEPIF(SHORTCOMP)
        Vow: [j|ñ|ń|ŋ|r|v] _ t             Vow:0 (%>:) (»:) X9: ; !first part of compounds
    	Vow: [b|j|r|v]     _ t j           Vow:0 (%>:) (»:) X9: ; !first part of compounds
    	Vow: [b|j|v|r]     _ t s           Vow:0 (%>:) (»:) X9: ; !first part of compounds
    	Vow:       h:      _ t ( [ j|s ] ) Vow:0 (%>:) (»:) X9: ; !first part of compounds
ENDKEEP

!"Gradation Series 4, II-I, h Loss 1"
!€ oajváladtj#
!€ oajvála0sj0

!h:0 <=> Vow: _ [ k: | p: | t: ] Vow: ( StemCns: ) (%>:) (»:) WeG: ;
!        Vow: _ (%>:) (»:) Q1: X1: n ; ! Deleting Final Cns in Short Essive of Uneven Syllables

"Gradation Series 4, II-I, tj and ts"
 h:0 <=> Vow: _ [ k: | p: | t: ] Vow: ( StemCns: ) (%>:) (»:) WeG: ;
         Vow: _ t [j|s] Vow: ( StemCns: ) (%>:) (»:) WeG: ;
         Vow: _ t: t: ( [ j|s ] ) Vow: (%>:) (»:) X6: ; ! III-I special gradation
         Vow: _ (%>:) (»:) Q1: X1: n ; ! Deleting Final Cns in Short Essive of Uneven Syllables
KEEPIF(SHORTCOMP)
         Vow: _ [ k: | p: | t: ]   Vow:0 (%>:) (»:) X9: ; !first part of compounds
         Vow: _ ( t: ) [j|s]   Vow:0 (%>:) (»:) X9: ; !first part of compounds
ENDKEEP
! (t:) for III-I CG)

!"Word final double consonant simplification"
!Cy:0 <=> Vow: Cx: _ (%>:) (»:) (Hyph) #: ;
!     where Cx in ( s )
!	   Cy in ( s )
!	   matched ;
! Moved to the CG chapter. But does this rule wait for more cases?



! Change rules
! - - - - - -


"Gradation Series 4, II-I"
 Cx:Cy <=> Vow: h:0 _ Vow: ( StemCns: ) (%>:) (»:) WeG: ;
 where Cx in ( k p t )
       Cy in ( g b d )
       matched ;

!"Gradation Series 5, III-II, dtj, dts"
! d:t <=> Vow: (»:) _ t Cx Vow: ( StemCns: ) (%>:) (»:) WeG: ;
! where Cx in ( j s ) ;

"bb:pp"
b:p <=> Vow:    _ b:  Vow: ( StemCns: ) (%>:) (»:) WeG: ;
        Vow: b: _     Vow: ( StemCns: ) (%>:) (»:) WeG: ;
KEEPIF(SHORTCOMP)
        Vow:    _   b:0 Vow:0 (%>:) (»:) X9: ; !first part of compounds
ENDKEEP
!       Vow:    _ (%>:) (»:) (Hyph) #:0 ;  ! This gives Jakob:Jakop, and it is unclear what it gives.
! The last rule is for unnep : unnebu. It must be revised in two ways:
! a. it should be extended to more consonants
! b. it should be restricted, perhaps b should be replaced with b8

!€ oabbáQ1
!€ oappá0

"gg:kk"
g:k <=> Vow:    _ g: Vow: ( StemCns: ) (%>:) (»:) WeG: ;
  	    Vow: g: _ Vow: ( StemCns: ) (%>:) (»:) WeG: ;
KEEPIF(SHORTCOMP)
	    Vow:    _  g:0 Vow:0 (%>:) (»:) X9: ; !first part of compounds
ENDKEEP

!€ vággeQ1
!€ vákke0

!$ vággeQ1
!$ vágge0

!€ vággeX9
!€ vák000

!$ vággeX9
!$ vág000

"g:k change for clitic -ge"
g:k => Vow: (%>:) (»:) (Dummy:) [ # | #: ] _ e: [ # | #: ] ; !clitic ge:k

"dd:tt and dtj, dts"
d:t <=> Vow: _ d: Vow: ( StemCns: ) (%>:) (»:) WeG: ;
	Vow: d: _  Vow: ( StemCns: ) (%>:) (»:) WeG: ;
KEEPIF(SHORTCOMP)
	Vow: _   d:0 Vow:0 (%>:) (»:) X9: ; !first part of compounds
ENDKEEP
	Vow: (»:) _ t [ j|s ] (%>:) (»:) Vow: ( StemCns: ) (%>:) (»:) WeG: ;


!"Gradation Series 6, III-II, bb, gg 1"
! Cx:Cy <=> Vow: _ Cz: Vow: ( StemCns: ) (%>:) (»:) WeG: ;
! where Cx in ( b d g )
!       Cy in ( p t k )
!       Cz in ( b d g )
!       matched ;

!"Gradation Series 6, III-II, bb, gg 2"
! Cx:Cy <=> Vow: :Cy _ Vow: ( StemCns: ) (%>:) (»:) WeG: ;
! where Cx in ( b d g )
!       Cy in ( p t k )
!       matched ;

"Gradation Series 7, III-II, ks(t), kt, ktj, kts"
 k:v <=> Vow: _ [ s|t ] (t) Vow: ( StemCns: ) (%>:) (»:) WeG: ;
	 Vow: _ t [ j|s ] Vow: ( StemCns: ) (%>:) (»:) WeG: ;

! Exceptional II-III inverse gradation in present participles
! -----------------------------------------------------------

! This gradation is only for II-I syllable verbs that get III as
! present participles.

! Candidates:
!! bbm - bm - m
!! ddn - dn - n
!! ddnj- dnj- nj
!! ggŋ - gŋ - ŋ
!! ddj - dj - dj

!! hkk - hk - g
!! hpp - hp - b

!! htt - ht - d
!! httj- htj- tj
!! htts- hts- ts

! Strategy: Do insertion rule for the initial element.

"Consonant insertion as II-III strengthening gradation with bm,gŋ"
 0:Cx <=> :Vow+ _ Cx Cy Vow: (%>:) (»:) [ Y6: | Y7: | Y9: ] ([Hyph|(Hyph) [ # | #: ]]) ;
      where Cx in ( b g )
            Cy in ( m ŋ )
	    matched ;

"Consonant insertion as II-III strengthening gradation with dn/j"
 0:d <=> :Vow+ _ d [ n | n j | j ] Vow: (%>:) (»:) [ Y6: | Y7: | Y9: ] ([Hyph|(Hyph) [ # | #: ]]) ;

"Consonant insertion as II-III strengthening gradation with hk, hp,"
 0:Cx <=> :Vow+ h _ Cx Vow: (%>:) (»:) [ Y6: | Y7: | Y9: ] ([Hyph|(Hyph) [ # | #: ]]) ;
      where Cx in ( k p ) ;

"Consonant insertion as II-III strengthening gradation with htt(j/s)"
 0:t <=> :Vow+ h _ t (j|s) Vow: (%>:) (»:) [ Y6: | Y7: | Y9: ] ([Hyph|(Hyph) [ # | #: ]]) ; !removed Q7, fixed in smj-lex

"Consonant insertion as I-III strengthening gradation with d"
 0:d <=> :Vow+ _ d Vow: (%>:) (»:) [ Y6: | Y7: | Y9: ] ([Hyph|(Hyph) [ # | #: ]]) ;



!Did more changes to the ñ issue: Replaced ñ with all three n-s also when it alternated with other letters.
!In the cases where ñ was the one to be deleted, in the context of double ñ, we made Cx / Cy variables, e.g. like !this:


