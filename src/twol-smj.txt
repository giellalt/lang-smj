
! ========================================================================== !
!                   Lule Sámi morphophonological rule set                    !
! ========================================================================== !

! twol-smj.txt
! The file contains the rule set for the non-segmental 
! Lule Sámi morphphonological rules

Alphabet
 a á b c d e f g h i j k l m n ñ o p q			! small
 r s t u v w x y z å ä ø ö å æ %-
 é ó ú í à è ò ù ì ë ü ï â ê ô û î ã ý þ ð ß ª ß ç

 A Á B C D E F G H I J K L M N Ñ O P Q			! capital
 R S T U V W X Y Z Å Ä Ø Ö Å Æ
 É Ó Ú Í À È Ò Ù Ì Ë Ü Ï Â Ê Ô Û Î Ã Ý þ Ð

 ':0			        ! Gradation mark

 h2:h g2:g m2:m n2:n		! these are deleted in nom
 b3:b d3:d g3:g m3:m h3:h r3:r	! these can not occur before #

 X1:0 X2:0 X3:0 X4:0 X5:0 X6:0 X7:0 X8:0 X9:0  ! Diacritics
 Q1:0 Q2:0 Q3:0 Q4:0 Q5:0 Q6:0 Q7:0 Q8:0 Q9:0  ! They trigger morphophono-
 Y1:0 Y2:0 Y3:0 Y4:0 Y5:0 Y6:0 Y7:0 Y8:0 ;     ! logical rules



Sets
 Vow = a á e i o u y æ ä ø ö å
       é ó ú í à è ò ù ì ë ü ï â ê ô û î ã ý ;
 Cns = b b3 c d d3 f g g2 g3 h h2 h3 j k l l3 m m2 m3 n n2 n3 ñ p q r r3 s t v w x z ' ;
! We cannot have set operations here.
! CnsPhon = [ Cns | n j | s j | t j | t s ] ;
! G3 = [ [ CnsPhon CnsPhon CnsPhon:+ ] - [[[j|l|m|n|v] s k ] | [l|r] s k | r s j t ] ] ;
! G2 = [ CnsPhon CnsPhon | [[[j|l|m|n|v] s k ] | [l|r] s k | r s j t ] ] ;
! G1 = CnsPhon ;
! G12 = [ CnsPhon | [ CnsPhon CnsPhon | [[[j|l|m|n|v] s k ] | [l|r] s k | r s j t ] ] ] ;
 DelCns = g2 m2 n2 h2 ;
 WeG = X2 X3 X7 Y5 Q1 Q2 Q3 ;
 StemCns = b b3 d d3 g g2 g3 h h2 h3 j l l3 m m2 m3 n n2 n3 ñ r r3 s ;
 Dummy = X2 X3 X4 X5 X6 X7 X8 Y1 Y2 Y3 Y4 Y5 Y6 Y7 Q1 Q2 Q3 ;


Rules


! Consonant alternations in certain pos
! -------------------------------------

! This is a dummy negative test, in order to get the test paradigm work.
!$ a
!$ b


"Word Final Devoicing of Certain Single Consonants d3 etc."
 Cx:Cy <=> Vow: ( Dummy: ) _ ( Dummy: ) #: ;
       where Cx in ( b3 d3 g3 m3 h3 )
	     Cy in ( p  t  k  v  j  )
	     matched ;
!€ iemed3#
!€ iemet0

"Word final weakening -tj and -ttj to -sj"
 t:s <=> Vow: ( Dummy: ) ( d: ) _ j ( Dummy: ) #: ;
! t:s <=> Vow: ( Dummy: ) _ ( t: ) j ( Dummy: ) #: ;
!€ jågåtj# 
!€ jågåsj0

!"Word final weakening of -ttj to -sj" 
! t:0 <=> Vow: t:s _ j #: ;
! oajválattja : oajválasj This is moved to the t:0 rule set.
! Still, this will fail if and when clitics are added to the word.
! d:s changed to d:
!€ oajváladtj#
!€ oajválas0j0

"Word Final Deletion of n2 m2 g2 h2"
  Cx:0 <=> Vow _  X3: ;
	where Cx in (DelCns) matched ;
! The twol-sme had [X4:|X8:|X6: Cons:|X9:], be prepared to 
! expand this rule accordingly.


"Word Final Neutralization of g2, h2, m2"
  Cx:Cy <=> Vow _  X2: #: ;
			where Cx in (g2 h2 m2)
				Cy in (t t n)
			matched ;
! The twol-sme had [X2: | X7: ], be prepared to expand this rule accordingly.
! Since we use X2, the "2" in g2 etc. may be superfluous...

!"Word-final Devoicing of d3, m3"
! Cx:Cy <=> Vow _ #: ;
!       where Cx in ( d3 m3 )
!	     Cy in ( t v )
!	     matched ;
! iemet:iemeda


"Deleting Final h3 in Short Essive of Uneven Syllables"
 h3:0 <=> Vow: _ Q1: X1: n ;
 
"Deleting Final l3 in Short Essive of Uneven Syllables"
 l3:0 <=> Vow: _ Q1: X1: n ;
 
"Deleting Final m3 in Short Essive of Uneven Syllables"
 m3:0 <=> Vow: _ Q1: X1: n ;
 
"Deleting Final n3 in Short Essive of Uneven Syllables"
 n3:0 <=> Vow: _ Q1: X1: n ;
 
"Deleting Final r3 in Short Essive of Uneven Syllables"
 r3:0 <=> Vow: _ Q1: X1: n ;

 
 
!      where Cx in ( s m h ) ;
! originally it was true for s, m, h. Keep an eye on this.

!"Word Final Cluster Simplification"  ! málestit : máles
! t:0 <=> Vow ( Dummy: ) s _ (Hyph) #:  ;			
!be prepared to define more rules on cluster simpl.
!moved to Consonant gradation 
!€ málest#
!€ máles00


! Vowel lengthening
! -----------------

"Compulsatory lengthening in grade I even-syllables"
! a:á <=> Cns [ a|i|u ] [ Cns:0 Cns: | Cns: Cns:0 ] _ WeG: ; ! in grade I
  a:á <=> (Cns) [ a|i|u ] [ Cns:0 Cns: | Cns: Cns:0 ] _ (Cns) WeG: ; ! in grade I
          (Cns) [ a|i|u ] Cns:0 [ n | s | t ] j _ WeG: ; ! nj, sj, tj in grade I
          (Cns) [ a|i|u ] Cns:0 t s _ WeG: ; ! ts in grade I
	 Cns _ Y2:0 ; ! Indicative Present Singular 3rd Final Vowel in verbs
     Cns:* _ Y6:0 X4:0 j ;!Ill sg of Actor nouns bassat : basse : bas'sáj
     Cns:* _ Y7: ;!Diminutive of Actor nouns bassat : basse : bassásj
! where Vx in ( a i u ) ; ! not all simple vowels ( e o å æ ) are evidently long!
!€ gussaQ1#
!€ gus0á00
!€ skihpaQ1s#
!€ ski0bá0s0

! the right-context facultative consonant in order to ensure skibás.
!"Compulsatory lengthening in palatal grade I even-syllables"
! a:á <=> Cns [ a|i|u ] [ n | s | t ] j _ WeG: ; ! nj, sj, tj in grade I
! where Vx in ( a i u ) ; ! not all simple vowels ( e o å æ ) are evidently long!
! example?
! delete if preceeding works


! Diphtong simplification
! -----------------------

"oa:å Diphtong Simplification Part I"
 o:0 <=> [ #: | Cns: | Dummy: ] _ a: Cns:* [ a: | i: ] WeG: ; ! WeG of a- and i-stems
!	 [ #: | Cns: | Dummy: ] _ a: Cns:* e: Q1: j ; ! ComSg & G-C Pl of e-stems
	 [ #: | Cns: | Dummy: ] _ a: Cns:* e: Q1: ( X5: ) j ; ! ComSg & G-C Pl of e-stems
     [ #: | Cns: | Dummy: ] _ a: Cns:* a: Y6: ( X5: ) j ; ! ComSg & G-C Pl of Actor nouns from a-verbs
     [ #: | Cns: | Dummy: ] _ a: Cns:* e: Y6: ( X5: ) j ; ! ComSg & G-C Pl of Actor nouns from e-verbs
	 [ #: | Cns: | Dummy: ] _ a: Cns:* e: Y1: ; ! Different verb forms
	 [ #: | Cns: | Dummy: ] _ Cns:* o: [ X4: | Q1: X5: ] j ; !Ill&ComSg. !instead of !line
	 [ #: | Cns: | Dummy: ] _ a: Cns:* o: Cns: X5: Vow ; ! odd-syll, oblique
	 [ #: | Cns: | Dummy: ] _ a: Cns:* a g2: X3: [#: | n ] ; !NE låvda
      _ Vow [ CnsPhon | [ CnsPhon CnsPhon ] | [[[j|l|m|n|v] s k ] | [l|r] s k | r s j t ] ]  o:u ;
!     _ Vow ( Cns: ) Cns: o:u ; ! Vovel change in front of stem vowel variation
!	 [ #: | Cns: | Dummy: ] _ a: Cns:* o: (Dummy:* - Q3:) j ; ! even-syll o-st, before j. ! rm when tested

"oa:å Diphtong Simplification Part II"
 a:å <=> [ #: | Cns: | Dummy: ] o: _ Cns:* [ a: | i: ] WeG: ; ! WeG of a- and i-stems
!	 [ #: | Cns: | Dummy: ] o: _ Cns:* e: Q1: j ; ! ComSg & G-C Pl of e-stems
     [ #: | Cns: | Dummy: ] o: _ Cns:* e: Q1: ( X5: ) j ; ! ComSg & G-C Pl of e-stems
     [ #: | Cns: | Dummy: ] o: _ Cns:* a: Y6: ( X5: ) j ; ! ComSg & G-C Pl of Actor nouns from a-verbs
     [ #: | Cns: | Dummy: ] o: _ Cns:* e: Y6: ( X5: ) j ; ! ComSg & G-C Pl of Actor nouns from e-verbs
	 [ #: | Cns: | Dummy: ] o: _ Cns:* e: Y1: ; ! Different verb forms
	 [ #: | Cns: | Dummy: ] _ Cns:* o: [ X4: | Q1: X5: ] j ; !Ill&ComSg. !instead of !line
	 [ #: | Cns: | Dummy: ] o: _ Cns:* o: Cns: X5: Vow ; ! odd-syll, oblique
	 [ #: | Cns: | Dummy: ] o: _ Cns:* a g2: X3: [#: | n ] ; !NE låvda
      _ [ CnsPhon | [ CnsPhon CnsPhon] | [[[j|l|m|n|v] s k ] | [l|r] s k | r s j t ] ] o:u ;
!	 _ ( Cns: ) Cns: o:u ; ! Vovel change in front of stem vowel variation
!€ boalloX4j
!€ b0ållu0j
!$ roavggoX4j
!$ r0åvggu0j
!	 [ #: | Cns: | Dummy: ] o: _ Cns:* o: ( Dummy:* - Q3: ) j ; ! o-stems, before j. !rm when tested
! The rule complexes are identical, but fix different parts of the monophtongisation.
! Note that the "o-stems before j" part is restricted to Ill & Com Sg, cf. bug #136.


"æ:e Diphtong Simplification"
 æ:e <=  [ #: | Cns: | Dummy: ] _ Cns:* [ a: | i: ] WeG: ; ! WeG of a- and i-stems
!	 [ #: | Cns: | Dummy: ] _ Cns:* e: Q1: j ; ! ComSg & G-C Pl of e-stems !this is not so
	 [ #: | Cns: | Dummy: ] _ Cns:* o: X4: j ; ! Ill Sg.
!	 [ #: | Cns: | Dummy: ] _ Cns:* o: [ X4: | Q1: X5: ] j ; ! Ill & Com Sg. !old, rm.
	 [ #: | Cns: | Dummy: ] _ Cns:* o: Cns: X5: Vow ; ! odd-syll, oblique
	 _ Cns Cns:+ o:u ; ! Vovel change in front of stem vowel variation

"æ:e Facultative Diphtong Simplification in Comitative"
 æ:e  => [ #: | Cns: | Dummy: ] _ Cns:* o: Q1: X5: j n ; ! Com Sg.
! The following rule lines are copied from the rule "æ:e Diphtong Simplification".
         [ #: | Cns: | Dummy: ] _ Cns:* [ a: | i: ] WeG: ; ! WeG of a- and i-stems
!	 [ #: | Cns: | Dummy: ] _ Cns:* e: Q1: j ; ! ComSg & G-C Pl of e-stems !this is not so
	 [ #: | Cns: | Dummy: ] _ Cns:* o: X4: j ; ! Ill Sg.
!	 [ #: | Cns: | Dummy: ] _ Cns:* o: [ X4: | Q1: X5: ] j ; ! Ill & Com Sg. !old, rm.
	 [ #: | Cns: | Dummy: ] _ Cns:* o: Cns: X5: Vow ; ! odd-syll, oblique
	 _ Cns Cns:+ o:u ; ! Vovel change in front of stem vowel variation
! Fix this!

"ie:æ Diphtong Simplification Part I"
 i:0 <=> [ #: | Cns: | Dummy: ] _ e: Cns:* e: X4: ;  
	 [ #: | Cns: | Dummy: ] _ e: Cns:* e: [ Q2: | Y2: | Y3: ] ;   ! Prs Sg of e-stem verbs
	 [ #: | Cns: | Dummy: ] _ e: Cns:* e: Cns: X5: Vow ; ! odd-syll, oblique
	 _ Vow (Cns) Cns:+ e:á ; ! Vovel change in front of stem vowel variation


"ie:æ Diphtong Simplification Part II"
 e:æ <=> [ #: | Cns: | Dummy: ] i: _ Cns:* e: X4: ;  
	 [ #: | Cns: | Dummy: ] i: _ Cns:* e: [ Q2: | Y2: | Y3: ] ;   ! Prs Sg of e-stem verbs
	 [ #: | Cns: | Dummy: ] i: _ Cns:* e: Cns: X5: Vow ; ! odd-syll, oblique
	 _ (Cns) Cns:+ e:á ; ! Vovel change in front of stem vowel variation
	 _ Cns Cns:+ a:á ; !Vowel change in front of stem vowel variation, bessat : bæssá
	 [ #: | Cns: | Dummy: ] _ Cns:* a: Y6: ( X5: ) [ #: | n: | v: | s: | j: ] ; !Actor nouns

! Note: Check that Q2 above does not overgenerate to nouns!



!"Diphtong simplification whenever weak grade in a-stems I"
! Vx:0 <=> _ Vy:Vz Cns:* a ( Cns:0 ) WeG: ;
!      where Vx in ( o i )
!	    Vy in ( a e )
!	    Vz in ( å æ )
!	    matched ;

!"Diphtong simplification whenever weak grade in a-stems II"
! Vy:Vz <=> Vx:0 _ Cns:* a ( Cns:0 ) WeG: ;
!      where Vx in ( o i )
!	    Vy in ( a e )
!	    Vz in ( å æ )
!	    matched ;
!€ boarkkaQ1
!€ b0år0ka0
!€ loavddag2#
!€ l0åvdda00



!"Vovel change in front of stem vowel variation"
! æ:e <=> _ Cns Cns:+ o:u ; ! ditto



! Stem vowel alternations
! -----------------------

! a-stem alternations
! - - - - - - - - - -

"a:e in Present Participle of even-syllable verbs"
 a:e <=> Cns: _ Y6: [ #: | v | n | s ] ; 
! bassat : bas'se
          
"a:i in Prs Prc of even-syllable verbs"   
 a:i <=> Cns: _ Y6:0 ( X5:0 ) j [ #: | Cns: ] ;
!€ bas'saY6jt#
!€ bas0si0jt0



! e-stem alternations
! - - - - - - - - - -


! e:i
! - -

"e:i in e-stems"
 e:i <=> Cns: _ Q1:0 ( X5:0 ) j [ #: | Cns: ] ; ! WeG or not, e:i, X5 is added to get e:i also in ComSg.
     Cns: _ Y6:0 ( X5:0 ) j [ #: | Cns: ] ; ! Actor nouns
     Cns _ Q1:0 t: j [ #: | Vow: ] [ #: | v | n | h | b | p | 0 ] ; !"e:i in potentialis" 



! e:á
! - -

! The following two rules constitute a <= / => rule pair.
 
"e:á in certain stem types"
 e:á <=  [ Vow: - å ] Cns:* _ X4: ; ! Ill Sg
     Cns:* _ Y6:0 X4:0 j ; ! Ill Sg Actor nouns
	 [ Vow: - å ] Cns:* _ Cns: X5: Vow ; ! Contracted N
	 [ Vow: - å ] Cns:* _ Y2: ; ! Indicative Present Singular 3rd Final Vowel in verbs
	 [ Vow: - å ] Cns:* _ Y3: ; ! PrfPrc, gállet : gállám
	 [ Vow: - å ] Cns:* _ Q2: ; ! gállet : gáláv
	 [ Vow: - å ] Cns:* _ X7: ; ! báhko : bágusj, goahte : godásj
	 Cns:* _ Y7: ; ! PrsPrc dim
	                             _ Q1:0 X8:0 ; ! 1st, 2nd Gen/Acc Px declension
                            Cns: _ Cns:* Y4: Vow ; !verb der. alla
!€ gálleY3m#
!€ gállá0m0

!€ gálleQ2v#
!€ gál0á0v0

!€ báhkoX7tj#
!€ bá0gu0sj0

!€ goahteX7tj#
!€ go00dá0sj0




! e:á <=> [ Vow: - å ] Cns* _ X4:0 [ j #: | s Vow: ] ;

"Optional e:á in Nom Px declension." ! bárnne : bárnnám/bárnnem, bárnnás/bárnnes
 e:á =>                    _ X4:0 (X8:0) [ m | t | s ] ;
        [ Vow: - å ] Cns:* _ X4: ;  ! Ill Sg
        Cns:* _ Y6:0 X4:0  j ; ! Ill Sg Actor Nouns
	[ Vow: - å ] Cns:* _ Cns: X5: Vow ; ! Contracted N
	[ Vow: - å ] Cns:* _ Y2: ; ! Indicative Present Singular 3rd Final Vowel in verbs
    [ Vow: - å ] Cns:* _ Y3: ; ! PrfPrc, gállet : gállám
	[ Vow: - å ] Cns:* _ Q2: ; ! gállet : gáláv
	[ Vow: - å ] Cns:* _ X7: ; ! báhko : bágusj, goahte : godásj
	Cns:* _ Y7: ; ! PrsPrc dim
	             	            _ Q1:0 X8:0 ; ! 1st, 2nd Gen/Acc Px declension
                           Cns: _ Cns:* Y4: Vow ; !verb der. alla
!€ bárnneX4m
!€ bárnná0m

!€ bárnneX4m
!€ bárnne0m


! The X7 rule could be replaced by a general vowel changer, combined with Q1 in
! the lexicon entry for the Dim suffix.


!"e:á in 1st, 2nd person Gen/Acc Px declension." ! bárnne : bárnám, bárnes
! e:á <=> _ Q1:0 X8:0 ;
! The rule for optional e:á does not work. Thus, it is commented out, and 
! the Px paradigm only gets ám etc. Return to this issue.

 
! e:å
! - -

"e:å in certain stem types with å as root vowel"
 e:å <=> å Cns:* _ X4:0 [ j #: | s Vow: ]  ;
	 å Cns:* _ Cns: X5: Vow ;
	 å Cns:* _ Q2:0 ; ! gådet : gådåv, jåhtet : jådåv
	 å Cns:* _ Y2:0 ; ! gådet : gådå,  jåhtet : jåhtå
	 å Cns:* _ Y3:0 ; ! jåhtet : jåhtåm
	 å Cns:* _ Y5:0 ; ! verb derivation
	 å Cns:* _ Y7:0 t: j [ #: | Vow: ] [ #: | v | n | h | b | p | j | s | t | 0 ]  ; ! noun derivation låhkke:låhkkåsj
	 å Cns:* _ Y7:0 X7:0 l ; ! noun derivation låhkke:låhkkålasj
     å Cns:* _ Y6:0 X4:0 j ; ! noun derivation
	 	! The first subrule gives Ill. sg. of even-stem
! No shorter version here, bec. of no å-å effect
! The second subrule gives oblique of uneven stem
! The third one is vowel harmony, e:å after root vowel å


! e:u
! - -

!"e:u in Verb Derivations" !This holds only for one word (TO)
! e:u <=> Cns:* _ Y4:0 ; 
! árret : árrusit

! e:a
! ----

"e:a in Verb Derivation"
 e:a <=> Cns:* _ Y5:0 ; ! láhppet : láhpadit
 
! e:0
! ----
 "Stem vowel deletion in even-syllable verbs, imp 1du, 1pl"
 e:0 <=> Cns: _ Y8:0 ;
      

 

! i-stem alternations
! - - - - - - - - - -


! i:á
! - -

"i:á in Verb Derivation"
 i:á <=> Cns:* _ Y5:0 ;
! guollit : guollár

 

! o-stem alternations
! - - - - - - - - - -

! The duplicates of the three lines of the two following rules are
! there to resolve the => conflict between the two rules.

"o:u in Com sg"
 o:u => Cns: _ Q1:0 X5:0 j n ; ! Q1 X5 ComSg. 
        Cns: _ X4: j ; !X4 IllSg
        Cns: _ Cns: X5: Vow ;
	Cns: _ [ Y1: | Y2: | Y3: ] ; ! Y1=  Y2= Y3=PrsPrc 
	Cns:* _ Y5: ; !verb der. (u/a/å)d
	Cns: _ Cns:* Y4: Vow ; !verb der. alla
	Cns: _ Y6:0 X5:0 j n ; ! Com sg of Actor nouns
    Cns: _ Y6:0 X4:0 j ; ! IllSg of Actor nouns
    Cns:* _ X7: ; ! báhko : bágusj, goahte : godásj
    Cns:* _ Y7: ; ! PrsPrc dim
  
"o:u in  certain stem types"
 o:u <= Cns: _ Q1:0 X5:0 j n ; 
        Cns: _ X4: j ; !X4 IllSg
        Cns: _ Cns: X5: Vow ;
	Cns: _ [ Y1: | Y2: | Y3: ] ; ! Y1=  Y2= Y3=PrsPrc 
	Cns:* _ Y5: ; !verb der. (u/a/å)d
	Cns: _ Cns:* Y4: Vow ; !verb der. alla
	Cns: _ Y6:0 X5:0 j n ; ! Com sg of Actor nouns
    Cns: _ Y6:0 X4:0 j ; ! IllSg of Actor nouns
    Cns:* _ X7: ; ! báhko : bágusj, goahte : godásj
    Cns:* _ Y7: ; ! PrsPrc dim
     
!Old o:u rules   
! o:u <=> Cns: _ [ X4: j | Q1: X5: ] ; !X4 IllSg, Q1 X5 ComSg.
! o:u <=> Cns: _  X4: j ; !X4 IllSg, Q1 X5 ComSg.

"u:o in contracted nouns"
 u:o <=> Cns:* _ [ Q3: | X6: ] ; !æddnu:ænoj, boahttsu:boatsoj

! å-stem alternations
! - - - - - - - - - -

"å:e in Present Participle of even-syllable verbs"
 å:e <=> Cns: _ Y6: [ #: | v: | n: | s: ] ;
! låhkåt : låhkke

"å:i in Actor nouns of even-syllable verbs"
 å:i <=> Cns: _ Y6:0 ( X5:0 ) j [ #: | Cns: ] ;
! låhkåt : låhkkijt

! alternations valid for several stem types
! - - - - - - - - - - - - - - - - - - - - -

"Stem vowel deletion in even-syllable verbs, imp 3sg, 3du, 2pl, 3pl"
 Vx:0 <=> Cns: _ Y1:0 ;
      where Vx in ( a å e ) ;
    


 

! Consonant gradation rules
! -------------------------

! New rules, without conflicts
! The numbering of rules refer to the overview in Spiiks grammar
! Thus !S1-3 means "Series 1, 3-letter patterns", etc.

! Deletion rules
! - - - - - - - -

"Consonant gradation b:0"
b:0 <=> Vow: ( b ) _ m Vow: ( StemCns: ) WeG: ; ! S1-3, S1-2
	Vow: _ [ d | j | l | n | r | s ] Vow: ( StemCns: ) WeG: ; ! S1-2
	Vow: [ j|l|r ] _ b Vow: ( StemCns: ) WeG: ; ! S3-3
	Vow: _ b: m Vow: X6: ; ! III-I special gradation
	Vow: b: _ m Vow: X6: ; ! III-I special gradation

"Consonant gradation d:0"
d:0 <=> Vow: ( d ) _ [ n | j ] Vow: ( StemCns: ) WeG: ; ! S1-3, S1-2
	Vow: ( d ) _ n j Vow: ( StemCns: ) WeG: ; ! S1-4 and S1, dnj:nj
	Vow: [ b | j | r | v ] _ d Vow: ( StemCns: ) WeG: ; ! S3
	Vow: _ d: n Vow: X6: ; ! III-I special gradation
	Vow: d: _ n Vow: X6: ; ! III-I special gradation
	Vow: _ t:s j #: ;      ! oajváladtj# : oajvála0sj0

"Consonant gradation g:0"
g:0 <=> Vow: ( g ) _ ñ Vow: (StemCns: ) WeG: ; ! S1-3, S1-2
	Vow: [ j|l|r|v ] _ g Vow: ( StemCns: ) WeG: ; ! S3
	Vow: _ g: ñ Vow: X6: ; ! III-I special gradation
	Vow: g: _ ñ Vow: X6: ; ! III-I special gradation

"Consonant gradation k:0"
k:0 <=> Vow: k _ n Vow: ( StemCns: ) WeG: ; ! S1-3
	Vow: k _ t j Vow: ( StemCns: ) WeG: ; ! S1-4
	Vow: [ j|l|m|r|v ] _ k Vow: ( StemCns: ) WeG: ; ! S3
	Vow: h _  k Vow: ( StemCns: ) WeG: ; ! S4, preasp stops

"Consonant gradation l:0"
l:0 <=> Vow: l _ [ d|j|t|n ] Vow: ( StemCns: ) WeG: ; ! S1-3
	Vow: l _ t j Vow: ( StemCns: ) WeG: ; ! S1-4
	Vow: l _ Vow: ( StemCns: ) WeG: ; ! S2, II-I
	Vow: [ j|b|v ] _ l  Vow: ( StemCns: ) WeG: ; ! S3

"Consonant gradation m:0"
m:0 <=> Vow: m _ [ b|p ] Vow: ( StemCns: ) WeG: ; ! S1-3
	Vow: m _ Vow: ( StemCns: ) WeG: ; ! S2, II-I
	Vow: [ j|l|r ] _ m Vow: ( StemCns: ) WeG: ; ! S3
	Vow: _ Q1: X1: n ; ! Deleting Final Cns in Short Essive of Uneven Syllables


"Consonant gradation n:0"
n:0 <=> Vow: n _ d Vow: ( StemCns: ) WeG: ; ! S1-3
	Vow: n _ t [ j|s ] Vow: ( StemCns: ) WeG: ; ! S1-4
	Vow: n _ ( j ) Vow: ( StemCns: ) WeG: ; ! S2, II-I, also palat.
	Vow: [ b|j|l|ñ|r|v ] _ n Vow: ( StemCns: ) WeG: ; ! S3, III-II
	Vow: [ b|r|v ] _ n j Vow: ( StemCns: ) WeG: ; ! S3-4, III-II

"Consonant gradation p:0"
p:0 <=> Vow: p _ [ s|t ] ( j ) Vow: ( StemCns: ) WeG: ; ! S1-3, S1-4 j
	Vow: p _ t s Vow: ( StemCns: ) WeG: ; ! S1-4 ppts:pts
	Vow: [ j|l|r|v ] _ p Vow: ( StemCns: ) WeG: ; ! S3 
	Vow: h _ p Vow: ( StemCns: ) WeG: ; ! S4 hpp:hp

"Consonant gradation s:0"
s:0 <=> Vow: s _ ( [ j|k|m|n|t ] ) Vow: ( StemCns: ) WeG: ; ! S1-3, S2 II-I
	Vow: s _ j [ k|m|v ] Vow: ( StemCns: ) WeG: ; ! S1-4
	Vow: [ b|j|l|m|n|r|v ] _ s Vow: ( StemCns: ) WeG: ; ! S3
	Vow: [ r|l|m ] _ s j Vow: ( StemCns: ) WeG: ; ! S3-4
	Vow: [ r|l|n ] _ s k Vow: ( StemCns: ) WeG: ; ! S3-4
	Vow: [ j|m|n|v ] _ s t Vow: ( StemCns: ) WeG: ; ! S3-4
	Vow: r s _ j t Vow: ( StemCns: ) WeG: ; ! S3 rssjt:rsjt
	Vow: s: _ #: ; ! This is a word fin simpl, ss# -> s#, and not CG
	Vow: _ Q1: X1: n ; ! Deleting Final Cns in Short Essive of Uneven Syllables

"Consonant gradation ñ:0"
ñ:0 <=> Vow: ñ _ ( [ g|k ] ) Vow: ( StemCns: ) WeG: ; ! S1-3, S2 II-I
	Vow: [ j|l|r|v ] _ ñ Vow: ( StemCns: ) WeG: ; ! S3 

	
"Consonant gradation f:0"
f:0 <=> Vow: ( [r|l] ) _ f Vow: ( StemCns: ) WeG: ; ! S2 ff:f, S3 rff:rf

"Consonant gradation r:0"
r:0 <=> Vow: ( [ b|j|v ] ) _ r Vow: ( StemCns: ) WeG: ; ! S2 rr:r, S3 brr etc.

"Consonant gradation v:0"
v:0 <=> Vow: ( [ j|l|r ] ) _ v Vow: ( StemCns: ) WeG: ; ! S2 , S3 jvv etc.

"Consonant gradation j:0"
j:0 <=> Vow: [b|r] _ j Vow: ( StemCns: ) WeG: ; ! S3

"Consonant gradation t:0"
t:0 <=> Vow: [j|ñ|r|v] _ t Vow: ( StemCns: ) WeG: ; ! S3 jtt:jt etc.
	Vow: [b|j|r|v] _ t j Vow: ( StemCns: ) WeG: ; ! S3
	Vow: [b|j|v] _ t s Vow: ( StemCns: ) WeG: ; ! S3
	Vow: h: _ t ( [ j|s ] ) Vow: ( StemCns: ) WeG: ; ! S4 htt(j)
    Vow: t:s _ j #: ; ! Word final weakening of -ttj to -sj
    Vow ( Dummy: ) s _ (Hyph) #:  ; !W-Final Cl. Simpl. málestit : máles
    Vow: h: _ t ( [ j|s ] ) Vow: X6: ;
! Second last rule will fail if and when clitics are added to the word.
! Note that there is no weak grade clitic here.

!"Gradation Series 4, II-I, h Loss 1"
!h:0 <=> Vow: _ [ k: | p: | t: ] Vow: ( StemCns: ) WeG: ;
!	Vow: _ Q1: X1: n ; ! Deleting Final Cns in Short Essive of Uneven Syllables	 

"Gradation Series 4, II-I, tj and ts"
 h:0 <=> Vow: _ [ k: | p: | t: ] Vow: ( StemCns: ) WeG: ;
	 Vow: _ t [j|s] Vow: ( StemCns: ) WeG: ;
	 Vow: _ t: t: ( [ j|s ] ) Vow: X6: ;
	 Vow: _ Q1: X1: n ; ! Deleting Final Cns in Short Essive of Uneven Syllables
! (t:) for III-I CG)

!"Word final double consonant simplification"
!Cy:0 <=> Vow: Cx: _ #: ;
!     where Cx in ( s ) 
!	   Cy in ( s ) 
!	   matched ;
! Moved to the CG chapter. But does this rule wait for more cases?


! Change rules
! - - - - - - 


"Gradation Series 4, II-I"
 Cx:Cy <=> Vow: h:0 _ Vow: ( StemCns: ) WeG: ;
 where Cx in ( k p t )
       Cy in ( g b d )
       matched ;

!"Gradation Series 5, III-II, dtj, dts"
! d:t <=> Vow: _ t Cx Vow: ( StemCns: ) WeG: ;
! where Cx in ( j s ) ;

"bb:pp"
b:p <=> Vow: _ b: Vow: ( StemCns: ) WeG: ;
	Vow: b: _ Vow: ( StemCns: ) WeG: ;
	Vow: _ #:0 ;
! The third rule is for unnep : unnebu. It must be revised in two ways:
! a. it should be extended to more consonants
! b. it should be restricted, perhaps b should be replaced with b2

"gg:kk"
g:k <=> Vow: _ g: Vow: ( StemCns: ) WeG: ;
	Vow: g: _ Vow: ( StemCns: ) WeG: ;

"dd:tt and dtj, dts"
d:t <=> Vow: _ d: Vow: ( StemCns: ) WeG: ;
	Vow: d: _ Vow: ( StemCns: ) WeG: ;
	Vow: _ t [ j|s ] Vow: ( StemCns: ) WeG: ;


!"Gradation Series 6, III-II, bb, gg 1"
! Cx:Cy <=> Vow: _ Cz: Vow: ( StemCns: ) WeG: ;
! where Cx in ( b d g )
!       Cy in ( p t k )
!       Cz in ( b d g )
!       matched ;

!"Gradation Series 6, III-II, bb, gg 2"
! Cx:Cy <=> Vow: :Cy _ Vow: ( StemCns: ) WeG: ;
! where Cx in ( b d g )
!       Cy in ( p t k )
!       matched ;

"Gradation Series 7, III-II, ks, kt, ktj, kts"
 k:v <=> Vow: _ [ s|t ] Vow: ( StemCns: ) WeG: ;
	 Vow: _ t [ j|s ] Vow: ( StemCns: ) WeG: ;

! Exceptional II-III inverse gradation in present participles
! -----------------------------------------------------------

! This gradation is only for II-I syllable verbs that get III as
! present participles.

! Candidates:
!! bbm - bm - m
! ddn - dn - n
! ddnj- dnj- nj
!! ggñ - gñ - ñ
! ddj - dj - dj

! hkk - hk - g
! hpp - hp - b

! htt - ht - d
! httj- htj- tj
! htts- hts- ts

! Strategy: Do insertion rule for the initial element.

"Consonant insertion as II-III strengthening gradation with bm,gñ"
 0:Cx <=> Vow _ Cx Cy Vow: [ Y6: | Y7: ] ;
      where Cx in ( b g )
	    Cy in ( m ñ )
	    matched ;

"Consonant insertion as II-III strengthening gradation with dn/j"
 0:d <=> Vow _ d [ n | n j | j ] Vow: [ Y6: | Y7: ] ;

"Consonant insertion as II-III strengthening gradation with hk, hp,"
 0:Cx <=> Vow h _ Cx Vow: [ Y6: | Y7: ] ;
      where Cx in ( k p ) ;
      
"Consonant insertion as II-III strengthening gradation with htt(j/s)"
 0:t <=> Vow h _ t (j|s) Vow: [ Y6: | Y7: ] ;

