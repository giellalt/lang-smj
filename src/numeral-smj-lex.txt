! Numerals
! ========



LEXICON Numeral					!^N^
MILIJÅVNNÅ ;          ! a noun of its own		!^N^
UNDERTUVSÁN ;      ! for generator under 1000	!^N^
JUSTTUVSÁN ;       ! going via 1000		!^N^
OVERTUVSÁN ;       ! for generator over 1000	!^N^
OLD ;             ! for "thirteen hundred, etc.	!^N^
!num-derived ;    ! still unimplemented		!^N^
num-imprecise ;   ! still almost unimplemented	!^N^
ARABIC ;          ! for the arabic numerals	!^C^
ROMAN ;           ! for the roman numerals	!^C^
NUM-PREFIXES ;    ! for §34 etc.		!^C^
ISOLATED-NUMEXP ; ! for ½ etc.			!^C^

! Million, a noun of its own
! --------------------------

LEXICON MILIJÅVNNÅ					!^N^
milijåvnnå:milijåvnnå AKTA ;			!^N^
millijåvnnå:millijåvnnå AKTA ;	!^N^
milijåvnåt+A+Ord:milijåvnåd3 VUOSTASJ ;	!^N^
millijåvnåt+A+Ord:millijåvnåd3 VUOSTASJ ;	!^N^
milliárdda:milliárdda AKTA ;		!^N^ ! Num or N?
milliárdat+A+Ord:milliárdad3 VUOSTASJ ;		!^N^

! Overtuvsán, for the numerals over 1000
! --------------------------------------

LEXICON OVERTUVSÁN				!^N^
O-AKTAF ;     ! 1000				!^N^
O-2TO9F ;     ! 2000, 3000, ...			!^N^
O-JUSTLÅGEVF ; ! 10000				!^N^
O-2TO9LÅG;   ! 20000, 30000, ...		!^N^
O-LÅGENAN ;  ! 11000, 12000, ...		!^N^
O-TJUOHTE ;     ! 100000				!^N^	
O-2TO9TJUO ;   ! 200000, 300000, ...		!^N^

! All the child lexica of OVERTUVSÁN have the prefix O-.
! They are directed via their respective numerals to the lexicon JUSTTUVSÁN.


! Thousands

LEXICON O-AKTAF		!^N^
akta JUSTDUHÁT ;	!^N^
JUSTDUHÁT ;		!^N^

LEXICON O-X1000		!^N^
akta JUSTTUVSÁN ;	!^N^

LEXICON O-2TO9F			!^N^
guokta JUSTTUVSÁN ;		!^N^	
gålmmå JUSTTUVSÁN ;		!^N^
niellja JUSTTUVSÁN ;		!^N^		
vihtta JUSTTUVSÁN ;		!^N^
guhtta JUSTTUVSÁN ;		!^N^
gietjav JUSTTUVSÁN ;		!^N^
gáktsa JUSTTUVSÁN ;		!^N^
aktse JUSTTUVSÁN ;		!^N^


! Tens of thousands

LEXICON O-JUSTLÅGEVF		!^N^
lågev JUSTTUVSÁN ;		!^N^
! This lexicon is for the number 10000 only. it is separated from the
! rest to avoid forms like *logivihttaduhát, etc.


LEXICON O-LÅGEVF			!^N^	     
låk JUSTTUVSÁN ;		!^N^ !XXXlong form too?
låk O-X1000 ;			!^N^ !XXXlong form too?
låk O-2TO9F ;			!^N^ !XXXlong form too?
! this lexicon is accessed only via other O-lexica, and not directly 
! from OVERTUVSÁN. Thus, *logivihttaduhát, etc. is avoided.

LEXICON O-2TO9LÅG		!^N^	  
guokta O-LÅGEVF ;		!^N^
gålmmå O-LÅGEVF ;		!^N^
niellja O-LÅGEVF ;		!^N^
vihtta O-LÅGEVF ;		!^N^
guhtta O-LÅGEVF ;		!^N^
gietjav O-LÅGEVF ;			!^N^
gáktsa O-LÅGEVF ;		!^N^
aktse O-LÅGEVF ;			!^N^


! Teens of thousands

LEXICON O-LÅGENAN		!^N^
lågenan#akta JUSTTUVSÁN ;        !^N^
lågenan#guokta:lågenan#guokta JUSTTUVSÁN ;          !^N^ 
lågenan#gålmmå JUSTTUVSÁN ;       !^N^
lågenan#niellja:lågenan#niellja JUSTTUVSÁN ;      !^N^ 
lågenan#vihtta:lågenan#vihtta JUSTTUVSÁN ;             !^N^
lågenan#guhtta:lågenan#guhtta JUSTTUVSÁN ;         !^N^ 
lågenan#gietjav:lågenan#giehtjav JUSTTUVSÁN ;       !^N^ 
lågenan#gáktsa:lågenan#gáktsa JUSTTUVSÁN ;       !^N^
lågenan#aktse:lågenan#aktse JUSTTUVSÁN ;                 !^N^

akta O-NL ;        !^N^
guokta O-NL ;          !^N^ 
gålmmå O-NL ;       !^N^
niellja O-NL ;      !^N^ 
vihtta O-NL ;             !^N^
guhtta O-NL ;         !^N^ 
gietjav O-NL ;       !^N^ 
gáktsa O-NL ;       !^N^
aktse O-NL ;                 !^N^


 LEXICON O-NL			!^N^
 lågenan JUSTTUVSÁN ;	!^N^


! Hundreds of thousands

LEXICON O-TJUOHTE					!^N^
tjuohte JUSTTUVSÁN ;  ! 100000			!^N^
tjuohte O-X1000 ;     ! 101000			!^N^
tjuohte O-2TO9F ;    ! 102000, 103000, ...	!^N^
tjuohte O-LÅGEVF ;    ! 110000, 113000, ...	!^N^
tjuohte O-2TO9LÅG ;  ! 120000, 123000, ...	!^N^
tjuohte O-LÅGENAN ; ! 115000, ...		!^N^

LEXICON O-TJUODE					!^N^
tjuode JUSTTUVSÁN ;  ! 200000			!^N^
tjuode O-X1000 ;     ! 201000			!^N^
tjuode O-2TO9F ;    ! 202000, 203000, ...	!^N^
tjuode O-LÅGEVF ;    ! 210000, 213000, ...	!^N^
tjuode O-2TO9LÅG ;  ! 220000, 223000, ...	!^N^
tjuode O-LÅGENAN ; ! 215000, ...		!^N^

LEXICON O-2TO9TJUO				!^N^
akta O-TJUOHTE ;       ! #one# hundred...		!^N^
guokta O-TJUODE ;				!^N^
gålmmå O-TJUODE ;				!^N^
niellja O-TJUODE ;				!^N^
vihtta O-TJUODE ;				!^N^
guhtta O-TJUODE ;				!^N^
gietjav O-TJUODE ;					!^N^
gáktsa O-TJUODE ;				!^N^
aktse O-TJUODE ;					!^N^

LEXICON JUSTDUHÁT				!^N^
tuvsán UNDERTUVSÁN ;  	!^N^
tuvsán+Num:tuvsán TUVSÁN ; ! Case infl of tuvsán	!^N^
tuvsánat+A+Ord:tuvsána TUVSÁNAT ;			!^N^
duhát UNDERTUVSÁN ;  	!^N^
duhát:duhád TUVSÁN ; ! Case infl of duhát	!^N^
duhádat+A+Ord:duháda TUVSÁNAT ;			!^N^




! Justtuvsán, for numerals going via 1000
! -------------------------------------

LEXICON JUSTTUVSÁN				!^N^
tuvsána UNDERTUVSÁN ;  ! Thousand-and-something	!^N^
tuvsán:tuvsán TUVSÁN ; ! Case infl of tuvsán	!^N^
tuvsánat+A+Ord:tuvsána TUVSÁNAT ;			!^N^
duháda UNDERTUVSÁN ;  ! Thousand-and-something	!^N^
duhát:duhád TUVSÁN ; ! Case infl of duhát	!^N^
duhádat+A+Ord:duháda TUVSÁNAT ;			!^N^


! Old, for the old counting thirteen hundred etc.	
! -----------------------------------------------

LEXICON OLD					!^N^
akta NLX ;					!^N^	
guokta NLX ;					!^N^
gålmmå NLX ;					!^N^
niellja NLX ;					!^N^
vihtta NLX ;					!^N^
guhtta NLX ;					!^N^
gietjav NLX ;					!^N^
gáktsa NLX ;					!^N^
aktse NLX ;					!^N^
lågenanakta GTJUODE ;					!^N^	
lågenanguokta GTJUODE ;					!^N^
lågenangålmmå GTJUODE ;					!^N^
lågenanniellja GTJUODE ;					!^N^
lågenanvihtta GTJUODE ;					!^N^
lågenanguhtta GTJUODE ;					!^N^
lågenangietjav GTJUODE ;					!^N^
lågenangáktsa GTJUODE ;					!^N^
lågenanaktse GTJUODE ;					!^N^

LEXICON NLX					!^N^
lågenan GTJUODE ;				!^N^




LEXICON UNDERTUVSÁN				!^N^
AKTAF ;     ! 1				!^N^
2TO9F ;     ! 2-9			!^N^
LÅGEVF ;     ! 10				!^N^
LÅGENAN ;   ! 11-19             !^N^
2TO9LÅG ;   ! 20-99				!^N^
TJUOHTE ;     ! 100-199				!^N^
2TO9TJUO ;   ! 200-999				!^N^

LEXICON AKTAF					!^N^
akta AKTA ;        !^N^
akta:åvtå ÅVTÅ ;		!^N^
vuostasj+A+Ord:vuostatj VUOSTASJ ;			 !^N^


LEXICON 2TO9F					!^N^ 
guokta:guokt GUOKTA ;                 !^N^ 
nubbe+A+Ord:nupp nubbecase ;		!^N^ 
nubbe+A+Ord+Sg+Nom:nubbe K ;        !^N^ 
nubbe+A+Ord+Ess:nubben K ;         !^N^ 
nubbe+A+Ord+Sg+Ill:nubbáj K ;       !^N^ 
nubbe+A+Ord+Sg+Gen:nubbáj NUMERALCOMPOUNDS ;       !^N^ ^C^ 
nuppát+A+Ord:nuppád3 VUOSTASJ ;       !^N^ 
gålmmå GÅLMMÅ ;       !^N^ 
goalmát+A+Ord:goalmád3 VUOSTASJ ;       !^N^ 
niellja:niellj GUOKTA ;             !^N^ 
nælját+A+Ord:næljád3 VUOSTASJ ;       !^N^ 
vihtta+Num:vidá GÅLMMÅ-OBL ;                    !^N^
vihtta+Num+Sg+Nom:vihtta K ;          !^N^
vihtta+Num+Sg+Acc:vihtta K ;         !^N^
vihtta+Num+Sg+Ill:vihttaj K ;         !^N^
vihtta+Num+Ess:vihttan K ;            !^N^
vidát+A+Ord:vidád3 VUOSTASJ ;       !^N^ 
guhtta+Num:gudá GÅLMMÅ-OBL ;               !^N^ 
guhtta+Num+Sg+Nom:guhtta K ;          !^N^
guhtta+Num+Sg+Acc:guhtta K ;         !^N^
guhtta+Num+Sg+Ill:guhttaj K ;         !^N^
guhtta+Num+Ess:guhttan K ;            !^N^
gudát+A+Ord:gudád3 VUOSTASJ ;       !^N^ 
gietjav:giehtja GIETJAV ;        !^N^ 
giehttjit+A+Ord:giehttjid3 VUOSTASJ ;       !^N^ 
gáktsa:gákts GÁKTSA ;              !^N^
gávtsát+A+Ord:gávtsád3 VUOSTASJ ;       !^N^ 
aktse:akts AKTSE ;                      !^N^
avtsát+A+Ord:avtsád3 VUOSTASJ ;       !^N^ 


LEXICON LÅGEVF					!^N^
lågev:låhk LÅGEV ;   !^N^
lågåt+A+Ord:lågåd3 VUOSTASJ ;			!^N^
låhke:låhk  TJUOHTECASE ; !^N^ 
låhke+Num+Sg+Ill:låhkåj K ;            !^N^

LEXICON LÅK-					!^N^
låk AKTAF ;				!^N^ !XXXlong form too?
låk 2TO9F ;				!^N^ !XXXlong form too?
låhke+Num+Sg+Nom:låk K ;         !^N^
låhke+Num+Sg+Acc+Attr:låk K ;         !^N^
låhke+Num+Sg+Gen:låk NUMERALCOMPOUNDS ;      !^N^ ^C^
låhke+Num+Sg+Gen:låge NUMERALCOMPOUNDS ;      !^N^ ^C^
låhke:låhk  TJUOHTECASE ; !^N^ ! vihttalåhke, collective number
låhke+Num+Sg+Ill:låhkåj K ;            !^N^
lågåt+A+Ord:lågåd3 VUOSTASJ ;			!^N^

LEXICON LÅGENAN					!^N^
lågenan#akta AKTA ;        !^N^
lågenan#akta:lågenan#åvtå ÅVTÅ ;		!^N^
lågenan#vuostasj+A+Ord:lågenan#vuostatj VUOSTASJ ;			 !^N^
lågenan#guokta:lågenan#guokt GUOKTA ;          !^N^ 
lågenan#nubbe+A+Ord:lågenan#nupp nubbecase ;		!^N^ 
lågenan#nubbe+A+Ord+Sg+Nom:lågenan#nubbe K ;      !^N^ 
lågenan#nubbe+A+Ord+Ess:lågenan#nubben K ;       !^N^ 
lågenan#nubbe+A+Ord+Sg+Ill:lågenan#nubbáj K ;    !^N^ 
lågenan#nuppát+A+Ord:lågenan#nuppád3 VUOSTASJ ;       !^N^ 
lågenan#gålmmå GÅLMMÅ ;       !^N^
lågenan#goalmát+A+Ord:lågenan#goalmád3 VUOSTASJ ;       !^N^ 
lågenan#niellja:lågenan#niellj GUOKTA ;      !^N^ 
lågenan#nælját+A+Ord:lågenan#næljád3 VUOSTASJ ;       !^N^ 
lågenan#vihtta+Num:lågenan#vidá GÅLMMÅ-OBL ;             !^N^
lågenan#vihtta+Num+Sg+Nom:lågenan#vihtta K ;          !^N^
lågenan#vihtta+Num+Sg+Acc:lågenan#vihtta K ;         !^N^
lågenan#vihtta+Num+Sg+Ill:lågenan#vihttaj K ;         !^N^
lågenan#vihtta+Num+Ess:lågenan#vihttan K ;            !^N^
lågenan#vidát+A+Ord:lågenan#vidád3 VUOSTASJ ;       !^N^ 
lågenan#guhtta+Num:lågenan#gudá GÅLMMÅ-OBL ;         !^N^ 
lågenan#guhtta+Num+Sg+Nom:lågenan#guhtta K ;          !^N^
lågenan#guhtta+Num+Sg+Acc:lågenan#guhtta K ;         !^N^
lågenan#guhtta+Num+Sg+Ill:lågenan#guhttaj K ;         !^N^
lågenan#guhtta+Num+Ess:lågenan#guhttan K ;            !^N^
lågenan#gudát+A+Ord:lågenan#gudád3 VUOSTASJ ;       !^N^ 
lågenan#gietjav:lågenan#giehtja GIETJAV ;       !^N^ 
lågenan#giehttjit+A+Ord:lågenan#giehttjid3 VUOSTASJ ;       !^N^  
lågenan#gáktsa:lågenan#gákts GÁKTSA ;       !^N^
lågenan#gávtsát+A+Ord:lågenan#gávtsád3 VUOSTASJ ;       !^N^
lågenan#aktse:lågenan#akts AKTSE ;                 !^N^
lågenan#avtsát+A+Ord:lågenan#avtsád3 VUOSTASJ ;       !^N^ 

akta AKTALÅGENAN ;        !^N^
akta:åvtå ÅVTÅLÅGENAN ;		!^N^
vuostasj#lågåt+A+Ord:vuostasj#lågåd3 VUOSTASJ ;			 !^N^
guokta:guokt GUOKTALÅGENAN ;          !^N^ 
!nubbe+A+Ord:nupp nubbecase ;		!^N^ XXX???
!nubbe+A+Ord+Sg+Nom:nubbe K ;      !^N^XXX??? 
!nubbe+A+Ord+Ess:nubben K ;       !^N^ XXX???
!nubbe+A+Ord+Sg+Ill:nubbáj K ;    !^N^ XXX???
nuppát#lågåt+A+Ord:nuppát#lågåd3 VUOSTASJ ;       !^N^ 
gålmmå GÅLMMÅLÅGENAN ;       !^N^
goalmát#lågåt+A+Ord:goalmát#lågåd3 VUOSTASJ ;       !^N^ 
niellja:niellj GUOKTALÅGENAN ;      !^N^ 
nælját#lågåt+A+Ord:nælját#lågåd3 VUOSTASJ ;       !^N^ 
vihtta+Num:vidá GÅLMMÅLÅGENAN-OBL ;             !^N^
vihtta#lågenan+Num+Sg+Nom:vihtta#lågenan K ;          !^N^
vihtta#lågenan+Num+Sg+Acc:vihtta#lågenan K ;         !^N^
vihtta#lågenan+Num+Sg+Ill:vihttaj#lågenan K ;         !^N^
vihtta#lågenan+Num+Ess:vihttan#lågenan K ;            !^N^
vidát#lågåt+A+Ord:vidát#lågåd3 VUOSTASJ ;       !^N^ 
guhtta+Num:gudá GÅLMMÅLÅGENAN-OBL ;         !^N^ 
guhtta#lågenan+Num+Sg+Nom:guhtta#lågenan K ;          !^N^
guhtta#lågenan+Num+Sg+Acc:guhtta#lågenan K ;         !^N^
guhtta#lågenan+Num+Sg+Ill:guhttaj#lågenan K ;         !^N^
guhtta#lågenan+Num+Ess:guhttan#lågenan K ;            !^N^
gudát#lågåt+A+Ord:gudát#lågåd3 VUOSTASJ ;       !^N^ 
gietjav:giehtja GIETJAVLÅGENAN ;       !^N^ 
giehttjit#lågåt+A+Ord:giehttjit#lågåd3 VUOSTASJ ;       !^N^  
gáktsa:gákts GÁKTSALÅGENAN ;       !^N^
gávtsát#lågåt+A+Ord:gávtsát#lågåt VUOSTASJ ;       !^N^
aktse:akts AKTSELÅGENAN ;                 !^N^
avtsát#lågåt+A+Ord:avtsát#lågåd3 VUOSTASJ ;       !^N^ 


! LEXICON NUPPELOT				!^N^ !XXX
! okta NL ;					!^N^
! guokte NL ;					!^N^
! golbma NL ;					!^N^
! njeallje NL ;					!^N^
! njeallja NL ;					!^N^
! vihtta NL ;					!^N^
! guhtta NL ;					!^N^
! čieža NL ;					!^N^
! gávcci NL ;					!^N^
! ovcci NL ;					!^N^

! LEXICON NL					!^N^
! :nuppe#lohkái JUSTTUVSÁN ;			!^N^
! :nuppe#lohkái NUPPELOHKAI ;			!^N^


LEXICON 2TO9LÅG					!^N^
guokta LÅK- ;					!^N^
gålmmå LÅK- ;					!^N^
niellja LÅK- ;				!^N^
vihtta LÅK- ;					!^N^
guhtta LÅK- ;					!^N^
gietjav LÅK- ;					!^N^
gáktsa LÅK- ;					!^N^
aktse LÅK- ;					!^N^

LEXICON TJUOHTE					!^N^
tjuohte:tjuoht TJUOHTECASE ;    !^N^
tjuohte+Num+Sg+Ill:tjuohtáj K ; !^N^ 
tjuohte+Num+Sg+Nom:tjuohte NUMERALCOMPOUNDS ; !^N^  ^C^
!čuohti:čuođi OVERTUVSÁN ;			!^N^
tjuohte LÅGEVF ;					!^N^ !to LÅK- too?
tjuohte 2TO9LÅG ;					!^N^
tjuohte AKTAF ;					!^N^
tjuohte 2TO9F ;					!^N^
tjuohte LÅGENAN ;				!^N^
tjuodát+A+Ord:tjuodád3 VUOSTASJ ;			!^N^


LEXICON 2TO9TJUO					!^N^
guokta GTJUODE ;					!^N^
gålmmå GTJUODE ;					!^N^
niellja GTJUODE ;				!^N^
njeallja GTJUODE ;				!^N^
vihtta GTJUODE ;					!^N^
guhtta GTJUODE ;					!^N^
gietjav GTJUODE ;					!^N^
gáktsa GTJUODE ;					!^N^
aktse GTJUODE ;					!^N^


LEXICON GTJUODE					!^N^
!čuođi OVERTUVSÁN ;				!^N^
tjuode+Num+Sg+Gen:tjuode NUMERALCOMPOUNDS ; !^N^ ^C^
tjuode LÅGEVF ;					!^N^ !to LÅK- too?
tjuode 2TO9LÅG ;					!^N^
tjuode AKTAF ;					!^N^
tjuode 2TO9F ;					!^N^
tjuode LÅGENAN ;				!^N^
tjuohte:tjuoht TJUOHTECASE ;    !^N^  ! imprecise number
tjuodát+A+Ord:tjuodád3 VUOSTASJ ;			!^N^


! Case forms of the basic digits
! ------------------------------

LEXICON AKTA                !^N^
+Num: AKTA-NIE ;               !^N^
+Num:Q1 AKTA-OBL ;              !^N^

LEXICON AKTA-NIE                !^N^
 +Sg+Nom: K ;               !^N^
 +Sg+Ill:j K ;              !^N^
 +Ess:n K ;                 !^N^

LEXICON AKTA-OBL                !^N^
 +Sg+Gen: K ;               !^N^
 +Sg+Gen: NUMERALCOMPOUNDS ;        !^N^ ^C^
 +Sg+Acc:v K ;              !^N^
 +Sg+Ine:n K ;              !^N^
 +Sg+Ela:s K ;              !^N^
 +Sg+Com:jn K ;             !^N^
 +Sg+Par:t K ;               !^N^
 +Pl+Nom: K ;                !^N^
 +Pl+Gen:j K ;               !^N^
 +Pl+Acc:jt K ;              !^N^
 +Pl+Ill:jda K ;             !^N^
 +Pl+Ine:jn K ;              !^N^
 +Pl+Ela:js K ;              !^N^
 +Pl+Com:j K ;               !^N^
 
LEXICON ÅVTÅ                 !^N^
+Num+Sg+Gen: K ;             !^N^
+Num+Sg+Gen: NUMERALCOMPOUNDS ;        !^N^ ^C^
+Num+Sg+Acc:v K ;            !^N^
+Num+Sg+Ine:n K ;            !^N^
+Num+Sg+Ela:s K ;            !^N^
+Num+Sg+Com:jn K ;           !^N^
+Num+Sg+Par:t K ;            !^N^
+Num+Pl+Nom: K ;             !^N^
+Num+Pl+Gen:j K ;            !^N^
+Num+Pl+Acc:jt K ;           !^N^
+Num+Pl+Ill:jda K ;          !^N^
+Num+Pl+Ine:jn K ;           !^N^
+Num+Pl+Ela:js K ;           !^N^
+Num+Pl+Com:j K ;            !^N^

LEXICON GUOKTA                  !^N^
+Num:eQ1 GUOVTE- ;               !^N^
+Num:iQ1 GUOVTI- ;               !^N^
+Num: GUOKTA- ;                 !^N^

LEXICON GUOVTE-             !^N^
 +Sg+Gen: K ;              !^N^
 +Sg+Gen: NUMERALCOMPOUNDS ;        !^N^  ^C^
 +Sg+Ine:n K ;             !^N^
 +Sg+Ela:s K ;             !^N^
 +Sg+Par:t K ;             !^N^
 +Pl+Nom: K ;              !^N^
 
LEXICON GUOVTI- 
 +Sg+Com:jn K ;            !^N^
 +Pl+Gen:j K ;             !^N^
 +Pl+Acc:jt K ;            !^N^
 +Pl+Ill:jda K ;           !^N^
 +Pl+Ine:jn K ;            !^N^
 +Pl+Ela:js K ;            !^N^
 +Pl+Com:j K ;             !^N^


LEXICON GUOKTA-          !^N^  
 +Sg+Nom:a K ;           !^N^
 +Sg+Acc:a K ;           !^N^
 +Sg+Ill:áj K ;          !^N^
 +Ess:an K ;             !^N^
                         
LEXICON GÅLMMÅ           !^N^
+Num: GÅLMMÅ-NAIE ;      !^N^
+Num:Q1 GÅLMMÅ-OBL ;     !^N^
                        
LEXICON GÅLMMÅ-NAIE      !^N^
 +Sg+Nom: K ;            !^N^
 +Sg+Acc: K ;            !^N^
 +Sg+Ill:j K ;           !^N^
 +Ess:n K ;              !^N^
                         
LEXICON GÅLMMÅ-OBL       !^N^
 +Sg+Gen: K ;            !^N^
 +Sg+Gen: NUMERALCOMPOUNDS ;        !^N^ ^C^
 +Sg+Ine:n K ;           !^N^
 +Sg+Ela:s K ;           !^N^
 +Sg+Com:jn K ;          !^N^
 +Sg+Par:t K ;           !^N^
 +Pl+Nom: K ;            !^N^
 +Pl+Gen:j K ;           !^N^
 +Pl+Acc:jt K ;          !^N^
 +Pl+Ill:jda K ;         !^N^
 +Pl+Ine:jn K ;          !^N^
 +Pl+Ela:js K ;          !^N^
 +Pl+Com:j K ;           !^N^


LEXICON GIETJAV             !^N^
+Num: GIETJAV-IE ;          !^N^
+Num:Q1 GIETJAV-OBL ;       !^N^
                            
LEXICON GIETJAV-IE          !^N^
 +Sg+Ill:j K ;              !^N^
 +Ess:n K ;                 !^N^
                        
LEXICON GIETJAV-OBL         !^N^
 +Sg+Nom:v K ;              !^N^
 +Sg+Gen: K ;               !^N^
 +Sg+Gen: NUMERALCOMPOUNDS ;        !^N^  ^C^
 +Sg+Acc:v K ;              !^N^
 +Sg+Ine:n K ;              !^N^
 +Sg+Ela:s K ;              !^N^
 +Sg+Com:jn K ;             !^N^
 +Sg+Par:t K ;              !^N^
 +Pl+Nom: K ;               !^N^
 +Pl+Gen:j K ;              !^N^
 +Pl+Acc:jt K ;             !^N^
 +Pl+Ill:jda K ;            !^N^
 +Pl+Ine:jn K ;             !^N^
 +Pl+Ela:js K ;             !^N^
 +Pl+Com:j K ;              !^N^
 

LEXICON GÁKTSA              !^N^
+Num:eQ1 GÁVTSE- ;           !^N^
+Num:iQ1 GÁVTSI- ;           !^N^
+Num: GÁKTSA- ;             !^N^
                            
LEXICON GÁVTSE-             !^N^
 +Sg+Gen: K ;              !^N^
 +Sg+Gen: NUMERALCOMPOUNDS ;        !^N^ ^C^
 +Sg+Ine:n K ;             !^N^
 +Sg+Ela:s K ;             !^N^
 +Sg+Par:t K ;             !^N^
 +Pl+Nom: K ;              !^N^

LEXICON GÁVTSI-
 +Sg+Com:jn K ;            !^N^
 +Pl+Gen:j K ;             !^N^
 +Pl+Acc:jt K ;            !^N^
 +Pl+Ill:jda K ;           !^N^
 +Pl+Ine:jn K ;            !^N^
 +Pl+Ela:js K ;            !^N^
 +Pl+Com:j K ;             !^N^
                            
                            
LEXICON GÁKTSA-             !^N^
 +Sg+Nom:a K ;              !^N^
 +Sg+Acc:a K ;              !^N^
 +Sg+Ill:áj K ; !^N^  !XXX Spiik 77 -aj
 +Ess:an K ;      !^N^
 

LEXICON AKTSE            !^N^
+Num:eQ1 AVTSE- ;           !^N^
+Num:iQ1 AVTSI- ;           !^N^
+Num: AKTS- ;             !^N^

LEXICON AVTSE-          !^N^  
 +Sg+Gen: K ;          !^N^
 +Sg+Gen: NUMERALCOMPOUNDS ;        !^N^ ^C^
 +Sg+Ine:n K ;         !^N^
 +Sg+Ela:s K ;         !^N^
 +Sg+Par:t K ;         !^N^
 +Pl+Nom: K ;          !^N^

LEXICON AVTSI-
 +Sg+Com:jn K ;        !^N^
 +Pl+Gen:j K ;         !^N^
 +Pl+Acc:jt K ;        !^N^
 +Pl+Ill:jda K ;       !^N^
 +Pl+Ine:jn K ;        !^N^
 +Pl+Ela:js K ;        !^N^
 +Pl+Com:j K ;         !^N^
                        
                    
LEXICON AKTS-           !^N^
 +Sg+Nom:e K ;          !^N^
 +Sg+Acc:e K ;          !^N^
 +Sg+Ill:áj K ;  !^N^ !XXXSpiik 77 -aj
 +Ess:an K ;        !^N^
 

LEXICON LÅGEV      !^N^           
+Num:eQ1 LÅGE- ;    !^N^
+Num:iQ1 LÅGI- ;    !^N^
+Num: LÅHK- ;      !^N^
                   
LEXICON LÅGE-      !^N^
 +Sg+Nom:v K ;    !^N^
 +Sg+Acc:v K ;    !^N^
 +Sg+Gen: K ;     !^N^
 +Sg+Gen: NUMERALCOMPOUNDS ;        !^N^ ^C^
 +Sg+Ine:n K ;    !^N^
 +Sg+Ela:s K ;    !^N^
 +Sg+Par:t K ;    !^N^
 +Pl+Nom: K ;     !^N^

LEXICON LÅGI-
 +Sg+Com:jn K ;   !^N^
 +Pl+Gen:j K ;    !^N^
 +Pl+Acc:jt K ;   !^N^
 +Pl+Ill:jda K ;  !^N^
 +Pl+Ine:jn K ;   !^N^
 +Pl+Ela:js K ;   !^N^
 +Pl+Com:j K ;    !^N^
                   
                   
LEXICON LÅHK-      !^N^
 +Sg+Ill:åj K ;    !^N^
 +Ess:en K ;       !^N^
                   
                   
LEXICON AKTALÅGENAN  !^N^
+Num: AKTALÅGENAN-NIE ;  !^N^
+Num:Q1 AKTALÅGENAN-OBL ;      !^N^
                   
LEXICON AKTALÅGENAN-NIE    !^N^
 +Sg+Nom:#lågenan K ;     !^N^
 +Sg+Ill:j#lågenan K ;       !^N^
 +Ess:n#lågenan K ;        !^N^
                   
LEXICON AKTALÅGENAN-OBL    !^N^
 +Sg+Gen:#lågenan K ;      !^N^
 +Sg+Gen:#lågenan NUMERALCOMPOUNDS ;      !^N^ ^C^
 +Sg+Acc:v#lågenan K ; !^N^
 +Sg+Ine:n#lågenan K ; !^N^
 +Sg+Ela:s#lågenan K ; !^N^
 +Sg+Com:jn#lågenan K ; !^N^
 +Sg+Par:t#lågenan K ; !^N^
 +Pl+Nom:#lågenan K ; !^N^
 +Pl+Gen:j#lågenan K ; !^N^
 +Pl+Acc:jt#lågenan K ; !^N^
 +Pl+Ill:jda#lågenan K ; !^N^
 +Pl+Ine:jn#lågenan K ; !^N^
 +Pl+Ela:js#lågenan K ; !^N^
 +Pl+Com:j#lågenan K ; !^N^
 
LEXICON ÅVTÅLÅGENAN                   !^N^
+Num+Sg+Gen:#lågenan K ;              !^N^
+Num+Sg+Gen:#lågenan NUMERALCOMPOUNDS ;      !^N^ ^C^
+Num+Sg+Acc:v#lågenan K ;             !^N^
+Num+Sg+Ine:n#lågenan K ;             !^N^
+Num+Sg+Ela:s#lågenan K ;             !^N^
+Num+Sg+Com:jn#lågenan K ;            !^N^
+Num+Sg+Par:t#lågenan K ;             !^N^
+Num+Pl+Nom:#lågenan K ;              !^N^
+Num+Pl+Gen:j#lågenan K ;             !^N^
+Num+Pl+Acc:jt#lågenan K ;            !^N^
+Num+Pl+Ill:jda#lågenan K ;           !^N^
+Num+Pl+Ine:jn#lågenan K ;            !^N^
+Num+Pl+Ela:js#lågenan K ;            !^N^
+Num+Pl+Com:j#lågenan K ;             !^N^
                                      
LEXICON GUOKTALÅGENAN                 !^N^
+Num:Q1 GUOVTELÅGENAN ;               !^N^
+Num: GUOKT-LÅGENAN ;                 !^N^
                                      
LEXICON GUOVTELÅGENAN                 !^N^
 +Sg+Gen:e#lågenan K ;                !^N^
 +Sg+Gen:e#lågenan NUMERALCOMPOUNDS ;           !^N^ ^C^
 +Sg+Ine:en#lågenan K ;               !^N^
 +Sg+Ela:es#lågenan K ;               !^N^
 +Sg+Com:ijn#lågenan K ;              !^N^
 +Sg+Par:et#lågenan K ;               !^N^
 +Pl+Nom:e#lågenan K ;                !^N^
 +Pl+Gen:ij#lågenan K ;               !^N^
 +Pl+Acc:ijt#lågenan K ;              !^N^
 +Pl+Ill:ijda#lågenan K ;             !^N^
 +Pl+Ine:ijn#lågenan K ;              !^N^
 +Pl+Ela:ijs#lågenan K ;              !^N^
 +Pl+Com:ij#lågenan K ;               !^N^
                                      
LEXICON GUOKT-LÅGENAN                 !^N^
 +Sg+Nom:a#lågenan K ;                !^N^
 +Sg+Acc:a#lågenan K ;                !^N^
 +Sg+Ill:áj#lågenan K ;               !^N^
 +Ess:an#lågenan K ;                  !^N^
                                      
LEXICON GÅLMMÅLÅGENAN                 !^N^
+Num: GÅLMMÅLÅGENAN-NAIE ;            !^N^
+Num:Q1 GÅLMMÅLÅGENAN-OBL ;           !^N^
                                      
LEXICON GÅLMMÅLÅGENAN-NAIE            !^N^
 +Sg+Nom:#lågenan K ;                 !^N^
 +Sg+Acc:#lågenan K ;                 !^N^
 +Sg+Ill:j#lågenan K ;                !^N^
 +Ess:n#lågenan K ;                   !^N^
                                      
LEXICON GÅLMMÅLÅGENAN-OBL             !^N^
 +Sg+Gen:#lågenan K ;                 !^N^
 +Sg+Gen:#lågenan NUMERALCOMPOUNDS ;         !^N^ ^C^
 +Sg+Ine:n#lågenan K ;                !^N^
 +Sg+Ela:s#lågenan K ;                !^N^
 +Sg+Com:jn#lågenan K ;               !^N^
 +Sg+Par:t#lågenan K ;                !^N^
 +Pl+Nom:#lågenan K ;                 !^N^
 +Pl+Gen:j#lågenan K ;                !^N^
 +Pl+Acc:jt#lågenan K ;               !^N^
 +Pl+Ill:jda#lågenan K ;              !^N^
 +Pl+Ine:jn#lågenan K ;               !^N^
 +Pl+Ela:js#lågenan K ;               !^N^
 +Pl+Com:j#lågenan K ;                !^N^
                                      
LEXICON GIETJAVLÅGENAN                !^N^
+Num: GIETJAVLÅGENAN-IE ;             !^N^
+Num:Q1 GIETJAVLÅGENAN-OBL ;          !^N^
                                      
LEXICON GIETJAVLÅGENAN-IE             !^N^
 +Sg+Ill:j#lågenan K ;                !^N^
 +Ess:n#lågenan K ;                   !^N^
                                      
LEXICON GIETJAVLÅGENAN-OBL            !^N^
 +Sg+Nom:v#lågenan K ;                !^N^
 +Sg+Gen:#lågenan K ;                 !^N^
 +Sg+Gen:#lågenan NUMERALCOMPOUNDS ;          !^N^ ^C^
 +Sg+Acc:v#lågenan K ;                !^N^
 +Sg+Ine:n#lågenan K ;                !^N^
 +Sg+Ela:s#lågenan K ;                !^N^
 +Sg+Com:jn#lågenan K ;               !^N^
 +Sg+Par:t#lågenan K ;                !^N^
 +Pl+Nom:#lågenan K ;                 !^N^
 +Pl+Gen:j#lågenan K ;                !^N^
 +Pl+Acc:jt#lågenan K ;               !^N^
 +Pl+Ill:jda#lågenan K ;              !^N^
 +Pl+Ine:jn#lågenan K ;               !^N^
 +Pl+Ela:js#lågenan K ;               !^N^
 +Pl+Com:j#lågenan K ;                !^N^
                                      
LEXICON GÁKTSALÅGENAN                 !^N^
+Num:Q1 GÁVTSELÅGENAN ;               !^N^
+Num: GÁKTS-LÅGENAN ;                 !^N^
                                      
LEXICON GÁVTSELÅGENAN                 !^N^
 +Sg+Gen:e#lågenan K ;                !^N^
 +Sg+Gen:e#lågenan NUMERALCOMPOUNDS ;       !^N^ ^C^
 +Sg+Ine:en#lågenan K ;               !^N^
 +Sg+Ela:es#lågenan K ;               !^N^
 +Sg+Com:ijn#lågenan K ;              !^N^
 +Sg+Par:et#lågenan K ;               !^N^
 +Pl+Nom:e#lågenan K ;                !^N^
 +Pl+Gen:ij#lågenan K ;               !^N^
 +Pl+Acc:ijt#lågenan K ;              !^N^
 +Pl+Ill:ijda#lågenan K ;             !^N^
 +Pl+Ine:ijn#lågenan K ;              !^N^
 +Pl+Ela:ijs#lågenan K ;              !^N^
 +Pl+Com:ij#lågenan K ;               !^N^
                                      
LEXICON GÁKTS-LÅGENAN                 !^N^
 +Sg+Nom:a#lågenan K ;                !^N^
 +Sg+Acc:a#lågenan K ;                !^N^
 +Sg+Ill:áj#lågenan K ; !^N^    !XXX Spiik 77 -aj
 +Ess:an#lågenan K ;                  !^N^
                                      
LEXICON AKTSELÅGENAN                  !^N^
+Num:Q1 AVTSELÅGENAN ;                !^N^
+Num: AKTS-LÅGENAN ;                  !^N^
                                      
LEXICON AVTSELÅGENAN                  !^N^
 +Sg+Gen:e#lågenan K ;                !^N^
 +Sg+Gen:e#lågenan NUMERALCOMPOUNDS ;      !^N^ ^C^
 +Sg+Ine:en#lågenan K ;               !^N^
 +Sg+Ela:es#lågenan K ;               !^N^
 +Sg+Com:ijn#lågenan K ;              !^N^
 +Sg+Par:et#lågenan K ;               !^N^
 +Pl+Nom:e#lågenan K ;                !^N^
 +Pl+Gen:ij#lågenan K ;               !^N^
 +Pl+Acc:ijt#lågenan K ;              !^N^
 +Pl+Ill:ijda#lågenan K ;             !^N^
 +Pl+Ine:ijn#lågenan K ;              !^N^
 +Pl+Ela:ijs#lågenan K ;              !^N^
 +Pl+Com:ij#lågenan K ;               !^N^
                                      
LEXICON AKTS-LÅGENAN                  !^N^
 +Sg+Nom:e#lågenan K ;                !^N^
 +Sg+Acc:e#lågenan K ;                !^N^
 +Sg+Ill:áj#lågenan K ;  !^N^    !XXXSpiik 77 -aj
 +Ess:an#lågenan K ;                  !^N^
                                      
LEXICON TJUOHTECASE           !^N^
 +Num:eQ1 TJUODE- ;                     !^N^
 +Num:iQ1 TJUODI- ;                     !^N^
 +Num: TJUOHT- ;                       !^N^
                                      
LEXICON TJUODE-                       !^N^
 +Sg+Acc:v K ;                       !^N^
 +Sg+Gen: K ;                        !^N^
 +Sg+Ine:n K ;                       !^N^
 +Sg+Ela:s K ;                       !^N^
 +Sg+Par:t K ;                       !^N^
 +Pl+Nom: K ;                        !^N^

LEXICON TJUODI-
 +Pl+Gen:j K ;                       !^N^
 +Sg+Com:jn K ;                      !^N^
 +Pl+Acc:jt K ;                      !^N^
 +Pl+Ill:jda K ;                     !^N^
 +Pl+Ine:jn K ;                      !^N^
 +Pl+Ela:js K ;                      !^N^
 +Pl+Com:j K ;                       !^N^
                                      
LEXICON TJUOHT-                       !^N^
 +Sg+Nom:e K ;                        !^N^
 +Ess:en K ;                          !^N^

LEXICON TUVSÁN				!^N^
 +Sg+Nom: K ;          !^N^
 +Sg+Gen:a K ;         !^N^
 +Sg+Gen: NUMERALCOMPOUNDS ;         !^N^  ^C^
 +Sg+Acc:av K ;            !^N^
 +Sg+Ill:ij K ;   !^N^
 +Sg+Ine:in K ;           !^N^
 +Sg+Ela:is K ;            !^N^
 +Sg+Com:ijn K ;            !^N^
 +Ess:in K ;                  !^N^
 +Pl+Nom:a K ;                 !^N^
 +Pl+Gen:ij K ;              !^N^
 +Pl+Acc:ijt K ;              !^N^
 +Pl+Ill:ijda K ;              !^N^
 +Pl+Ine:ijn K ;                !^N^
 +Pl+Ela:ijs K ;                 !^N^
 +Pl+Com:ij K ;                 !^N^




LEXICON NUMERALCOMPOUNDS ! numeral as first part !^N^
 NAMÁK ;			   ! tuvsánjienat, logigielat, etc. !^N^
 SASJ ; !^N^
! NounSecond ;	! golmmačiegahas	!^N^
! geardásaš:geardás DABALAS ;	   
! !tuvsán#geardásaš !^N^
! jahkásaš:jahkás DABALAS ; !^N^


! Ordinal numbers
! ---------------


LEXICON VUOSTASJ				!^N^
 +Sg+Nom: K ;          !^N^
 +Sg+Gen:a K ;         !^N^
 +Sg+Acc:av K ;            !^N^
 +Sg+Ill:ij K ;   !^N^
 +Sg+Ine:in K ;           !^N^
 +Sg+Ela:is K ;            !^N^
 +Sg+Com:ijn K ;            !^N^
 +Ess:in K ;                  !^N^
 +Pl+Nom:a K ;                 !^N^
 +Pl+Gen:ij K ;              !^N^
 +Pl+Acc:ijt K ;              !^N^
 +Pl+Ill:ijda K ;              !^N^
 +Pl+Ine:ijn K ;                !^N^
 +Pl+Ela:ijs K ;                 !^N^
 +Pl+Com:ij K ;                 !^N^
 +Attr: K ;					!^N^

LEXICON TUVSÁNAT				!^N^
 +Sg+Nom:t K ;          !^N^
 +Sg+Gen:tta K ;         !^N^
 +Sg+Acc:ttav K ;            !^N^
 +Sg+Ill:ddaj K ;   !^N^
 +Sg+Ine:ttan K ;           !^N^
 +Sg+Ela:ttas K ;            !^N^
 +Sg+Com:ttajn K ;            !^N^
 +Ess:ddan K ;                  !^N^
 +Pl+Nom:tta K ;                 !^N^
 +Pl+Gen:ttaj K ;              !^N^
 +Pl+Acc:ttajt K ;              !^N^
 +Pl+Ill:ttajda K ;              !^N^
 +Pl+Ine:ttajn K ;                !^N^
 +Pl+Ela:ttajs K ;                 !^N^
 +Pl+Com:ttaj K ;                 !^N^
 +Attr:t K ;					!^N^


! Imprecise numbers
! -----------------

LEXICON num-imprecise				!^N^
galla+Num+Sg+Nom:galla K ;
galli+Num:galli gallicase ;
galla+Num+Sg+Nom:galla K ;
galla+Num+Sg+Ill:galláj K ;
galla+Num:gall nubbecase ;
máŋŋga+Num+Sg+Nom:máŋŋga K ;
máŋŋga+Num+Sg+Acc:máŋŋga K ;
máŋŋga+Num+Sg+Ill:máŋŋgaj K ;
máŋŋga+Num:máŋga máŋŋgacase ;
moadda+Num+Sg+Nom:moadda K ;
máŋŋga+Num+Sg+Com:máŋgajn K ;
moadda+Num+Sg+Com:måttijn K ;
moadda+Num+Sg+Ill:moaddaj K ;
moadda+Num:moatte máŋŋgacase ;
moadda+Num+Sg+Acc:moadda K ;
ållo+Num+Sg+Nom:ållo K ;
ållo+Num+Sg+Ill:ålluj K ;
ållo+Num+Sg+Com:ålujn K ;
ållo+Num:ålo máŋŋgacase ;
!máŋga num-imprecise-distribution ;		!^N^
!moadde   num-imprecise-distribution ;		!^N^
!galle  num-imprecise-distribution ;		!^N^
!máŋ0ga:máŋ'ga GOLBMA ;				!^N^
!moadde:moadd GUOKTE ;				!^N^
!gal0le:gal'le GOLBMA ;				!^N^
!beal-nup+Num:beal%-nup3 # ; ! ad hoc		!^N^
biel#nup+Num+Sg+Nom:biel#nup # ;				!^N^
biel#nup+Num+Sg+Gen:biel#nup # ;				!^N^
biel#nup+Num+Sg+Acc:biel#nup # ;				!^N^
biel#nup+Num+Sg+Attr:biel#nup SASJ ;				!^N^
biel#goalmát+A+Ord:biel#goalmád3 VUOSTASJ ;       !^N^ 
biel#nælját+A+Ord:biel#næljád3 VUOSTASJ ;       !^N^ 
biel#vidát+A+Ord:biel#vidád3 VUOSTASJ ;       !^N^ 
biel#gudát+A+Ord:biel#gudád3 VUOSTASJ ;       !^N^ 
biel#giehttjit+A+Ord:biel#giehttjid3 VUOSTASJ ;       !^N^  
biel#gávtsát+A+Ord:biel#gávtsád3 VUOSTASJ ;       !^N^
biel#avtsát+A+Ord:biel#avtsád3 VUOSTASJ ;       !^N^ 
biel#lågåt+A+Ord:biel#lågåd3 VUOSTASJ ;			 !^N^
biel#goalmát+A+Ord+Attr:biel#goalmád3 SASJ ;       !^N^ 
biel#nælját+A+Ord+Attr:biel#næljád3 SASJ ;       !^N^ 
biel#vidát+A+Ord+Attr:biel#vidád3 SASJ ;       !^N^ 
biel#gudát+A+Ord+Attr:biel#gudád3 SASJ ;       !^N^ 
biel#giehttjit+A+Ord+Attr:biel#giehttjid3 SASJ ;       !^N^  
biel#gávtsát+A+Ord+Attr:biel#gávtsád3 SASJ ;       !^N^
biel#avtsát+A+Ord+Attr:biel#avtsád3 SASJ ;       !^N^ 
biel#lågåt+A+Ord+Attr:biel#lågåd3 SASJ ;			 !^N^


!LEXICON num-imprecise-distribution		!^N^
!NUPPELOHKAIGENERATOR ;  ! máŋga#nuppe#lohkái, máŋga !^N^
!LOGIF ;        ! máŋga#logi, máŋga#lot		  !^N^
!CUODI ;        ! máŋga#čuođi			  !^N^
!NL ;           ! máŋga#nuppe#lohkái#duhát		  !^N^
!O-NL ;         ! máŋga#nuppe#lohkái#duhát		  !^N^
!O-LOGIF ;      ! máŋga#logi#duhát			  !^N^
!JUSTTUVSÁN ;    ! máŋga#duhát			  !^N^
!O-CUODI ;      ! máŋga#duhát			  !^N^




! Arabic numerals
! ---------------


LEXICON NUM-PREFIXES				!^C^
§	ARABIC ;      ! §24			!^C^
§§	ARABIC ;     ! §§24			!^C^
§% 	ARABIC ;    ! § 24			!^C^
§§% 	ARABIC ;   ! §§ 24			!^C^
%-	ARABIC ;     ! -24			!^C^
%‒	ARABIC ;     ! -24 U+2012 Figure line	!^C^
%–	ARABIC ;     ! -24 U+2013 en dash	!^C^
%—	ARABIC ;     ! -24 U+2014 em dash	!^C^
%―	ARABIC ;     ! -24 U+2015 horizontal l	!^C^
%+	ARABIC ;     ! +24			!^C^
%-% 	ARABIC ;	! - 24			!^C^
%‒% 	ARABIC ;	! - 24 2012		!^C^
%–% 	ARABIC ;	! - 24 2013	 	!^C^
%—% 	ARABIC ;	! - 24 2014		!^C^
%―% 	ARABIC ;	! - 24 2015 		!^C^
%+% 	ARABIC ;	! + 24			!^C^
%*	ARABIC ;     ! *24			!^C^
%$	ARABIC ;     ! $24  ! And the Euro?	!^C^
%<	ARABIC ;     ! <24			!^C^
%>	ARABIC ;     ! >24			!^C^
s%.	ARABIC ;     ! s.24			!^C^
	


LEXICON ARABIC					!^C^
1 ARABICLOOP ;					!^C^
2 ARABICLOOP ;					!^C^
3 ARABICLOOP ;					!^C^
4 ARABICLOOP ;					!^C^
5 ARABICLOOP ;					!^C^
6 ARABICLOOP ;					!^C^
7 ARABICLOOP ;					!^C^
8 ARABICLOOP ;					!^C^
9 ARABICLOOP ;					!^C^
%0 ARABICLOOP ;					!^C^

LEXICON ARABICLOOP				!^C^
ARABIC ;					!^C^
%-	ARABIC ;	      ! 1-2    multipart numbers	!^C^
%.%- 	ARABIC ;	      ! 1.-2   multipart numbers	!^C^
%-% 	ARABIC ;	      ! 1- 2   multipart numbers	!^C^
% %-	ARABIC ;	      ! 1 -2   multipart numbers	!^C^
% %-% 	ARABIC ;	      ! 1 - 2  multipart numbers	!^C^
% %-%-%   ARABIC ;	      ! 1 -- 2 multipart numbers	!^C^
% %-      ARABICDELIMITER ;   ! 1 -   multipart numbers		!^C^
% %-%-	  ARABICDELIMITER ;   ! 1 --  multipart numbers		!^C^
%. ARABIC ;							!^C^
%, ARABIC ;							!^C^
%: ARABIC ;							!^C^
%  ARABIC ;	   ! Allowing for "23 500".			!^C^
   ARABIC ;	   ! Allowing for "23 500" w/nbsp (initial char is alt-space)	!^C^
%/ ARABIC ;	   ! 24/12					!^C^
!%' ARABIC ;	   ! Since ' denotes cons.grad. This is not optimal either. 
!+Num:%- R ; !^C^	   ! Number compounds
+Num: R ; !^C^	   ! Hyphen removed bec. of "123- ja 124-viessu" !^C^
+A+Ord:%. # ;							 !^C^
,%-+Num:,%- # ;		! 10,-					 !^C^
%%+Num+Nom:%% # ;	! 50%. It is wrong, but 75 % of us write "75%".		!^C^
% %%+Num+Nom:% %% # ;	! We must analyse the correct ones as well... 50 % etc.	!^C^
+Num: ARABICDELIMITER ; ! list of number-case delimiters			!^C^


LEXICON ARABICDELIMITER								!^C^
  ARABICCASE ;      ! This is the 1984s case.					!^C^
 :%: ARABICCASE ;    ! This is the 1984:s case.					!^C^
 :%' ARABICCASE ;    ! This is the 1984's case.					!^C^
 :%- ARABICCASE ;    ! This is the 1984-s case.					!^C^
                     ! So, what does the lg council prefer?			

LEXICON ARABICCASE								!^C^
+Nom:f # ;		! s. 123f.						!^C^
+Nom:ff # ;		! s. 123ff.						!^C^
+Nom:b # ;		! § 123b						!^C^
+Nom:c # ;		! § 123c						!^C^
+Nom:d # ;		! § 123d						!^C^
+Nom:e # ;		! § 123e						!^C^
! # ;		! 123 is now 123+Num as well as 123+Num+Nom etc. Out again!	!^C^
+Nom: # ;									!^C^
+Gen: # ;									!^C^
!+Acc: # ;									!^C^
+Gen:a # ;	! But here 123a 123+Num+Gen					!^C^
+Acc:av # ;									!^C^
+Ill:j # ;									!^C^
+Ill:aj # ;									!^C^
+Ill:áj # ;	! Testing over the weekend.					!^C^
+Ela:s # ;									!^C^
+Ela:is # ;									!^C^
+Ela:as # ;									!^C^
+Ine:s # ;									!^C^
+Ine:is # ;									!^C^
+Ine:as # ;									!^C^
+Com:jn # ;									!^C^
+Com:ajn # ;									!^C^
+Ess:n # ;									!^C^
+Ess:an # ;									!^C^
! No plural numbers, hmm.


! The Roman numerals !
! ------------------ !

LEXICON ROMAN						!^C^
 ROM-THOUSAND ;						!^C^
 ROM-HUNDRED ;						!^C^
 ROM-TEN ;						!^C^
 ROM-ONE ;						!^C^

LEXICON ROM-THOUSAND              !^C^
 M ROM-THOUSAND-TAG ;						!^C^
 MM ROM-THOUSAND-TAG ;					!^C^
 MMM ROM-THOUSAND-TAG ;					!^C^
 MMMM ROM-THOUSAND-TAG ;					!^C^
 MMMMM ROM-THOUSAND-TAG ;					!^C^

LEXICON ROM-THOUSAND-TAG					!^C^
  ROMNUMTAG ;						!^C^
 ROM-HUNDRED ;					!^C^
 ROM-SPLIT	;				!^C^

LEXICON ROM-HUNDRED					!^C^
 C ROM-HUNDRED-TAG ;						!^C^
 CC ROM-HUNDRED-TAG ;						!^C^
 CCC ROM-HUNDRED-TAG ;						!^C^
 CD ROM-HUNDRED-TAG ;						!^C^
 D ROM-HUNDRED-TAG ;						!^C^
 DC ROM-HUNDRED-TAG ;						!^C^
 DCC ROM-HUNDRED-TAG ;						!^C^
 DCCC ROM-HUNDRED-TAG ;						!^C^
 CM ROM-HUNDRED-TAG ;						!^C^

LEXICON ROM-HUNDRED-TAG					!^C^
  ROMNUMTAG ;						!^C^
 ROM-TEN ;					!^C^
 ROM-SPLIT ;					!^C^

LEXICON ROM-TEN						!^C^
 X ROM-TEN-TAG ;						!^C^
 XX ROM-TEN-TAG ;						!^C^
 XXX ROM-TEN-TAG ;						!^C^
 XL ROM-TEN-TAG ;						!^C^
 L ROM-TEN-TAG ;						!^C^
 LX ROM-TEN-TAG ;						!^C^
 LXX ROM-TEN-TAG ;						!^C^
 LXXX ROM-TEN-TAG ;						!^C^
 XC ROM-TEN-TAG ;						!^C^

LEXICON ROM-TEN-TAG					!^C^
  ROMNUMTAG ;						!^C^
 ROM-ONE ;						!^C^
 ROM-SPLIT ;						!^C^

LEXICON ROM-ONE						!^C^
 I ROM-ONE-TAG ;						!^C^
 II ROM-ONE-TAG ;						!^C^
 III ROM-ONE-TAG ;						!^C^
 IV  ROM-ONE-TAG ;						!^C^
 V ROM-ONE-TAG ;						!^C^
 VI ROM-ONE-TAG ;						!^C^
 VII ROM-ONE-TAG ;						!^C^
 VIII ROM-ONE-TAG ;						!^C^
 IX ROM-ONE-TAG ;						!^C^

LEXICON ROM-ONE-TAG					!^C^
  ROMNUMTAG ;						!^C^
! +N: ROMNUMTAG ;						!^C^ !The Olav viđeš fix (Roman V now gets A)
 ROM-SPLIT ;						!^C^

! Here, we split the Roman numerals, in order to account for cases like "Kapihtal II-IV".
! We may send this first part directly to ROM-TAG below (and get the +Num tag), or we may
! send them through a second loop, identical to the first one, but marked wit "2" (the lexica
! are called 2ROMAN, etc. We do this instead of making a loop, since we do not want cases
! like "II-IV-VI-VII-IX". If that should turn out to be a good idea, a loop would do the 
! trick.

LEXICON ROM-SPLIT					!^C^
 :%- 2ROMAN ; ! II-VI, etc.				!^C^


! Here goes loop 2.

LEXICON 2ROMAN						!^C^
 2ROM-THOUSAND ;						!^C^
 2ROM-HUNDRED ;						!^C^
 2ROM-TEN ;						!^C^
 2ROM-ONE ;						!^C^

LEXICON 2ROM-THOUSAND              !^C^
 M 2ROM-THOUSAND-TAG ;						!^C^
 MM 2ROM-THOUSAND-TAG ;					!^C^
 MMM 2ROM-THOUSAND-TAG ;					!^C^
 MMMM 2ROM-THOUSAND-TAG ;					!^C^
 MMMMM 2ROM-THOUSAND-TAG ;					!^C^

LEXICON 2ROM-THOUSAND-TAG					!^C^
  ROMNUMTAG ;						!^C^
 2ROM-HUNDRED ;					!^C^

LEXICON 2ROM-HUNDRED					!^C^
 C 2ROM-HUNDRED-TAG ;						!^C^
 CC 2ROM-HUNDRED-TAG ;						!^C^
 CCC 2ROM-HUNDRED-TAG ;						!^C^
 CD 2ROM-HUNDRED-TAG ;						!^C^
 D 2ROM-HUNDRED-TAG ;						!^C^
 DC 2ROM-HUNDRED-TAG ;						!^C^
 DCC 2ROM-HUNDRED-TAG ;						!^C^
 DCCC 2ROM-HUNDRED-TAG ;						!^C^
 CM 2ROM-HUNDRED-TAG ;						!^C^

LEXICON 2ROM-HUNDRED-TAG					!^C^
  ROMNUMTAG ;						!^C^
 2ROM-TEN ;					!^C^

LEXICON 2ROM-TEN						!^C^
 X 2ROM-TEN-TAG ;						!^C^
 XX 2ROM-TEN-TAG ;						!^C^
 XXX 2ROM-TEN-TAG ;						!^C^
 XL 2ROM-TEN-TAG ;						!^C^
 L 2ROM-TEN-TAG ;						!^C^
 LX 2ROM-TEN-TAG ;						!^C^
 LXX 2ROM-TEN-TAG ;						!^C^
 LXXX 2ROM-TEN-TAG ;						!^C^
 XC 2ROM-TEN-TAG ;						!^C^

LEXICON 2ROM-TEN-TAG					!^C^
  ROMNUMTAG ;					!^C^
 2ROM-ONE ;					!^C^

LEXICON 2ROM-ONE						!^C^
 I 2ROM-ONE-TAG ;						!^C^
 II 2ROM-ONE-TAG ;						!^C^
 III 2ROM-ONE-TAG ;						!^C^
 IV  2ROM-ONE-TAG ;						!^C^
 V 2ROM-ONE-TAG ;						!^C^
 VI 2ROM-ONE-TAG ;						!^C^
 VII 2ROM-ONE-TAG ;						!^C^
 VIII 2ROM-ONE-TAG ;						!^C^
 IX 2ROM-ONE-TAG ;						!^C^

LEXICON 2ROM-ONE-TAG						!^C^
  ROMNUMTAG ;						!^C^
 
LEXICON ROMNUMTAG 						!^C^
 +Num+Nom: # ;									!^C^
 +Num+Gen: # ;									!^C^
! +Num+Acc: # ;									!^C^
 +A+Ord:%. # ;									!^C^ !III. as ordinal.
 +A+Ord: # ;								!^C^ !Looking for trouble...
 +Num:%: ROMNUMTAGOBL ;						!^C^
 +Num:%' ROMNUMTAGOBL ;						!^C^
 +Num:%- ROMNUMTAGOBL ;						!^C^
 +Num:%´ ROMNUMTAGOBL ;						!^C^
 +Num:   ROMNUMTAGOBL ;						!^C^

LEXICON ROMNUMTAGOBL 						!^C^
+Gen:a # ;	! But here 123a 123+Num+Gen					!^C^
+Acc:av # ;									!^C^
+Ill:j # ;									!^C^
+Ill:ij # ;									!^C^
+Ill:áj # ;					!^C^
+Ela:s # ;									!^C^
+Ine:s # ;									!^C^
+Com:jn # ;									!^C^
+Ess:n # ;									!^C^




! A final section with some isolated numeral expresssions !
! ------------------------------------------------------- !

      
LEXICON ISOLATED-NUMEXP					!^C^
½+Num:½ # ;						!^C^
¹+Num:¹ # ;						!^C^
²+Num:² # ;						!^C^
³+Num:³ # ;						!^C^
¼+Num:¼ # ;						!^C^




