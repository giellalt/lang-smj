! Numerals
! ========



LEXICON Numeral					
MILIJÅVNNÅ ;          ! a noun of its own		
UNDERTUVSÁN ;      ! for generator under 1000	
JUSTTUVSÁN ;       ! going via 1000		
JUSTDUHÁT ;                                     
OVERTUVSÁN ;       ! for generator over 1000	
OLD ;             ! for "thirteen hundred, etc.	
!num-derived ;    ! still unimplemented		
num-imprecise ;                              
ARABIC ;          ! for the arabic numerals !^C^
ROMAN ;           ! for the roman numerals	!^C^
NUM-PREFIXES ;    ! for §34 etc.		!^C^
ISOLATED-NUMEXP ; ! for ½ etc.			!^C^
COLLECTIVE ;                            
O-NL ;  

! Million, a noun of its own
! --------------------------

LEXICON MILIJÅVNNÅ					
milijåvnnå:milijåvnnå AKTA ;			
millijåvnnå:millijåvnnå AKTA ;
milijåvnåt+A+Ord:milijåvnåd3 VUOSTASJ ;	
millijåvnåt+A+Ord:millijåvnåd3 VUOSTASJ ;
milliárdda:milliárdda AKTA ;		! Num or N?
milliárdat+A+Ord:milliárdad3 VUOSTASJ ;	

! Overtuvsán, for the numerals over 1000
! --------------------------------------

LEXICON OVERTUVSÁN				
O-AKTAF ;     ! 1000				
O-2TO9F ;     ! 2000, 3000, ...			
O-JUSTLÅGEVF ; ! 10000				
O-2TO9LÅG;   ! 20000, 30000, ...		
O-LÅGENAN ;  ! 11000, 12000, ...		
O-TJUOHTE ;     ! 100000					
O-2TO9TJUO ;   ! 200000, 300000, ...		

! All the child lexica of OVERTUVSÁN have the prefix O-.
! They are directed via their respective numerals to the lexicon JUSTTUVSÁN.


! Thousands

LEXICON O-AKTAF		
akta# JUSTDUHÁT ;
JUSTDUHÁT ;		

LEXICON O-X1000		
akta# JUSTTUVSÁN ;

LEXICON O-2TO9F                               
guokta# JUSTTUVSÁN ;                                
gålmmå# JUSTTUVSÁN ;                            
niellja# JUSTTUVSÁN ;                                  
vihtta# JUSTTUVSÁN ;                            
guhtta# JUSTTUVSÁN ;                            
gietjav# JUSTTUVSÁN ;                             
gieža# JUSTTUVSÁN ;                             
gáktsa# JUSTTUVSÁN ;                            
aktse# JUSTTUVSÁN ;                             

! Tens of thousands

LEXICON O-JUSTLÅGEVF		
lågev# JUSTTUVSÁN ;		
! This lexicon is for the number 10000 only. it is separated from the
! rest to avoid forms like *logivihttaduhát, etc.


LEXICON O-LÅGEVF				     
låk# JUSTTUVSÁN ;		
låk# O-X1000 ;			
låk# O-2TO9F ;			
! this lexicon is accessed only via other O-lexica, and not directly 
! from OVERTUVSÁN. Thus, *logivihttaduhát, etc. is avoided.

LEXICON O-2TO9LÅG			  
guokta# O-LÅGEVF ;		
gålmmå# O-LÅGEVF ;		
niellja# O-LÅGEVF ;		
vihtta# O-LÅGEVF ;		
guhtta# O-LÅGEVF ;		
gietjav# O-LÅGEVF ;			
gáktsa# O-LÅGEVF ;		
aktse# O-LÅGEVF ;			


! Teens of thousands

LEXICON O-LÅGENAN		
lågenan#akta# JUSTTUVSÁN ;        
lågenan#guokta#:lågenan#guokta# JUSTTUVSÁN ;           
lågenan#gålmmå# JUSTTUVSÁN ;       
lågenan#niellja#:lågenan#niellja# JUSTTUVSÁN ;       
lågenan#vihtta#:lågenan#vihtta# JUSTTUVSÁN ;             
lågenan#guhtta#:lågenan#guhtta# JUSTTUVSÁN ;          
lågenan#gietjav#:lågenan#giehtjav# JUSTTUVSÁN ;        
lågenan#gáktsa#:lågenan#gáktsa# JUSTTUVSÁN ;       
lågenan#aktse#:lågenan#aktse# JUSTTUVSÁN ;                 

akta# O-NL ;        
guokta# O-NL ;           
gålmmå# O-NL ;       
niellja# O-NL ;       
vihtta# O-NL ;             
guhtta# O-NL ;          
gietjav# O-NL ;        
gáktsa# O-NL ;       
aktse# O-NL ;                 


 LEXICON O-NL			
 lågenan# JUSTTUVSÁN ;


! Hundreds of thousands

LEXICON O-TJUOHTE					
tjuohte# JUSTTUVSÁN ;  ! 100000			
tjuohte# O-X1000 ;     ! 101000			
tjuohte# O-2TO9F ;    ! 102000, 103000, ...	
tjuohte# O-LÅGEVF ;    ! 110000, 113000, ...	
tjuohte# O-2TO9LÅG ;  ! 120000, 123000, ...	
tjuohte# O-LÅGENAN ; ! 115000, ...		

LEXICON O-TJUODE					
tjuode# JUSTTUVSÁN ;  ! 200000			
tjuode# O-X1000 ;     ! 201000			
tjuode# O-2TO9F ;    ! 202000, 203000, ...	
tjuode# O-LÅGEVF ;    ! 210000, 213000, ...	
tjuode# O-2TO9LÅG ;  ! 220000, 223000, ...	
tjuode# O-LÅGENAN ; ! 215000, ...		

LEXICON O-2TO9TJUO				
akta# O-TJUOHTE ;       ! #one# hundred...		
guokta# O-TJUODE ;				
gålmmå# O-TJUODE ;				
niellja# O-TJUODE ;				
vihtta# O-TJUODE ;				
guhtta# O-TJUODE ;				
gietjav# O-TJUODE ;					
gáktsa# O-TJUODE ;				
aktse# O-TJUODE ;					

LEXICON JUSTDUHÁT				
tuvsán# UNDERTUVSÁN ;  	
tuvsán TUVSÁN ; ! Case infl of tuvsán
tuvsánat+A+Ord:tuvsána TUVSÁNAT ;			
duhát# UNDERTUVSÁN ;  	
duhát:duhád3 TUVSÁN ; ! Case infl of duhát	
duhádat+A+Ord:duháda TUVSÁNAT ;			




! Justtuvsán, for numerals going via 1000
! -------------------------------------

LEXICON JUSTTUVSÁN				
tuvsán# UNDERTUVSÁN ;  ! Thousand-and-something	
tuvsán TUVSÁN ; ! Case infl of tuvsán	
tuvsánat+A+Ord:tuvsána TUVSÁNAT ;			
duhát# UNDERTUVSÁN ;  ! Thousand-and-something	
duhát:duhád3 TUVSÁN ; ! Case infl of duhát	
duhádat+A+Ord:duháda TUVSÁNAT ;			


! Old, for the old counting thirteen hundred etc.	
! -----------------------------------------------

LEXICON OLD					
akta# NLX ;						
guokta# NLX ;					
gålmmå# NLX ;					
niellja# NLX ;					
vihtta# NLX ;					
guhtta# NLX ;					
gietjav# NLX ;					
gáktsa# NLX ;					
aktse# NLX ;					
lågenan#akta# GTJUODE ;						
lågenan#guokta# GTJUODE ;					
lågenan#gålmmå# GTJUODE ;					
lågenan#niellja# GTJUODE ;					
lågenan#vihtta# GTJUODE ;					
lågenan#guhtta# GTJUODE ;					
lågenan#gietjav# GTJUODE ;					
lågenan#gáktsa# GTJUODE ;					
lågenan#aktse# GTJUODE ;

LEXICON NLX					
lågenan# GTJUODE ;				




LEXICON UNDERTUVSÁN				
AKTAF ;     ! 1				
2TO9F ;     ! 2-9			
LÅGEVF ;     ! 10				
LÅGENAN ;   ! 11-19             
2TO9LÅG ;   ! 20-99				
TJUOHTE ;     ! 100-199				
2TO9TJUO ;   ! 200-999				

LEXICON AKTAF					
akta# AKTA ;        
akta#:åvtå# ÅVTÅ ;		
vuostasj+A+Ord:vuostatj VUOSTASJ ;			 


LEXICON 2TO9F					
guokta:guokt GUOKTA ;                  
nubbe+A+Ord:nupp nubbecase ;		 
nubbe+A+Ord+Sg+Nom:nubbe K ;         
nubbe+A+Ord+Ess:nubben K ;          
nubbe+A+Ord+Sg+Ill:nubbáj K ;        
nubbe#+A+Ord+Sg+Gen:nuppe# NUMERALCOMPOUNDS ;
nuppát+A+Ord:nuppád3 VUOSTASJ ;  
guovtes+N+Num+Sg+Nom:guovtes K ;      
guovtes+N+Num:guoktás VIDÁS ;      
gålmmå GÅLMMÅ ;       
goalmát+A+Ord:goalmád3 VUOSTASJ ;
gålmås+N+Num:gålmmås VIDÁS ;        
gålmås+N+Num+Sg+Nom:gålmås K ;      
niellja:niellj GUOKTA ;              
nælját+A+Ord:næljád3 VUOSTASJ ; 
nieljes+N+Num+Sg+Nom:nieljes K ;      
nieljes+N+Num:nælljás VIDÁS ;       
vihtta+Num:vidá GÅLMMÅ-OBL ;                    
vihtta+Num+Sg+Nom:vihtta K ;          
vihtta+Num+Sg+Acc:vihtta K ;         
vihtta+Num+Sg+Ill:vihttaj K ;         
vihtta+Num+Ess:vihttan K ;            
vidát+A+Ord:vidád3 VUOSTASJ ;
vidás+N+Num+Sg+Nom:vidás K ;      
viđás+N+Num:vihttas VIDÁS ;       
guhtta+Num:gudá GÅLMMÅ-OBL ;                
guhtta+Num+Sg+Nom:guhtta K ;          
guhtta+Num+Sg+Acc:guhtta K ;         
guhtta+Num+Sg+Ill:guhttaj K ;         
guhtta+Num+Ess:guhttan K ;            
gudát+A+Ord:gudád3 VUOSTASJ ;
gudás+N+Num+Sg+Nom:gudás K ;   
guđás+N+Num:guhttas VIDÁS ;      
gietjav:giehtja GIETJAV ;         
giehttjit+A+Ord:giehttjid3 VUOSTASJ ; 
gietjas+N+Num+Sg+Nom:gietjas K ;   
gietjas+N+Num:giehtjas VIDÁS ;       
gáktsa:gákts GÁKTSA ;              
gávtsát+A+Ord:gávtsád3 VUOSTASJ ;
gávtses+N+Num+Sg+Nom:gávtses K ;   
gávtses+N+Num:gáktsás VIDÁS ;        
aktse:akts AKTSE ;                      
avtsát+A+Ord:avtsád3 VUOSTASJ ; 
avtses+N+Num+Sg+Nom:avtses K ;   
avtses+N+Num:aktsás VIDÁS ;       


LEXICON LÅGEVF					
lågev:låhk LÅGEV ;   
lågåt+A+Ord:lågåd3 VUOSTASJ ;			
låhke:låhk  TJUOHTECASE ; 
låhke+Num+Sg+Ill:låhkåj K ;            
låges+N+Num+Sg+Nom:låges K ;   
låges+N+Num:låhkås VIDÁS ;       


LEXICON LÅK-					
låk# AKTAF ;				
låk# 2TO9F ;				
låhke+Num+Sg+Acc+Attr:låk K ;         
låhke#+Num+Sg+Attr:låk# NUMERALCOMPOUNDS ;       !^C^
låhke#+Num+Sg+Gen:låge# NUMERALCOMPOUNDS ;       !^C^
låhke:låhk  TJUOHTECASE ;  ! vihttalåhke, collective number
låhke+Num+Sg+Ill:låhkåj K ;            
lågåt+A+Ord:lågåd3 VUOSTASJ ;			

LEXICON LÅGENAN					
lågenan#akta AKTA ;        
lågenan#akta:lågenan#åvtå ÅVTÅ ;		
lågenan#vuostasj+A+Ord:lågenan#vuostatj VUOSTASJ ;			 
lågenan#guokta:lågenan#guokt GUOKTA ;           
lågenan#nubbe+A+Ord:lågenan#nupp nubbecase ;		 
lågenan#nubbe+A+Ord+Sg+Nom:lågenan#nubbe K ;       
lågenan#nubbe+A+Ord+Ess:lågenan#nubben K ;        
lågenan#nubbe+A+Ord+Sg+Ill:lågenan#nubbáj K ;     
lågenan#nuppát+A+Ord:lågenan#nuppád3 VUOSTASJ ;       
guovtes#lågenan+Num+Sg+Nom:guovtes#lågenan K ;      
guovtes#lågenan+Num:guoktás VIDÁSLÅGENAN ;       
lågenan#gålmmå GÅLMMÅ ;       
lågenan#goalmát+A+Ord:lågenan#goalmád3 VUOSTASJ ;        
gålmås#lågenan+Num+Sg+Nom:gålmås#lågenan K ;      
gålmås#lågenan+Num:gålmmås VIDÁSLÅGENAN ;        
lågenan#niellja:lågenan#niellj GUOKTA ;       
lågenan#nælját+A+Ord:lågenan#næljád3 VUOSTASJ ;        
nieljes#lågenan+Num+Sg+Nom:nieljes#lågenan K ;      
nieljes#lågenan+Num:nælljás VIDÁSLÅGENAN ;       
lågenan#vihtta+Num:lågenan#vidá GÅLMMÅ-OBL ;             
lågenan#vihtta+Num+Sg+Nom:lågenan#vihtta K ;          
lågenan#vihtta+Num+Sg+Acc:lågenan#vihtta K ;         
lågenan#vihtta+Num+Sg+Ill:lågenan#vihttaj K ;         
lågenan#vihtta+Num+Ess:lågenan#vihttan K ;            
lågenan#vidát+A+Ord:lågenan#vidád3 VUOSTASJ ;       
vidás#lågenan+Num+Sg+Nom:vidás#lågenan K ;      
vidás#lågenan+Num:vihttas VIDÁSLÅGENAN ;   
gudás#lågenan+Num+Sg+Nom:gudás#lågenan K ;      
guđás#lågenan+Num:guhttas VIDÁSLÅGENAN ;       
lågenan#guhtta+Num:lågenan#gudá GÅLMMÅ-OBL ;          
lågenan#guhtta+Num+Sg+Nom:lågenan#guhtta K ;          
lågenan#guhtta+Num+Sg+Acc:lågenan#guhtta K ;         
lågenan#guhtta+Num+Sg+Ill:lågenan#guhttaj K ;         
lågenan#guhtta+Num+Ess:lågenan#guhttan K ;            
lågenan#gudát+A+Ord:lågenan#gudád3 VUOSTASJ ;        
lågenan#gietjav:lågenan#giehtja GIETJAV ;        
lågenan#giehttjit+A+Ord:lågenan#giehttjid3 VUOSTASJ ;         
gietjas#lågenan+Num+Sg+Nom:gietjas#lågenan K ;      
gietjas#lågenan+Num:giehtjas VIDÁSLÅGENAN ;       
gávtses#lågenan+Num+Sg+Nom:gávtses#lågenan K ;      
gávtses#lågenan+Num:gáktsás VIDÁSLÅGENAN ;        
avtses#lågenan+Num+Sg+Nom:avtses#lågenan K ;      
avtses#lågenan+Num:aktsás#lågenan VIDÁSLÅGENAN ;       
lågenan#gáktsa:lågenan#gákts GÁKTSA ;       
lågenan#gávtsát+A+Ord:lågenan#gávtsád3 VUOSTASJ ;       
lågenan#aktse:lågenan#akts AKTSE ;                 
lågenan#avtsát+A+Ord:lågenan#avtsád3 VUOSTASJ ;        
akta#lågenan AKTALÅGENAN ;        
akta#lågenan:åvtå ÅVTÅLÅGENAN ;		
vuostasj#lågåt+A+Ord:vuostasj#lågåd3 VUOSTASJ ;			 
guokta#lågenan:guokt GUOKTALÅGENAN ;           
nuppát#lågåt+A+Ord:nuppát#lågåd3 VUOSTASJ ;        
gålmmå#lågenan GÅLMMÅLÅGENAN ;       
goalmát#lågåt+A+Ord:goalmát#lågåd3 VUOSTASJ ;        
niellja#lågenan:niellj GUOKTALÅGENAN ;       
nælját#lågåt+A+Ord:nælját#lågåd3 VUOSTASJ ;        
vihtta#lågenan+Num:vidá GÅLMMÅLÅGENAN-OBL ;             
vihtta#lågenan+Num+Sg+Nom:vihtta#lågenan K ;          
vihtta#lågenan+Num+Sg+Acc:vihtta#lågenan K ;         
vihtta#lågenan+Num+Sg+Ill:vihttaj#lågenan K ;         
vihtta#lågenan+Num+Ess:vihttan#lågenan K ;            
vidát#lågåt+A+Ord:vidát#lågåd3 VUOSTASJ ;        
guhtta#lågenan+Num:gudá GÅLMMÅLÅGENAN-OBL ;          
guhtta#lågenan+Num+Sg+Nom:guhtta#lågenan K ;          
guhtta#lågenan+Num+Sg+Acc:guhtta#lågenan K ;         
guhtta#lågenan+Num+Sg+Ill:guhttaj#lågenan K ;         
guhtta#lågenan+Num+Ess:guhttan#lågenan K ;            
gudát#lågåt+A+Ord:gudát#lågåd3 VUOSTASJ ;        
gietjav#lågenan:giehtja GIETJAVLÅGENAN ;        
giehttjit#lågåt+A+Ord:giehttjit#lågåd3 VUOSTASJ ;         
gáktsa#lågenan:gákts GÁKTSALÅGENAN ;       
gávtsát#lågåt+A+Ord:gávtsát#lågåt VUOSTASJ ;       
aktse#lågenan:akts AKTSELÅGENAN ;                 
avtsát#lågåt+A+Ord:avtsát#lågåd3 VUOSTASJ ;        



LEXICON 2TO9LÅG					
guokta# LÅK- ;					
gålmmå# LÅK- ;					
niellja# LÅK- ;				
vihtta# LÅK- ;					
guhtta# LÅK- ;					
gietjav# LÅK- ;					
gáktsa# LÅK- ;					
aktse# LÅK- ;					

LEXICON TJUOHTE					
tjuohte:tjuoht TJUOHTECASE ;    
tjuohte+Num+Sg+Ill:tjuohtáj K ;  
tjuohte#+Num+Attr:tjuohte# NUMERALCOMPOUNDS ;   !^C^
tjuohte# LÅGEVF ;					 !to LÅK- too?
tjuohte# 2TO9LÅG ;					
tjuohte# AKTAF ;					
tjuohte# 2TO9F ;					
tjuohte# LÅGENAN ;				
tjuodát+A+Ord:tjuodád3 VUOSTASJ ;			
akta#tjuohte:akta#tjuoht TJUOHTECASE ;    
akta#tjuohte+Num+Sg+Ill:akta#tjuohtáj K ;  
akta#tjuohte# LÅGEVF ;					 !to LÅK- too?
akta#tjuohte# 2TO9LÅG ;					
akta#tjuohte# AKTAF ;					
akta#tjuohte# 2TO9F ;					
akta#tjuohte# LÅGENAN ;				
tjuodes+N+Num+Sg+Nom:tjuodes K ;      
tjuodes+N+Num:tjuohtás VIDÁS ;       
akta#tjuodes+N+Num+Sg+Nom:akta#tjuodes K ;      
akta#tjuodes+N+Num:akta#tjuohtás VIDÁS ;       



LEXICON 2TO9TJUO					
guokta# GTJUODE ;					
gålmmå# GTJUODE ;					
niellja# GTJUODE ;				
njeallja# GTJUODE ;				
vihtta# GTJUODE ;					
guhtta# GTJUODE ;					
gietjav# GTJUODE ;					
gáktsa# GTJUODE ;					
aktse# GTJUODE ;					


LEXICON GTJUODE					
tjuode#+Num+Attr:tjuode# NUMERALCOMPOUNDS ;  !^C^
tjuode LÅGEVF ;					
tjuode# 2TO9LÅG ;					
tjuode# AKTAF ;					
tjuode# 2TO9F ;					
tjuode# LÅGENAN ;				
tjuohte:tjuoht TJUOHTECASE ;      ! imprecise number
tjuodát+A+Ord:tjuodád3 VUOSTASJ ;			
tjuodes+N+Num+Sg+Nom:tjuodes K ;      
tjuodes+N+Num:tjuohtás VIDÁS ;       


LEXICON COLLECTIVE    !Some collective numbers that do not fit in anywhere else
aktagis+N+Num:aktagis TJUODAGIS ;               
guoktagis+N+Num:guoktagis TJUODAGIS ;           
gålmmågis+N+Num:gålmmågis TJUODAGIS ;           
nielljagis+N+Num:nielljagis TJUODAGIS ;         
vihttagis+N+Num:vihttagis TJUODAGIS ;     
guhttagis+N+Num:guhttagis TJUODAGIS ;               
giehtjagis+N+Num:giehtjagis TJUODAGIS ;             
gáktsagis+N+Num:gáktsagis TJUODAGIS ;           
aktsegis+N+Num:aktsegis TJUODAGIS ;         
låhkegis+N+Num:låhkegis TJUODAGIS ;         
guovte#lågåk+Num:guovte#lågåg3 VUOSTASJ ;			
gålmå#lågåk+Num:gålmå#lågåg3 VUOSTASJ ;			
nielje#lågåk+Num:nielje#lågåg3 VUOSTASJ ;			
vidá#lågåk+Num:vidá#lågåg3 VUOSTASJ ;			
gudá#lågåk+Num:gudá#lågåg3 VUOSTASJ ;			
gietja#lågåk+Num:gietja#lågåg3 VUOSTASJ ;			
gávtse#lågåk+Num:gávtse#lågåg3 VUOSTASJ ;			
avtse#lågåk+Num:avtse#lågåg3 VUOSTASJ ;			
guokta#låk#vihttagis+N+Num:guokta#låk#vihttagis TJUODAGIS ;     
gålmmå#låk#vihttagis+N+Num:gålmmå#låk#vihttagis TJUODAGIS ;     
niellja#låk#vihttagis+N+Num:niellja#låk#vihttagis TJUODAGIS ;     
vihtta#låk#vihttagis+N+Num:vihtta#låk#vihttagis TJUODAGIS ;     
guhtta#låk#vihttagis+N+Num:guhtta#låk#vihttagis TJUODAGIS ;     
gietjav#låk#vihttagis+N+Num:gietjav#låk#vihttagis TJUODAGIS ;     
gáktsa#låk#vihttagis+N+Num:gáktsa#låk#vihttagis TJUODAGIS ;     
aktse#låk#vihttagis+N+Num:aktse#låk#vihttagis TJUODAGIS ;     
tjuodak+Num:tjuodag3 VUOSTASJ ;			
tjuodagis+N+Num:tjuodagis TJUODAGIS ;     
avta#tjuodak+Num:avta#tjuodag3 VUOSTASJ ;			
avta#tjuodagis+N+Num:avta#tjuodagis TJUODAGIS ;     
guovte#tjuodak+Num:guovte#tjuodag3 VUOSTASJ ;			
guovte#tjuodagis+N+Num:guovte#tjuodagis TJUODAGIS ;     
gålmå#tjuodak+Num:gålmå#tjuodag3 VUOSTASJ ;			
gålmå#tjuodagis+N+Num:gålmå#tjuodagis TJUODAGIS ;     
nielje#tjuodak+Num:nielje#tjuodag3 VUOSTASJ ;			
nielje#tjuodagis+N+Num:nielje#tjuodagis TJUODAGIS ;     
vidá#tjuodak+Num:vidá#tjuodag3 VUOSTASJ ;			
vidá#tjuodagis+N+Num:vidá#tjuodagis TJUODAGIS ;     
gudá#tjuodak+Num:gudá#tjuodag3 VUOSTASJ ;			
gudá#tjuodagis+N+Num:gudá#tjuodagis TJUODAGIS ;     
gietja#tjuodak+Num:gietja#tjuodag3 VUOSTASJ ;			
gietja#tjuodagis+N+Num:gietja#tjuodagis TJUODAGIS ;     
gávtse#tjuodak+Num:gávtse#tjuodag3 VUOSTASJ ;			
gávtse#tjuodagis+N+Num:gávtse#tjuodagis TJUODAGIS ;     
avtse#tjuodak+Num:avtse#tjuodag3 VUOSTASJ ;			
avtse#tjuodagis+N+Num:avtse#tjuodagis TJUODAGIS ;     
tjuohte#vidá#lågåk+Num:tjuohte#vidá#lågåg3 VUOSTASJ ;			
guokta#tjuode#vidá#lågåk+Num:guokta#tjuode#vidá#lågåg3 VUOSTASJ ;			
gålmmå#tjuode#vidá#lågåk+Num:gålmmå#tjuode#vidá#lågåg3 VUOSTASJ ;			
niellja#tjuode#vidá#lågåk+Num:niellja#tjuode#vidá#lågåg3 VUOSTASJ ;			
vihtta#tjuode#vidá#lågåk+Num:vihtta#tjuode#vidá#lågåg3 VUOSTASJ ;			
guhtta#tjuode#vidá#lågåk+Num:guhtta#tjuode#vidá#lågåg3 VUOSTASJ ;			
gietjav#tjuode#vidá#lågåk+Num:gietjav#tjuode#vidá#lågåg3 VUOSTASJ ;			
gáktsa#tjuode#vidá#lågåk+Num:gáktsa#tjuode#vidá#lågåg3 VUOSTASJ ;			
aktse#tjuode#vidá#lågåk+Num:aktse#tjuode#vidá#lågåg3 VUOSTASJ ;			


! Case forms of the basic digits
! ------------------------------

LEXICON AKTA                
+Num: AKTA-NIE ;               
+Num:Q1 AKTA-OBL ;              

LEXICON AKTA-NIE                
 +Sg+Nom: K ;               
 +Sg+Ill:j K ;              
 +Ess:n K ;                 

LEXICON AKTA-OBL                
 +Sg+Gen: K ;               
 +Sg+Gen:# NUMERALCOMPOUNDS ;         !^C^
 +Sg+Acc:v K ;              
 +Sg+Ine:n K ;              
 +Sg+Ela:s K ;              
 +Sg+Com:jn K ;             
 +Sg+Par:t K ;               
 +Pl+Nom: K ;                
 +Pl+Gen:j K ;               
 +Pl+Acc:jt K ;              
 +Pl+Ill:jda K ;             
 +Pl+Ine:jn K ;              
 +Pl+Ela:js K ;              
 +Pl+Com:j K ;               
 
LEXICON ÅVTÅ                 
+Num+Sg+Gen: K ;             
+Num+Sg+Gen:# NUMERALCOMPOUNDS ;         !^C^
+Num+Sg+Acc:v K ;            
+Num+Sg+Ine:n K ;            
+Num+Sg+Ela:s K ;            
+Num+Sg+Com:jn K ;           
+Num+Sg+Par:t K ;            
+Num+Pl+Nom: K ;             
+Num+Pl+Gen:j K ;            
+Num+Pl+Acc:jt K ;           
+Num+Pl+Ill:jda K ;          
+Num+Pl+Ine:jn K ;           
+Num+Pl+Ela:js K ;           
+Num+Pl+Com:j K ;            

LEXICON GUOKTA                  
+Num:eQ1 GUOVTE- ;               
+Num:iQ1 GUOVTI- ;               
+Num: GUOKTA- ;                 

LEXICON GUOVTE-             
 +Sg+Gen: K ;              
 +Sg+Gen:# NUMERALCOMPOUNDS ;   
 +Sg+Ine:n K ;             
 +Sg+Ela:s K ;             
 +Sg+Par:t K ;             
 +Pl+Nom: K ;              
 
LEXICON GUOVTI-   
 +Sg+Com:jn K ;            
 +Pl+Gen:j K ;             
 +Pl+Acc:jt K ;            
 +Pl+Ill:jda K ;           
 +Pl+Ine:jn K ;            
 +Pl+Ela:js K ;            
 +Pl+Com:j K ;             


LEXICON GUOKTA-            
 +Sg+Nom:a K ;           
 +Sg+Acc:a K ;           
 +Sg+Ill:áj K ;          
 +Ess:an K ;             
                         
LEXICON GÅLMMÅ           
+Num: GÅLMMÅ-NAIE ;      
+Num:Q1 GÅLMMÅ-OBL ;     
                        
LEXICON GÅLMMÅ-NAIE      
 +Sg+Nom: K ;            
 +Sg+Acc: K ;            
 +Sg+Ill:j K ;           
 +Ess:n K ;              
                         
LEXICON GÅLMMÅ-OBL       
 +Sg+Gen: K ;            
 +Sg+Gen:# NUMERALCOMPOUNDS ; 
 +Sg+Ine:n K ;           
 +Sg+Ela:s K ;           
 +Sg+Com:jn K ;          
 +Sg+Par:t K ;           
 +Pl+Nom: K ;            
 +Pl+Gen:j K ;           
 +Pl+Acc:jt K ;          
 +Pl+Ill:jda K ;         
 +Pl+Ine:jn K ;          
 +Pl+Ela:js K ;          
 +Pl+Com:j K ;           


LEXICON GIETJAV             
+Num: GIETJAV-IE ;          
+Num:Q1 GIETJAV-OBL ;       
                            
LEXICON GIETJAV-IE          
 +Sg+Ill:j K ;              
 +Ess:n K ;                 
                        
LEXICON GIETJAV-OBL         
 +Sg+Nom:v K ;              
 +Sg+Gen: K ;               
 +Sg+Gen:# NUMERALCOMPOUNDS ;          !^C^
 +Sg+Acc:v K ;              
 +Sg+Ine:n K ;              
 +Sg+Ela:s K ;              
 +Sg+Com:jn K ;             
 +Sg+Par:t K ;              
 +Pl+Nom: K ;               
 +Pl+Gen:j K ;              
 +Pl+Acc:jt K ;             
 +Pl+Ill:jda K ;            
 +Pl+Ine:jn K ;             
 +Pl+Ela:js K ;             
 +Pl+Com:j K ;              
 

LEXICON GÁKTSA              
+Num:eQ1 GÁVTSE- ;           
+Num:iQ1 GÁVTSI- ;           
+Num: GÁKTSA- ;             
                            
LEXICON GÁVTSE-             
 +Sg+Gen: K ;              
 +Sg+Gen:# NUMERALCOMPOUNDS ;         !^C^
 +Sg+Ine:n K ;             
 +Sg+Ela:s K ;             
 +Sg+Par:t K ;             
 +Pl+Nom: K ;              

LEXICON GÁVTSI-        
 +Sg+Com:jn K ;            
 +Pl+Gen:j K ;             
 +Pl+Acc:jt K ;            
 +Pl+Ill:jda K ;           
 +Pl+Ine:jn K ;            
 +Pl+Ela:js K ;            
 +Pl+Com:j K ;             
                            
                            
LEXICON GÁKTSA-             
 +Sg+Nom:a K ;              
 +Sg+Acc:a K ;              
 +Sg+Ill:áj K ;             
 +Ess:an K ;      
 

LEXICON AKTSE            
+Num:eQ1 AVTSE- ;           
+Num:iQ1 AVTSI- ;           
+Num: AKTS- ;             

LEXICON AVTSE-            
 +Sg+Gen: K ;          
 +Sg+Gen:# NUMERALCOMPOUNDS ;         !^C^
 +Sg+Ine:n K ;         
 +Sg+Ela:s K ;         
 +Sg+Par:t K ;         
 +Pl+Nom: K ;          

LEXICON AVTSI-         
 +Sg+Com:jn K ;        
 +Pl+Gen:j K ;         
 +Pl+Acc:jt K ;        
 +Pl+Ill:jda K ;       
 +Pl+Ine:jn K ;        
 +Pl+Ela:js K ;        
 +Pl+Com:j K ;         
                        
                    
LEXICON AKTS-           
 +Sg+Nom:e K ;          
 +Sg+Acc:e K ;          
 +Sg+Ill:áj K ;         
 +Ess:an K ;        
 

LEXICON LÅGEV                
+Num:eQ1 LÅGE- ;    
+Num:iQ1 LÅGI- ;    
+Num: LÅHK- ;     
                   
LEXICON LÅGE-      
 +Sg+Nom:v K ;    
 +Sg+Acc:v K ;    
 +Sg+Gen: K ;     
 +Sg+Gen:# NUMERALCOMPOUNDS ;         !^C^
 +Sg+Ine:n K ;    
 +Sg+Ela:s K ;    
 +Sg+Par:t K ;    
 +Pl+Nom: K ;     

LEXICON LÅGI-      
 +Sg+Com:jn K ;   
 +Pl+Gen:j K ;    
 +Pl+Acc:jt K ;   
 +Pl+Ill:jda K ;  
 +Pl+Ine:jn K ;   
 +Pl+Ela:js K ;   
 +Pl+Com:j K ;    
                   
                   
LEXICON LÅHK-      
 +Sg+Ill:åj K ;    
 +Ess:en K ;       
                   
                   
LEXICON AKTALÅGENAN  
+Num: AKTALÅGENAN-NIE ;  
+Num:Q1 AKTALÅGENAN-OBL ;      
                   
LEXICON AKTALÅGENAN-NIE    
 +Sg+Nom:#lågenan K ;     
 +Sg+Ill:j#lågenan K ;       
 +Ess:n#lågenan K ;        
                   
LEXICON AKTALÅGENAN-OBL    
 +Sg+Gen:#lågenan K ;      
 +Sg+Gen:#lågenan# NUMERALCOMPOUNDS ;       !^C^
 +Sg+Acc:v#lågenan K ; 
 +Sg+Ine:n#lågenan K ; 
 +Sg+Ela:s#lågenan K ; 
 +Sg+Com:jn#lågenan K ; 
 +Sg+Par:t#lågenan K ; 
 +Pl+Nom:#lågenan K ; 
 +Pl+Gen:j#lågenan K ; 
 +Pl+Acc:jt#lågenan K ; 
 +Pl+Ill:jda#lågenan K ; 
 +Pl+Ine:jn#lågenan K ; 
 +Pl+Ela:js#lågenan K ; 
 +Pl+Com:j#lågenan K ; 
 
LEXICON ÅVTÅLÅGENAN                   
+Num+Sg+Gen:#lågenan K ;              
+Num+Sg+Gen:#lågenan# NUMERALCOMPOUNDS ;       !^C^
+Num+Sg+Acc:v#lågenan K ;             
+Num+Sg+Ine:n#lågenan K ;             
+Num+Sg+Ela:s#lågenan K ;             
+Num+Sg+Com:jn#lågenan K ;            
+Num+Sg+Par:t#lågenan K ;             
+Num+Pl+Nom:#lågenan K ;              
+Num+Pl+Gen:j#lågenan K ;             
+Num+Pl+Acc:jt#lågenan K ;            
+Num+Pl+Ill:jda#lågenan K ;           
+Num+Pl+Ine:jn#lågenan K ;            
+Num+Pl+Ela:js#lågenan K ;            
+Num+Pl+Com:j#lågenan K ;             
                                      
LEXICON GUOKTALÅGENAN                 
+Num: GUOVTELÅGENAN ;               
+Num: GUOKT-LÅGENAN ;                 
                                      
LEXICON GUOVTELÅGENAN                 
 +Sg+Gen:eQ1#lågenan K ;                
 +Sg+Gen:eQ1#lågenan# NUMERALCOMPOUNDS ;            !^C^
 +Sg+Ine:eQ1n#lågenan K ;               
 +Sg+Ela:eQ1s#lågenan K ;               
 +Sg+Com:iQ1jn#lågenan K ;              
 +Sg+Par:eQ1t#lågenan K ;               
 +Pl+Nom:eQ1#lågenan K ;                
 +Pl+Gen:iQ1j#lågenan K ;               
 +Pl+Acc:iQ1jt#lågenan K ;              
 +Pl+Ill:iQ1jda#lågenan K ;             
 +Pl+Ine:iQ1jn#lågenan K ;              
 +Pl+Ela:iQ1js#lågenan K ;              
 +Pl+Com:iQ1j#lågenan K ;               
                                      
LEXICON GUOKT-LÅGENAN                 
 +Sg+Nom:a#lågenan K ;                
 +Sg+Acc:a#lågenan K ;                
 +Sg+Ill:áj#lågenan K ;               
 +Ess:an#lågenan K ;                  
                                      
LEXICON GÅLMMÅLÅGENAN                 
+Num: GÅLMMÅLÅGENAN-NAIE ;            
+Num:Q1 GÅLMMÅLÅGENAN-OBL ;           
                                      
LEXICON GÅLMMÅLÅGENAN-NAIE            
 +Sg+Nom:#lågenan K ;                 
 +Sg+Acc:#lågenan K ;                 
 +Sg+Ill:j#lågenan K ;                
 +Ess:n#lågenan K ;                   
                                      
LEXICON GÅLMMÅLÅGENAN-OBL             
 +Sg+Gen:#lågenan K ;                 
 +Sg+Gen:#lågenan# NUMERALCOMPOUNDS ;          !^C^
 +Sg+Ine:n#lågenan K ;                
 +Sg+Ela:s#lågenan K ;                
 +Sg+Com:jn#lågenan K ;               
 +Sg+Par:t#lågenan K ;                
 +Pl+Nom:#lågenan K ;                 
 +Pl+Gen:j#lågenan K ;                
 +Pl+Acc:jt#lågenan K ;               
 +Pl+Ill:jda#lågenan K ;              
 +Pl+Ine:jn#lågenan K ;               
 +Pl+Ela:js#lågenan K ;               
 +Pl+Com:j#lågenan K ;                
                                      
LEXICON GIETJAVLÅGENAN                
+Num: GIETJAVLÅGENAN-IE ;             
+Num:Q1 GIETJAVLÅGENAN-OBL ;          
                                      
LEXICON GIETJAVLÅGENAN-IE             
 +Sg+Ill:j#lågenan K ;                
 +Ess:n#lågenan K ;                   
                                      
LEXICON GIETJAVLÅGENAN-OBL            
 +Sg+Nom:v#lågenan K ;                
 +Sg+Gen:#lågenan K ;                 
 +Sg+Gen:#lågenan# NUMERALCOMPOUNDS ;           !^C^
 +Sg+Acc:v#lågenan K ;                
 +Sg+Ine:n#lågenan K ;                
 +Sg+Ela:s#lågenan K ;                
 +Sg+Com:jn#lågenan K ;               
 +Sg+Par:t#lågenan K ;                
 +Pl+Nom:#lågenan K ;                 
 +Pl+Gen:j#lågenan K ;                
 +Pl+Acc:jt#lågenan K ;               
 +Pl+Ill:jda#lågenan K ;              
 +Pl+Ine:jn#lågenan K ;               
 +Pl+Ela:js#lågenan K ;               
 +Pl+Com:j#lågenan K ;                
                                      
LEXICON GÁKTSALÅGENAN                 
+Num: GÁVTSELÅGENAN ;               
+Num: GÁKTS-LÅGENAN ;                 
                                      
LEXICON GÁVTSELÅGENAN                 
 +Sg+Gen:eQ1#lågenan K ;                
 +Sg+Gen:eQ1#lågenan# NUMERALCOMPOUNDS ;        !^C^
 +Sg+Ine:eQ1n#lågenan K ;               
 +Sg+Ela:eQ1s#lågenan K ;               
 +Sg+Com:iQ1jn#lågenan K ;              
 +Sg+Par:eQ1t#lågenan K ;               
 +Pl+Nom:eQ1#lågenan K ;                
 +Pl+Gen:iQ1j#lågenan K ;               
 +Pl+Acc:iQ1jt#lågenan K ;              
 +Pl+Ill:iQ1jda#lågenan K ;             
 +Pl+Ine:iQ1jn#lågenan K ;              
 +Pl+Ela:iQ1js#lågenan K ;              
 +Pl+Com:iQ1j#lågenan K ;               
                                      
LEXICON GÁKTS-LÅGENAN                 
 +Sg+Nom:a#lågenan K ;                
 +Sg+Acc:a#lågenan K ;                
 +Sg+Ill:áj#lågenan K ;               !^N
 +Ess:an#lågenan K ;                  
                                      
LEXICON AKTSELÅGENAN                  
+Num: AVTSELÅGENAN ;                
+Num: AKTS-LÅGENAN ;                  
                                      
LEXICON AVTSELÅGENAN                  
 +Sg+Gen:eQ1#lågenan K ;                
 +Sg+Gen:eQ1#lågenan# NUMERALCOMPOUNDS ;       !^C^
 +Sg+Ine:eQ1n#lågenan K ;               
 +Sg+Ela:eQ1s#lågenan K ;               
 +Sg+Com:iQ1jn#lågenan K ;              
 +Sg+Par:eQ1t#lågenan K ;               
 +Pl+Nom:eQ1#lågenan K ;                
 +Pl+Gen:iQ1j#lågenan K ;               
 +Pl+Acc:iQ1jt#lågenan K ;              
 +Pl+Ill:iQ1jda#lågenan K ;             
 +Pl+Ine:iQ1jn#lågenan K ;              
 +Pl+Ela:iQ1js#lågenan K ;              
 +Pl+Com:iQ1j#lågenan K ;               
                                      
LEXICON AKTS-LÅGENAN                  
 +Sg+Nom:e#lågenan K ;                
 +Sg+Acc:e#lågenan K ;                
 +Sg+Ill:áj#lågenan K ;               
 +Ess:an#lågenan K ;                  
                                      
LEXICON TJUOHTECASE           
 +Num:eQ1 TJUODE- ;                     
 +Num:iQ1 TJUODI- ;                     
 +Num: TJUOHT- ;                       
                                      
LEXICON TJUODE-                       
 +Sg+Acc:v K ;                       
 +Sg+Gen: K ;                        
 +Sg+Ine:n K ;                       
 +Sg+Ela:s K ;                       
 +Sg+Par:t K ;                       
 +Pl+Nom: K ;                        

LEXICON TJUODI-                      
 +Pl+Gen:j K ;                       
 +Sg+Com:jn K ;                      
 +Pl+Acc:jt K ;                      
 +Pl+Ill:jda K ;                     
 +Pl+Ine:jn K ;                      
 +Pl+Ela:js K ;                      
 +Pl+Com:j K ;                       
                                      
LEXICON TJUOHT-                       
 +Sg+Nom:e K ;                        
 +Ess:en K ;                          
 +Sg+Ill:áj K ;                       

LEXICON TUVSÁN				
 +Num+Sg+Nom: K ;          
 +Num+Sg+Gen:a K ;         
 +Num+Sg+Attr:# NUMERALCOMPOUNDS ;           !^C^
 +Num+Sg+Acc:av K ;            
 +Num+Sg+Ill:ij K ;    
 +Num+Sg+Ine:in K ;           
 +Num+Sg+Ela:is K ;            
 +Num+Sg+Com:ijn K ;            
 +Num+Ess:in K ;                  
 +Num+Pl+Nom:a K ;                 
 +Num+Pl+Gen:ij K ;              
 +Num+Pl+Acc:ijt K ;              
 +Num+Pl+Ill:ijda K ;              
 +Num+Pl+Ine:ijn K ;                
 +Num+Pl+Ela:ijs K ;                 
 +Num+Pl+Com:ij K ;                 



LEXICON NUMERALCOMPOUNDS  ! numeral as first part 
 NAMÁK ;			   ! tuvsánjienat, logigielat, etc. 
 SASJ ; 
! NounSecond ;	! golmmačiegahas	
! geardásaš:geardás DABALAS ;	   
! !tuvsán#geardásaš 
! jahkásaš:jahkás DABALAS ; 


! Ordinal numbers
! ---------------


LEXICON VUOSTASJ      				
 +Sg+Nom: K ;          
 +Sg+Gen:a K ;         
 +Sg+Acc:av K ;            
 +Sg+Ill:ij K ;   
 +Sg+Ine:in K ;           
 +Sg+Ela:is K ;            
 +Sg+Com:ijn K ;            
 +Ess:in K ;                  
 +Pl+Nom:a K ;                 
 +Pl+Gen:ij K ;              
 +Pl+Acc:ijt K ;              
 +Pl+Ill:ijda K ;              
 +Pl+Ine:ijn K ;                
 +Pl+Ela:ijs K ;                 
 +Pl+Com:ij K ;                 
 +Attr: K ;					

LEXICON TUVSÁNAT				
 +Sg+Nom:t K ;          
 +Sg+Gen:tta K ;         
 +Sg+Acc:ttav K ;            
 +Sg+Ill:ddaj K ;   
 +Sg+Ine:ttan K ;           
 +Sg+Ela:ttas K ;            
 +Sg+Com:ttajn K ;            
 +Ess:ddan K ;                  
 +Pl+Nom:tta K ;                 
 +Pl+Gen:ttaj K ;              
 +Pl+Acc:ttajt K ;              
 +Pl+Ill:ttajda K ;              
 +Pl+Ine:ttajn K ;                
 +Pl+Ela:ttajs K ;                 
 +Pl+Com:ttaj K ;                 
 +Attr:t K ;					
 
! Collection numbers
! ---------------

LEXICON VIDÁS           
 +Sg+Gen:a K ;          
 +Sg+Acc:av K ;                 
 +Sg+Ill:ij K ;                     
 +Sg+Ine:in K ;                 
 +Sg+Ela:is K ;                 
 +Sg+Com:ijn K ;                    
 +Ess:in K ;                    
 +Pl+Nom:a K ;                  
 +Pl+Gen:ij K ;                 
 +Pl+Acc:ijt K ;                
 +Pl+Ill:ijda K ;                   
 +Pl+Ine:ijn K ;                        
 +Pl+Ela:ijs K ;                        
 +Pl+Com:ij K ;                         
 +Sg+Nom: PxC ;     
 +Sg+Gen: PxC ;     
 +Sg+Acc: PxC ;     
 +Sg+Ill:as PxCs ;      
 +Sg+Ine:in PxCs ;      
 +Sg+Ela:ist PxCs ;     
 +Sg+Com:ijn PxCs;      
 +Pl+Gen:ij PxCs ;      
 +Pl+Acc:ijd PxCs ;     
 +Pl+Ill:ijdas PxC ;        
 +Pl+Ine:ijn PxCs ;     
 +Pl+Ela:ijst PxCs ;        
 +Pl+Com:ij PxCs ;      


LEXICON VIDÁSLÅGENAN                        
 +Sg+Gen:a#lågenan K ;      
 +Sg+Acc:av#lågenan K ; 
 +Sg+Ill:ij#lågenan K ;       
 +Sg+Ine:in#lågenan K ; 
 +Sg+Ela:is#lågenan K ; 
 +Sg+Com:ijn#lågenan K ; 
 +Sg+Par:it#lågenan K ; 
 +Pl+Nom:a#lågenan K ;                      
 +Pl+Gen:ij#lågenan K ;             
 +Pl+Acc:ijt#lågenan K ;                
 +Pl+Ill:ijda#lågenan K ;               
 +Pl+Ine:ijn#lågenan K ;                    
 +Pl+Ela:ijs#lågenan K ;                        
 +Pl+Com:ij#lågenan K ;                     
 
LEXICON TJUODAGIS                       
 +Sg+Nom: K ;                          
 +Sg+Ill:saj K ;                                       
 +Sg+Gen:á K ;                          
 +Sg+Acc:áv K ;                         
 +Sg+Ine:án K ;                         
 +Sg+Ela:ás K ;                                     
 +Pl+Nom:á K ;                                  
 +Pl+Gen:áj K ;                                         
 +Pl+Acc:ájt K ;                            
 +Pl+Ill:ájda K ;                       
 +Pl+Ine:ájn K ;                                    
 +Pl+Ela:ájs K ;                                
 +Pl+Com:áj K ;                                     
 +Sg+Com:ájn K ;                            
 +Sg+Nom:á PxV ;        
 +Sg+Ill:sas PxCi ;      
 +Sg+Gen:á PxV ;        
 +Sg+Acc:á PxV ;        
 +Sg+Ine:án PxC ;       
 +Sg+Ela:ást PxC ;      
 +Pl+Nom:á PxNomPlV ;       
 +Pl+Gen:áj PxC ;       
 +Pl+Acc:ájd PxC ;      
 +Pl+Ill:ájdis PxCs ;       
 +Pl+Ine:ájn PxC ;      
 +Pl+Ela:ájst PxC ;     
 +Pl+Com:áj PxC ;       
 +Sg+Com:ájn PxC ;        



! Imprecise numbers
! -----------------

LEXICON num-imprecise				
galla+Num+Sg+Nom:galla K ;
galli+Num:galli gallicase ;
galla+Num+Sg+Nom:galla K ;
galla+Num+Sg+Ill:galláj K ;
galla+Num:gall nubbecase ;
máŋŋga+Num+Sg+Nom:máŋŋga K ;
máŋŋga+Num+Sg+Acc:máŋŋga K ;
máŋŋga+Num+Sg+Ill:máŋŋgaj K ;
máŋŋga+Num:máŋga máŋŋgacase ;
moadda+Num+Sg+Nom:moadda K ;
máŋŋga+Num+Sg+Com:máŋgajn K ;
moadda+Num+Sg+Com:måttijn K ;
moadda+Num+Sg+Ill:moaddaj K ;
moadda+Num:moatte máŋŋgacase ;
moadda+Num+Sg+Acc:moadda K ;
ållo+Num+Sg+Nom:ållo K ;
ållo+Num+Sg+Ill:ålluj K ;
ållo+Num+Sg+Com:ålojn K ;
ållo+Num:ålo máŋŋgacase ;
biel#nup+Num+Sg+Nom:biel#nup # ;				
biel#nup+Num+Sg+Gen:biel#nup # ;				
biel#nup+Num+Sg+Acc:biel#nup # ;				
biel#nup#+Num+Sg+Attr:biel#nup# SASJ ;				
biel#goalmát+A+Ord:biel#goalmád3 VUOSTASJ ;        
biel#nælját+A+Ord:biel#næljád3 VUOSTASJ ;        
biel#vidát+A+Ord:biel#vidád3 VUOSTASJ ;        
biel#gudát+A+Ord:biel#gudád3 VUOSTASJ ;        
biel#giehttjit+A+Ord:biel#giehttjid3 VUOSTASJ ;         
biel#gávtsát+A+Ord:biel#gávtsád3 VUOSTASJ ;       
biel#avtsát+A+Ord:biel#avtsád3 VUOSTASJ ;        
biel#lågåt+A+Ord:biel#lågåd3 VUOSTASJ ;			 
biel#goalmát+A+Ord+Attr:biel#goalmád3 SASJ ;        
biel#nælját+A+Ord+Attr:biel#næljád3 SASJ ;        
biel#vidát+A+Ord+Attr:biel#vidád3 SASJ ;        
biel#gudát+A+Ord+Attr:biel#gudád3 SASJ ;        
biel#giehttjit+A+Ord+Attr:biel#giehttjid3 SASJ ;         
biel#gávtsát+A+Ord+Attr:biel#gávtsád3 SASJ ;       
biel#avtsát+A+Ord+Attr:biel#avtsád3 SASJ ;        
biel#lågåt+A+Ord+Attr:biel#lågåd3 SASJ ;			 


!LEXICON num-imprecise-distribution		
!NUPPELOHKAIGENERATOR ;  ! máŋga#nuppe#lohkái, máŋga 
!LOGIF ;        ! máŋga#logi, máŋga#lot		  
!CUODI ;        ! máŋga#čuođi			  
!NL ;           ! máŋga#nuppe#lohkái#duhát		  
!O-NL ;         ! máŋga#nuppe#lohkái#duhát		  
!O-LOGIF ;      ! máŋga#logi#duhát			  
!JUSTTUVSÁN ;    ! máŋga#duhát			  
!O-CUODI ;      ! máŋga#duhát			  

LEXICON máŋŋgacase           
 +Sg+Gen: K ;        
 +Sg+Acc:v K ;      
 +Sg+Ela:s K ;      
 +Sg+Ine:n K ;      
 +Sg+Part:t K ;     





! Arabic numerals
! ---------------


LEXICON NUM-PREFIXES				!!^C^
§	ARABIC ;      ! §24			!^C^
§§	ARABIC ;     ! §§24			!^C^
§% 	ARABIC ;    ! § 24			!^C^
§§% 	ARABIC ;   ! §§ 24			!^C^
%-	ARABIC ;     ! -24			!^C^
%‒	ARABIC ;     ! -24 U+2012 Figure line	!^C^
%–	ARABIC ;     ! -24 U+2013 en dash	!^C^
%—	ARABIC ;     ! -24 U+2014 em dash	!^C^
%―	ARABIC ;     ! -24 U+2015 horizontal l	!^C^
%+	ARABIC ;     ! +24			!^C^
%-% 	ARABIC ;	! - 24			!^C^
%‒% 	ARABIC ;	! - 24 2012		!^C^
%–% 	ARABIC ;	! - 24 2013	 	!^C^
%—% 	ARABIC ;	! - 24 2014		!^C^
%―% 	ARABIC ;	! - 24 2015 		!^C^
%+% 	ARABIC ;	! + 24			!^C^
%*	ARABIC ;     ! *24			!^C^
%$	ARABIC ;     ! $24  ! And the Euro?	!^C^
%<	ARABIC ;     ! <24			!^C^
%>	ARABIC ;     ! >24			!^C^
s%.	ARABIC ;     ! s.24			!^C^
	


LEXICON ARABIC		!^C^			
1 ARABICLOOP ;			!^C^		
2 ARABICLOOP ;				!^C^	
3 ARABICLOOP ;					!^C^
4 ARABICLOOP ;					!^C^
5 ARABICLOOP ;					!^C^
6 ARABICLOOP ;					!^C^
7 ARABICLOOP ;					!^C^
8 ARABICLOOP ;					!^C^
9 ARABICLOOP ;					!^C^
%0 ARABICLOOP ;					!^C^

LEXICON ARABICLOOP				!^C^
ARABIC ;					!^C^
%-	ARABIC ;	      ! 1-2    multipart numbers	!^C^
%.%- 	ARABIC ;	      ! 1.-2   multipart numbers	!^C^
%-% 	ARABIC ;	      ! 1- 2   multipart numbers	!^C^
% %-	ARABIC ;	      ! 1 -2   multipart numbers	!^C^
% %-% 	ARABIC ;	      ! 1 - 2  multipart numbers	!^C^
% %-%-%   ARABIC ;	      ! 1 -- 2 multipart numbers	!^C^
% %-      ARABICDELIMITER ;   ! 1 -   multipart numbers		!^C^
% %-%-	  ARABICDELIMITER ;   ! 1 --  multipart numbers		!^C^
%. ARABIC ;							!^C^
%, ARABIC ;							!^C^
%: ARABIC ;							!^C^
%  ARABIC ;	   ! Allowing for "23 500". !^C^			
   ARABIC ;	   ! Allowing for "23 500" w/nbsp (initial char is alt-space)	!^C^
%/ ARABIC ;	   ! 24/12			!^C^		
!%' ARABIC ;	   ! Since ' denotes cons.grad. This is not optimal either. 
!+Num:%- R ; !^C^	   ! Number compounds
+Num: R ; !^C^	   ! Hyphen removed bec. of "123- ja 124-viessu" !^C^
+A+Ord:%. # ;							 !^C^
,%-+Num:,%- # ;		! 10,-					 !^C^
%%+Num+Nom:%% # ;	! 50%. It is wrong, but 75 % of us write "75%".		!^C^
% %%+Num+Nom:% %% # ;	! We must analyse the correct ones as well... 50 % etc.	!^C^
+Num: ARABICDELIMITER ; ! list of number-case delimiters			!^C^


LEXICON ARABICDELIMITER								!^C^
  ARABICCASE ;      ! This is the 1984s case.					!^C^
 :%: ARABICCASE ;    ! This is the 1984:s case.					!^C^
 :%' ARABICCASE ;    ! This is the 1984's case.					!^C^
 :%- ARABICCASE ;    ! This is the 1984-s case.					!^C^
                     ! So, what does the lg council prefer?			

LEXICON ARABICCASE								!^C^
+Nom:f # ;		! s. 123f.						!^C^
+Nom:ff # ;		! s. 123ff.						!^C^
+Nom:b # ;		! § 123b						!^C^
+Nom:c # ;		! § 123c						!^C^
+Nom:d # ;		! § 123d						!^C^
+Nom:e # ;		! § 123e						!^C^
! # ;		! 123 is now 123+Num as well as 123+Num+Nom etc. Out again!	!^C^
+Nom: # ;									!^C^
+Gen: # ;									!^C^
!+Acc: # ;									!^C^
+Gen:a # ;	! But here 123a 123+Num+Gen					!^C^
+Acc:av # ;									!^C^
+Ill:j # ;									!^C^
+Ill:aj # ;									!^C^
+Ill:áj # ;	! Testing over the weekend.					!^C^
+Ela:s # ;									!^C^
+Ela:is # ;									!^C^
+Ela:as # ;									!^C^
+Ine:s # ;									!^C^
+Ine:is # ;									!^C^
+Ine:as # ;									!^C^
+Com:jn # ;									!^C^
+Com:ajn # ;									!^C^
+Ess:n # ;									!^C^
+Ess:an # ;									!^C^
! No plural numbers, hmm.


! The Roman numerals !
! ------------------ !

LEXICON ROMAN						!^C^
 ROM-THOUSAND ;						!^C^
 ROM-HUNDRED ;						!^C^
 ROM-TEN ;						!^C^
 ROM-ONE ;						!^C^

LEXICON ROM-THOUSAND              !^C^
 M ROM-THOUSAND-TAG ;						!^C^
 MM ROM-THOUSAND-TAG ;					!^C^
 MMM ROM-THOUSAND-TAG ;					!^C^
 MMMM ROM-THOUSAND-TAG ;					!^C^
 MMMMM ROM-THOUSAND-TAG ;					!^C^

LEXICON ROM-THOUSAND-TAG					!^C^
  ROMNUMTAG ;						!^C^
 ROM-HUNDRED ;					!^C^
 ROM-SPLIT	;				!^C^

LEXICON ROM-HUNDRED					!^C^
 C ROM-HUNDRED-TAG ;						!^C^
 CC ROM-HUNDRED-TAG ;						!^C^
 CCC ROM-HUNDRED-TAG ;						!^C^
 CD ROM-HUNDRED-TAG ;						!^C^
 D ROM-HUNDRED-TAG ;						!^C^
 DC ROM-HUNDRED-TAG ;						!^C^
 DCC ROM-HUNDRED-TAG ;						!^C^
 DCCC ROM-HUNDRED-TAG ;						!^C^
 CM ROM-HUNDRED-TAG ;						!^C^

LEXICON ROM-HUNDRED-TAG					!^C^
  ROMNUMTAG ;						!^C^
 ROM-TEN ;					!^C^
 ROM-SPLIT ;					!^C^

LEXICON ROM-TEN						!^C^
 X ROM-TEN-TAG ;						!^C^
 XX ROM-TEN-TAG ;						!^C^
 XXX ROM-TEN-TAG ;						!^C^
 XL ROM-TEN-TAG ;						!^C^
 L ROM-TEN-TAG ;						!^C^
 LX ROM-TEN-TAG ;						!^C^
 LXX ROM-TEN-TAG ;						!^C^
 LXXX ROM-TEN-TAG ;						!^C^
 XC ROM-TEN-TAG ;						!^C^

LEXICON ROM-TEN-TAG					!^C^
  ROMNUMTAG ;						!^C^
 ROM-ONE ;						!^C^
 ROM-SPLIT ;						!^C^

LEXICON ROM-ONE						!^C^
 I ROM-ONE-TAG ;						!^C^
 II ROM-ONE-TAG ;						!^C^
 III ROM-ONE-TAG ;						!^C^
 IV  ROM-ONE-TAG ;						!^C^
 V ROM-ONE-TAG ;						!^C^
 VI ROM-ONE-TAG ;						!^C^
 VII ROM-ONE-TAG ;						!^C^
 VIII ROM-ONE-TAG ;						!^C^
 IX ROM-ONE-TAG ;						!^C^

LEXICON ROM-ONE-TAG					!^C^
  ROMNUMTAG ;						!^C^
! +N: ROMNUMTAG ;						!^C^ !The Olav viđeš fix (Roman V now gets A)
 ROM-SPLIT ;						!^C^

! Here, we split the Roman numerals, in order to account for cases like "Kapihtal II-IV".
! We may send this first part directly to ROM-TAG below (and get the +Num tag), or we may
! send them through a second loop, identical to the first one, but marked wit "2" (the lexica
! are called 2ROMAN, etc. We do this instead of making a loop, since we do not want cases
! like "II-IV-VI-VII-IX". If that should turn out to be a good idea, a loop would do the 
! trick.

LEXICON ROM-SPLIT					!^C^
 :%- 2ROMAN ; ! II-VI, etc.				!^C^


! Here goes loop 2.

LEXICON 2ROMAN						!^C^
 2ROM-THOUSAND ;						!^C^
 2ROM-HUNDRED ;						!^C^
 2ROM-TEN ;						!^C^
 2ROM-ONE ;						!^C^

LEXICON 2ROM-THOUSAND              !^C^
 M 2ROM-THOUSAND-TAG ;						!^C^
 MM 2ROM-THOUSAND-TAG ;					!^C^
 MMM 2ROM-THOUSAND-TAG ;					!^C^
 MMMM 2ROM-THOUSAND-TAG ;					!^C^
 MMMMM 2ROM-THOUSAND-TAG ;					!^C^

LEXICON 2ROM-THOUSAND-TAG					!^C^
  ROMNUMTAG ;						!^C^
 2ROM-HUNDRED ;					!^C^

LEXICON 2ROM-HUNDRED					!^C^
 C 2ROM-HUNDRED-TAG ;						!^C^
 CC 2ROM-HUNDRED-TAG ;						!^C^
 CCC 2ROM-HUNDRED-TAG ;						!^C^
 CD 2ROM-HUNDRED-TAG ;						!^C^
 D 2ROM-HUNDRED-TAG ;						!^C^
 DC 2ROM-HUNDRED-TAG ;						!^C^
 DCC 2ROM-HUNDRED-TAG ;						!^C^
 DCCC 2ROM-HUNDRED-TAG ;						!^C^
 CM 2ROM-HUNDRED-TAG ;						!^C^

LEXICON 2ROM-HUNDRED-TAG					!^C^
  ROMNUMTAG ;						!^C^
 2ROM-TEN ;					!^C^

LEXICON 2ROM-TEN						!^C^
 X 2ROM-TEN-TAG ;						!^C^
 XX 2ROM-TEN-TAG ;						!^C^
 XXX 2ROM-TEN-TAG ;						!^C^
 XL 2ROM-TEN-TAG ;						!^C^
 L 2ROM-TEN-TAG ;						!^C^
 LX 2ROM-TEN-TAG ;						!^C^
 LXX 2ROM-TEN-TAG ;						!^C^
 LXXX 2ROM-TEN-TAG ;						!^C^
 XC 2ROM-TEN-TAG ;						!^C^

LEXICON 2ROM-TEN-TAG					!^C^
  ROMNUMTAG ;					!^C^
 2ROM-ONE ;					!^C^

LEXICON 2ROM-ONE						!^C^
 I 2ROM-ONE-TAG ;						!^C^
 II 2ROM-ONE-TAG ;						!^C^
 III 2ROM-ONE-TAG ;						!^C^
 IV  2ROM-ONE-TAG ;						!^C^
 V 2ROM-ONE-TAG ;						!^C^
 VI 2ROM-ONE-TAG ;						!^C^
 VII 2ROM-ONE-TAG ;						!^C^
 VIII 2ROM-ONE-TAG ;						!^C^
 IX 2ROM-ONE-TAG ;						!^C^

LEXICON 2ROM-ONE-TAG						!^C^
  ROMNUMTAG ;						!^C^
 
LEXICON ROMNUMTAG 						!^C^
 +Num+Nom: # ;									!^C^
 +Num+Gen: # ;									!^C^
! +Num+Acc: # ;									!^C^
 +A+Ord:%. # ;									!^C^ !III. as ordinal.
 +A+Ord: # ;								!^C^ !Looking for trouble...
 +Num:%: ROMNUMTAGOBL ;						!^C^
 +Num:%' ROMNUMTAGOBL ;						!^C^
 +Num:%- ROMNUMTAGOBL ;						!^C^
 +Num:%´ ROMNUMTAGOBL ;						!^C^
 +Num:   ROMNUMTAGOBL ;						!^C^

LEXICON ROMNUMTAGOBL 						!^C^
+Gen:a # ;	! But here 123a 123+Num+Gen					!^C^
+Acc:av # ;									!^C^
+Ill:j # ;									!^C^
+Ill:ij # ;									!^C^
+Ill:áj # ;					!^C^
+Ela:s # ;									!^C^
+Ine:s # ;									!^C^
+Com:jn # ;									!^C^
+Ess:n # ;									!^C^




! A final section with some isolated numeral expresssions !
! ------------------------------------------------------- !

      
LEXICON ISOLATED-NUMEXP					!^C^
½+Num:½ # ;						!^C^
¹+Num:¹ # ;						!^C^
²+Num:² # ;						!^C^
³+Num:³ # ;						!^C^
¼+Num:¼ # ;						!^C^



