! Numerals
! ========



LEXICON Numeral					!^N^
MILIJÅVNNÅ ;          ! a noun of its own		!^N^
UNDERTUVSÁN ;      ! for generator under 1000	!^N^
JUSTTUVSÁN ;       ! going via 1000		!^N^
JUSTDUHÁT ;                                     !^N^
OVERTUVSÁN ;       ! for generator over 1000	!^N^
OLD ;             ! for "thirteen hundred, etc.	!^N^
!num-derived ;    ! still unimplemented		!^N^
num-imprecise ;                              !^N^
ARABIC ;          ! for the arabic numerals !^C^
ROMAN ;           ! for the roman numerals	!^C^
NUM-PREFIXES ;    ! for §34 etc.		!^C^
ISOLATED-NUMEXP ; ! for ½ etc.			!^C^
COLLECTIVE ;                            !^N^
O-NL ;  !^N^

! Million, a noun of its own
! --------------------------

LEXICON MILIJÅVNNÅ					
milijåvnnå:milijåvnnå AKTA ;			
millijåvnnå:millijåvnnå AKTA ;
milijåvnåt+A+Ord:milijåvnåd3 VUOSTASJ ;	
millijåvnåt+A+Ord:millijåvnåd3 VUOSTASJ ;
milliárdda:milliárdda AKTA ;		! Num or N?
milliárdat+A+Ord:milliárdad3 VUOSTASJ ;	

! Overtuvsán, for the numerals over 1000
! --------------------------------------

LEXICON OVERTUVSÁN				!^N^
O-AKTAF ;     ! 1000				!^N^
O-2TO9F ;     ! 2000, 3000, ...			!^N^
O-JUSTLÅGEVF ; ! 10000				!^N^
O-2TO9LÅG;   ! 20000, 30000, ...		!^N^
O-LÅGENAN ;  ! 11000, 12000, ...		!^N^
O-TJUOHTE ;     ! 100000				!^N^	
O-2TO9TJUO ;   ! 200000, 300000, ...		!^N^

! All the child lexica of OVERTUVSÁN have the prefix O-.
! They are directed via their respective numerals to the lexicon JUSTTUVSÁN.


! Thousands

LEXICON O-AKTAF		!^N^
akta# JUSTDUHÁT ;
JUSTDUHÁT ;		!^N^

LEXICON O-X1000		!^N^
akta# JUSTTUVSÁN ;

LEXICON O-2TO9F                               !^N^
guokta# JUSTTUVSÁN ;                                
gålmmå# JUSTTUVSÁN ;                            
niellja# JUSTTUVSÁN ;                                  
vihtta# JUSTTUVSÁN ;                            
guhtta# JUSTTUVSÁN ;                            
gietjav# JUSTTUVSÁN ;                             
gieža# JUSTTUVSÁN ;                             
gáktsa# JUSTTUVSÁN ;                            
aktse# JUSTTUVSÁN ;                             

! Tens of thousands

LEXICON O-JUSTLÅGEVF		!^N^
lågev# JUSTTUVSÁN ;		
! This lexicon is for the number 10000 only. it is separated from the
! rest to avoid forms like *logivihttaduhát, etc.


LEXICON O-LÅGEVF			!^N^	     
låk# JUSTTUVSÁN ;		!^N^
låk# O-X1000 ;			!^N^
låk# O-2TO9F ;			!^N^
! this lexicon is accessed only via other O-lexica, and not directly 
! from OVERTUVSÁN. Thus, *logivihttaduhát, etc. is avoided.

LEXICON O-2TO9LÅG		!^N^	  
guokta# O-LÅGEVF ;		
gålmmå# O-LÅGEVF ;		
niellja# O-LÅGEVF ;		
vihtta# O-LÅGEVF ;		
guhtta# O-LÅGEVF ;		
gietjav# O-LÅGEVF ;			
gáktsa# O-LÅGEVF ;		
aktse# O-LÅGEVF ;			


! Teens of thousands

LEXICON O-LÅGENAN		!^N^
lågenan#akta# JUSTTUVSÁN ;        
lågenan#guokta#:lågenan#guokta# JUSTTUVSÁN ;           
lågenan#gålmmå# JUSTTUVSÁN ;       
lågenan#niellja#:lågenan#niellja# JUSTTUVSÁN ;       
lågenan#vihtta#:lågenan#vihtta# JUSTTUVSÁN ;             
lågenan#guhtta#:lågenan#guhtta# JUSTTUVSÁN ;          
lågenan#gietjav#:lågenan#giehtjav# JUSTTUVSÁN ;        
lågenan#gáktsa#:lågenan#gáktsa# JUSTTUVSÁN ;       
lågenan#aktse#:lågenan#aktse# JUSTTUVSÁN ;                 

akta# O-NL ;        
guokta# O-NL ;           
gålmmå# O-NL ;       
niellja# O-NL ;       
vihtta# O-NL ;             
guhtta# O-NL ;          
gietjav# O-NL ;        
gáktsa# O-NL ;       
aktse# O-NL ;                 


 LEXICON O-NL			!^N^
 lågenan# JUSTTUVSÁN ;


! Hundreds of thousands

LEXICON O-TJUOHTE					!^N^
tjuohte# JUSTTUVSÁN ;  ! 100000			
tjuohte# O-X1000 ;     ! 101000			
tjuohte# O-2TO9F ;    ! 102000, 103000, ...	
tjuohte# O-LÅGEVF ;    ! 110000, 113000, ...	
tjuohte# O-2TO9LÅG ;  ! 120000, 123000, ...	
tjuohte# O-LÅGENAN ; ! 115000, ...		

LEXICON O-TJUODE					!^N^
tjuode# JUSTTUVSÁN ;  ! 200000		!^N^	
tjuode# O-X1000 ;     ! 201000			!^N^
tjuode# O-2TO9F ;    ! 202000, 203000, ...	!^N^
tjuode# O-LÅGEVF ;    ! 210000, 213000, ...	!^N^
tjuode# O-2TO9LÅG ;  ! 220000, 223000, ...	!^N^
tjuode# O-LÅGENAN ; ! 215000, ...		!^N^

LEXICON O-2TO9TJUO				!^N^
akta# O-TJUOHTE ;       ! #one# hundred...		
guokta# O-TJUODE ;				
gålmmå# O-TJUODE ;				
niellja# O-TJUODE ;				
vihtta# O-TJUODE ;				
guhtta# O-TJUODE ;				
gietjav# O-TJUODE ;					
gáktsa# O-TJUODE ;				
aktse# O-TJUODE ;					

LEXICON JUSTDUHÁT		!^N^		
tuvsán# UNDERTUVSÁN ;  	
tuvsán TUVSÁN ; ! Case infl of tuvsán
tuvsánat+A+Ord:tuvsána TUVSÁNAT ;			
duhát# UNDERTUVSÁN ;  	
duhát:duhád3 TUVSÁN ; ! Case infl of duhát	
duhádat+A+Ord:duháda TUVSÁNAT ;			




! Justtuvsán, for numerals going via 1000
! -------------------------------------

LEXICON JUSTTUVSÁN				!^N^
tuvsán# UNDERTUVSÁN ;  ! Thousand-and-something	
tuvsán TUVSÁN ; ! Case infl of tuvsán	!^N^
tuvsánat+A+Ord:tuvsána TUVSÁNAT ;			!^N^
duhát# UNDERTUVSÁN ;  ! Thousand-and-something	
duhát:duhád3 TUVSÁN ; ! Case infl of duhát	!^N^
duhádat+A+Ord:duháda TUVSÁNAT ;			!^N^


! Old, for the old counting thirteen hundred etc.	
! -----------------------------------------------

LEXICON OLD					!^N^
akta# NLX ;						
guokta# NLX ;					
gålmmå# NLX ;					
niellja# NLX ;					
vihtta# NLX ;					
guhtta# NLX ;					
gietjav# NLX ;					
gáktsa# NLX ;					
aktse# NLX ;					
lågenan#akta# GTJUODE ;						
lågenan#guokta# GTJUODE ;					
lågenan#gålmmå# GTJUODE ;					
lågenan#niellja# GTJUODE ;					
lågenan#vihtta# GTJUODE ;					
lågenan#guhtta# GTJUODE ;					
lågenan#gietjav# GTJUODE ;					
lågenan#gáktsa# GTJUODE ;					
lågenan#aktse# GTJUODE ;

LEXICON NLX					!^N^
lågenan# GTJUODE ;				!^N^




LEXICON UNDERTUVSÁN				!^N^
AKTAF ;     ! 1				!^N^
2TO9F ;     ! 2-9			!^N^
LÅGEVF ;     ! 10				!^N^
LÅGENAN ;   ! 11-19             !^N^
2TO9LÅG ;   ! 20-99				!^N^
TJUOHTE ;     ! 100-199				!^N^
2TO9TJUO ;   ! 200-999				!^N^

LEXICON AKTAF					!^N^
akta# AKTA ;        
akta#:åvtå# ÅVTÅ ;		
vuostasj+A+Ord:vuostatj VUOSTASJ ;			 


LEXICON 2TO9F					!^N^
guokta:guokt GUOKTA ;                  
nubbe+A+Ord:nupp nubbecase ;		 
nubbe+A+Ord+Sg+Nom:nubbe K ;         
nubbe+A+Ord+Ess:nubben K ;          
nubbe+A+Ord+Sg+Ill:nubbáj K ;        
nubbe#+A+Ord+Sg+Gen:nuppe# NUMERALCOMPOUNDS ;
nuppát+A+Ord:nuppád3 VUOSTASJ ;  
guovtes+Num+Sg+Nom:guovtes K ;      
guovtes+Num:guoktás VIDÁS ;      
gålmmå GÅLMMÅ ;       
goalmát+A+Ord:goalmád3 VUOSTASJ ;
gålmås+Num:gålmmås VIDÁS ;        
gålmås+Num+Sg+Nom:gålmås K ;      
niellja:niellj GUOKTA ;              
nælját+A+Ord:næljád3 VUOSTASJ ; 
nieljes+Num+Sg+Nom:nieljes K ;      
nieljes+Num:nælljás VIDÁS ;       
vihtta+Num:vidá GÅLMMÅ-OBL ;                    
vihtta+Num+Sg+Nom:vihtta K ;          
vihtta+Num+Sg+Acc:vihtta K ;         
vihtta+Num+Sg+Ill:vihttaj K ;         
vihtta+Num+Ess:vihttan K ;            
vidát+A+Ord:vidád3 VUOSTASJ ;
vidás+Num+Sg+Nom:vidás K ;      
viđás+Num:vihttas VIDÁS ;       
guhtta+Num:gudá GÅLMMÅ-OBL ;                
guhtta+Num+Sg+Nom:guhtta K ;          
guhtta+Num+Sg+Acc:guhtta K ;         
guhtta+Num+Sg+Ill:guhttaj K ;         
guhtta+Num+Ess:guhttan K ;            
gudát+A+Ord:gudád3 VUOSTASJ ;
gudás+Num+Sg+Nom:gudás K ;   
guđás+Num:guhttas VIDÁS ;      
gietjav:giehtja GIETJAV ;         
giehttjit+A+Ord:giehttjid3 VUOSTASJ ; 
gietjas+Num+Sg+Nom:gietjas K ;   
gietjas+Num:giehtjas VIDÁS ;       
gáktsa:gákts GÁKTSA ;              
gávtsát+A+Ord:gávtsád3 VUOSTASJ ;
gávtses+Num+Sg+Nom:gávtses K ;   
gávtses+Num:gáktsás VIDÁS ;        
aktse:akts AKTSE ;                      
avtsát+A+Ord:avtsád3 VUOSTASJ ; 
avtses+Num+Sg+Nom:avtses K ;   
avtses+Num:aktsás VIDÁS ;       


LEXICON LÅGEVF				!^N^	
lågev:låhk LÅGEV ;   
lågåt+A+Ord:lågåd3 VUOSTASJ ;			
låhke:låhk  TJUOHTECASE ; 
låhke+Num+Sg+Ill:låhkåj K ;            
låges+Num+Sg+Nom:låges K ;   
låges+Num:låhkås VIDÁS ;       


LEXICON LÅK-					!^N^
låk# AKTAF ;				!^N^
låk# 2TO9F ;				!^N^
låhke+Num+Sg+Acc+Attr:låk K ;         !^N^
låhke#+Num+Sg+Attr:låk# NUMERALCOMPOUNDS ;      !^N^ ^C^
låhke#+Num+Sg+Gen:låge# NUMERALCOMPOUNDS ;      !^N^ ^C^
låhke:låhk  TJUOHTECASE ; !^N^ ! vihttalåhke, collective number
låhke+Num+Sg+Ill:låhkåj K ;            !^N^
lågåt+A+Ord:lågåd3 VUOSTASJ ;			!^N^

LEXICON LÅGENAN					!^N^
lågenan#akta AKTA ;        
lågenan#akta:lågenan#åvtå ÅVTÅ ;		
lågenan#vuostasj+A+Ord:lågenan#vuostatj VUOSTASJ ;			 
lågenan#guokta:lågenan#guokt GUOKTA ;           
lågenan#nubbe+A+Ord:lågenan#nupp nubbecase ;		 
lågenan#nubbe+A+Ord+Sg+Nom:lågenan#nubbe K ;       
lågenan#nubbe+A+Ord+Ess:lågenan#nubben K ;        
lågenan#nubbe+A+Ord+Sg+Ill:lågenan#nubbáj K ;     
lågenan#nuppát+A+Ord:lågenan#nuppád3 VUOSTASJ ;       
guovtes#lågenan+Num+Sg+Nom:guovtes#lågenan K ;      
guovtes#lågenan+Num:guoktás VIDÁSLÅGENAN ;       
lågenan#gålmmå GÅLMMÅ ;       
lågenan#goalmát+A+Ord:lågenan#goalmád3 VUOSTASJ ;        
gålmås#lågenan+Num+Sg+Nom:gålmås#lågenan K ;      
gålmås#lågenan+Num:gålmmås VIDÁSLÅGENAN ;        
lågenan#niellja:lågenan#niellj GUOKTA ;       
lågenan#nælját+A+Ord:lågenan#næljád3 VUOSTASJ ;        
nieljes#lågenan+Num+Sg+Nom:nieljes#lågenan K ;      
nieljes#lågenan+Num:nælljás VIDÁSLÅGENAN ;       
lågenan#vihtta+Num:lågenan#vidá GÅLMMÅ-OBL ;             
lågenan#vihtta+Num+Sg+Nom:lågenan#vihtta K ;          
lågenan#vihtta+Num+Sg+Acc:lågenan#vihtta K ;         
lågenan#vihtta+Num+Sg+Ill:lågenan#vihttaj K ;         
lågenan#vihtta+Num+Ess:lågenan#vihttan K ;            
lågenan#vidát+A+Ord:lågenan#vidád3 VUOSTASJ ;       
vidás#lågenan+Num+Sg+Nom:vidás#lågenan K ;      
vidás#lågenan+Num:vihttas VIDÁSLÅGENAN ;   
gudás#lågenan+Num+Sg+Nom:gudás#lågenan K ;      
guđás#lågenan+Num:guhttas VIDÁSLÅGENAN ;       
lågenan#guhtta+Num:lågenan#gudá GÅLMMÅ-OBL ;          
lågenan#guhtta+Num+Sg+Nom:lågenan#guhtta K ;          
lågenan#guhtta+Num+Sg+Acc:lågenan#guhtta K ;         
lågenan#guhtta+Num+Sg+Ill:lågenan#guhttaj K ;         
lågenan#guhtta+Num+Ess:lågenan#guhttan K ;            
lågenan#gudát+A+Ord:lågenan#gudád3 VUOSTASJ ;        
lågenan#gietjav:lågenan#giehtja GIETJAV ;        
lågenan#giehttjit+A+Ord:lågenan#giehttjid3 VUOSTASJ ;         
gietjas#lågenan+Num+Sg+Nom:gietjas#lågenan K ;      
gietjas#lågenan+Num:giehtjas VIDÁSLÅGENAN ;       
gávtses#lågenan+Num+Sg+Nom:gávtses#lågenan K ;      
gávtses#lågenan+Num:gáktsás VIDÁSLÅGENAN ;        
avtses#lågenan+Num+Sg+Nom:avtses#lågenan K ;      
avtses#lågenan+Num:aktsás#lågenan VIDÁSLÅGENAN ;       
lågenan#gáktsa:lågenan#gákts GÁKTSA ;       
lågenan#gávtsát+A+Ord:lågenan#gávtsád3 VUOSTASJ ;       
lågenan#aktse:lågenan#akts AKTSE ;                 
lågenan#avtsát+A+Ord:lågenan#avtsád3 VUOSTASJ ;        
akta#lågenan AKTALÅGENAN ;        
akta#lågenan:åvtå ÅVTÅLÅGENAN ;		
vuostasj#lågåt+A+Ord:vuostasj#lågåd3 VUOSTASJ ;			 
guokta#lågenan:guokt GUOKTALÅGENAN ;           
nuppát#lågåt+A+Ord:nuppát#lågåd3 VUOSTASJ ;        
gålmmå#lågenan GÅLMMÅLÅGENAN ;       
goalmát#lågåt+A+Ord:goalmát#lågåd3 VUOSTASJ ;        
niellja#lågenan:niellj GUOKTALÅGENAN ;       
nælját#lågåt+A+Ord:nælját#lågåd3 VUOSTASJ ;        
vihtta#lågenan+Num:vidá GÅLMMÅLÅGENAN-OBL ;             
vihtta#lågenan+Num+Sg+Nom:vihtta#lågenan K ;          
vihtta#lågenan+Num+Sg+Acc:vihtta#lågenan K ;         
vihtta#lågenan+Num+Sg+Ill:vihttaj#lågenan K ;         
vihtta#lågenan+Num+Ess:vihttan#lågenan K ;            
vidát#lågåt+A+Ord:vidát#lågåd3 VUOSTASJ ;        
guhtta#lågenan+Num:gudá GÅLMMÅLÅGENAN-OBL ;          
guhtta#lågenan+Num+Sg+Nom:guhtta#lågenan K ;          
guhtta#lågenan+Num+Sg+Acc:guhtta#lågenan K ;         
guhtta#lågenan+Num+Sg+Ill:guhttaj#lågenan K ;         
guhtta#lågenan+Num+Ess:guhttan#lågenan K ;            
gudát#lågåt+A+Ord:gudát#lågåd3 VUOSTASJ ;        
gietjav#lågenan:giehtja GIETJAVLÅGENAN ;        
giehttjit#lågåt+A+Ord:giehttjit#lågåd3 VUOSTASJ ;         
gáktsa#lågenan:gákts GÁKTSALÅGENAN ;       
gávtsát#lågåt+A+Ord:gávtsát#lågåt VUOSTASJ ;       
aktse#lågenan:akts AKTSELÅGENAN ;                 
avtsát#lågåt+A+Ord:avtsát#lågåd3 VUOSTASJ ;        



LEXICON 2TO9LÅG					!^N^
guokta# LÅK- ;					
gålmmå# LÅK- ;					
niellja# LÅK- ;				
vihtta# LÅK- ;					
guhtta# LÅK- ;					
gietjav# LÅK- ;					
gáktsa# LÅK- ;					
aktse# LÅK- ;					

LEXICON TJUOHTE					!^N^
tjuohte:tjuoht TJUOHTECASE ;    
tjuohte+Num+Sg+Ill:tjuohtáj K ;  
tjuohte#+Num+Attr:tjuohte# NUMERALCOMPOUNDS ;   !^C^
tjuohte# LÅGEVF ;					 !to LÅK- too?
tjuohte# 2TO9LÅG ;					
tjuohte# AKTAF ;					
tjuohte# 2TO9F ;					
tjuohte# LÅGENAN ;				
tjuodát+A+Ord:tjuodád3 VUOSTASJ ;			
akta#tjuohte:akta#tjuoht TJUOHTECASE ;    
akta#tjuohte+Num+Sg+Ill:akta#tjuohtáj K ;  
akta#tjuohte# LÅGEVF ;					 !to LÅK- too?
akta#tjuohte# 2TO9LÅG ;					
akta#tjuohte# AKTAF ;					
akta#tjuohte# 2TO9F ;					
akta#tjuohte# LÅGENAN ;				
tjuodes+Num+Sg+Nom:tjuodes K ;      
tjuodes+Num:tjuohtás VIDÁS ;       
akta#tjuodes+Num+Sg+Nom:akta#tjuodes K ;      
akta#tjuodes+Num:akta#tjuohtás VIDÁS ;       



LEXICON 2TO9TJUO					!^N^
guokta# GTJUODE ;					
gålmmå# GTJUODE ;					
niellja# GTJUODE ;				
njeallja# GTJUODE ;				
vihtta# GTJUODE ;					
guhtta# GTJUODE ;					
gietjav# GTJUODE ;					
gáktsa# GTJUODE ;					
aktse# GTJUODE ;					


LEXICON GTJUODE					!^N^
tjuode#+Num+Attr:tjuode# NUMERALCOMPOUNDS ; !^N^ ^C^
tjuode LÅGEVF ;					!^N^
tjuode# 2TO9LÅG ;					!^N^
tjuode# AKTAF ;					!^N^
tjuode# 2TO9F ;					!^N^
tjuode# LÅGENAN ;				!^N^
tjuohte:tjuoht TJUOHTECASE ;    !^N^  ! imprecise number
tjuodát+A+Ord:tjuodád3 VUOSTASJ ;			!^N^
tjuodes+Num+Sg+Nom:tjuodes K ;      !^N^
tjuodes+Num:tjuohtás VIDÁS ;       !^N^


LEXICON COLLECTIVE   !^N^ !Some collective numbers that do not fit in anywhere else
aktagis+Num:aktagis TJUODAGIS ;               
guoktagis+Num:guoktagis TJUODAGIS ;           
gålmmågis+Num:gålmmågis TJUODAGIS ;           
nielljagis+Num:nielljagis TJUODAGIS ;         
vihttagis+Num:vihttagis TJUODAGIS ;     
guhttagis+Num:guhttagis TJUODAGIS ;               
giehtjagis+Num:giehtjagis TJUODAGIS ;             
gáktsagis+Num:gáktsagis TJUODAGIS ;           
aktsegis+Num:aktsegis TJUODAGIS ;         
låhkegis+Num:låhkegis TJUODAGIS ;         
guovte#lågåk+Num:guovte#lågåg3 VUOSTASJ ;			
gålmå#lågåk+Num:gålmå#lågåg3 VUOSTASJ ;			
nielje#lågåk+Num:nielje#lågåg3 VUOSTASJ ;			
vidá#lågåk+Num:vidá#lågåg3 VUOSTASJ ;			
gudá#lågåk+Num:gudá#lågåg3 VUOSTASJ ;			
gietja#lågåk+Num:gietja#lågåg3 VUOSTASJ ;			
gávtse#lågåk+Num:gávtse#lågåg3 VUOSTASJ ;			
avtse#lågåk+Num:avtse#lågåg3 VUOSTASJ ;			
guokta#låk#vihttagis+Num:guokta#låk#vihttagis TJUODAGIS ;     
gålmmå#låk#vihttagis+Num:gålmmå#låk#vihttagis TJUODAGIS ;     
niellja#låk#vihttagis+Num:niellja#låk#vihttagis TJUODAGIS ;     
vihtta#låk#vihttagis+Num:vihtta#låk#vihttagis TJUODAGIS ;     
guhtta#låk#vihttagis+Num:guhtta#låk#vihttagis TJUODAGIS ;     
gietjav#låk#vihttagis+Num:gietjav#låk#vihttagis TJUODAGIS ;     
gáktsa#låk#vihttagis+Num:gáktsa#låk#vihttagis TJUODAGIS ;     
aktse#låk#vihttagis+Num:aktse#låk#vihttagis TJUODAGIS ;     
tjuodak+Num:tjuodag3 VUOSTASJ ;			
tjuodagis+Num:tjuodagis TJUODAGIS ;     
avta#tjuodak+Num:avta#tjuodag3 VUOSTASJ ;			
avta#tjuodagis+Num:avta#tjuodagis TJUODAGIS ;     
guovte#tjuodak+Num:guovte#tjuodag3 VUOSTASJ ;			
guovte#tjuodagis+Num:guovte#tjuodagis TJUODAGIS ;     
gålmå#tjuodak+Num:gålmå#tjuodag3 VUOSTASJ ;			
gålmå#tjuodagis+Num:gålmå#tjuodagis TJUODAGIS ;     
nielje#tjuodak+Num:nielje#tjuodag3 VUOSTASJ ;			
nielje#tjuodagis+Num:nielje#tjuodagis TJUODAGIS ;     
vidá#tjuodak+Num:vidá#tjuodag3 VUOSTASJ ;			
vidá#tjuodagis+Num:vidá#tjuodagis TJUODAGIS ;     
gudá#tjuodak+Num:gudá#tjuodag3 VUOSTASJ ;			
gudá#tjuodagis+Num:gudá#tjuodagis TJUODAGIS ;     
gietja#tjuodak+Num:gietja#tjuodag3 VUOSTASJ ;			
gietja#tjuodagis+Num:gietja#tjuodagis TJUODAGIS ;     
gávtse#tjuodak+Num:gávtse#tjuodag3 VUOSTASJ ;			
gávtse#tjuodagis+Num:gávtse#tjuodagis TJUODAGIS ;     
avtse#tjuodak+Num:avtse#tjuodag3 VUOSTASJ ;			
avtse#tjuodagis+Num:avtse#tjuodagis TJUODAGIS ;     
tjuohte#vidá#lågåk+Num:tjuohte#vidá#lågåg3 VUOSTASJ ;			
guokta#tjuode#vidá#lågåk+Num:guokta#tjuode#vidá#lågåg3 VUOSTASJ ;			
gålmmå#tjuode#vidá#lågåk+Num:gålmmå#tjuode#vidá#lågåg3 VUOSTASJ ;			
niellja#tjuode#vidá#lågåk+Num:niellja#tjuode#vidá#lågåg3 VUOSTASJ ;			
vihtta#tjuode#vidá#lågåk+Num:vihtta#tjuode#vidá#lågåg3 VUOSTASJ ;			
guhtta#tjuode#vidá#lågåk+Num:guhtta#tjuode#vidá#lågåg3 VUOSTASJ ;			
gietjav#tjuode#vidá#lågåk+Num:gietjav#tjuode#vidá#lågåg3 VUOSTASJ ;			
gáktsa#tjuode#vidá#lågåk+Num:gáktsa#tjuode#vidá#lågåg3 VUOSTASJ ;			
aktse#tjuode#vidá#lågåk+Num:aktse#tjuode#vidá#lågåg3 VUOSTASJ ;			


! Case forms of the basic digits
! ------------------------------

LEXICON AKTA                !^N^
+Num: AKTA-NIE ;               !^N^
+Num:Q1 AKTA-OBL ;              !^N^

LEXICON AKTA-NIE                !^N^
 +Sg+Nom: K ;               !^N^
 +Sg+Ill:j K ;              !^N^
 +Ess:n K ;                 !^N^

LEXICON AKTA-OBL                !^N^
 +Sg+Gen: K ;               !^N^
 +Sg+Gen:# NUMERALCOMPOUNDS ;        !^N^ ^C^
 +Sg+Acc:v K ;     !^N^         
 +Sg+Ine:n K ;         !^N^     
 +Sg+Ela:s K ;             !^N^ 
 +Sg+Com:jn K ;             !^N^
 +Sg+Par:t K ;               !^N^
 +Pl+Nom: K ;                !^N^
 +Pl+Gen:j K ;               !^N^
 +Pl+Acc:jt K ;              !^N^
 +Pl+Ill:jda K ;             !^N^
 +Pl+Ine:jn K ;              !^N^
 +Pl+Ela:js K ;              !^N^
 +Pl+Com:j K ;               !^N^
 
LEXICON ÅVTÅ                 !^N^
+Num+Sg+Gen: K ;             !^N^
+Num+Sg+Gen:# NUMERALCOMPOUNDS ;        !^N^ ^C^
+Num+Sg+Acc:v K ;            !^N^
+Num+Sg+Ine:n K ;            !^N^
+Num+Sg+Ela:s K ;            !^N^
+Num+Sg+Com:jn K ;           !^N^
+Num+Sg+Par:t K ;            !^N^
+Num+Pl+Nom: K ;             !^N^
+Num+Pl+Gen:j K ;            !^N^
+Num+Pl+Acc:jt K ;           !^N^
+Num+Pl+Ill:jda K ;          !^N^
+Num+Pl+Ine:jn K ;           !^N^
+Num+Pl+Ela:js K ;           !^N^
+Num+Pl+Com:j K ;            !^N^

LEXICON GUOKTA                  !^N^
+Num:eQ1 GUOVTE- ;               !^N^
+Num:iQ1 GUOVTI- ;               !^N^
+Num: GUOKTA- ;                 !^N^

LEXICON GUOVTE-             !^N^
 +Sg+Gen: K ;              !^N^
 +Sg+Gen:# NUMERALCOMPOUNDS ;   !^N^
 +Sg+Ine:n K ;             !^N^
 +Sg+Ela:s K ;             !^N^
 +Sg+Par:t K ;             !^N^
 +Pl+Nom: K ;              !^N^
 
LEXICON GUOVTI-   !^N^
 +Sg+Com:jn K ;       !^N^     
 +Pl+Gen:j K ;            !^N^ 
 +Pl+Acc:jt K ;            !^N^
 +Pl+Ill:jda K ;           !^N^
 +Pl+Ine:jn K ;            !^N^
 +Pl+Ela:js K ;            !^N^
 +Pl+Com:j K ;             !^N^


LEXICON GUOKTA-            !^N^
 +Sg+Nom:a K ;           !^N^
 +Sg+Acc:a K ;           !^N^
 +Sg+Ill:áj K ;          !^N^
 +Ess:an K ;             !^N^
                         
LEXICON GÅLMMÅ           !^N^
+Num: GÅLMMÅ-NAIE ;      !^N^
+Num:Q1 GÅLMMÅ-OBL ;     !^N^
                        
LEXICON GÅLMMÅ-NAIE      !^N^
 +Sg+Nom: K ;            !^N^
 +Sg+Acc: K ;            !^N^
 +Sg+Ill:j K ;           !^N^
 +Ess:n K ;              !^N^
                         
LEXICON GÅLMMÅ-OBL       !^N^
 +Sg+Gen: K ;            !^N^
 +Sg+Gen:# NUMERALCOMPOUNDS ; !^N^
 +Sg+Ine:n K ;           !^N^
 +Sg+Ela:s K ;           !^N^
 +Sg+Com:jn K ;          !^N^
 +Sg+Par:t K ;           !^N^
 +Pl+Nom: K ;            !^N^
 +Pl+Gen:j K ;           !^N^
 +Pl+Acc:jt K ;          !^N^
 +Pl+Ill:jda K ;         !^N^
 +Pl+Ine:jn K ;          !^N^
 +Pl+Ela:js K ;          !^N^
 +Pl+Com:j K ;           !^N^


LEXICON GIETJAV             !^N^
+Num: GIETJAV-IE ;          !^N^
+Num:Q1 GIETJAV-OBL ;       !^N^
                            
LEXICON GIETJAV-IE          !^N^
 +Sg+Ill:j K ;              !^N^
 +Ess:n K ;                 !^N^
                        
LEXICON GIETJAV-OBL         !^N^
 +Sg+Nom:v K ;              !^N^
 +Sg+Gen: K ;               !^N^
 +Sg+Gen:# NUMERALCOMPOUNDS ;        !^N^  ^C^
 +Sg+Acc:v K ;              !^N^
 +Sg+Ine:n K ;              !^N^
 +Sg+Ela:s K ;              !^N^
 +Sg+Com:jn K ;             !^N^
 +Sg+Par:t K ;              !^N^
 +Pl+Nom: K ;               !^N^
 +Pl+Gen:j K ;              !^N^
 +Pl+Acc:jt K ;             !^N^
 +Pl+Ill:jda K ;            !^N^
 +Pl+Ine:jn K ;             !^N^
 +Pl+Ela:js K ;             !^N^
 +Pl+Com:j K ;              !^N^
 

LEXICON GÁKTSA              !^N^
+Num:eQ1 GÁVTSE- ;           !^N^
+Num:iQ1 GÁVTSI- ;           !^N^
+Num: GÁKTSA- ;             !^N^
                            
LEXICON GÁVTSE-             !^N^
 +Sg+Gen: K ;              !^N^
 +Sg+Gen:# NUMERALCOMPOUNDS ;        !^N^ ^C^
 +Sg+Ine:n K ;             !^N^
 +Sg+Ela:s K ;             !^N^
 +Sg+Par:t K ;             !^N^
 +Pl+Nom: K ;              !^N^

LEXICON GÁVTSI-        !^N^
 +Sg+Com:jn K ;            !^N^
 +Pl+Gen:j K ;             !^N^
 +Pl+Acc:jt K ;            !^N^
 +Pl+Ill:jda K ;           !^N^
 +Pl+Ine:jn K ;            !^N^
 +Pl+Ela:js K ;            !^N^
 +Pl+Com:j K ;             !^N^
                            
                            
LEXICON GÁKTSA-             !^N^
 +Sg+Nom:a K ;              !^N^
 +Sg+Acc:a K ;              !^N^
 +Sg+Ill:áj K ;             !^N^
 +Ess:an K ;      !^N^
 

LEXICON AKTSE         !^N^   
+Num:eQ1 AVTSE- ;         !^N^  
+Num:iQ1 AVTSI- ;           !^N^
+Num: AKTS- ;             !^N^

LEXICON AVTSE-            !^N^
 +Sg+Gen: K ;          !^N^
 +Sg+Gen:# NUMERALCOMPOUNDS ;        !^N^ ^C^
 +Sg+Ine:n K ;         !^N^
 +Sg+Ela:s K ;         !^N^
 +Sg+Par:t K ;         !^N^
 +Pl+Nom: K ;          !^N^

LEXICON AVTSI-         !^N^
 +Sg+Com:jn K ;        !^N^
 +Pl+Gen:j K ;         !^N^
 +Pl+Acc:jt K ;        !^N^
 +Pl+Ill:jda K ;       !^N^
 +Pl+Ine:jn K ;        !^N^
 +Pl+Ela:js K ;        !^N^
 +Pl+Com:j K ;         !^N^
                        
                    
LEXICON AKTS-           !^N^
 +Sg+Nom:e K ;          !^N^
 +Sg+Acc:e K ;          !^N^
 +Sg+Ill:áj K ;         !^N^
 +Ess:an K ;        !^N^
 

LEXICON LÅGEV           !^N^     
+Num:eQ1 LÅGE- ;    !^N^
+Num:iQ1 LÅGI- ;    !^N^
+Num: LÅHK- ;     !^N^
                   
LEXICON LÅGE-      !^N^
 +Sg+Nom:v K ;    !^N^
 +Sg+Acc:v K ;    !^N^
 +Sg+Gen: K ;     !^N^
 +Sg+Gen:# NUMERALCOMPOUNDS ;        !^N^ ^C^
 +Sg+Ine:n K ;    !^N^
 +Sg+Ela:s K ;    !^N^
 +Sg+Par:t K ;    !^N^
 +Pl+Nom: K ;     !^N^

LEXICON LÅGI-      !^N^
 +Sg+Com:jn K ;   !^N^
 +Pl+Gen:j K ;    !^N^
 +Pl+Acc:jt K ;   !^N^
 +Pl+Ill:jda K ;  !^N^
 +Pl+Ine:jn K ;   !^N^
 +Pl+Ela:js K ;   !^N^
 +Pl+Com:j K ;    !^N^
                   
                   
LEXICON LÅHK-      !^N^
 +Sg+Ill:åj K ;    !^N^
 +Ess:en K ;       !^N^
                   
                   
LEXICON AKTALÅGENAN  !^N^
+Num: AKTALÅGENAN-NIE ;  !^N^
+Num:Q1 AKTALÅGENAN-OBL ;      !^N^
                   
LEXICON AKTALÅGENAN-NIE    !^N^
 +Sg+Nom:#lågenan K ;     !^N^
 +Sg+Ill:j#lågenan K ;       !^N^
 +Ess:n#lågenan K ;        !^N^
                   
LEXICON AKTALÅGENAN-OBL    !^N^
 +Sg+Gen:#lågenan K ;      !^N^
 +Sg+Gen:#lågenan# NUMERALCOMPOUNDS ;      !^N^ ^C^
 +Sg+Acc:v#lågenan K ; !^N^
 +Sg+Ine:n#lågenan K ; !^N^
 +Sg+Ela:s#lågenan K ; !^N^
 +Sg+Com:jn#lågenan K ; !^N^
 +Sg+Par:t#lågenan K ; !^N^
 +Pl+Nom:#lågenan K ; !^N^
 +Pl+Gen:j#lågenan K ; !^N^
 +Pl+Acc:jt#lågenan K ; !^N^
 +Pl+Ill:jda#lågenan K ; !^N^
 +Pl+Ine:jn#lågenan K ; !^N^
 +Pl+Ela:js#lågenan K ; !^N^
 +Pl+Com:j#lågenan K ; !^N^
 
LEXICON ÅVTÅLÅGENAN                   !^N^
+Num+Sg+Gen:#lågenan K ;              !^N^
+Num+Sg+Gen:#lågenan# NUMERALCOMPOUNDS ;      !^N^ ^C^
+Num+Sg+Acc:v#lågenan K ;             !^N^
+Num+Sg+Ine:n#lågenan K ;             !^N^
+Num+Sg+Ela:s#lågenan K ;             !^N^
+Num+Sg+Com:jn#lågenan K ;            !^N^
+Num+Sg+Par:t#lågenan K ;             !^N^
+Num+Pl+Nom:#lågenan K ;              !^N^
+Num+Pl+Gen:j#lågenan K ;             !^N^
+Num+Pl+Acc:jt#lågenan K ;            !^N^
+Num+Pl+Ill:jda#lågenan K ;           !^N^
+Num+Pl+Ine:jn#lågenan K ;            !^N^
+Num+Pl+Ela:js#lågenan K ;            !^N^
+Num+Pl+Com:j#lågenan K ;             !^N^
                                      
LEXICON GUOKTALÅGENAN                 !^N^
+Num: GUOVTELÅGENAN ;               !^N^
+Num: GUOKT-LÅGENAN ;                 !^N^
                                      
LEXICON GUOVTELÅGENAN                 !^N^
 +Sg+Gen:eQ1#lågenan K ;                !^N^
 +Sg+Gen:eQ1#lågenan# NUMERALCOMPOUNDS ;           !^N^ ^C^
 +Sg+Ine:eQ1n#lågenan K ;               !^N^
 +Sg+Ela:eQ1s#lågenan K ;               !^N^
 +Sg+Com:iQ1jn#lågenan K ;              !^N^
 +Sg+Par:eQ1t#lågenan K ;               !^N^
 +Pl+Nom:eQ1#lågenan K ;                !^N^
 +Pl+Gen:iQ1j#lågenan K ;               !^N^
 +Pl+Acc:iQ1jt#lågenan K ;              !^N^
 +Pl+Ill:iQ1jda#lågenan K ;             !^N^
 +Pl+Ine:iQ1jn#lågenan K ;              !^N^
 +Pl+Ela:iQ1js#lågenan K ;              !^N^
 +Pl+Com:iQ1j#lågenan K ;               !^N^
                                      
LEXICON GUOKT-LÅGENAN                 !^N^
 +Sg+Nom:a#lågenan K ;                !^N^
 +Sg+Acc:a#lågenan K ;                !^N^
 +Sg+Ill:áj#lågenan K ;               !^N^
 +Ess:an#lågenan K ;                  !^N^
                                      
LEXICON GÅLMMÅLÅGENAN                 !^N^
+Num: GÅLMMÅLÅGENAN-NAIE ;            !^N^
+Num:Q1 GÅLMMÅLÅGENAN-OBL ;           !^N^
                                      
LEXICON GÅLMMÅLÅGENAN-NAIE            !^N^
 +Sg+Nom:#lågenan K ;                 !^N^
 +Sg+Acc:#lågenan K ;                 !^N^
 +Sg+Ill:j#lågenan K ;                !^N^
 +Ess:n#lågenan K ;                   !^N^
                                      
LEXICON GÅLMMÅLÅGENAN-OBL             !^N^
 +Sg+Gen:#lågenan K ;                 !^N^
 +Sg+Gen:#lågenan# NUMERALCOMPOUNDS ;         !^N^ ^C^
 +Sg+Ine:n#lågenan K ;                !^N^
 +Sg+Ela:s#lågenan K ;                !^N^
 +Sg+Com:jn#lågenan K ;               !^N^
 +Sg+Par:t#lågenan K ;                !^N^
 +Pl+Nom:#lågenan K ;                 !^N^
 +Pl+Gen:j#lågenan K ;                !^N^
 +Pl+Acc:jt#lågenan K ;               !^N^
 +Pl+Ill:jda#lågenan K ;              !^N^
 +Pl+Ine:jn#lågenan K ;               !^N^
 +Pl+Ela:js#lågenan K ;               !^N^
 +Pl+Com:j#lågenan K ;                !^N^
                                      
LEXICON GIETJAVLÅGENAN                !^N^
+Num: GIETJAVLÅGENAN-IE ;             !^N^
+Num:Q1 GIETJAVLÅGENAN-OBL ;          !^N^
                                      
LEXICON GIETJAVLÅGENAN-IE             !^N^
 +Sg+Ill:j#lågenan K ;                !^N^
 +Ess:n#lågenan K ;                   !^N^
                                      
LEXICON GIETJAVLÅGENAN-OBL            !^N^
 +Sg+Nom:v#lågenan K ;                !^N^
 +Sg+Gen:#lågenan K ;                 !^N^
 +Sg+Gen:#lågenan# NUMERALCOMPOUNDS ;          !^N^ ^C^
 +Sg+Acc:v#lågenan K ;                !^N^
 +Sg+Ine:n#lågenan K ;                !^N^
 +Sg+Ela:s#lågenan K ;                !^N^
 +Sg+Com:jn#lågenan K ;               !^N^
 +Sg+Par:t#lågenan K ;                !^N^
 +Pl+Nom:#lågenan K ;                 !^N^
 +Pl+Gen:j#lågenan K ;                !^N^
 +Pl+Acc:jt#lågenan K ;               !^N^
 +Pl+Ill:jda#lågenan K ;              !^N^
 +Pl+Ine:jn#lågenan K ;               !^N^
 +Pl+Ela:js#lågenan K ;               !^N^
 +Pl+Com:j#lågenan K ;                !^N^
                                      
LEXICON GÁKTSALÅGENAN                 !^N^
+Num: GÁVTSELÅGENAN ;               !^N^
+Num: GÁKTS-LÅGENAN ;                 !^N^
                                      
LEXICON GÁVTSELÅGENAN                 !^N^
 +Sg+Gen:eQ1#lågenan K ;                !^N^
 +Sg+Gen:eQ1#lågenan# NUMERALCOMPOUNDS ;       !^N^ ^C^
 +Sg+Ine:eQ1n#lågenan K ;               !^N^
 +Sg+Ela:eQ1s#lågenan K ;               !^N^
 +Sg+Com:iQ1jn#lågenan K ;              !^N^
 +Sg+Par:eQ1t#lågenan K ;               !^N^
 +Pl+Nom:eQ1#lågenan K ;                !^N^
 +Pl+Gen:iQ1j#lågenan K ;               !^N^
 +Pl+Acc:iQ1jt#lågenan K ;              !^N^
 +Pl+Ill:iQ1jda#lågenan K ;             !^N^
 +Pl+Ine:iQ1jn#lågenan K ;              !^N^
 +Pl+Ela:iQ1js#lågenan K ;              !^N^
 +Pl+Com:iQ1j#lågenan K ;               !^N^
                                      
LEXICON GÁKTS-LÅGENAN                 !^N^
 +Sg+Nom:a#lågenan K ;                !^N^
 +Sg+Acc:a#lågenan K ;                !^N^
 +Sg+Ill:áj#lågenan K ;               !^N
 +Ess:an#lågenan K ;                  !^N^
                                      
LEXICON AKTSELÅGENAN                  !^N^
+Num: AVTSELÅGENAN ;                !^N^
+Num: AKTS-LÅGENAN ;                  !^N^
                                      
LEXICON AVTSELÅGENAN                  !^N^
 +Sg+Gen:eQ1#lågenan K ;                !^N^
 +Sg+Gen:eQ1#lågenan# NUMERALCOMPOUNDS ;      !^N^ ^C^
 +Sg+Ine:eQ1n#lågenan K ;               !^N^
 +Sg+Ela:eQ1s#lågenan K ;               !^N^
 +Sg+Com:iQ1jn#lågenan K ;              !^N^
 +Sg+Par:eQ1t#lågenan K ;               !^N^
 +Pl+Nom:eQ1#lågenan K ;                !^N^
 +Pl+Gen:iQ1j#lågenan K ;               !^N^
 +Pl+Acc:iQ1jt#lågenan K ;              !^N^
 +Pl+Ill:iQ1jda#lågenan K ;             !^N^
 +Pl+Ine:iQ1jn#lågenan K ;              !^N^
 +Pl+Ela:iQ1js#lågenan K ;              !^N^
 +Pl+Com:iQ1j#lågenan K ;               !^N^
                                      
LEXICON AKTS-LÅGENAN                  !^N^
 +Sg+Nom:e#lågenan K ;                !^N^
 +Sg+Acc:e#lågenan K ;                !^N^
 +Sg+Ill:áj#lågenan K ;               !^N^
 +Ess:an#lågenan K ;                  !^N^
                                      
LEXICON TJUOHTECASE           !^N^
 +Num:eQ1 TJUODE- ;               !^N^      
 +Num:iQ1 TJUODI- ;                   !^N^  
 +Num: TJUOHT- ;                       !^N^
                                      
LEXICON TJUODE-                       !^N^
 +Sg+Acc:v K ;                       !^N^
 +Sg+Gen: K ;                        !^N^
 +Sg+Ine:n K ;                       !^N^
 +Sg+Ela:s K ;                       !^N^
 +Sg+Par:t K ;                       !^N^
 +Pl+Nom: K ;                        !^N^

LEXICON TJUODI-                      !^N^
 +Pl+Gen:j K ;                       !^N^
 +Sg+Com:jn K ;                      !^N^
 +Pl+Acc:jt K ;                      !^N^
 +Pl+Ill:jda K ;                     !^N^
 +Pl+Ine:jn K ;                      !^N^
 +Pl+Ela:js K ;                      !^N^
 +Pl+Com:j K ;                       !^N^
                                      
LEXICON TJUOHT-                       !^N^
 +Sg+Nom:e K ;                        !^N^
 +Ess:en K ;                          !^N^
 +Sg+Ill:áj K ;                       !^N^

LEXICON TUVSÁN				!^N^
 +Num+Sg+Nom: K ;          !^N^
 +Num+Sg+Gen:a K ;         !^N^
 +Num+Sg+Attr:# NUMERALCOMPOUNDS ;         !^N^  ^C^
 +Num+Sg+Acc:av K ;            !^N^
 +Num+Sg+Ill:ij K ;    !^N^
 +Num+Sg+Ine:in K ;        !^N^   
 +Num+Sg+Ela:is K ;            !^N^
 +Num+Sg+Com:ijn K ;            !^N^
 +Num+Ess:in K ;                  !^N^
 +Num+Pl+Nom:a K ;                 !^N^
 +Num+Pl+Gen:ij K ;              !^N^
 +Num+Pl+Acc:ijt K ;              !^N^
 +Num+Pl+Ill:ijda K ;              !^N^
 +Num+Pl+Ine:ijn K ;                !^N^
 +Num+Pl+Ela:ijs K ;                 !^N^
 +Num+Pl+Com:ij K ;                 !^N^



LEXICON NUMERALCOMPOUNDS !^N^ ! numeral as first part 
 NAMÁK ;			  !^N^ ! tuvsánjienat, logigielat, etc. 
 SASJ ; !^N^
! NounSecond ;	! golmmačiegahas	!^N^
! geardásaš:geardás DABALAS ;	   
! !tuvsán#geardásaš !^N^
! jahkásaš:jahkás DABALAS ; !^N^


! Ordinal numbers
! ---------------


LEXICON VUOSTASJ      !^N^				
 +Sg+Nom: K ;          !^N^
 +Sg+Gen:a K ;         !^N^
 +Sg+Acc:av K ;            !^N^
 +Sg+Ill:ij K ;   !^N^
 +Sg+Ine:in K ;       !^N^    
 +Sg+Ela:is K ;           !^N^ 
 +Sg+Com:ijn K ;            !^N^
 +Ess:in K ;                  !^N^
 +Pl+Nom:a K ;                 !^N^
 +Pl+Gen:ij K ;              !^N^
 +Pl+Acc:ijt K ;              !^N^
 +Pl+Ill:ijda K ;              !^N^
 +Pl+Ine:ijn K ;                !^N^
 +Pl+Ela:ijs K ;                 !^N^
 +Pl+Com:ij K ;                 !^N^
 +Attr: K ;					!^N^

LEXICON TUVSÁNAT				!^N^
 +Sg+Nom:t K ;          !^N^
 +Sg+Gen:tta K ;         !^N^
 +Sg+Acc:ttav K ;            !^N^
 +Sg+Ill:ddaj K ;   !^N^
 +Sg+Ine:ttan K ;           !^N^
 +Sg+Ela:ttas K ;            !^N^
 +Sg+Com:ttajn K ;            !^N^
 +Ess:ddan K ;                  !^N^
 +Pl+Nom:tta K ;                 !^N^
 +Pl+Gen:ttaj K ;              !^N^
 +Pl+Acc:ttajt K ;              !^N^
 +Pl+Ill:ttajda K ;              !^N^
 +Pl+Ine:ttajn K ;                !^N^
 +Pl+Ela:ttajs K ;                 !^N^
 +Pl+Com:ttaj K ;                 !^N^
 +Attr:t K ;					!^N^
 
! Collection numbers
! ---------------

LEXICON VIDÁS           !^N^
 +Sg+Gen:a K ;          !^N^
 +Sg+Acc:av K ;             !^N^    
 +Sg+Ill:ij K ;                 !^N^    
 +Sg+Ine:in K ;                 !^N^
 +Sg+Ela:is K ;                 !^N^
 +Sg+Com:ijn K ;                    !^N^
 +Ess:in K ;                    !^N^
 +Pl+Nom:a K ;                  !^N^
 +Pl+Gen:ij K ;                 !^N^
 +Pl+Acc:ijt K ;                !^N^
 +Pl+Ill:ijda K ;                   !^N^
 +Pl+Ine:ijn K ;                        !^N^
 +Pl+Ela:ijs K ;                        !^N^
 +Pl+Com:ij K ;                         !^N^
 +Sg+Nom: PxC ;     !^N^
 +Sg+Gen: PxC ;     !^N^
 +Sg+Acc: PxC ;     !^N^
 +Sg+Ill:as PxCs ;      !^N^
 +Sg+Ine:in PxCs ;      !^N^
 +Sg+Ela:ist PxCs ;     !^N^
 +Sg+Com:ijn PxCs;      !^N^
 +Pl+Gen:ij PxCs ;      !^N^
 +Pl+Acc:ijd PxCs ;     !^N^
 +Pl+Ill:ijdas PxC ;        !^N^
 +Pl+Ine:ijn PxCs ;     !^N^
 +Pl+Ela:ijst PxCs ;        !^N^
 +Pl+Com:ij PxCs ;      !^N^


LEXICON VIDÁSLÅGENAN                        !^N^
 +Sg+Gen:a#lågenan K ;      !^N^
 +Sg+Acc:av#lågenan K ; !^N^
 +Sg+Ill:ij#lågenan K ;       !^N^
 +Sg+Ine:in#lågenan K ; !^N^
 +Sg+Ela:is#lågenan K ; !^N^
 +Sg+Com:ijn#lågenan K ; !^N^
 +Sg+Par:it#lågenan K ; !^N^
 +Pl+Nom:a#lågenan K ;                      !^N^
 +Pl+Gen:ij#lågenan K ;             !^N^
 +Pl+Acc:ijt#lågenan K ;                !^N^
 +Pl+Ill:ijda#lågenan K ;               !^N^
 +Pl+Ine:ijn#lågenan K ;                    !^N^
 +Pl+Ela:ijs#lågenan K ;                        !^N^
 +Pl+Com:ij#lågenan K ;                     !^N^
 
LEXICON TJUODAGIS                       !^N^
 +Sg+Nom: K ;                          !^N^
 +Sg+Ill:saj K ;                                       !^N^
 +Sg+Gen:á K ;                          !^N^
 +Sg+Acc:áv K ;                         !^N^
 +Sg+Ine:án K ;                         !^N^
 +Sg+Ela:ás K ;                                     !^N^
 +Pl+Nom:á K ;                                  !^N^
 +Pl+Gen:áj K ;                                         !^N^
 +Pl+Acc:ájt K ;                            !^N^
 +Pl+Ill:ájda K ;                       !^N^
 +Pl+Ine:ájn K ;                                    !^N^
 +Pl+Ela:ájs K ;                                !^N^
 +Pl+Com:áj K ;                                     !^N^
 +Sg+Com:ájn K ;                            !^N^
 +Sg+Nom:á PxV ;        !^N^
 +Sg+Ill:sas PxCi ;      !^N^
 +Sg+Gen:á PxV ;        !^N^
 +Sg+Acc:á PxV ;        !^N^
 +Sg+Ine:án PxC ;       !^N^
 +Sg+Ela:ást PxC ;      !^N^
 +Pl+Nom:á PxNomPlV ;       !^N^
 +Pl+Gen:áj PxC ;       !^N^
 +Pl+Acc:ájd PxC ;      !^N^
 +Pl+Ill:ájdis PxCs ;       !^N^
 +Pl+Ine:ájn PxC ;      !^N^
 +Pl+Ela:ájst PxC ;     !^N^
 +Pl+Com:áj PxC ;       !^N^
 +Sg+Com:ájn PxC ;        !^N^



! Imprecise numbers
! -----------------

LEXICON num-imprecise				
galla+Num+Sg+Nom:galla K ;
galli+Num:galli gallicase ;
galla+Num+Sg+Nom:galla K ;
galla+Num+Sg+Ill:galláj K ;
galla+Num:gall nubbecase ;
máŋŋga+Num+Sg+Nom:máŋŋga K ;
máŋŋga+Num+Sg+Acc:máŋŋga K ;
máŋŋga+Num+Sg+Ill:máŋŋgaj K ;
máŋŋga+Num:máŋga máŋŋgacase ;
moadda+Num+Sg+Nom:moadda K ;
máŋŋga+Num+Sg+Com:máŋgajn K ;
moadda+Num+Sg+Com:måttijn K ;
moadda+Num+Sg+Ill:moaddaj K ;
moadda+Num:moatte máŋŋgacase ;
moadda+Num+Sg+Acc:moadda K ;
ållo+Num+Sg+Nom:ållo K ;
ållo+Num+Sg+Ill:ålluj K ;
ållo+Num+Sg+Com:ålojn K ;
ållo+Num:ålo máŋŋgacase ;
biel#nup+Num+Sg+Nom:biel#nup # ;				
biel#nup+Num+Sg+Gen:biel#nup # ;				
biel#nup+Num+Sg+Acc:biel#nup # ;				
biel#nup#+Num+Sg+Attr:biel#nup# SASJ ;				
biel#goalmát+A+Ord:biel#goalmád3 VUOSTASJ ;        
biel#nælját+A+Ord:biel#næljád3 VUOSTASJ ;        
biel#vidát+A+Ord:biel#vidád3 VUOSTASJ ;        
biel#gudát+A+Ord:biel#gudád3 VUOSTASJ ;        
biel#giehttjit+A+Ord:biel#giehttjid3 VUOSTASJ ;         
biel#gávtsát+A+Ord:biel#gávtsád3 VUOSTASJ ;       
biel#avtsát+A+Ord:biel#avtsád3 VUOSTASJ ;        
biel#lågåt+A+Ord:biel#lågåd3 VUOSTASJ ;			 
biel#goalmát+A+Ord+Attr:biel#goalmád3 SASJ ;        
biel#nælját+A+Ord+Attr:biel#næljád3 SASJ ;        
biel#vidát+A+Ord+Attr:biel#vidád3 SASJ ;        
biel#gudát+A+Ord+Attr:biel#gudád3 SASJ ;        
biel#giehttjit+A+Ord+Attr:biel#giehttjid3 SASJ ;         
biel#gávtsát+A+Ord+Attr:biel#gávtsád3 SASJ ;       
biel#avtsát+A+Ord+Attr:biel#avtsád3 SASJ ;        
biel#lågåt+A+Ord+Attr:biel#lågåd3 SASJ ;			 


!LEXICON num-imprecise-distribution		!^N^
!NUPPELOHKAIGENERATOR ;  ! máŋga#nuppe#lohkái, máŋga !^N^
!LOGIF ;        ! máŋga#logi, máŋga#lot		  !^N^
!CUODI ;        ! máŋga#čuođi			  !^N^
!NL ;           ! máŋga#nuppe#lohkái#duhát		  !^N^
!O-NL ;         ! máŋga#nuppe#lohkái#duhát		  !^N^
!O-LOGIF ;      ! máŋga#logi#duhát			  !^N^
!JUSTTUVSÁN ;    ! máŋga#duhát			  !^N^
!O-CUODI ;      ! máŋga#duhát			  !^N^

LEXICON máŋŋgacase           !^N^
 +Sg+Gen: K ;        !^N^
 +Sg+Acc:v K ;      !^N^
 +Sg+Ela:s K ;      !^N^
 +Sg+Ine:n K ;      !^N^
 +Sg+Part:t K ;     !^N^





! Arabic numerals
! ---------------


LEXICON NUM-PREFIXES				!^C^
§	ARABIC ;      ! §24			!^C^
§§	ARABIC ;     ! §§24			!^C^
§% 	ARABIC ;    ! § 24			!^C^
§§% 	ARABIC ;   ! §§ 24			!^C^
%-	ARABIC ;     ! -24			!^C^
%‒	ARABIC ;     ! -24 U+2012 Figure line	!^C^
%–	ARABIC ;     ! -24 U+2013 en dash	!^C^
%—	ARABIC ;     ! -24 U+2014 em dash	!^C^
%―	ARABIC ;     ! -24 U+2015 horizontal l	!^C^
%+	ARABIC ;     ! +24			!^C^
%-% 	ARABIC ;	! - 24			!^C^
%‒% 	ARABIC ;	! - 24 2012		!^C^
%–% 	ARABIC ;	! - 24 2013	 	!^C^
%—% 	ARABIC ;	! - 24 2014		!^C^
%―% 	ARABIC ;	! - 24 2015 		!^C^
%+% 	ARABIC ;	! + 24			!^C^
%*	ARABIC ;     ! *24			!^C^
%$	ARABIC ;     ! $24  ! And the Euro?	!^C^
%<	ARABIC ;     ! <24			!^C^
%>	ARABIC ;     ! >24			!^C^
s%.	ARABIC ;     ! s.24			!^C^
	


LEXICON ARABIC		!^C^			
1 ARABICLOOP ;			!^C^		
2 ARABICLOOP ;				!^C^	
3 ARABICLOOP ;					!^C^
4 ARABICLOOP ;					!^C^
5 ARABICLOOP ;					!^C^
6 ARABICLOOP ;					!^C^
7 ARABICLOOP ;					!^C^
8 ARABICLOOP ;					!^C^
9 ARABICLOOP ;					!^C^
%0 ARABICLOOP ;					!^C^

LEXICON ARABICLOOP				!^C^
ARABIC ;					!^C^
%-	ARABIC ;	      ! 1-2    multipart numbers	!^C^
%.%- 	ARABIC ;	      ! 1.-2   multipart numbers	!^C^
%-% 	ARABIC ;	      ! 1- 2   multipart numbers	!^C^
% %-	ARABIC ;	      ! 1 -2   multipart numbers	!^C^
% %-% 	ARABIC ;	      ! 1 - 2  multipart numbers	!^C^
% %-%-%   ARABIC ;	      ! 1 -- 2 multipart numbers	!^C^
% %-      ARABICDELIMITER ;   ! 1 -   multipart numbers		!^C^
% %-%-	  ARABICDELIMITER ;   ! 1 --  multipart numbers		!^C^
%. ARABIC ;							!^C^
%, ARABIC ;							!^C^
%: ARABIC ;							!^C^
%  ARABIC ;	   ! Allowing for "23 500". !^C^			
   ARABIC ;	   ! Allowing for "23 500" w/nbsp (initial char is alt-space)	!^C^
%/ ARABIC ;	   ! 24/12			!^C^		
!%' ARABIC ;	   ! Since ' denotes cons.grad. This is not optimal either. 
!+Num:%- R ; !^C^	   ! Number compounds
+Num: R ; !^C^	   ! Hyphen removed bec. of "123- ja 124-viessu" !^C^
+A+Ord:%. # ;							 !^C^
,%-+Num:,%- # ;		! 10,-					 !^C^
%%+Num+Nom:%% # ;	! 50%. It is wrong, but 75 % of us write "75%".		!^C^
% %%+Num+Nom:% %% # ;	! We must analyse the correct ones as well... 50 % etc.	!^C^
+Num: ARABICDELIMITER ; ! list of number-case delimiters			!^C^


LEXICON ARABICDELIMITER								!^C^
  ARABICCASE ;      ! This is the 1984s case.					!^C^
 :%: ARABICCASE ;    ! This is the 1984:s case.					!^C^
 :%' ARABICCASE ;    ! This is the 1984's case.					!^C^
 :%- ARABICCASE ;    ! This is the 1984-s case.					!^C^
                     ! So, what does the lg council prefer?			

LEXICON ARABICCASE								!^C^
+Nom:f # ;		! s. 123f.						!^C^
+Nom:ff # ;		! s. 123ff.						!^C^
+Nom:b # ;		! § 123b						!^C^
+Nom:c # ;		! § 123c						!^C^
+Nom:d # ;		! § 123d						!^C^
+Nom:e # ;		! § 123e						!^C^
! # ;		! 123 is now 123+Num as well as 123+Num+Nom etc. Out again!	!^C^
+Nom: # ;									!^C^
+Gen: # ;									!^C^
!+Acc: # ;									!^C^
+Gen:a # ;	! But here 123a 123+Num+Gen					!^C^
+Acc:av # ;									!^C^
+Ill:j # ;									!^C^
+Ill:aj # ;									!^C^
+Ill:áj # ;	! Testing over the weekend.					!^C^
+Ela:s # ;									!^C^
+Ela:is # ;									!^C^
+Ela:as # ;									!^C^
+Ine:s # ;									!^C^
+Ine:is # ;									!^C^
+Ine:as # ;									!^C^
+Com:jn # ;									!^C^
+Com:ajn # ;									!^C^
+Ess:n # ;									!^C^
+Ess:an # ;									!^C^
! No plural numbers, hmm.


! The Roman numerals !
! ------------------ !

LEXICON ROMAN						!^C^
 ROM-THOUSAND ;						!^C^
 ROM-HUNDRED ;						!^C^
 ROM-TEN ;						!^C^
 ROM-ONE ;						!^C^

LEXICON ROM-THOUSAND              !^C^
 M ROM-THOUSAND-TAG ;						!^C^
 MM ROM-THOUSAND-TAG ;					!^C^
 MMM ROM-THOUSAND-TAG ;					!^C^
 MMMM ROM-THOUSAND-TAG ;					!^C^
 MMMMM ROM-THOUSAND-TAG ;					!^C^

LEXICON ROM-THOUSAND-TAG					!^C^
  ROMNUMTAG ;						!^C^
 ROM-HUNDRED ;					!^C^
 ROM-SPLIT	;				!^C^

LEXICON ROM-HUNDRED					!^C^
 C ROM-HUNDRED-TAG ;						!^C^
 CC ROM-HUNDRED-TAG ;						!^C^
 CCC ROM-HUNDRED-TAG ;						!^C^
 CD ROM-HUNDRED-TAG ;						!^C^
 D ROM-HUNDRED-TAG ;						!^C^
 DC ROM-HUNDRED-TAG ;						!^C^
 DCC ROM-HUNDRED-TAG ;						!^C^
 DCCC ROM-HUNDRED-TAG ;						!^C^
 CM ROM-HUNDRED-TAG ;						!^C^

LEXICON ROM-HUNDRED-TAG					!^C^
  ROMNUMTAG ;						!^C^
 ROM-TEN ;					!^C^
 ROM-SPLIT ;					!^C^

LEXICON ROM-TEN						!^C^
 X ROM-TEN-TAG ;						!^C^
 XX ROM-TEN-TAG ;						!^C^
 XXX ROM-TEN-TAG ;						!^C^
 XL ROM-TEN-TAG ;						!^C^
 L ROM-TEN-TAG ;						!^C^
 LX ROM-TEN-TAG ;						!^C^
 LXX ROM-TEN-TAG ;						!^C^
 LXXX ROM-TEN-TAG ;						!^C^
 XC ROM-TEN-TAG ;						!^C^

LEXICON ROM-TEN-TAG					!^C^
  ROMNUMTAG ;						!^C^
 ROM-ONE ;						!^C^
 ROM-SPLIT ;						!^C^

LEXICON ROM-ONE						!^C^
 I ROM-ONE-TAG ;						!^C^
 II ROM-ONE-TAG ;						!^C^
 III ROM-ONE-TAG ;						!^C^
 IV  ROM-ONE-TAG ;						!^C^
 V ROM-ONE-TAG ;						!^C^
 VI ROM-ONE-TAG ;						!^C^
 VII ROM-ONE-TAG ;						!^C^
 VIII ROM-ONE-TAG ;						!^C^
 IX ROM-ONE-TAG ;						!^C^

LEXICON ROM-ONE-TAG					!^C^
  ROMNUMTAG ;						!^C^
! +N: ROMNUMTAG ;						!^C^ !The Olav viđeš fix (Roman V now gets A)
 ROM-SPLIT ;						!^C^

! Here, we split the Roman numerals, in order to account for cases like "Kapihtal II-IV".
! We may send this first part directly to ROM-TAG below (and get the +Num tag), or we may
! send them through a second loop, identical to the first one, but marked wit "2" (the lexica
! are called 2ROMAN, etc. We do this instead of making a loop, since we do not want cases
! like "II-IV-VI-VII-IX". If that should turn out to be a good idea, a loop would do the 
! trick.

LEXICON ROM-SPLIT					!^C^
 :%- 2ROMAN ; ! II-VI, etc.				!^C^


! Here goes loop 2.

LEXICON 2ROMAN						!^C^
 2ROM-THOUSAND ;						!^C^
 2ROM-HUNDRED ;						!^C^
 2ROM-TEN ;						!^C^
 2ROM-ONE ;						!^C^

LEXICON 2ROM-THOUSAND              !^C^
 M 2ROM-THOUSAND-TAG ;						!^C^
 MM 2ROM-THOUSAND-TAG ;					!^C^
 MMM 2ROM-THOUSAND-TAG ;					!^C^
 MMMM 2ROM-THOUSAND-TAG ;					!^C^
 MMMMM 2ROM-THOUSAND-TAG ;					!^C^

LEXICON 2ROM-THOUSAND-TAG					!^C^
  ROMNUMTAG ;						!^C^
 2ROM-HUNDRED ;					!^C^

LEXICON 2ROM-HUNDRED					!^C^
 C 2ROM-HUNDRED-TAG ;						!^C^
 CC 2ROM-HUNDRED-TAG ;						!^C^
 CCC 2ROM-HUNDRED-TAG ;						!^C^
 CD 2ROM-HUNDRED-TAG ;						!^C^
 D 2ROM-HUNDRED-TAG ;						!^C^
 DC 2ROM-HUNDRED-TAG ;						!^C^
 DCC 2ROM-HUNDRED-TAG ;						!^C^
 DCCC 2ROM-HUNDRED-TAG ;						!^C^
 CM 2ROM-HUNDRED-TAG ;						!^C^

LEXICON 2ROM-HUNDRED-TAG					!^C^
  ROMNUMTAG ;						!^C^
 2ROM-TEN ;					!^C^

LEXICON 2ROM-TEN						!^C^
 X 2ROM-TEN-TAG ;						!^C^
 XX 2ROM-TEN-TAG ;						!^C^
 XXX 2ROM-TEN-TAG ;						!^C^
 XL 2ROM-TEN-TAG ;						!^C^
 L 2ROM-TEN-TAG ;						!^C^
 LX 2ROM-TEN-TAG ;						!^C^
 LXX 2ROM-TEN-TAG ;						!^C^
 LXXX 2ROM-TEN-TAG ;						!^C^
 XC 2ROM-TEN-TAG ;						!^C^

LEXICON 2ROM-TEN-TAG					!^C^
  ROMNUMTAG ;					!^C^
 2ROM-ONE ;					!^C^

LEXICON 2ROM-ONE						!^C^
 I 2ROM-ONE-TAG ;						!^C^
 II 2ROM-ONE-TAG ;						!^C^
 III 2ROM-ONE-TAG ;						!^C^
 IV  2ROM-ONE-TAG ;						!^C^
 V 2ROM-ONE-TAG ;						!^C^
 VI 2ROM-ONE-TAG ;						!^C^
 VII 2ROM-ONE-TAG ;						!^C^
 VIII 2ROM-ONE-TAG ;						!^C^
 IX 2ROM-ONE-TAG ;						!^C^

LEXICON 2ROM-ONE-TAG						!^C^
  ROMNUMTAG ;						!^C^
 
LEXICON ROMNUMTAG 						!^C^
 +Num+Nom: # ;									!^C^
 +Num+Gen: # ;									!^C^
! +Num+Acc: # ;									!^C^
 +A+Ord:%. # ;									!^C^ !III. as ordinal.
 +A+Ord: # ;								!^C^ !Looking for trouble...
 +Num:%: ROMNUMTAGOBL ;						!^C^
 +Num:%' ROMNUMTAGOBL ;						!^C^
 +Num:%- ROMNUMTAGOBL ;						!^C^
 +Num:%´ ROMNUMTAGOBL ;						!^C^
 +Num:   ROMNUMTAGOBL ;						!^C^

LEXICON ROMNUMTAGOBL 						!^C^
+Gen:a # ;	! But here 123a 123+Num+Gen					!^C^
+Acc:av # ;									!^C^
+Ill:j # ;									!^C^
+Ill:ij # ;									!^C^
+Ill:áj # ;					!^C^
+Ela:s # ;									!^C^
+Ine:s # ;									!^C^
+Com:jn # ;									!^C^
+Ess:n # ;									!^C^




! A final section with some isolated numeral expresssions !
! ------------------------------------------------------- !

      
LEXICON ISOLATED-NUMEXP					!^C^
½+Num:½ # ;						!^C^
¹+Num:¹ # ;						!^C^
²+Num:² # ;						!^C^
³+Num:³ # ;						!^C^
¼+Num:¼ # ;						!^C^



