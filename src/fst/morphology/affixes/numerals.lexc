! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@uit.no or feedback@divvun.no

! ############################## !
!This file contains the numerals !
! ############################## !




! Numerals
! ========

! The initial lexica
! ------------------



! Case forms of the basic digits
! ------------------------------

! 1-10

LEXICON AKTA
             :a%>   AKTA-NIE ;
             :a%>Q1 AKTA-OBL ;
+Der/akti+Adv:i%>   K        ;

LEXICON AKTA-VOWELHARMONY
             :å%>   AKTA-NIE ;
             :å%>Q1 AKTA-OBL ;
+Der/akti+Adv:i%>   K        ;

LEXICON AKTA-NIE
 +Sg+Nom:  K ;
 +Sg+Ill:j K ;
    +Ess:n K ;

LEXICON AKTA-OBL
      +Sg+Gen:    K                ;
   +Cmp/SgGen:%>  NUMERALCOMPOUNDS ;
      +Sg+Acc:v   K                ;
      +Sg+Ine:n   K                ;
      +Sg+Ela:s   K                ;
      +Sg+Com:jn  K                ;
      +Sg+Par:t   K                ;

 +Sg+Ill+Attr:    K                ;
 +Sg+Ine+Attr:n   K                ;
 +Sg+Ela+Attr:t   K                ;
 +Sg+Com+Attr:jn  K                ;

      +Pl+Nom:    K                ;
      +Pl+Gen:j   K                ;
      +Pl+Acc:jt  K                ;
      +Pl+Ill:jda K                ;
      +Pl+Ine:jn  K                ;
      +Pl+Ela:js  K                ;
      +Pl+Com:j   K                ;
      
 
LEXICON ÅVTÅ
      +Sg+Gen:%>    K                ;
 +Sg+Ill+Attr:%>    K                ;
   +Cmp/SgGen:%>    NUMERALCOMPOUNDS ;
      +Sg+Acc:%>v   K                ;
      +Sg+Ine:%>n   K                ;
      +Sg+Ela:%>s   K                ;
      +Sg+Com:%>jn  K                ;
      +Sg+Par:%>t   K                ;
      +Pl+Nom:%>    K                ;
      +Pl+Gen:%>j   K                ;
      +Pl+Acc:%>jt  K                ;
      +Pl+Ill:%>jda K                ;
      +Pl+Ine:%>jn  K                ;
      +Pl+Ela:%>js  K                ;
      +Pl+Com:%>j   K                ;

LEXICON ẈG-LÅK-ÅVTÅ                 
 +Sg+Gen:%> K ; 
 +Sg+Ill+Attr:%> K ;                         
 +Cmp/SgGen:%> NUMERALCOMPOUNDS ;
 +Sg+Acc:%>v K ;            
 +Sg+Ine:%>n K ;            
 +Sg+Ela:%>s K ;            
! +Sg+Com:%>jn K ;           
 +Sg+Par:%>t K ;            

LEXICON ẈG-LÅK-AKTA                
 :a%>   ẈG-LÅK-AKTA-NIE ;               
 :a%>Q1 ẈG-LÅK-AKTA-OBL ;              
+Der/akti+Adv:i%>   K        ;

LEXICON ẈG-LÅK-AKTA-NIE                
 +Sg+Ill:j K ;              
! +Ess:n K ;                 

LEXICON ẈG-LÅK-AKTA-OBL                
 +Sg+Gen:   K ;                           
 +Cmp/SgGen:%> NUMERALCOMPOUNDS ;
 +Sg+Acc:v  K ;              
 +Sg+Ine:n  K ;              
 +Sg+Ela:s  K ;              
! +Sg+Com:jn K ;             
 +Sg+Par:t  K ;
 
 +Sg+Ill+Attr:       K ;   
 +Sg+Ine+Attr:n      K ;  
 +Sg+Ela+Attr:t      K ;  
! +Sg+Com+Attr:jn     K ;  
                  
! +Pl+Nom:    K ;                
! +Pl+Gen:j   K ;               
! +Pl+Acc:jt  K ;              
! +Pl+Ill:jda K ;             
! +Pl+Ine:jn  K ;              
! +Pl+Ela:js  K ;              
! +Pl+Com:j   K ;               

LEXICON GUOKTA 
                +Sg+Nom:a%>   K       ; !pred&attr   
+Use/Marg+Sg+Nom+Use/NG:e%>   K       ; ! not sure about the e-ending, dont think its standardized             
         +Sg+Acc+Use/NG:a%>   K       ; !dialect differences in whether it's guokta or guovtev in accusative
                +Sg+Ill:a%>j  K       ; !pred&attr     
                   +Ess:a%>n  K       ;  
   +Use/Marg+Ess+Use/NG:e%>n  K       ; ! not sure about the e-ending, dont think its standardized             
                       :e%>Q1 GUOVTE- ; !other cases (in weak grade)
                       :i%>Q1 GUOVTI- ; !plural     
          +Der/akti+Adv:i%>   K;
!+Sg+Gen:e%>Q1 K ; ! redundant, see GUOVTE-         
!+Sg+Gen:e%>Q1 NUMERALCOMPOUNDS ;  ! redundant, see GUOVTE-          
!  GUOKTA- ;                 

LEXICON GUOVTE- 
        +Sg+Acc:v  K                ;    
        +Sg+Gen:   K                ;   
       ! +Sg+Gen:%- #                ; ! outcommented because what is this?                       
        +Cmp/SgGen:   NUMERALCOMPOUNDS ; 
        +Sg+Ine:n  K                ; !pred&attr            
        +Sg+Ela:s  K                ; !pred&attr             
        +Sg+Par:t  K                ; !pred&attr  
        
   +Sg+Ill+Attr:   K                ; !only attr 
   +Sg+Ela+Attr:t  K                ; !only attr 
   +Sg+Ine+Attr:n  K                ; !only attr 
                    
        +Pl+Nom:   K                ;              
 
LEXICON GUOVTI-   
      +Sg+Com:jn  K ;
 +Sg+Com+Attr:jn  K ;
          
 +Pl+Gen:j   K ;             
 +Pl+Acc:jt  K ;            
 +Pl+Ill:jda K ;           
 +Pl+Ine:jn  K ;            
 +Pl+Ela:js  K ;            
 +Pl+Com:j   K ;             
         
LEXICON ẈG-LÅK-GUOKTA                
        ẈG-LÅK-GUOKTA- ; 
              :e%>Q1 ẈG-LÅK-GUOVTE- ;
 +Der/akti+Adv:i%>   K;
 !:i%>Q1 ẈG-LÅK-GUOVTI- ;               

!+Sg+Gen:a%>Q1 K ; !redunant         
!+Sg+Gen:a%>Q1 NUMERALCOMPOUNDS ; !redundant                

LEXICON ẈG-LÅK-GUOKTA- 
+Sg+Acc+Use/NG:a%>   K ;             
!          +Ess:a%>n  K ;   
!   +Ess+Use/NG:e%>n  K ; ! not sure about the e-ending, dont think its standardized

LEXICON ẈG-LÅK-GUOVTE- 
        +Sg+Acc:v     K ;           
        +Sg+Gen:      K ; 
        +Sg+Ill+Attr: K ;                       
        +Cmp/SgGen:      NUMERALCOMPOUNDS ;   
        +Sg+Ine:n     K ;             
        +Sg+Ela:s     K ;             
        +Sg+Par:t     K ;    
        
!        +Sg+Gen:%- K ; ! ???            
 
!LEXICON ẈG-LÅK-GUOVTI-   
! +Sg+Com:jn K ;                        
                
LEXICON GÅLMMÅ           
              :å%> GÅLMMÅ-NAIE ; !strong grade cases   
              :å%>Q1 GÅLMMÅ-OBL ; !weak grade cases  
 +Der/akti+Adv:i%>   K;
                        
LEXICON GÅLMMÅ-NAIE      
        +Sg+Nom:  K ;            
 +Sg+Acc+Use/NG:  K ;            
        +Sg+Ill:j K ;           
           +Ess:n K ;              
                         
LEXICON GÅLMMÅ-OBL 
        +Sg+Acc:v      K ;                 
        +Sg+Gen:       K ;                     
        +Cmp/SgGen:       NUMERALCOMPOUNDS ; 
        +Sg+Ine:n      K ;           
        +Sg+Ela:s      K ;           
        +Sg+Com:jn     K ;          
        +Sg+Par:t      K ;  
  
   +Sg+Ill+Attr:       K ;  
   +Sg+Ine+Attr:n      K ;  
   +Sg+Ela+Attr:t      K ;  
   +Sg+Com+Attr:jn     K ;  
                 
        +Pl+Nom:       K ;            
        +Pl+Gen:j      K ;           
        +Pl+Acc:jt     K ;          
        +Pl+Ill:jda    K ;         
        +Pl+Ine:jn     K ;          
        +Pl+Ela:js     K ;          
        +Pl+Com:j      K ;           

LEXICON ẈG-LÅK-GÅLMMÅ
              :å%> ẈG-LÅK-GÅLMMÅ-NAIE ;      
              :å%>Q1 ẈG-LÅK-GÅLMMÅ-OBL ;     
 +Der/akti+Adv:i%>   K ;
                        
LEXICON ẈG-LÅK-GÅLMMÅ-NAIE      
        +Sg+Ill:j K ;           
!           +Ess:n K ; 
 +Sg+Acc+Use/NG:  K ;              
                         
LEXICON ẈG-LÅK-GÅLMMÅ-OBL 
        +Sg+Acc:v  K ;                  
        +Sg+Gen:   K ;                      
        +Cmp/SgGen:   NUMERALCOMPOUNDS ; 
        +Sg+Ine:n  K ;           
        +Sg+Ela:s  K ;           
!        +Sg+Com:jn K ;          
        +Sg+Par:t  K ; 
        
   +Sg+Ill+Attr:   K ; 
   +Sg+Ine+Attr:n  K ;  
   +Sg+Ela+Attr:t  K ;  
   +Sg+Com+Attr:jn K ;             

LEXICON GIETJAV
              :ja%>    GIETJAV-IE ;          
              :ja%>Q1  GIETJAV-OBL ; !weak grade cases     
 +Der/akti+Adv:tji%>   K ;

LEXICON GIETJAV-IE  
  +Sg+Ill:j K ;        
    +Ess:n K ;
 +Sg+Nom+Use/NG:  K ; !giehtja                

LEXICON GIETJAV-OBL         
 +Sg+Nom:v  K ; !in Spiik, OK and AK giehtja nom
 +Sg+Nom+Use/NG:   K ; ! ??? gietja                        
        +Sg+Gen:   K ;                             
        +Cmp/SgGen:   NUMERALCOMPOUNDS ;
        +Sg+Acc:v  K ;              
        +Sg+Ine:n  K ;              
        +Sg+Ela:s  K ;              
        +Sg+Com:jn K ;             
        +Sg+Par:t  K ; 
        
   +Sg+Ill+Attr:   K ; 
   +Sg+Ine+Attr:n  K ;  
   +Sg+Ela+Attr:t  K ;  
   +Sg+Com+Attr:jn K ;    
                     
        +Pl+Nom: K ;               
        +Pl+Gen:j K ;              
        +Pl+Acc:jt K ;             
        +Pl+Ill:jda K ;            
        +Pl+Ine:jn K ;             
        +Pl+Ela:js K ;             
        +Pl+Com:j K ;              
 
LEXICON ẈG-LÅK-GIETJAV
              :ja%> ẈG-LÅK-GIETJAV-IE ;          
              :ja%>Q1 ẈG-LÅK-GIETJAV-OBL ;       
 +Der/akti+Adv:tji%>   K ;
                            
LEXICON ẈG-LÅK-GIETJAV-IE  
 +Sg+Ill:j K ;        
! +Ess:n K ;
                        
LEXICON ẈG-LÅK-GIETJAV-OBL         
! +Sg+Nom+Use/NG:v  K ;
        +Sg+Gen:   K ;                             
        +Cmp/SgGen:   NUMERALCOMPOUNDS ;
        +Sg+Acc:v  K ;              
        +Sg+Ine:n  K ;              
        +Sg+Ela:s  K ;              
!        +Sg+Com:jn K ;             
        +Sg+Par:t  K ;              

   +Sg+Ill+Attr:   K ; 
   +Sg+Ine+Attr:n  K ;  
   +Sg+Ela+Attr:t  K ;  
!   +Sg+Com+Attr:jn K ;    

LEXICON GÁKTSA              
                     GÁKTSA- ; !gáktsa   
              :e%>Q1 GÁVTSE- ; !gávtse          
              :i%>Q1 GÁVTSI- ; !gávtsi                   
 +Der/akti+Adv:i%>   K       ;
 
LEXICON GÁKTSA-             
          +Sg+Nom:%>a  K ;   
   +Sg+Nom+Use/NG:%>e  K ; ! not sure about the e-ending, dont think its standardized                        
   +Sg+Acc+Use/NG:%>a  K ;              
          +Sg+Ill:á%>j K ;             
             +Ess:a%>n K ;   
      +Ess+Use/NG:e%>n K ; ! not sure about the e-ending, dont think its standardized 
                               
LEXICON GÁVTSE-   
        +Sg+Acc:v  K ; !in OK, but standardized?                      
        +Sg+Gen:   K ;                            
        +Cmp/SgGen:   NUMERALCOMPOUNDS ;
        +Sg+Ine:n  K ;             
        +Sg+Ela:s  K ;             
        +Sg+Par:t  K ;
        
   +Sg+Ill+Attr:   K ;  
   +Sg+Ine+Attr:n  K ;  
   +Sg+Ela+Attr:t  K ;     

        +Pl+Nom:   K ;              

LEXICON GÁVTSI-        
      +Sg+Com:jn  K ;
 +Sg+Com+Attr:jn  K ;   

      +Pl+Gen:j   K ;             
      +Pl+Acc:jt  K ;            
      +Pl+Ill:jda K ;           
      +Pl+Ine:jn  K ;            
      +Pl+Ela:js  K ;            
      +Pl+Com:j   K ;             

LEXICON ẈG-LÅK-GÁKTSA
                     ẈG-LÅK-GÁKTSA- ; 
              :e%>Q1 ẈG-LÅK-GÁVTSE- ; 
 +Der/akti+Adv:i%>   K       ;
  !:i%>Q1 ẈG-LÅK-GÁVTSI- ;                       

LEXICON ẈG-LÅK-GÁKTSA-             
!        +Sg+Nom:%>a K ;   
! +Sg+Nom+Use/NG:%>e K ;                         
 +Sg+Acc+Use/NG:%>a K ;              
        +Sg+Ill:á%>j K ;             
!           +Ess:a%>n K ;   
!    +Ess+Use/NG:e%>n K ;   
                            
LEXICON ẈG-LÅK-GÁVTSE-   
       +Sg+Acc:v  K ;                       
       +Sg+Gen:   K ;                         
       +Cmp/SgGen:   NUMERALCOMPOUNDS ;
       +Sg+Ine:n  K ;             
       +Sg+Ela:s  K ;             
       +Sg+Par:t  K ; 
 
  +Sg+Ill+Attr:   K ;  
  +Sg+Ine+Attr:n  K ;  
  +Sg+Ela+Attr:t  K ;              

!LEXICON ẈG-LÅK-GÁVTSI-        
!      +Sg+Com:jn  K ;
! +Sg+Com+Attr:jn  K ;                  

LEXICON AKTSE
                     AKTS-  ; 
              :e%>Q1 AVTSE- ;           
              :i%>Q1 AVTSI- ;                       
 +Der/akti+Adv:i%>   K      ;
 
LEXICON AKTS-           
         +Sg+Nom:e%>  K ;          
 +Sg+Acc+Use/NG:e%>  K ;          
         +Sg+Ill:á%>j K ;         
            +Ess:e%>n  K ; 

LEXICON AVTSE- 
        +Sg+Acc:v  K ; !in OK, but standardized?                 
        +Sg+Gen:   K ;                   
        +Cmp/SgGen:   NUMERALCOMPOUNDS ;
        +Sg+Ine:n  K ;         
        +Sg+Ela:s  K ;         
        +Sg+Par:t  K ; 
  
   +Sg+Ill+Attr:   K ;  
   +Sg+Ine+Attr:n  K ;  
   +Sg+Ela+Attr:t  K ;        
                
        +Pl+Nom:   K ;          

LEXICON AVTSI-         
      +Sg+Com:jn  K ;
 +Sg+Com+Attr:jn  K ;   
         
      +Pl+Gen:j   K ;         
      +Pl+Acc:jt  K ;        
      +Pl+Ill:jda K ;       
      +Pl+Ine:jn  K ;        
      +Pl+Ela:js  K ;        
      +Pl+Com:j   K ;         

LEXICON ẈG-LÅK-AKTSE
                     ẈG-LÅK-AKTS- ;  
              :e%>Q1 ẈG-LÅK-AVTSE- ;           
              :i%>Q1 ẈG-LÅK-AVTSI- ;                      
 +Der/akti+Adv:i%>   K       ;
 
LEXICON ẈG-LÅK-AKTS-           
!        +Sg+Nom:e%>  K ;          
 +Sg+Acc+Use/NG:e%>  K ;          
        +Sg+Ill:á%>j K ;         
!           +Ess:e%>n K ;     

LEXICON ẈG-LÅK-AVTSE- 
        +Sg+Acc:v  K ;                    
        +Sg+Gen:   K ;                          
        +Cmp/SgGen:   NUMERALCOMPOUNDS ;
        +Sg+Ine:n  K ;         
        +Sg+Ela:s  K ;         
        +Sg+Par:t  K ;     
        
   +Sg+Ill+Attr:   K ;  
   +Sg+Ine+Attr:n  K ;  
   +Sg+Ela+Attr:t  K ;                
            
!        +Pl+Nom:   K ;          

LEXICON ẈG-LÅK-AVTSI-         
!      +Sg+Com:jn  K ;
! +Sg+Com+Attr:jn  K ;   
       
      +Pl+Gen:j   K ;         
      +Pl+Acc:jt  K ;        
      +Pl+Ill:jda K ;       
      +Pl+Ine:jn  K ;        
      +Pl+Ela:js  K ;        
!      +Pl+Com:j   K ;         

LEXICON LÅGEV
                      LÅHK- ; !låhk- 
               :e%>Q1 LÅGE- ; !låge-  
               :i%>Q1 LÅGI- ; !lågi-  
  +Der/akti+Adv:ki%>  K     ;
LEXICON LÅHK-   
 +Sg+Nom+Use/NG:e%>  K ;     
 +Sg+Ill:å%>j K ;    
    +Ess:e%>n K ;     
                 
LEXICON LÅGE-      
        +Sg+Nom:v K ; !in Spiik, but AK & gielaj hilá låhke   
        +Sg+Acc:v K ;    
        +Sg+Gen:  K ;                             
        +Cmp/SgGen:  NUMERALCOMPOUNDS ;
        +Sg+Ine:n K ;    
        +Sg+Ela:s K ;    
        +Sg+Par:t K ;  

   +Sg+Ill+Attr:   K ;  
   +Sg+Ine+Attr:n  K ;  
   +Sg+Ela+Attr:t  K ;  
  
        +Pl+Nom:   K ;     

LEXICON LÅGI-      
      +Sg+Com:jn  K ;
 +Sg+Com+Attr:jn  K ;    
 
      +Pl+Gen:j   K ;    
      +Pl+Acc:jt  K ;   
      +Pl+Ill:jda K ;  
      +Pl+Ine:jn  K ;   
      +Pl+Ela:js  K ;   
      +Pl+Com:j   K ;       
                   
!Old counting system. Removed from speller, use/-spell tagged in lexicon LÅGENAN
                   
LEXICON AKTALÅGENAN
 :%> AKTALÅGENAN-NIE ;  
 :%>Q1 AKTALÅGENAN-OBL ;      
                   
LEXICON AKTALÅGENAN-NIE    
 +Sg+Nom:#lågenan%> K ;     
 +Sg+Ill:j#lågenan%> K ;       
 +Ess:n#lågenan%> K ;        
                   
LEXICON AKTALÅGENAN-OBL    
 +Sg+Gen:#lågenan%> K ; 
 +Sg+Ill+Attr:#lågenan%> K ;           
 +Cmp/SgGen:#lågenan%> NUMERALCOMPOUNDS ;
 +Sg+Acc:v#lågenan%> K ; 
 +Sg+Ine:n#lågenan%> K ; 
 +Sg+Ela:s#lågenan%> K ; 
 +Sg+Com:jn#lågenan%> K ; 
 +Sg+Par:t#lågenan%> K ; 
 +Pl+Nom:#lågenan%> K ; 
 +Pl+Gen:j#lågenan%> K ; 
 +Pl+Acc:jt#lågenan%> K ; 
 +Pl+Ill:jda#lågenan%> K ; 
 +Pl+Ine:jn#lågenan%> K ; 
 +Pl+Ela:js#lågenan%> K ; 
 +Pl+Com:j#lågenan%> K ; 
 
LEXICON ÅVTÅLÅGENAN
 +Sg+Gen:%>#lågenan%> K ; 
 +Sg+Ill+Attr:%>#lågenan%> K ;                         
 +Cmp/SgGen:%>#lågenan%> NUMERALCOMPOUNDS ;
 +Sg+Acc:%>v#lågenan%> K ;             
 +Sg+Ine:%>n#lågenan%> K ;             
 +Sg+Ela:%>s#lågenan%> K ;             
 +Sg+Com:%>jn#lågenan%> K ;            
 +Sg+Par:%>t#lågenan%> K ;             
 +Pl+Nom:%>#lågenan%> K ;              
 +Pl+Gen:%>j#lågenan%> K ;             
 +Pl+Acc:%>jt#lågenan%> K ;            
 +Pl+Ill:%>jda#lågenan%> K ;           
 +Pl+Ine:%>jn#lågenan%> K ;            
 +Pl+Ela:%>js#lågenan%> K ;            
 +Pl+Com:%>j#lågenan%> K ;             

LEXICON GUOKTALÅGENAN
 GUOVTELÅGENAN ;               
 GUOKT-LÅGENAN ;                 

LEXICON GUOVTELÅGENAN     
 +Sg+Acc:e%>Q1v#lågenan%> K ;                           
 +Sg+Gen:e%>Q1#lågenan%> K ; 
 +Sg+Ill+Attr:e%>Q1#lågenan%> K ;                               
 +Cmp/SgGen:e%>Q1#lågenan%> NUMERALCOMPOUNDS ;
 +Sg+Ine:e%>Q1n#lågenan%> K ;               
 +Sg+Ela:e%>Q1s#lågenan%> K ;               
 +Sg+Com:i%>Q1jn#lågenan%> K ;              
 +Sg+Par:e%>Q1t#lågenan%> K ;               
 +Pl+Nom:e%>Q1#lågenan%> K ;                
 +Pl+Gen:i%>Q1j#lågenan%> K ;               
 +Pl+Acc:i%>Q1jt#lågenan%> K ;              
 +Pl+Ill:i%>Q1jda#lågenan%> K ;             
 +Pl+Ine:i%>Q1jn#lågenan%> K ;              
 +Pl+Ela:i%>Q1js#lågenan%> K ;              
 +Pl+Com:i%>Q1j#lågenan%> K ;               
                                      
LEXICON GUOKT-LÅGENAN                 
 +Sg+Nom:a%>#lågenan%> K ;    
 +Sg+Nom+Use/NG:e%>#lågenan%> K ;                            
 +Sg+Acc:a%>#lågenan%> K ;                
 +Ess:a%>n#lågenan%> K ;       
 +Ess:e%>n#lågenan%> K ;                             

LEXICON GÅLMMÅLÅGENAN                 
 :%> GÅLMMÅLÅGENAN-NAIE ;            
 :%>Q1 GÅLMMÅLÅGENAN-OBL ;           

LEXICON GÅLMMÅLÅGENAN-NAIE            
 +Sg+Nom:#lågenan%> K ;                 
 +Sg+Acc:#lågenan%> K ;                 
 +Sg+Ill:j#lågenan%> K ;                
 +Ess:n#lågenan%> K ;                   
                                      
LEXICON GÅLMMÅLÅGENAN-OBL  
 +Sg+Acc:v#lågenan%> K ;                           
 +Sg+Gen:#lågenan%> K ;
 +Sg+Ill+Attr:#lågenan%> K ;                                  
 +Cmp/SgGen:#lågenan%> NUMERALCOMPOUNDS ;
 +Sg+Ine:n#lågenan%> K ;                
 +Sg+Ela:s#lågenan%> K ;                
 +Sg+Com:jn#lågenan%> K ;               
 +Sg+Par:t#lågenan%> K ;                
 +Pl+Nom:#lågenan%> K ;                 
 +Pl+Gen:j#lågenan%> K ;                
 +Pl+Acc:jt#lågenan%> K ;               
 +Pl+Ill:jda#lågenan%> K ;              
 +Pl+Ine:jn#lågenan%> K ;               
 +Pl+Ela:js#lågenan%> K ;               
 +Pl+Com:j#lågenan%> K ;                

LEXICON GIETJAVLÅGENAN
 :%> GIETJAVLÅGENAN-IE ;             
 :%>Q1 GIETJAVLÅGENAN-OBL ;          

LEXICON GIETJAVLÅGENAN-IE  
 +Sg+Ill:j#lågenan%> K ;           
 +Ess:n#lågenan%> K ; 
 +Sg+Nom:#lågenan%> K ;                
                  
                                      
LEXICON GIETJAVLÅGENAN-OBL            
 +Sg+Nom:v#lågenan%> K ;   
 +Sg+Nom+Use/NG:#lågenan%> K ;                             
 +Sg+Gen:#lågenan%> K ;  
 +Sg+Ill+Attr:#lågenan%> K ;                                
 +Cmp/SgGen:#lågenan%> NUMERALCOMPOUNDS ;
 +Sg+Acc:v#lågenan%> K ;                
 +Sg+Ine:n#lågenan%> K ;                
 +Sg+Ela:s#lågenan%> K ;                
 +Sg+Com:jn#lågenan%> K ;               
 +Sg+Par:t#lågenan%> K ;                
 +Pl+Nom:#lågenan%> K ;                 
 +Pl+Gen:j#lågenan%> K ;                
 +Pl+Acc:jt#lågenan%> K ;               
 +Pl+Ill:jda#lågenan%> K ;              
 +Pl+Ine:jn#lågenan%> K ;               
 +Pl+Ela:js#lågenan%> K ;               
 +Pl+Com:j#lågenan%> K ;                

LEXICON GÁKTSALÅGENAN
 GÁVTSELÅGENAN ;               
 GÁKTS-LÅGENAN ;                 

LEXICON GÁVTSELÅGENAN    
 +Sg+Acc:e%>Q1v#lågenan%> K ;                            
 +Sg+Gen:e%>Q1#lågenan%> K ; 
 +Sg+Ill+Attr:e%>Q1#lågenan%> K ;                               
 +Cmp/SgGen:e%>Q1#lågenan%> NUMERALCOMPOUNDS ;
 +Sg+Ine:e%>Q1n#lågenan%> K ;               
 +Sg+Ela:e%>Q1s#lågenan%> K ;               
 +Sg+Com:i%>Q1jn#lågenan%> K ;              
 +Sg+Par:e%>Q1t#lågenan%> K ;               
 +Pl+Nom:e%>Q1#lågenan%> K ;                
 +Pl+Gen:i%>Q1j#lågenan%> K ;               
 +Pl+Acc:i%>Q1jt#lågenan%> K ;              
 +Pl+Ill:i%>Q1jda#lågenan%> K ;             
 +Pl+Ine:i%>Q1jn#lågenan%> K ;              
 +Pl+Ela:i%>Q1js#lågenan%> K ;              
 +Pl+Com:i%>Q1j#lågenan%> K ;               
                                      
LEXICON GÁKTS-LÅGENAN                 
 +Sg+Nom:a%>#lågenan%> K ;           
 +Sg+Nom+Use/NG:e%>#lågenan%> K ;                     
 +Sg+Acc:a%>#lågenan%> K ;                
 +Sg+Ill:á%>j#lågenan%> K ;
 +Ess:a%>n#lågenan%> K ;       
 +Ess:e%>n#lågenan%> K ;                  

LEXICON AKTSELÅGENAN
 AVTSELÅGENAN ;                
 AKTS-LÅGENAN ;                  

LEXICON AVTSELÅGENAN 
 +Sg+Acc:e%>Q1v#lågenan%> K ;                                
 +Sg+Gen:e%>Q1#lågenan%> K ;  
 +Sg+Ill+Attr:e%>Q1#lågenan%> K ;                              
 +Cmp/SgGen:e%>Q1#lågenan%> NUMERALCOMPOUNDS ;
 +Sg+Ine:e%>Q1n#lågenan%> K ;               
 +Sg+Ela:e%>Q1s#lågenan%> K ;               
 +Sg+Com:i%>Q1jn#lågenan%> K ;              
 +Sg+Par:e%>Q1t#lågenan%> K ;               
 +Pl+Nom:e%>Q1#lågenan%> K ;                
 +Pl+Gen:i%>Q1j#lågenan%> K ;               
 +Pl+Acc:i%>Q1jt#lågenan%> K ;              
 +Pl+Ill:i%>Q1jda#lågenan%> K ;             
 +Pl+Ine:i%>Q1jn#lågenan%> K ;              
 +Pl+Ela:i%>Q1js#lågenan%> K ;              
 +Pl+Com:i%>Q1j#lågenan%> K ;               
                                      
LEXICON AKTS-LÅGENAN                  
 +Sg+Nom:e%>#lågenan%> K ;                
 +Sg+Acc:e%>#lågenan%> K ;                
 +Sg+Ill:á%>j#lågenan%> K ;               
 +Ess:e%>n#lågenan%> K ;                  

! Tjuhte and tuvsán              
                                    
LEXICON TJUOHTECASE !incoming from TJUOHTE           
              :e%>Q1 TJUODE- ; !tjuohte-                   
              :i%>Q1 TJUODI- ; !tjuohti-               
 +Der/akti+Adv:ti%>  K       ;
       +Sg+Nom:e     K       ; !vihtta(låhke)
!       TJUOHT- ;                       
                                      
LEXICON TJUODE-                       
 +Sg+Acc:v K ;                       
 +Sg+Gen: K ; 
 +Cmp/SgGen: NUMERALCOMPOUNDS ;  
 +Sg+Ill+Attr: K ;                                             
 +Sg+Ine:n K ;                       
 +Sg+Ela:s K ;                       
 +Sg+Par:t K ;                       
 +Pl+Nom: K ;                        

LEXICON TJUODI-                      
 +Pl+Gen:j K ;                       
 +Sg+Com:jn K ;                      
 +Pl+Acc:jt K ;                      
 +Pl+Ill:jda K ;                     
 +Pl+Ine:jn K ;                      
 +Pl+Ela:js K ;                      
 +Pl+Com:j K ;                       
                                      
!LEXICON TJUOHT-                       
! +Sg+Nom:%>e K ;                        
! +Ess:e%>n K ;                          
! +Sg+Ill:á%>j K ;                       

LEXICON ẈG-LÅK-TJUOHTECASE
 +Use/NG:e%>Q1 ẈG-LÅK-TJUODE- ;                     

LEXICON ẈG-LÅK-TJUODE-                       
 +Sg+Acc:v K ;                       
 +Sg+Gen: K ;                        
 +Sg+Ine:n K ;                       

LEXICON TUVSÁN !incoming from justtuvsán
 +Sg+Nom:%>     K ;          
 +Sg+Gen:%>a    K ; 
 +Cmp/SgGen:%>a    NUMERALCOMPOUNDS ;          
!+Sg+Attr:%>   NUMERALCOMPOUNDS ; !sg gen go to numeralcmp
 +Sg+Acc:%>av   K ;            
 +Sg+Ill:%>ij   K ;    
 +Sg+Ine:%>in   K ;           
 +Sg+Ela:%>is   K ;            
 +Sg+Com:%>ijn  K ;            
    +Ess:%>in   K ;                  
 +Pl+Nom:%>a    K ;                 
 +Pl+Gen:%>ij   K ;              
 +Pl+Acc:%>ijt  K ;              
 +Pl+Ill:%>ijda K ;              
 +Pl+Ine:%>ijn  K ;                
 +Pl+Ela:%>ijs  K ;                 
 +Pl+Com:%>ij   K ;                 

LEXICON WG-LÅK-TUVSÁN
 +Sg+Gen:%>a K ;         
 +Cmp/SgGen:%>a NUMERALCOMPOUNDS ; !changed from sg+attr to sg+gen
 +Sg+Acc:%>av K ;            
 +Sg+Ill:%>ij K ;    
 +Sg+Ine:%>in K ;           
 +Sg+Ela:%>is K ;            
! +Sg+Com:%>ijn K ;            
! +Ess:%>in K ;                  

LEXICON WG-TUVSÁN
 +Sg+Gen:%>a K ;         
 +Cmp/SgGen:%> NUMERALCOMPOUNDS ; !changed from sg+attr to sg+gen
 +Sg+Acc:%>av K ;            
 +Sg+Ill:%>ij K ;    
 +Sg+Ine:%>in K ;           

! compounding and derivations

LEXICON ARABICCOMPOUNDS   !!≈ * **@CODE@**  ! arabic as first part, 
 +Cmp/Hyph+Cmp#:-  NAMÁKCont ;            ! 10-jienat, etc. NAMAT derivs are
                    ! treated as compounds and excluded from the nonrec
                    ! variants, otherwise the PLX lexicon size explodes
+Cmp/Hyph+Cmp#:-»»# SASJCont ; !6-jahkásaš
+Cmp/Hyph+Cmp#jahkásasj+A:-»»#jahkásadtj ÅLLAGASJ ; !
+Cmp/Hyph+Cmp#jahkásasj+A+Err/Orth:-»»#jahkasadtj ÅLLAGASJ ; !
: RHyph ; ! 3-


LEXICON NUMERALCOMPOUNDS  ! numeral as first part 
        NAMÁK   ; ! tuvsánajienak, lågegielak, etc. 
        SASJ    ;
 +Cmp#:# NUM2ADJ ;
 : RHyph ; ! 3-

LEXICON SASJ ! Second compounds. From numerals
+Cmp#: SASJCont ;

 
LEXICON NUM2ADJ
tjuohte+Num:#»tjuohtásadtj DER-SASJ ;
tjuohte+Num:#»tjuodag9 DER-AGAdj ; !from OK
tjuohte+Num:#»tjuoedeg9 DER-AGAdj ; !from Kintel

! NounSecond ;	! golmmačiegahas	
! geardásaš:geardás DABALAS ;	   
! !tuvsán#geardásaš 
! jahkásaš:jahkás DABALAS ; 


! Ordinal numbers
! ---------------


LEXICON VUOSTASJ      				
 +Sg+Nom:%> K ;          
 +Sg+Gen:%>a K ;         
 +Sg+Acc:%>av K ;            
 +Sg+Ill:%>ij K ;   
 +Sg+Ine:%>in K ;           
 +Sg+Ela:%>is K ;            
 +Sg+Com:%>ijn K ;            
 +Ess:%>in K ;                  
 +Pl+Nom:%>a K ;                 
 +Pl+Gen:%>ij K ;              
 +Pl+Acc:%>ijt K ;              
 +Pl+Ill:%>ijda K ;              
 +Pl+Ine:%>ijn K ;                
 +Pl+Ela:%>ijs K ;                 
 +Pl+Com:%>ij K ;                 
 +Attr:%> K ;					
 +Cmp/SgNom: R ;

LEXICON TUVSÁNAT				
 +Sg+Nom:t%> K ;          
 +Sg+Gen:tta%> K ;         
 +Sg+Acc:tta%>v K ;            
 +Sg+Ill:dda%>j K ;   
 +Sg+Ine:tta%>n K ;           
 +Sg+Ela:tta%>s K ;            
 +Sg+Com:tta%>jn K ;            
 +Ess:dda%>n K ;                  
 +Pl+Nom:tta%> K ;                 
 +Pl+Gen:tta%>j K ;              
 +Pl+Acc:tta%>jt K ;              
 +Pl+Ill:tta%>jda K ;              
 +Pl+Ine:tta%>jn K ;                
 +Pl+Ela:tta%>js K ;                 
 +Pl+Com:tta%>j K ;                 
 +Attr:t%> K ;					
 
! Collection numbers
! ---------------

LEXICON GUOVTES           
 +Sg+Gen+Sem/Hum:á%> K ;          
 +Sg+Acc+Sem/Hum:á%>v K ;                 
 +Sg+Ill+Sem/Hum:á%>j K ;                     
 +Sg+Ine+Sem/Hum:á%>n K ;                 
 +Sg+Ela+Sem/Hum:á%>s K ;                 
 +Sg+Com+Sem/Hum:á%>jn K ;                    
    +Ess+Sem/Hum:á%>n K ;                    
 +Pl+Nom+Sem/Hum:á%> K ;                  
 +Pl+Gen+Sem/Hum:á%>j K ;                 
 +Pl+Acc+Sem/Hum:á%>jt K ;                
 +Pl+Ill+Sem/Hum:á%>jda K ;                   
 +Pl+Ine+Sem/Hum:á%>jn K ;                        
 +Pl+Ela+Sem/Hum:á%>js K ;                        
 +Pl+Com+Sem/Hum:á%>j K ;                         
 +Sg+Nom+Sem/Hum:á%> PxV ;     
 +Sg+Gen+Sem/Hum:á%> PxV ;     
 +Sg+Acc+Sem/Hum:á%> PxV ;     
 +Sg+Ill+Sem/Hum:á%>s PxCi ;      
 +Sg+Ine+Sem/Hum:á%>n PxC ;      
 +Sg+Ela+Sem/Hum:á%>st PxC ;     
 +Sg+Com+Sem/Hum:á%>jn PxC ;      
 +Pl+Gen+Sem/Hum:á%>j PxC ;      
 +Pl+Acc+Sem/Hum:á%>jd PxC ;     
 +Pl+Ill+Sem/Hum:á%>jdis PxC ;        
 +Pl+Ine+Sem/Hum:á%>jn PxC ;     
 +Pl+Ela+Sem/Hum:á%>jst PxC ;        
 +Pl+Com+Sem/Hum:á%>j PxC ;      


LEXICON VIDÁS           
 +Sg+Gen+Sem/Hum:%>a K ;          
 +Sg+Acc+Sem/Hum:%>av K ;                 
 +Sg+Ill+Sem/Hum:%>ij K ;                     
 +Sg+Ine+Sem/Hum:%>in K ;                 
 +Sg+Ela+Sem/Hum:%>is K ;                 
 +Sg+Com+Sem/Hum:%>ijn K ;                    
    +Ess+Sem/Hum:%>in K ;                    
 +Pl+Nom+Sem/Hum:%>a K ;                  
 +Pl+Gen+Sem/Hum:%>ij K ;                 
 +Pl+Acc+Sem/Hum:%>ijt K ;                
 +Pl+Ill+Sem/Hum:%>ijda K ;                   
 +Pl+Ine+Sem/Hum:%>ijn K ;                        
 +Pl+Ela+Sem/Hum:%>ijs K ;                        
 +Pl+Com+Sem/Hum:%>ij K ;                         
 +Sg+Nom+Sem/Hum:%> PxC ;     
 +Sg+Gen+Sem/Hum:%> PxC ;     
 +Sg+Acc+Sem/Hum:%> PxC ;     
 +Sg+Ill+Sem/Hum:%>as PxCs ;      
 +Sg+Ine+Sem/Hum:%>in PxCs ;      
 +Sg+Ela+Sem/Hum:%>ist PxCs ;     
 +Sg+Com+Sem/Hum:%>ijn PxCs;      
 +Pl+Gen+Sem/Hum:%>ij PxCs ;      
 +Pl+Acc+Sem/Hum:%>ijd PxCs ;     
 +Pl+Ill+Sem/Hum:%>ijdas PxC ;        
 +Pl+Ine+Sem/Hum:%>ijn PxCs ;     
 +Pl+Ela+Sem/Hum:%>ijst PxCs ;        
 +Pl+Com+Sem/Hum:%>ij PxCs ;      


LEXICON VIDÁSLÅGENAN                        
 +Sg+Gen+Sem/Hum:%>a#lågenan%> K ;      
 +Sg+Acc+Sem/Hum:%>av#lågenan%> K ; 
 +Sg+Ill+Sem/Hum:%>ij#lågenan%> K ;       
 +Sg+Ine+Sem/Hum:%>in#lågenan%> K ; 
 +Sg+Ela+Sem/Hum:%>is#lågenan%> K ; 
 +Sg+Com+Sem/Hum:%>ijn#lågenan%> K ; 
 +Sg+Par+Sem/Hum:%>it#lågenan%> K ; 
 +Pl+Nom+Sem/Hum:%>a#lågenan%> K ;                      
 +Pl+Gen+Sem/Hum:%>ij#lågenan%> K ;             
 +Pl+Acc+Sem/Hum:%>ijt#lågenan%> K ;                
 +Pl+Ill+Sem/Hum:%>ijda#lågenan%> K ;               
 +Pl+Ine+Sem/Hum:%>ijn#lågenan%> K ;                    
 +Pl+Ela+Sem/Hum:%>ijs#lågenan%> K ;                        
 +Pl+Com+Sem/Hum:%>ij#lågenan%> K ;                     
 
LEXICON TJUODAGIS                       
 +Sg+Nom+Sem/Ani:%> K ;                          
 +Sg+Ill+Sem/Ani:sa%>j K ;                                       
 +Sg+Gen+Sem/Ani:á%> K ;                          
 +Sg+Acc+Sem/Ani:á%>v K ;                         
 +Sg+Ine+Sem/Ani:á%>n K ;                         
 +Sg+Ela+Sem/Ani:á%>s K ;                                     
 +Pl+Nom+Sem/Ani:á%> K ;                                  
 +Pl+Gen+Sem/Ani:á%>j K ;                                         
 +Pl+Acc+Sem/Ani:á%>jt K ;                            
 +Pl+Ill+Sem/Ani:á%>jda K ;                       
 +Pl+Ine+Sem/Ani:á%>jn K ;                                    
 +Pl+Ela+Sem/Ani:á%>js K ;                                
 +Pl+Com+Sem/Ani:á%>j K ;                                     
 +Sg+Com+Sem/Ani:á%>jn K ;                            
 +Sg+Nom+Sem/Ani:á%> PxV ;        
 +Sg+Ill+Sem/Ani:sa%>s PxCi ;      
 +Sg+Gen+Sem/Ani:á%> PxV ;        
 +Sg+Acc+Sem/Ani:á%> PxV ;        
 +Sg+Ine+Sem/Ani:á%>n PxC ;       
 +Sg+Ela+Sem/Ani:á%>st PxC ;      
 +Pl+Nom+Sem/Ani:á%> PxNomPlV ;       
 +Pl+Gen+Sem/Ani:á%>j PxC ;       
 +Pl+Acc+Sem/Ani:á%>jd PxC ;      
 +Pl+Ill+Sem/Ani:á%>jdis PxCs ;       
 +Pl+Ine+Sem/Ani:á%>jn PxC ;      
 +Pl+Ela+Sem/Ani:á%>jst PxC ;     
 +Pl+Com+Sem/Ani:á%>j PxC ;       
 +Sg+Com+Sem/Ani:á%>jn PxC ;        

LEXICON yeartagpl
          +Num+Sem/Year+Pl+Nom:        # ;
          +Num+Sem/Year+Pl+Gen:        # ;
          +Num+Sem/Year+Pl+Acc:        # ;
 +Num+Sem/Year+Err/Orth+Pl+Ine:%:dinie # ;



LEXICON dateyearcase_nullsuff_w_dot
     +Sg+Nom+Use/NG: MARKDOT ;
     +Sg+Gen: MARKDOT ;
     +Sg+Acc: MARKDOT ;
     +Sg+Ill+Attr: MARKDOT ;
     +Sg+Ela+Attr: MARKDOT ;


LEXICON dateyearcase
 dateyearcase_nullsuff ;
 dateyearcase_fullsuff ;

LEXICON dateyearcase_nullsuff
      +Sg+Nom: # ;
      +Sg+Gen: # ;
      +Pl+Nom: # ;
 +Sg+Ill+Attr: # ;
 +Sg+Ine+Attr: # ;

LEXICON dateyearcase_fullsuff
            +Sg+Acc:%:v  #      ;
            +Sg+Gen:     #      ;
   +Err/Orth+Sg+Acc:v    #      ;
            +Sg+Ine:%:n  #      ;
   +Err/Orth+Sg+Ine:n    #      ;
            +Sg+Ela:%:s  #      ;
   +Err/Orth+Sg+Ela:%:es #      ;
   +Err/Orth+Sg+Ela:s    #      ;
            +Sg+Ill:%:j  #      ;
   +Err/Orth+Sg+Ill:j    #      ;
 +Cmp/SplitR+Use/NG:%-   ENDLEX ;
        +Cmp/SplitR:%–   ENDLEX ;


LEXICON ARABICCASES   !!≈ * **@CODE@**  adds +Arab
 +Arab:  DIGITCASES ;

LEXICON ARABICCASE   !!≈ * **@CODE@**  adds +Arab
 +Arab:  DIGITCASE ;

LEXICON ARABICCASE0   !!≈ * **@CODE@**  adds +Arab
 +Arab:  DIGITCASE0 ;


LEXICON DIGITCASES   !!≈ * **@CODE@**  to distinguish between 0 and oblique
DIGITCASE0 ;
DIGITCASE ;

LEXICON DIGITCASE0   !!≈ * **@CODE@**
    +Sg+Nom:%> # ;
    +Sg+Gen:%> # ;
    +Sg+Ill+Attr:   # ;
    +Sg+Ine+Attr:   # ;
    +Sg+Ela+Attr:   # ;
 !   +Err/Orth+Sg+Acc:%> # ;
 !   +Err/Orth+Sg+Com:%> # ;
 !   +Err/Orth+Ess:%> # ;


LEXICON DIGITCASE
!  f+Sg+Nom+Err/Orth:f     # ; ! s. 123f.
! ff+Sg+Nom+Err/Orth:ff    # ; ! s. 123ff.
!  b+Sg+Nom+Err/Orth:b     # ; ! § 123b
!  c+Sg+Nom+Err/Orth:c     # ; ! § 123c
!  d+Sg+Nom+Err/Orth:d     # ; ! § 123d
!  e+Sg+Nom+Err/Orth:e     # ; ! § 123e
!
!   +Sg+Nom+Err/Orth:      # ;
!   +Sg+Gen+Err/Orth:      # ;
!            +Sg+Acc:      # ;
    +Sg+Gen+Err/Orth:%>a   # ; !But here 123a 123
    +Sg+Acc+Err/Orth:%>av  # ;
             +Sg+Acc:%>v   # ;
             +Sg+Ill:%>j   # ;
    +Sg+Ill+Err/Orth:%>aj  # ;
             +Sg+Ela:%>s   # ;
    +Sg+Ela+Err/Orth:%>is  # ;
    +Sg+Ela+Err/Orth:%>as  # ;
             +Sg+Ine:%>n   # ;
    +Sg+Ine+Err/Orth:%>in  # ;
    +Sg+Ine+Err/Orth:%>an  # ;
             +Sg+Com:%>jn  # ;
    +Sg+Com+Err/Orth:%>ajn # ;
                +Ess:%>n   # ;
       +Ess+Err/Orth:%>an  # ;
             +Pl+Gen:%>j   K ; !
             +Pl+Acc:%>jt  K ; !
             +Pl+Ill:%>jda K ; ! 100:iide čuđiide
             +Pl+Ine:%>jn  K ; !
             +Pl+Ela:%>js  K ; !
             +Pl+Com:%>j   K ; !

!ordinals

LEXICON ARABICCASEORD
 +Sg+Gen:%>da    K ;
 +Sg+Acc:%>dav   K ;
 +Sg+Ill:%>dij   K ;
 +Sg+Ine:%>din   K ;
 +Sg+Ela:%>dis   K ;
 +Sg+Com:%>dijn  K ;
    +Ess:%>din   K ;
 +Pl+Nom:%>da    K ;
 +Pl+Gen:%>dij   K ;
 +Pl+Acc:%>dijt  K ;
 +Pl+Ill:%>dijda K ;
 +Pl+Ine:%>dijn  K ;
 +Pl+Ela:%>dijs  K ;
 +Pl+Com:%>dij   K ;

LEXICON ARABICCASEORD-ERR
 +Sg+Gen:%>a    K ;
 +Sg+Acc:%>av   K ;
 +Sg+Ill:%>ij   K ;
 +Sg+Ine:%>in   K ;
 +Sg+Ela:%>is   K ;
 +Sg+Com:%>ijn  K ;
    +Ess:%>in   K ;
 +Pl+Nom:%>a    K ;
 +Pl+Gen:%>ij   K ;
 +Pl+Acc:%>ijt  K ;
 +Pl+Ill:%>ijda K ;
 +Pl+Ine:%>ijn  K ;
 +Pl+Ela:%>ijs  K ;
 +Pl+Com:%>ij   K ;

LEXICON ARABICCASECOLL
 +Sg+Nom:%>s   K ;
 +Sg+Gen:%>sa   K ;
 +Sg+Acc:%>sav  K ;
 +Sg+Ill:%>sij  K ;
 +Sg+Loc:%>sin  K ;
 +Sg+Loc:%>sis  K ;
 +Sg+Com:%>sijn K ;

 
!LEXICON ROMNUMTAG
! +Num+Rom+Sg+Nom: # ;
! +Num+Rom+Sg+Gen: # ;
!!+Num+Acc: # ;
! +A+Rom+Ord+Sg:%. # ; !III. as ordinal.
! +A+Rom+Ord+Sg:   # ; !Looking for trouble...
! +Num:%: ROMNUMTAGOBL  ;
! +Num:%' ROMNUMTAGOBL ;
! +Num:%- ROMNUMTAGOBL  ;
! +Num:%´ ROMNUMTAGOBL  ;
!! +Num:   ROMNUMTAGOBL ;

LEXICON ROMNUMTAGOBL
 +Rom+Sg+Nom:  # ; ! But here 123a 123
 +Rom+Sg+Gen:  # ; ! But here 123a 123
 +Rom+Sg+Gen+Err/Orth:a  # ; ! But here 123a 123
 +Rom+Sg+Acc+Err/Orth:av # ;
 +Rom+Sg+Ill:%>j        # ;
 +Rom+Sg+Ill+Err/Orth:ij # ;
 +Rom+Sg+Ill+Err/Orth:áj # ;
 +Rom+Sg+Ela:%>s        # ;
 +Rom+Sg+Ine:%>n        # ;
 +Rom+Sg+Com:%>jn       # ;
 +Rom+Ess:%>n        # ;
 +Rom+Sg+Acc:%>v        # ;
