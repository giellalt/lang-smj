! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@uit.no or feedback@divvun.no

! ==================== !
! The Lule Sámi clock !
! ==================== !


Multichar_Symbols

@U.TIME.1@ 
@U.TIME.2@ 
@U.TIME.3@ 
@U.TIME.4@ 
@U.TIME.5@ 
@U.TIME.6@ 
@U.TIME.7@ 
@U.TIME.8@ 
@U.TIME.9@ 
@U.TIME.10@ 
@U.TIME.11@ 
@U.TIME.12@ 

+Use/NG
+Use/NA


LEXICON Root
     %0: smallhour ;
+Use/NA: smallhour ;
         largehour ;
         KLOAHKKA  ;

LEXICON smallhour
  @U.TIME.1@1:@U.TIME.1@  AFTpunkt ; !01:00 - 01:19 lågenanaktse minuhta badjel avta
  @U.TIME.2@1:@U.TIME.2@  BEFpunkt ; !01:20 - 01:59 lågev minuhta biel guoktáj, lågenangáktsa minuhta guoktáj
  @U.TIME.2@2:@U.TIME.2@  AFTpunkt ; !02:00 - 02:19
  @U.TIME.3@2:@U.TIME.3@  BEFpunkt ; !02:20 - 02:59
  @U.TIME.3@3:@U.TIME.3@  AFTpunkt ; !03:00 - 03:19
  @U.TIME.4@3:@U.TIME.4@  BEFpunkt ; !03:20 - 03:59
  @U.TIME.4@4:@U.TIME.4@  AFTpunkt ; !04:00 - 04:19
  @U.TIME.5@4:@U.TIME.5@  BEFpunkt ; !04:20 - 04:59
  @U.TIME.5@5:@U.TIME.5@  AFTpunkt ; !05:00 - 05:19
  @U.TIME.6@5:@U.TIME.6@  BEFpunkt ; !05:20 - 05:59
  @U.TIME.6@6:@U.TIME.6@  AFTpunkt ; !06:00 - 06:19
  @U.TIME.7@6:@U.TIME.7@  BEFpunkt ; !06:20 - 06:59
  @U.TIME.7@7:@U.TIME.7@  AFTpunkt ; !07:00 - 07:19
  @U.TIME.8@7:@U.TIME.8@  BEFpunkt ; !07:20 - 07:59
  @U.TIME.8@8:@U.TIME.8@  AFTpunkt ; !08:00 - 08:19
  @U.TIME.9@8:@U.TIME.9@  BEFpunkt ; !08:20 - 08:59
  @U.TIME.9@9:@U.TIME.9@  AFTpunkt ; !09:00 - 09:19
 @U.TIME.10@9:@U.TIME.10@ BEFpunkt ; !09:20 - 09:59

LEXICON largehour
   @U.TIME.10@1%0:@U.TIME.10@                  AFTpunkt ; !10:00 - 10:19
   @U.TIME.11@1%0:@U.TIME.11@                  BEFpunkt ; !10:20 - 10:59
    @U.TIME.11@11:@U.TIME.11@                  AFTpunkt ; !11:00 - 11:59
    @U.TIME.12@11:@U.TIME.12@                  BEFpunkt ; !11:19 - 11:59
    @U.TIME.12@12:@U.TIME.12@                  AFTpunkt ; !12:00 - 12:19
     @U.TIME.1@12:@U.TIME.1@                   BEFpunkt ; !12:19 - 12:59
     @U.TIME.1@13:@U.TIME.1@                   AFTpunkt ; !13:00 - 13:19
     @U.TIME.2@13:@U.TIME.2@                   BEFpunkt ; !13:19 - 13:59
     @U.TIME.2@14:@U.TIME.2@                   AFTpunkt ; !14:00 - 114:19
     @U.TIME.3@14:@U.TIME.3@                   BEFpunkt ; !14:19 - 14:59
     @U.TIME.3@15:@U.TIME.3@                   AFTpunkt ; !15:00 - 15:19
     @U.TIME.4@15:@U.TIME.4@                   BEFpunkt ; !15:19 - 15:59
     @U.TIME.4@16:@U.TIME.4@                   AFTpunkt ; !16:00 - 16:19
     @U.TIME.5@16:@U.TIME.5@                   BEFpunkt ; !16:19 - 16:59
     @U.TIME.5@17:@U.TIME.5@                   AFTpunkt ; !17:00 - 17:19
     @U.TIME.6@17:@U.TIME.6@                   BEFpunkt ; !17:19 - 17:59
     @U.TIME.6@18:@U.TIME.6@                   AFTpunkt ; !18:00 - 18:19
     @U.TIME.7@18:@U.TIME.7@                   BEFpunkt ; !18:19 - 18:59
     @U.TIME.7@19:@U.TIME.7@                   AFTpunkt ; !19:00 - 19:19
     @U.TIME.8@19:@U.TIME.8@                   BEFpunkt ; !19:20 - 19:59
    @U.TIME.8@2%0:@U.TIME.8@                   AFTpunkt ; !20:00 - 20:19
    @U.TIME.9@2%0:@U.TIME.9@                   BEFpunkt ; !20:20 - 20:59
     @U.TIME.9@21:@U.TIME.9@                   AFTpunkt ; !21:00 - 21:19
    @U.TIME.10@21:@U.TIME.10@                  BEFpunkt ; !21:20 - 21:59
    @U.TIME.10@22:@U.TIME.10@                  AFTpunkt ; !22:00 - 22:19
    @U.TIME.11@22:@U.TIME.11@                  BEFpunkt ; !22:19 - 22:59
    @U.TIME.11@23:@U.TIME.11@                  AFTpunkt ; !23:00 - 23:19
    @U.TIME.12@23:@U.TIME.12@                  BEFpunkt ; !23:19 - 23:59
  @U.TIME.12@%0%0:@U.TIME.12@                  AFTpunkt ; !00:00 - 00:19
   @U.TIME.1@%0%0:@U.TIME.1@                   BEFpunkt ; !00:19 - 00:59
              24%:%0%0+Use/NG:guoktalåkniellja #        ; !24:00
! %0%0@U.TIME.12@:@U.TIME.12@                  AFTpunkt ; !00:00 - 00:19
!  %0%0@U.TIME.1@:@U.TIME.1@                   BEFpunkt ; !00:19 - 00:59



 LEXICON BEFpunkt
 %:: BEF ;
 %.+Use/NA: BEF ;

LEXICON AFTpunkt
 %:: AFT ;  ! 2:00 = göökte
 %.+Use/NA: AFT ;  ! 2.00 = göökte
   +Use/NA:  HOUR ;  ! 2    = göökte


LEXICON BEF !lågev minuhta biel guoktáj, lågenangáktsa minuhta guoktáj
       2%0:lågev%           TOHALF      ;
        21:aktse%           TOHALF      ;
        22:gáktsa%          TOHALF      ;
        23:gietjav%         TOHALF      ;
        24:guhtta%          TOHALF      ;
        25:vihtta%          TOHALF      ;
        26:niellja%         TOHALF      ;
        27:gålmmå%          TOHALF      ;
        28:guokta%          TOHALF      ;
 29+Use/NG:akta% minuhtta%  TOHALF-BIEL ;
        29:minuhtta%        TOHALF-BIEL ;
       3%0:biel%            HOUR        ;
       3%0:biel%            HOUR        ;
        31:akta%            OVERHALF    ;
 31+Use/NG:minuhta%         OVERHALF    ;
        32:guokta%          OVERHALF    ;
        33:gålmmå%          OVERHALF    ;
        34:niellja%         OVERHALF    ;
        35:vihtta%          OVERHALF    ;
        36:guhtta%          OVERHALF    ;
        37:gietjav%         OVERHALF    ;
        38:gáktsa%          OVERHALF    ;
        39:aktse%           OVERHALF    ;
       4%0:lågev%           OVERHALF    ;
        41:lågenanaktse%    TO          ;
        42:lågenangáktsa%   TO          ;
        43:lågenangietjav%  TO          ;
        44:lågenanguhtta%   TO          ;
        45:kvártta%         HOUR-ILL    ;
        46:lågenanniellja%  TO          ;
        47:lågenangålmmå%   TO          ;
        48:lågenanguokta%   TO          ;
        49:lågenanakta%     TO          ;
       5%0:lågev%           TO          ;
        51:aktse%           TO          ;
        52:gáktsa%          TO          ;
        53:gietjav%         TO          ;
        54:guhtta%          TO          ;
        55:vihtta%          TO          ;
        56:niellja%         TO          ;
        57:gålmmå%          TO          ;
        58:guokta%          TO          ;
 59+Use/NG:akta% minuhtta%  HOUR-ILL    ;
        59:minuhtta%        HOUR-ILL    ;



LEXICON AFT  !lågenanaktse minuhta badjel avta
       %0%0:                 HOUR ;
        %01:akta%            OVER ;
 %01+Use/NG:minuhta%         OVER ;
        %02:guokta%          OVER ;
        %03:gålmmå%          OVER ;
        %04:niellja%         OVER ;
        %05:vihtta%          OVER ;
        %06:guhtta%          OVER ;
        %07:gietjav%         OVER ;
        %08:gáktsa%          OVER ;
        %09:aktse%           OVER ;
        1%0:lågev%           OVER ;
         11:lågenanakta%     OVER ;
         12:lågenanguokta%   OVER ;
         13:lågenangålmmå%   OVER ;
         14:lågenanniellja%  OVER ;
         15:kvártta%         OVER-KVÁRTTA ;
         16:lågenanguhtta%   OVER ;
         17:lågenangietjav%  OVER ;
         18:lågenangáktsa%   OVER ;
         19:lågenanaktse%    OVER ;

LEXICON TOHALF
 :minuhta%     TOHALF-BIEL ;

LEXICON TOHALF-BIEL
 :biel%    HOUR-ILL ;

LEXICON OVERHALF
 :minuhta% badjel% biel%     HOUR-GEN ;

LEXICON TO
 :minuhta%    HOUR-ILL ;

LEXICON OVER
 :minuhta% badjel%   HOUR-GEN ;

LEXICON OVER-KVÁRTTA
 :badjel%   HOUR-GEN ;

LEXICON HOUR
NOMHOUR ; !^N^
INEHOUR ;

LEXICON NOMHOUR                              
  @U.TIME.1@:@U.TIME.1@akta           # ;
  @U.TIME.2@:@U.TIME.2@guokta         # ;
  @U.TIME.3@:@U.TIME.3@gålmmå         # ;
  @U.TIME.4@:@U.TIME.4@niellja        # ;
  @U.TIME.5@:@U.TIME.5@vihtta         # ;
  @U.TIME.6@:@U.TIME.6@guhtta         # ;
  @U.TIME.7@:@U.TIME.7@gietjav        # ;
  @U.TIME.8@:@U.TIME.8@gáktsa         # ;
  @U.TIME.9@:@U.TIME.9@aktse          # ;
 @U.TIME.10@:@U.TIME.10@lågev         # ;
 @U.TIME.11@:@U.TIME.11@lågenanakta   # ;
 @U.TIME.12@:@U.TIME.12@lågenanguokta # ;


LEXICON INEHOUR                              
  @U.TIME.1@:@U.TIME.1@avtan           # ;
  @U.TIME.2@:@U.TIME.2@guovten         # ;
  @U.TIME.3@:@U.TIME.3@gålmån          # ;
  @U.TIME.4@:@U.TIME.4@nieljen         # ;
  @U.TIME.5@:@U.TIME.5@vidán           # ;
  @U.TIME.6@:@U.TIME.6@gudán           # ;
  @U.TIME.7@:@U.TIME.7@gietjan         # ;
  @U.TIME.8@:@U.TIME.8@gávtsen         # ;
  @U.TIME.9@:@U.TIME.9@avtsen          # ;
 @U.TIME.10@:@U.TIME.10@lågen          # ;
 @U.TIME.11@:@U.TIME.11@lågenanavtan   # ;
 @U.TIME.12@:@U.TIME.12@lågenanguovten # ;



LEXICON HOUR-GEN
GENHOUR ; !^N^


LEXICON GENHOUR                              
  @U.TIME.1@:@U.TIME.1@avta           # ;
  @U.TIME.2@:@U.TIME.2@guovte         # ;
  @U.TIME.3@:@U.TIME.3@gålmå          # ;
  @U.TIME.4@:@U.TIME.4@nielje         # ;
  @U.TIME.5@:@U.TIME.5@vidá           # ;
  @U.TIME.6@:@U.TIME.6@gudá           # ;
  @U.TIME.7@:@U.TIME.7@gietja         # ;
  @U.TIME.8@:@U.TIME.8@gávtse         # ;
  @U.TIME.9@:@U.TIME.9@avtse          # ;
 @U.TIME.10@:@U.TIME.10@låge          # ;
 @U.TIME.11@:@U.TIME.11@lågenanavta   # ;
 @U.TIME.12@:@U.TIME.12@lågenanguovte # ;


LEXICON HOUR-ILL
ILLHOUR ; !^N^


LEXICON ILLHOUR                              
  @U.TIME.1@:@U.TIME.1@aktaj           # ;
  @U.TIME.2@:@U.TIME.2@guoktaj         # ;
  @U.TIME.3@:@U.TIME.3@gålmmåj         # ;
  @U.TIME.4@:@U.TIME.4@nielljaj        # ;
  @U.TIME.5@:@U.TIME.5@vihttaj         # ;
  @U.TIME.6@:@U.TIME.6@guhttaj         # ;
  @U.TIME.7@:@U.TIME.7@giehtjaj        # ;
  @U.TIME.8@:@U.TIME.8@gáktsaj         # ;
  @U.TIME.9@:@U.TIME.9@aktsáj          # ;
 @U.TIME.10@:@U.TIME.10@låhkåj         # ;
 @U.TIME.11@:@U.TIME.11@lågenanaktaj   # ;
 @U.TIME.12@:@U.TIME.12@lågenanguoktaj # ;


! --------------------------------------------------------------------------
! 24 digits clock, 22:39=kloahkka guoktalåkguokta gålmmålåkaktse

LEXICON KLOAHKKA 

 :kloahkka%     HOUR-24 ;

LEXICON HOUR-24
  %0%0:nulla% nulla%  MINUTES-00-59 ; !
  %0:nulla%         1-9-HOUR      ;
  %0:nulla%         1-9-HOUR      ;
  %0:nulla%         1-9-HOUR      ;
  %0:nulla%         1-9-HOUR      ;
  %0:nulla%         1-9-HOUR      ;
  %0:nulla%         1-9-HOUR      ;
  %0:nulla%         1-9-HOUR      ;
  %0:nulla%         1-9-HOUR      ;
  %0:nulla%         1-9-HOUR      ;
 1%0:lågev          MINUTES-00-59 ;
   1:lågenan        1-9-HOUR     ;
   2:guoktalåk      HOUR-1-3      ;

LEXICON 1-9-HOUR
  1:akta    MINUTES-00-59 ;
  2:guokta  MINUTES-00-59 ;
  3:gålmmå  MINUTES-00-59 ;
  4:niellja MINUTES-00-59 ;
  5:vihtta  MINUTES-00-59 ;
  6:guhtta  MINUTES-00-59 ;
  7:gietjav MINUTES-00-59 ;
  8:gáktsa  MINUTES-00-59 ;
  9:aktse   MINUTES-00-59 ;
  
LEXICON HOUR-1-3
  1:akta    MINUTES-00-59 ;
  2:guokta  MINUTES-00-59 ;
  3:gålmmå  MINUTES-00-59 ;
  
LEXICON MINUTES-00-59
%:: MINUTES ;

LEXICON MINUTES
   %0:% nulla%         ONES ;
 %0%0:% nulla% nulla   #    ;
  1%0:% lågev          #    ;
    1:% lågenan        ONES ;
  2%0:% guoktalåk      #    ;
    2:% guoktalåk      ONES ;
    3:% gålmmålåk      ONES ;
  3%0:% gålmmålåk      #    ;
    4:% nielljalåk     ONES ;
  4%0:% nielljalåk     #    ;
    5:% vihttalåk      ONES ;
  5%0:% vihttalåk      #    ;

 
LEXICON ONES
  1:akta    # ;
  2:guokta  # ;
  3:gålmmå  # ;
  4:niellja # ;
  5:vihtta  # ;
  6:guhtta  # ;
  7:gietjav # ;
  8:gáktsa  # ;
  9:aktse   # ;  
