! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@uit.no or feedback@divvun.no

! ==================== !
! The Lule Sámi dates !
! ==================== !

!! This is still a dummy file.


! 12.8.
! Borgemánu 12. b.
! Borgemánu guoktenuppelot beaivi

Multichar_Symbols

@U.MONTH.1@ 
@U.MONTH.2@ 
@U.MONTH.3@ 
@U.MONTH.4@ 
@U.MONTH.5@ 
@U.MONTH.6@ 
@U.MONTH.7@ 
@U.MONTH.8@ 
@U.MONTH.9@ 
@U.MONTH.10@ 
@U.MONTH.11@ 
@U.MONTH.12@ 

+Use/NG


LEXICON Root

! For each month: First the normative, then the non-normatives, then initial capital

         @U.MONTH.1@:@U.MONTH.1@ådåjakmáno%      31DAYS ;
  @U.MONTH.1@+Use/NG:@U.MONTH.1@Ådåjakmáno%      31DAYS ;

         @U.MONTH.2@:@U.MONTH.2@guovvamáno%      29DAYS ;
  @U.MONTH.2@+Use/NG:@U.MONTH.2@Guovvamáno%      29DAYS ;

         @U.MONTH.3@:@U.MONTH.3@snjuktjamáno%    31DAYS ;
  @U.MONTH.3@+Use/NG:@U.MONTH.3@sjnjuktjamáno%   31DAYS ;
  @U.MONTH.3@+Use/NG:@U.MONTH.3@Snjuktjamáno%    31DAYS ;
  @U.MONTH.3@+Use/NG:@U.MONTH.3@Sjnjuktjamáno%   31DAYS ;

         @U.MONTH.4@:@U.MONTH.4@vuoratjismáno%   30DAYS ;
  @U.MONTH.4@+Use/NG:@U.MONTH.4@Vuoratjismáno%   30DAYS ;

         @U.MONTH.5@:@U.MONTH.5@moarmesmáno%     31DAYS ;
  @U.MONTH.5@+Use/NG:@U.MONTH.5@Moarmesmáno%     31DAYS ;

         @U.MONTH.6@:@U.MONTH.6@biehtsemáno%     30DAYS ;
  @U.MONTH.6@+Use/NG:@U.MONTH.6@Biehtsemáno%     30DAYS ;

         @U.MONTH.7@:@U.MONTH.7@snjilltjamáno%   31DAYS ;
  @U.MONTH.7@+Use/NG:@U.MONTH.7@sjnjilltjamáno%  31DAYS ;
  @U.MONTH.7@+Use/NG:@U.MONTH.7@Snjilltjamáno%   31DAYS ;
  @U.MONTH.7@+Use/NG:@U.MONTH.7@Sjnjilltjamáno%  31DAYS ;

         @U.MONTH.8@:@U.MONTH.8@bårggemáno%      31DAYS ;
  @U.MONTH.8@+Use/NG:@U.MONTH.8@Bårggemáno%      31DAYS ;

         @U.MONTH.9@:@U.MONTH.9@ragátmáno%       30DAYS ;
  @U.MONTH.9@+Use/NG:@U.MONTH.9@Ragátmáno%       30DAYS ;

        @U.MONTH.10@:@U.MONTH.10@gålgådismáno%   31DAYS ;
 @U.MONTH.10@+Use/NG:@U.MONTH.10@Gålgådismáno%   31DAYS ;

        @U.MONTH.11@:@U.MONTH.11@basádismáno%    30DAYS ; !
 @U.MONTH.11@+Use/NG:@U.MONTH.11@Basádismáno%    30DAYS ; !

        @U.MONTH.12@:@U.MONTH.12@javllamáno%     31DAYS ;
 @U.MONTH.12@+Use/NG:@U.MONTH.12@Javllamáno%     31DAYS ;


LEXICON 31DAYS
 31:gålmmålåkvuostasj B      ;
                      30DAYS ;

LEXICON 30DAYS
 3%0:gålmmålågåt B      ;
                 29DAYS ;

LEXICON 29DAYS
    1:vuostasj           B ;
    2:nuppát             B ;
    3:goalmát            B ;
    4:nælját             B ;
    5:vidát              B ;
    6:gudát              B ;
    7:giehttjit          B ;
    8:gávtsát            B ;
    9:avtsát             B ;
  %01:vuostasj           B ;
  %02:nuppát             B ;
  %03:goalmát            B ;
  %04:nælját             B ;
  %05:vidát              B ;
  %06:gudát              B ;
  %07:giehttjit          B ;
  %08:gávtsát            B ;
  %09:avtsát             B ;
  1%0:lågåt              B ;
   11:lågenanvuostasj    B ;
   12:lågenannuppát      B ;
   13:lågenangoalmát     B ;
   14:lågenannælját      B ;
   15:lågenanvidát       B ;
   16:lågenangudát       B ;
   17:lågenangiehttjit   B ;
   18:lågenangávtsát     B ;
   19:lågenanavtsát      B ;
  2%0:guoktalågåt        B ;
   21:guoktalåkvuostasj  B ;
   22:guoktalåknuppát    B ;
   23:guoktalåkgoalmát   B ;
   24:guoktalåknælját    B ;
   25:guoktalåkvidát     B ;
   26:guoktalåkgudát     B ;
   27:guoktalåkgiehttjit B ;
   28:guoktalåkgávtsát   B ;
   29:guoktalåkavtsát    B ;



LEXICON B
%.:% biejve MONTH ;

LEXICON MONTH
    1@U.MONTH.1@:@U.MONTH.1@  X ;
    2@U.MONTH.2@:@U.MONTH.2@  X ;
    3@U.MONTH.3@:@U.MONTH.3@  X ;
    4@U.MONTH.4@:@U.MONTH.4@  X ;
    5@U.MONTH.5@:@U.MONTH.5@  X ;
    6@U.MONTH.6@:@U.MONTH.6@  X ;
    7@U.MONTH.7@:@U.MONTH.7@  X ;
    8@U.MONTH.8@:@U.MONTH.8@  X ;
    9@U.MONTH.9@:@U.MONTH.9@  X ;
  %01@U.MONTH.1@:@U.MONTH.1@  X ;
  %02@U.MONTH.2@:@U.MONTH.2@  X ;
  %03@U.MONTH.3@:@U.MONTH.3@  X ;
  %04@U.MONTH.4@:@U.MONTH.4@  X ;
  %05@U.MONTH.5@:@U.MONTH.5@  X ;
  %06@U.MONTH.6@:@U.MONTH.6@  X ;
  %07@U.MONTH.7@:@U.MONTH.7@  X ;
  %08@U.MONTH.8@:@U.MONTH.8@  X ;
  %09@U.MONTH.9@:@U.MONTH.9@  X ;
 1%0@U.MONTH.10@:@U.MONTH.10@ X ;
  11@U.MONTH.11@:@U.MONTH.11@ X ;
  12@U.MONTH.12@:@U.MONTH.12@ X ;

LEXICON X
%.: # ;
%.:%  YEAR ;
! # ; ! sloppy punctuation

LEXICON YEAR
           ONEST         ; ! 2000-9999
  1:tuvsán THOUSAND      ; ! 1000-1999
1+Use/NG:duhát THOUSAND      ; ! 1000-1999
           UNDERTHOUSAND ; ! 100-999
           TENS          ; ! 20-99
           TEENS         ; ! 10-19
           ONES          ; ! 1-9

LEXICON ONEST
  1:        THOUSANDS ; ! Rett! 
  2:guokta  THOUSANDS ;
  3:gålmmå  THOUSANDS ;
  4:niellja THOUSANDS ;
  5:vihtta  THOUSANDS ;
  6:guhtta  THOUSANDS ;
  7:gietjav THOUSANDS ;
  8:gáktsa  THOUSANDS ;
  9:aktse   THOUSANDS ;

LEXICON THOUSANDS      ! x > 1
       :tuvsán   THOUSAND ;    ! Denne for tekst-til-tale og andre normative verkty
+Use/NG:duhát    THOUSAND ;    ! 
%0%0%0.:tuvsánat #        ;

LEXICON THOUSAND
         UNDERTHOUSAND ; ! 1100-1999
     %0: TENS          ; ! 1020-1099
     %0: TEENS         ; ! 1010-1019
   %0%0: ONES          ; ! 1001-1009
 %0%0%0: COMMA         ; ! 1000

LEXICON UNDERTHOUSAND
           HUNDREDS ;
 1:tjuohte HUNDRED  ;
 1:tjuot   HUNDRED  ; ! XXX - Only in compounds? Sjekk med Inga. Bare i cmp med noe forann, og bare som siste ledd når tallordet er attr til noe. Feks "gålmmåtjuot bijla". Eller når det er ledd etter også. Gålmmåtjuotguoktalåkgålmmå.
 1:tjuodát TJUODAAT ;

LEXICON HUNDREDS
  2:guokta  CUODI ;
  3:gålmmå  CUODI ;
  4:niellja CUODI ;
  5:vihtta  CUODI ;
  6:guhtta  CUODI ;
  7:gietjav CUODI ;
  8:gáktsa  CUODI ;
  9:aktse   CUODI ;

LEXICON CUODI
 :tjuode  HUNDRED  ;
 :tjuodát TJUODAAT ;
 
LEXICON HUNDRED
       TENS  ;
       TEENS ;
   %0: ONES  ;
 %0%0: COMMA ;

LEXICON TJUODAAT
 %0%0.: COMMA ;

LEXICON TEENS
 1+Use/NG:        TEEN  ;
        1:lågenan ONES ;

LEXICON TEEN
  1:akta    LOHKAI ;
  2:guokta  LOHKAI ;
  3:gålmmå  LOHKAI ;
  4:niellja LOHKAI ;
  5:vihtta  LOHKAI ;
  6:guhtta  LOHKAI ;
  7:gietjav LOHKAI ;
  8:gáktsa  LOHKAI ;
  9:aktse   LOHKAI ;

LEXICON LOHKAI
 :lågenan COMMA ;

LEXICON TENS
  1%0:lågev   COMMA ;
1%0%.:lågåt   COMMA ;
    2:guokta  LÅGEV ;
    3:gålmmå  LÅGEV ;
    4:niellja LÅGEV ;
    5:vihtta  LÅGEV ;
    6:guhtta  LÅGEV ;
    7:gietjav LÅGEV ;
    8:gáktsa  LÅGEV ;
    9:aktse   LÅGEV ;

LEXICON LÅGEV
 %0:låhke COMMA    ;
 %0:låk   COMMA    ;
   :låk   ONES     ;
%0.:lågåt COMMA    ;

LEXICON ONES
  CARDINAL ;
  ORDINAL  ;

LEXICON CARDINAL
  1:akta    COMMA ;
  2:guokta  COMMA ;
  3:gålmmå  COMMA ;
  4:niellja COMMA ;
  5:vihtta  COMMA ;
  6:guhtta  COMMA ;
  7:gietjav COMMA ;
  8:gáktsa  COMMA ;
  9:aktse   COMMA ;

LEXICON COMMA
  ENDLEX       ;
  
LEXICON ENDLEX
  # ;
