! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@uit.no or feedback@divvun.no

! ========================================================================== !
!                      Lule Sámi morphological analyser                      !
! ========================================================================== !


! ===========================
!! # Sublexica for Adjective
! ===========================

!! ## Even-syllable stems
! -------------------


! With CG 

LEXICON GIEVRRA !!= @CODE@ Adjectives with attribute in WeG and -s. As 1a in Spiik. Sg Acc: gievrav, Attr: gievras.

                    :%>Q1s ATTR              ;
                    :%>    NE-EVENCASEadj    ;
                    :%>X4  ILL-EVENCASEadj   ;
                    :%>Q1  N-EVENWEAKSTEMadj ;
                    :%>Q1  BU/MUS            ;
  +Der5+Der/AAdv+Adv:%>Q1t K-CONS            ;
                    :%>    MUORRACmp         ;

! Test data:
!!€gt-norm: gárttje # Even-syllable test
!!€ gárttje:             gárttje+A+Sg+Nom
!!€ gártjev:             gárttje+A+Sg+Acc
!!€ gártjes:             gárttje+A+Attr
!!€ gártjep:             gárttje+A+Der/Comp+A+Sg+Nom 



LEXICON NUORRA !!= @CODE@  Adjectives with attribute same as pred. As 1b in Spiik. Sg Acc: nuorav, Attr: nuorra.
                    :%>    ATTR                ;
                    :%>    N-EVENSTRONGSTEMadj ;
                    :%>Q1  N-EVENWEAKSTEMadj   ; ! cg in nominal paradigm
                    :%>Q1  BU/MUS              ;
  +Der5+Der/AAdv+Adv:%>Q1t K-CONS              ; ! Presumably WeG as adverb as well
                    :%>    MUORRACmp           ;

! Test data:
!!€gt-norm: visská # Even-syllable test
!!€ visská:             visská+A+Sg+Nom
!!€ viskáv:             visská+A+Sg+Acc
!!€ visská:             visská+A+Attr
!!€ viskáp:             visská+A+Der/Comp+A+Sg+Nom 



LEXICON GALLJE !!= @CODE@  Adjectives on -e, the attribute is in WeG and e > a. As 1d in Spiik. Sg Acc: galjev, Attr: galja.
                 :a%>Q1  ATTR              ; ! a-attr.
!     :es%>Q1 ATTR ; ! e-attr. !These now hardcoded
! :a%>Q1 VUOHTA ; !moved to ATTR
! :es%>Q1 VUOHTA ; !moved to ATTR
                 :e%>    NE-EVENCASEadj    ; ! presumably normal adjective
                 :e%>X4  ILL-EVENCASEadj   ;
                 :e%>Q1  N-EVENWEAKSTEMadj ; ! behaviour, with cg, and with
                 :e%>Q1  BU/MUS            ; ! the e-form as a starting +A 
 +Der5+Der/AAdv+Adv:e%>Q1t K-CONS            ; ! point.
                 :e%>    MUORRACmp         ;

! Test data:
!!€gt-norm: uhttse # Even-syllable test
!!€ uhttse:             uhttse+A+Sg+Nom
!!€ uhtsev:             uhttse+A+Sg+Acc
!!€ uhtsa:              uhttse+A+Attr
!!€ uhtses:             uhttse+A+Attr # gets this attr from 
!!€ uhtsep:             uhttse+A+Der/Comp+A+Sg+Nom 

LEXICON TJÁBBE !!= @CODE@  Adjectives on -e, the attribute is in WeG and e > a. Same as GALLJE only different adv derivation. Sg Acc: tjáppev, Attr: tjáppa.
                 :a%>Q1 ATTR              ; ! a-attr.
!     :es%>Q1 ATTR ; ! e-attr. !These now hardcoded
! :a%>Q1 VUOHTA ; !moved to ATTR
! :es%>Q1 VUOHTA ; !moved to ATTR
                 :e%>   NE-EVENCASEadj    ; ! presumably normal adjective
                 :e%>X4 ILL-EVENCASEadj   ;
                 :e%>Q1 N-EVENWEAKSTEMadj ; ! behaviour, with cg, and with
                 :e%>Q1 BU/MUS            ; ! the e-form as a starting
 +Der5+Der/AAdv+Adv:á%>t  K-CONS            ; ! point.
                 :e%>   MUORRACmp         ;

! Test data:
!!€gt-norm: njálgge # Even-syllable test
!!€ njálgge:             njálgge+A+Sg+Nom
!!€ njálgev:             njálgge+A+Sg+Acc
!!€ njálga:              njálgge+A+Attr
!!€ njálgep:             njálgge+A+Der/Comp+A+Sg+Nom 




! Without CG 

LEXICON VILLDA !!= @CODE@  Adjectives with attribute same as pred, without CG. As 1b in Spiik. Sg Acc: nuorav, Attr: nuorra.
                    :%>    ATTR                ;
                    :%>    N-EVENSTRONGSTEMadj ;
                    :%>Q4  N-EVENWEAKSTEMadj   ; ! 
                    :%>Q4  BU/MUS              ;
  +Der5+Der/AAdv+Adv:%>Q4t K-CONS              ; ! 
                    :%>    MUORRACmp           ;

! Test data:
!!€gt-norm: frisska # Even-syllable test
!!€ frisska:              frisska+A+Sg+Nom
!!€ frisskav:             frisska+A+Sg+Acc
!!€ frisska:              frisska+A+Attr
!!€ frisskap:             frisska+A+Der/Comp+A+Sg+Nom 

LEXICON HÁVSSKE !!= @CODE@  Adjectives with attribute -s, without WeG. As 1c in Spiik. Sg Acc: hávsskev, Attr: hávsskes.
                    :%>s     ATTR              ;
                    :%>      NE-EVENCASEadj    ;
                    :%>X4    ILL-EVENCASEadj   ;
                    :%>Q4    N-EVENWEAKSTEMadj ; ! 
                    :%>Q4    BU/MUS            ;
  +Der5+Der/AAdv+Adv:%>Q4t   K-CONS            ;
                    :%>      MUORRACmp         ;

! Test data:
!!€gt-norm: hoallá # Even-syllable test
!!€ hoallá:              hoallá+A+Sg+Nom
!!€ hoalláv:             hoallá+A+Sg+Acc
!!€ hoallás:             hoallá+A+Attr
!!€ hoalláp:             hoallá+A+Der/Comp+A+Sg+Nom 

LEXICON TJUODDJE !!= @CODE@ Adjectives with attribute -is, without WeG. presently only "Tjuoddje" Sg Acc: tjuoddjev, Attr: tjuoddjis.
                   :i%>s   ATTR              ;
                   :e%>    NE-EVENCASEadj    ;
                   :e%>X4  ILL-EVENCASEadj   ;
                   :e%>Q4  N-EVENWEAKSTEMadj ; ! Presumably WeG in nominal paradigm
                   :e%>Q4  BU/MUS            ;
 +Der5+Der/AAdv+Adv:e%>Q4t K-CONS            ;
                   :e%>    MUORRACmp         ;


! Test data:
!!€gt-norm: tjuoddje # Even-syllable test
!!€ tjuoddje:              tjuoddje+A+Sg+Nom
!!€ tjuoddjev:             tjuoddje+A+Sg+Acc
!!€ tjuoddjis:             tjuoddje+A+Attr
!!€ tjuoddjep:             tjuoddje+A+Der/Comp+A+Sg+Nom 



!!
!! Pres.participles 
!!

LEXICON SÁVADAHTTE  !!= @CODE@ Causative-participles. No attribute. No comparision. As 1e in Spiik. Sg Acc: sávadahttev. PrsPrc of causative verbs "uttrykker at handlingen lar seg gjøre eller er verdt å gjøre" (Kintel 1991).
 :%>   NE-EVENCASEadj    ;
 :%>X4 ILL-EVENCASEadj   ;
 :%>Q4 N-EVENWEAKSTEMadj ;
 :%>   MUORRACmp         ;
 :%>  ATTR_PrsPrc        ;
 
! Test data:
!!€gt-norm: vuojedahtte # Even-syllable test
!!€ vuojedahtte:             vuojedahtte+A+Sg+Nom
!!€ vuojedahttev:            vuojedahtte+A+Sg+Acc

LEXICON JUHKKE  !!= @CODE@ participles with -s attributive. No comparision As 1e in Spiik. Sg Acc: juhkkev, Attr: juhkkes. Spiik: presens particip har med den attributive formen på -s betydelsen "någon som är duktig i, snabb til att, begiven att utföra handlingen".
      :%>s  ATTR_PrsPrc       ;
      :%>   NE-EVENCASEadj    ;
      :%>X4 ILL-EVENCASEadj   ;
      :%>Q4 N-EVENWEAKSTEMadj ;
      :%>   MUORRACmp         ;

! Test data:
!!€gt-norm: vuohttje # Even-syllable test
!!€ vuohttje:              vuohttje+A+Sg+Nom
!!€ vuohttjev:             vuohttje+A+Sg+Acc
!!€ vuohttjes:             vuohttje+A+Attr

LEXICON BÅRRE  !!= @CODE@ participles without the -s attributive. As 1e in Spiik. Sg Acc: bårrev, Attr: bårre. Spiik: presens particip har med den attributiva formen utan -s betydelsen ºdem  som utför handlingenº.
      :%>   ATTR_PrsPrc       ;
      :%>   NE-EVENCASEadj    ;
      :%>X4 ILL-EVENCASEadj   ;
      :%>Q4 N-EVENWEAKSTEMadj ;
      :%>   MUORRACmp         ;

! Test data:
!!€gt-norm: ednabårre # Even-syllable test
!!€ ednabårre:              ednabårre+A+Sg+Nom
!!€ ednabårrev:             ednabårre+A+Sg+Acc
!!€ ednabårre:              ednabårre+A+Attr


!LEXICON DÁBBELIJ !!= @CODE@ presenty only for "Dábbelij". Sg Acc: dábbelaháv, Attr: dábbelij. Similar forms are analyzed as adpositions and adverbss (dábbela, dábbelin). This lexica is thus outcommented as it is not an adjective.
!                 +Sg+Nom:ij%>   K                 ;
!                       :ij%>   ATTR              ;
! :ij VUOHTA ; !moved to ATTR
! +Sg+Nom+Err/Orth:ahá%>  PxV               ; !
!                    +Ess:ahá%>n K-CONS            ;
!                        :ahá%>  ILL-EVENCASEadj   ;
!                        :ahá%>  N-EVENWEAKSTEMadj ;
!        +Der5+Der/AAdv+Adv:ahá%>t K-CONS            ;
!                        :ahá%>  BU/MUS            ;
!               +Cmp/SgGen:ahá%>  R                 ;
!               +Cmp/PlGen:ahá%>j R                 ;

!! Test data:
!!!€gt-norm: dábbelij # Even-syllable test
!!!€ dábbelij             dábbelij+A+Sg+Nom
!!!€ dábbelaháv           dábbelij+A+Sg+Acc
!!!€ dábbelij             dábbelij+A+Attr
!!!€ dábbelaháp           dábbelij+A+Der/Comp+A+Sg+Nom 


!!
!! Loan words lexicas
!!


!! ###  Correctly assimilated loanwords, derived from real noun.


LEXICON METÅVDÅLASJ !!= @CODE@ LOAN! Foreign -isk adjectives adapted in updated normative way. To smj ending -alasj, adjective is truly derived from a noun. Mekanisk-mekanihkka-mekanihkalasj, instead of mekánalasj that goes to MEKÁNALASJ_BADASS. Pred and attr are both -alasj. Attr same as pred. With comparatives. 

+OLang/NOB+CmpNP/Last: METÅVDÅLASJ_CMP_INFL ;


LEXICON METÅVDÅLASJ_CMP_INFL !!= @CODE@

                        :asj%>      ATTR              ;
                 +Sg+Nom:asj%>      K-CONS            ;
                 +Sg+Ill:attja%>j   K-CONS            ;
        +Err/Orth+Sg+Ill:adtja%>j   K-CONS            ;
                    +Ess:attja%>n   K-CONS            ;
           +Err/Orth+Ess:adtja%>n   K-CONS            ;
                        :attja%>    N-EVENWEAKSTEMadj ; !This seems right. TO
                        :%>         ABBO/AMOS         ;
      +Der5+Der/AAdv+Adv:attja%>t   K-CONS            ;
              +Cmp/SgNom:asj%>      R                 ;
              +Cmp/SgGen:lattja%>   R                 ;
              +Cmp/PlGen:lattja%>j  R                 ;
      
        +Err/Orth+Sg+Ill:attja%>s%> PxCi              ; !
        +Err/Orth+Sg+Nom:attja%>    PxV               ; !
        +Err/Orth+Sg+Ill:adtja%>s%> PxCi              ; !
        +Err/Orth+Sg+Nom:adtja%>    PxV               ; !                
                
           +Err/Orth+Sg+Ill:ahttja%>j   K-CONS            ;
              +Err/Orth+Ess:ahttja%>n   K-CONS            ;
                  +Err/Orth:ahttja%>    N-EVENWEAKSTEMadj ; 
+Err/Orth+Der5+Der/AAdv+Adv:ahttja%>t   K-CONS            ;               

! Test data:
!!€gt-norm: kapitalismalasj # Even-syllable test
!!€ kapitalismalasj:             kapitalismalasj+A+Sg+Nom
!!€ kapitalismalattjav:          kapitalismalasj+A+Sg+Acc
!!€ kapitalismalasj:             kapitalismalasj+A+Attr
!!€ kapitalismalabbo:            kapitalismalasj+A+Der/Comp+A+Sg+Nom 


LEXICON MEKANIHKA_MEKANIJKA_LASJ !!= @CODE@ LOAN! Same type of adjectives as METÅVDÅLASJ, only for adjektives that become mekanihkalasj in norway and mekanijkalasj in sweden, because of differences mekanik vs mekanikk>mekanijkka vs mekanihkka.  Attr same as pred. With comparatives. 

+OLang/NOB+CmpNP/Last+Area/NO:ihkal%> METÅVDÅLASJ_CMP_INFL ;
+OLang/NOB+CmpNP/Last+Area/SE:ijkal%> METÅVDÅLASJ_CMP_INFL ;


LEXICON IJJALASJ !!= @CODE@ Just lik  METÅVDÅLASJ only for words ending on ijjalasj/iddjalasj, so that we don't need a lot of Area and Err tags in stems file.

+OLang/NOB+CmpNP/Last: IJJALASJ_CMP_INFL ;

LEXICON IJJALASJ_CMP_INFL !!= @CODE@

         +Area/NO:ijjal   METÅVDÅLASJ_CMP_INFL ;
         +Area/SE:idjal   METÅVDÅLASJ_CMP_INFL ;
        +Err/Orth:ial     METÅVDÅLASJ_CMP_INFL ;
        +Err/Orth:iddjal  METÅVDÅLASJ_CMP_INFL ;
        +Err/Orth:ijal    METÅVDÅLASJ_CMP_INFL ;
        +Err/Orth:al      METÅVDÅLASJ_CMP_INFL ;
  

LEXICON OGIJJALASJ !!= @CODE@ Just like IJJALASJ only for words ending on ogijjalasj/ogiddjalasj, so that we don't need a lot of err tags in stems files. For words like "pedagogijjalasj" which also have "pedagåvgålasj" (not really a wrong derivation, but doesn't mean pedagogisk) and "pedagogalasj" err taged.  
 
+OLang/NOB+CmpNP/Last: OGIJJALASJ_CMP_INFL ;
  
LEXICON OGIJJALASJ_CMP_INFL !!= @CODE@ 
  
  +Area/NO:ogijjal   METÅVDÅLASJ_CMP_INFL ;
  +Area/SE:ogidjal   METÅVDÅLASJ_CMP_INFL ;
 +Err/Orth:ogiddjal  METÅVDÅLASJ_CMP_INFL ;
 +Err/Orth:ogijal    METÅVDÅLASJ_CMP_INFL ;
 +Err/Orth:ogial     METÅVDÅLASJ_CMP_INFL ;
 +Err/Orth:ogal      METÅVDÅLASJ_CMP_INFL ;
 +Err/Orth:åvgål     METÅVDÅLASJ_CMP_INFL ; !Not really a wrong derivation but it means someting else than the norwegian -isk adjective. "Pedagåvgålasj" we get from the noun pedagåvggå. 

LEXICON SJÅNÅLASJ_SJONAL !!= @CODE@ -sjonal/sjonell and -tional/tionel loanwords. Only for words that work as nouns, so that they are REAL dervations, as nasjonal-nasjåvnnå-nasjåvnålasj. NOT for words like "rasjonell", with no real noun. Words as "rasjonell>rasjonálla-rasjonálalasj" go to lexicon ÁLLA.  The fake derivation "nasjonálalasj" is err taged, so is the strange "nasjonálla/nasjunálla". 

+OLang/NOB: SJÅNÅLASJ_SJONAL_CMP_INFL ;

LEXICON SJÅNÅLASJ_SJONAL_CMP_INFL !!= @CODE@ 

                            +CmpNP/Last:sjåvnål       METÅVDÅLASJ_CMP_INFL ;
        +Err/Orth+Use/-Spell+CmpNP/Last:sjon         ÁLA_LASJ_INFL_CMP  ; ! Makes BADASS "nasjonálalasj/nasjoalaladj,nasjonállalasj,nasjonállá, nasjonalla, nasjonella, nasjnælla"
        +Err/Orth+Use/-Spell+CmpNP/Last:sjun         ÁLA_LASJ_INFL_CMP  ; ! 
+Err/Orth+Area/SE+Use/-Spell+CmpNP/Last:tion         ÁLA_LASJ_INFL_CMP  ; ! 

                     +CmpNP/Pref+Cmp/Sh:sjonal%> R    ; !Short cmp "nasjonalvárre"
            +Err/Orth+CmpNP/Pref+Cmp/Sh:sjonál%> R    ; !Short cmp
            +Err/Orth+CmpNP/Pref+Cmp/Sh:sjunál%> R    ; !Short cmp


! Test data:
!!€gt-norm: konstitusjåvnålasj # Even-syllable test
!!€ nasjåvnålasj:             nasjåvnålasj+A+Sg+Nom
!!€ nasjåvnålattjav:          nasjåvnålasj+A+Sg+Acc
!!€ nasjåvnålasj:             nasjåvnålasj+A+Attr
  


LEXICON SJÅNÅLASJ_SJONELL !!= @CODE@ -sjonal/sjonell and -tional/tionel loanwords. Only for words that work as nouns, so that they are REAL dervations, as nasjonal-nasjåvnnå-nasjåvnålasj. NOT for words like "rasjonell", with no real noun. Words as "rasjonell>rasjonálla-rasjonálalasj" go to lexicon ÁLLA.  The fake derivation "nasjonálalasj" is err taged, so is the strange "nasjonálla/nasjunálla". 

+OLang/NOB+CmpNP/Last: SJÅNÅLASJ_SJONELL_CMP_INFL ;

LEXICON SJÅNÅLASJ_SJONELL_CMP_INFL !!= @CODE@ 

                            :sjåvnål       METÅVDÅLASJ_CMP_INFL ;
                   +Err/Orth:sjon         ELLA_LASJ_INFL_CMP     ; ! ! Makes strange "emosjonellalasj/emosjonælla/emosjonálla/emosjonella
                   +Err/Orth:tion         ELLA_LASJ_INFL_CMP     ; ! 
                   +Err/Orth:sjun         ELLA_LASJ_INFL_CMP     ; ! 
        +Err/Orth+Use/-Spell:sjonálal      METÅVDÅLASJ_CMP_INFL ; ! Makes BADASS "emosjonálalasj"
        +Err/Orth+Use/-Spell:sjunálal      METÅVDÅLASJ_CMP_INFL ; ! Makes BADASS "emosjunálalasj"

! Test data:
!!€gt-norm: konstitusjåvnålasj # Even-syllable test
!!€ konstitusjåvnålasj:             konstitusjåvnålasj+A+Sg+Nom
!!€ konstitusjåvnålattjav:          konstitusjåvnålasj+A+Sg+Acc
!!€ konstitusjåvnålasj:             konstitusjåvnålasj+A+Attr


!! ###  Badly assimilated loanwords, some against norm others with no norm

LEXICON MEKÁNALASJ_BADASS !!= @CODE@ LOAN! Wronly assimilated -lasj adjectives from SE/NO -isk. Looks derived but isn't since there is no real noun to be derived from. Like mekanisk-mekánalasj, but "mekádna" is no real noun! Like  METÅVDÅLASJ, but gives the Err/Der tag, so it's only for these wronly/non-derived loan adjectives. 

+OLang/NOB+CmpNP/Last+Err/Der: MEKÁNALASJ_CMP_INFL ;

 

LEXICON ARKTALASJ_NOT_DERIVED
+OLang/NOB+CmpNP/Last: ARKTALASJ_CMP_INFL ;

LEXICON ARKTALASJ_CMP_INFL !!= @CODE@ Foreign -isk, that are not real derivations. Same as MEKÁNALASJ_BADASS, but no +Use/-Spell tag since ther is no "right" way to assimilate these. This is a question for GG. Adapted to smj by simply adding -alasj in place of -isk. These are not real derivations, but sitation borrowed loan adjectives. Only words without a noun base, like arktisk and syntetisk. Pred and attr are both -lasj. No comparatives. 

! :asj VUOHTA ; !moved to ATTR

! : VUOHTA ; !moved to ATTR
                          :%>     ATTR_PrsPrc   ; ! No vuohta derivation in this lexicon
                   +Sg+Nom:%>     K-CONS        ;
          +Sg+Nom+Err/Orth:a%>    PxV           ; !
                          :a%>    SURGULASJ-EVEN ; ! Declined as four-syllable noun
! :a%>Q1 BU/MUS ;
         +Der5+Der/AAdv+Adv:a%>Q1t K-CONS        ;
                 +Cmp/SgNom:%>     R             ;
                 +Cmp/SgGen:a%>Q1  R             ;
                 +Cmp/PlGen:a%>Q1j R             ;

! Test data:
!!€gt-norm: syntetalasj # Even-syllable test
!!€ syntetalasj:           syntetalasj+A+Sg+Nom
!!€ syntetalattjav:        syntetalasj+A+Sg+Acc
!!€ syntetalasj:           syntetalasj+A+Attr

LEXICON ORÁNSSJA !!= @CODE@ Loan adjectives, not -isk. Used without the -lasj. Adjectives with attribute same as pred. So far only for oránssja.

+OLang/NOB+CmpNP/Last: NUORRA ; 

LEXICON DEMONSTRATIJVA_LASJ_NO_NORM !!= @CODE@ Loan adjectives from norwegian/swedish (Not adjectives ending on -isk). Words like demonstrativ, transitiv, dupleks, informativ, analog, privat. Gives both "demonstratijvva" and "demonstratijvalasj". Two ways of adapting these adjectives are used, the adding of -lasj isn't okey, because that's a false derivation. But GG hasn't decided how these should be handled. Looks like noun instead of adjective when adapted without the -lasj ending.  Attr is in weak grad, used in strong grad ass pred even thou this seems a little bit odd "Værbba l transitijvva". 


+OLang/NOB+CmpNP/Last: DEMONSTRATIJVA_LASJ_CMP_INFL ;


LEXICON DEMONSTRATIJVA_LASJ_CMP_INFL !!= @CODE@   

                      :Q1   ATTR_PrsPrc         ; ! No vuohta derivation in this lexicon
                      :     N-EVENSTRONGSTEMadj ;
                      :Q1   N-EVENWEAKSTEMadj   ; ! cg in nominal paradigm
                      :%>Q1 BU/MUS              ;
    +Der5+Der/AAdv+Adv:Q1t  K-CONS              ; ! Presumably WeG as adverb as well
                      :     MUORRACmp           ;

 +Use/NotDNorm+Err/Der:X7l  MEKÁNALASJ_CMP_INFL ; !Err/der-taged since -lasj derivation isn't possible since there is no "demonstratijvva" noun. -lasj w.grade and vowel changes (not used since all lemmas end on letter a) demostratijvalasj
      +Err/Orth+Use/NG:l    MEKÁNALASJ_CMP_INFL ; ! Err/Orth-lasj with strong grade, demonstratijvvalasj


!            +Sg+Nom:Q1lasj%>      K-CONS              ;
!                   :Q1lasj%>      ATTR                ;
!   +Err/Orth+Sg+Ill:Q1ladtja%>j   K-CONS              ;
!      +Err/Orth+Ess:Q1ladtja%>n   K-CONS              ;
!            +Sg+Ill:Q1lattja%>j   K-CONS              ;
!               +Ess:Q1lattja%>n   K-CONS              ;
!                   :Q1lattja%>    N-EVENWEAKSTEMadj   ;
!                   :Q1l%>         ABBO/AMOS           ;
! +Der5+Der/AAdv+Adv:Q1lattja%>t   K-CONS              ;
!         +Cmp/SgNom:Q1lasj%>      R                   ;
!         +Cmp/SgGen:Q1lattja%>    R                   ;
!         +Cmp/PlGen:Q1lattja%>j   R                   ;
!   +Err/Orth+Sg+Nom:Q1ladtja%>    PxV                 ; !
!   +Err/Orth+Sg+Ill:Q1ladtja%>s%> PxCi                ; !
!            +Sg+Nom:Q1lattja%>    PxV                 ; !
!            +Sg+Ill:Q1lattja%>s%> PxCi                ; !


! Test data:
!!€gt-norm: aktijvva # Even-syllable test
!!€ aktijva:                    aktijvva+A+Attr
!!€ aktijvva:                   aktijvva+A+Sg+Nom
!!€ aktijvav:                   aktijvva+A+Sg+Acc






LEXICON ÁLA_LASJ_NO_NORM !!= @CODE@ Same as DEMONSTRATIJVA_LASJ_NO_NORM. Only for adjectives ending on -al. Words like digital,liberal, lokal.  Gives both "eksponentiálla" and "eksponentiálalasj". Different lexicon for these -al adjectives because of Err/Orth tags. OBS, "dialektal", is assimilated "dialevtalasj", and goes to lexicon  METÅVDÅLASJ.

+OLang/NOB+CmpNP/Last: ÁLA_LASJ_INFL_CMP ;

LEXICON ÁLA_LASJ_INFL_CMP !!= @CODE@

            +Use/NotDNorm+Err/Der:álal           MEKÁNALASJ_CMP_INFL ; !No possible "digiálalasj" derivation since digitálla isn't a noun.
                                 :áll            ÁLLA-ÆLLA           ;

                        +Err/Orth:all            ÁLLA-ÆLLA           ;
                        +Err/Orth:elºl           ÁLLA-ÆLLA           ;
                +Err/Orth+Area/NO:æ9lºl          ÁLLA-ÆLLA           ;
         +Err/Orth+Use/NG+Area/SE:ä9lºl          ÁLLA-ÆLLA           ;
                        +Err/Orth:alal           MEKÁNALASJ_CMP_INFL ;
                        +Err/Orth:állal          MEKÁNALASJ_CMP_INFL ;
!                +Sg+Nom+Err/Orth:álalasj%>      K-CONS              ;
!                       +Err/Orth:álalasj%>      ATTR                ;
!                       +Err/Orth:álalattja%>    N-EVENWEAKSTEMadj   ;
!                   +Ess+Err/Orth:álalattja%>n   K-CONS              ;
!          +Ess+Err/Orth+Err/Orth:álaladtja%>n   K-CONS              ;
!                +Sg+Ill+Err/Orth:álalattja%>j   K-CONS              ;
!       +Sg+Ill+Err/Orth+Err/Orth:álaladtja%>j   K-CONS              ;
! !                     +Err/Orth:álal%>         ABBO/AMOS           ;
!     +Der5+Der/AAdv+Adv+Err/Orth:álalattja%>t   K-CONS              ;
!             +Cmp/SgNom+Err/Orth:álalasj%>      R                   ;
!             +Cmp/SgGen+Err/Orth:álalattja%>    R                   ;
!             +Cmp/PlGen+Err/Orth:álalattja%>j   R                   ;
!                +Sg+Nom+Err/Orth:álaladtja%>    PxV                 ; ! Not possible with possesive suffixes, Case?
!                +Sg+Nom+Err/Orth:álalattja%>    PxV                 ; ! Not possible with possesive suffixes, Case?
!                +Sg+Ill+Err/Orth:álaladtja%>s%> PxCi                ; ! Not possible with possesive suffixes
!                +Sg+Ill+Err/Orth:álalattja%>s%> PxCi                ; ! Not possible with possesive suffixes



LEXICON ELLA_LASJ_NO_NORM !!= @CODE@ Loanwords, same as ÁLA_LASJ_NO_NORM  and  DEMONSTRANTIJVA_LASJ_NO_NORM. For NO and SE adjectives ending on -ell, eksperimentell, ideell, parallell. The short form is nom parallælla, attr, parallella The long form: paralellalasj, attr parallellalasj. Different lexicon for these -ell adjectives because of err/orth tags. OBS, "individuell", is assimilated "indivijdalasj", and goes to lexicon  METÅVDÅLASJ.

+OLang/NOB+CmpNP/Last: ELLA_LASJ_INFL_CMP ;

LEXICON ELLA_LASJ_INFL_CMP !!= @CODE@ 

  +Use/NotDNorm+Err/Der:ellal MEKÁNALASJ_CMP_INFL ; !No possible "ideellalasj" derivation since "ideælla" isn't a noun.
                       :ælºl  ÁLLA-ÆLLA           ;

              +Err/Orth:álal  MEKÁNALASJ_CMP_INFL ;
              +Err/Orth:alal  MEKÁNALASJ_CMP_INFL ;
              +Err/Orth:áll   ÁLLA-ÆLLA           ;
              +Err/Orth:elºl  ÁLLA-ÆLLA           ;
              +Err/Orth:ell   ÁLLA-ÆLLA           ;



LEXICON ÁLLA-ÆLLA !!= @CODE@
                   :a%>Q1  ATTR_PrsPrc         ; ! No vuohta derivation in this lexicon
                   :a%>    N-EVENSTRONGSTEMadj ;
                   :a%>Q1  N-EVENWEAKSTEMadj   ; ! cg in nominal paradigm
                   :a%>Q1    BU/MUS                  ;
                   :a%>    MUORRACmp           ;
 +Der5+Der/AAdv+Adv:a%>Q1t K-CONS             ; !
  
LEXICON MEKÁNALASJ_CMP_INFL !!= @CODE@ Same as  METÅVDÅLASJ only without vuohta. 

                        :asj%>      ATTR_PrsPrc       ; ! No vuohta derivation in this lexicon
                 +Sg+Nom:asj%>      K-CONS            ;
                 +Sg+Ill:attja%>j   K-CONS            ;
        +Err/Orth+Sg+Ill:adtja%>j   K-CONS            ;
                    +Ess:attja%>n   K-CONS            ;
           +Err/Orth+Ess:adtja%>n   K-CONS            ;
                        :attja%>    N-EVENWEAKSTEMadj ; 
                        :%>         ABBO/AMOS         ;
      +Der5+Der/AAdv+Adv:attja%>t   K-CONS            ;
              +Cmp/SgNom:asj%>      R                 ;
              +Cmp/SgGen:lattja%>   R                 ;
              +Cmp/PlGen:lattja%>j  R                 ;
      
        +Err/Orth+Sg+Ill:attja%>s%> PxCi              ; !
        +Err/Orth+Sg+Nom:attja%>    PxV               ; !
        +Err/Orth+Sg+Ill:adtja%>s%> PxCi              ; !
        +Err/Orth+Sg+Nom:adtja%>    PxV               ; !  



! LEXICON DEMONSTRATIJVALASJ !!= @CODE@ Lexicon DEMONSTRATIJVA_LASJ now gives both demonstratijvva and demonstratijvalasj
! +OLang/NOB+CmpNP/Last: DEMONSTRATIJVALASJ_CMP_INFL ;
! 							
! LEXICON DEMONSTRATIJVALASJ_CMP_INFL !!= @CODE@  Look like derived loan adjectives. However, they are not derived. Rather, these are "long" forms of short adjektives as those in DEMONSTRATIJVA and thus not true derivations. Same NO words as i DEMONSTRATIJVA, only another way of making the fit into sami, guess it's because of the odd predicative.  Odd-syllable comparison. Sg Acc: destruktijvalattjav, Attr: destruktijvalasj. (The -lasj is unpopular.)
! ! :asj VUOHTA ; !moved to ATTR
!                  +Sg+Nom:lasj%>      K-CONS            ;
!                         :lasj%>      ATTR              ;
!         +Err/Orth+Sg+Ill:ladtja%>j   K-CONS            ;
!            +Err/Orth+Ess:ladtja%>n   K-CONS            ;
!                  +Sg+Ill:lattja%>j   K-CONS            ;
!                     +Ess:lattja%>n   K-CONS            ;
!                         :lattja%>    N-EVENWEAKSTEMadj ;
!                         :l%>         ABBO/AMOS         ;
!         +Der5+Der/AAdv+Adv:lattja%>t   K-CONS            ;
!                +Cmp/SgNom:lasj%>      R                 ;
!                +Cmp/SgGen:lattja%>    R                 ;
!                +Cmp/PlGen:lattja%>j   R                 ;
!          +Err/Orth+Sg+Nom:ladtja%>    PxV               ; !
!          +Err/Orth+Sg+Ill:ladtja%>s%> PxCi              ; !
!                   +Sg+Nom:lattja%>    PxV               ; !
!                   +Sg+Ill:lattja%>s%> PxCi              ; !
! 							
! 							


!LEXICON A_ADJEKTIV ! now in DEMONSTRATIJVA_LASJ
!+OLang/NOB: ADJEKTIJVVA_CMP_INFL ;
!
!LEXICON A_TABLETT ! now in DEMONSTRATIJVA_LASJ
!+OLang/NOB: TABLÆHTTA_CMP_INFL ;
!
!
! LEXICON AALASJ
!           +OLang/NOB+CmpNP/Last:åladtj  METÅVDÅLASJ_CMP_INFL ;
!  +Err/Orth+OLang/NOB+CmpNP/Last:aladtj  METÅVDÅLASJ_CMP_INFL ;
!                     +CmpNP/Last:åladtj ASIDASJ_CMP_INFL      ;
!                     +CmpNP/Last:åladtj DÁRBULASJ_CMP_INFL    ;
!                     +CmpNP/Last:åladtj ÅLLAGSJ_CMP_INFL      ;
! 
! LEXICON ULASJ_DÁRBULASJ
! +CmpNP/Last:uladtj DÁRBULASJ_CMP_INFL ;
! +CmpNP/Last:uladtj ÅLLAGSJ_CMP_INFL ;
! +CmpNP/Last:uladtj ASIDASJ_CMP_INFL  ;
! +Err/Orth+CmpNP/Last:oladtj DÁRBULASJ_CMP_INFL ;
 
!   DÁRBULASJ ; !Err/Orth
!   DÁRBULASJ ;
! +CmpNP/Last: DÁRBULASJ_CMP_INFL ;
! 
!   
!
!
! LEXICON ALASJ
!   +Area/NO+OLang/NOB+CmpNP/Last:aladtj
!   +Area/SE+OLang/NOB+CmpNP/Last:aladtj
!
!   DEMONSTRATIJVALASJ ; ! LOAN
! +OLang/NOB+CmpNP/Last: DEMONSTRATIJVALASJ_CMP_INFL ;
!   ARKTALASJ ;
! +OLang/NOB+CmpNP/Last: ARKTALASJ_CMP_INFL ;
!   ÅLLAGASJ ;
! +CmpNP/Last: ÅLLAGSJ_CMP_INFL ;
!   
!
!
! LEXICON ELASJ
!   +Area/NO+OLang/NOB+CmpNP/Last:eladtj
!   +Area/SE+OLang/NOB+CmpNP/Last:eladtj
!
!   ÅLLAGASJ ; 
! +CmpNP/Last: ÅLLAGSJ_CMP_INFL ;
!   ASIDASJ ; !
! +CmpNP/Last: ASIDASJ_CMP_INFL  ;
!   DÁRBULASJ ;
! +CmpNP/Last: DÁRBULASJ_CMP_INFL ;
!   
!
!
! LEXICON ÁLASJ
!   +Area/NO+OLang/NOB+CmpNP/Last:áladtj
!   +Area/SE+OLang/NOB+CmpNP/Last:áladtj
!
!   ÅLLAGASJ ; !
! +CmpNP/Last: ÅLLAGSJ_CMP_INFL ;
!   DÁRBULASJ ;
! +CmpNP/Last: DÁRBULASJ_CMP_INFL ;
!   
!
!
! LEXICON OLASJ - eksisterer ikke slike ord?! 
!  +Area/NO+OLang/NOB+CmpNP/Last:oladtj
!  +Area/SE+OLang/NOB+CmpNP/Last:oladtj
!  Funnet to ord, men to ulike lexicon.
! girkkolasj+Sem/Dummytag:girkkoladtj ÅLLAGASJ ;
! oavdolasj+CmpN/SgN+CmpN/PlG+Sem/Dummytag:oavdol DÁRBULASJ ; !


!!
!! Inherent comparatives and superlatives lexica
!!

LEXICON OANEP  !!= @CODE@ Inherent comparatives, gives comp and superl. There are two main groups of word here: Adjectives that are lexicalized in their comparative (and superlative) forms, like sisŋep, bárep. And Nouns that can be compared, like nuortap, gáttep, oarjep (some of these are compared from their noun lexicas and thus are found twice). Some entries are likely incorrect compared forms of other adjectives, like ådåp and ruvvap (more research needed).


                                    +Gram/Comp:           BUStem       ;
                            +Der1+Der/Superl+A:muss       BUOREMUS     ;
                  +Gram/Comp+Der5+Der/AAdv+Adv:but        K-CONS       ;
          +Der1+Der/Superl+A+Der5+Der/AAdv+Adv:musá%>t    K-CONS       ;
            +Gram/Comp+Der2+Der/Dimin+A+Sg+Nom:bu»sj%>    K-CONS       ;
                   +Gram/Comp+Der2+Der/Dimin+A:bu»sj%>    ATTR         ;
                   +Gram/Comp+Der2+Der/Dimin+A:bu»ttja%>  CompSup-EVEN ;
+Err/Lex+Use/-Spell+Gram/Comp+Der2+Der/Dimin+A:bu»dtja%>  CompSup-EVEN ;
 +Gram/Comp+Der2+Der/Dimin+A+Der5+Der/AAdv+Adv:bu»ttja%>t K-CONS       ;


! Test data:
!!€gt-norm: lagáp # Even-syllable test
!!€ lagáp:             lagáp+A+Sg+Nom
!!€ lagámus:           lagáp+A+Der/Superl+A+Sg+Nom


LEXICON TJAVGGÁMUS   !!= @CODE@ Inherent superlatives, only gives superl. Some words are lexicalized in their superlative forms, like dájvvámus. Some are likely incorrect superlative forms, like tjábbámus (more research is needed)
                           
                    +Gram/Superl+Attr:muss%>  K-CONS            ;
                  +Gram/Superl+Sg+Nom:muss%>  K-CONS            ; ! the short form, twol shortens ss#
+Gram/Superl+Sg+Nom+Use/Marg+Err/Orth:mussa%> PxV               ; ! ! ! separate nom bec of the short form
                         +Gram/Superl:mussa%> BUOREMUS-OBL      ; ! Add -a, directly to the obliques
       +Gram/Superl+Der5+Der/AAdv+Adv:musá%>t K-CONS            ;

! Test data:
!!€gt-norm: dájvvámus # Even-syllable test
!!€ dájvvámus:           dájvvámus+A+Sg+Nom

!!
!! 4-syllable miscellanious stems
!!

LEXICON ÁRMMOGIS !!= @CODE@ Adjectives on -is, attribute same as pred. Odd-syllable comparison. As 2 in Spiik. Sg Acc: ármmogisáv, Attr: ármmogis. 
! :is VUOHTA ; !moved to ATTR
                        :is%>      ATTR          ; ! twol fixes the ss > s
                 +Sg+Nom:is%>      K-CONS        ;
        +Sg+Nom+Err/Orth:issa%>    PxV           ; !
                        :issa%>    ÁRMMOGIS-EVEN ; ! Declined as four-syllable noun
! :issa%>Q1 BU/MUS ; !this is wrong:ármmogisáp
                         :%>        ABBO/AMOS     ; !ármmogabbo
       +Der5+Der/AAdv+Adv:issa%>Q1t K-CONS        ;
               +Cmp/SgGen:issa%>Q1  R             ;
               +Cmp/PlGen:issa%>Q1j R             ;

! Test data:
!!€gt-norm: bahágis # Even-syllable test
!!€ bahágis:             bahágis+A+Sg+Nom
!!€ bahágisáv:           bahágis+A+Sg+Acc
!!€ bahágis:             bahágis+A+Attr
!!€ bahágabbo:           bahágis+A+Der/Comp+A+Sg+Nom 

LEXICON SÆHKÁLAK !!= @CODE@ Adjectives on -álak, attribute same as pred. Odd-syllable comparison. So far only for "sæhkálak".
! :is VUOHTA ; !moved to ATTR
                        :ak%>      ATTR          ; !
                 +Sg+Nom:ak%>      K-CONS        ;
        +Sg+Nom+Err/Orth:agá%>    PxV           ; !
                        :agá%>    ÁRMMOGIS-EVEN ; ! Declined as four-syllable noun
! :issa%>Q1 BU/MUS ; !this is wrong:ármmogisáp
                         :%>        ABBO/AMOS     ; !sæhkálabbo
               +Cmp/SgGen:lagá%>  R             ;
               +Cmp/PlGen:lagá%>j R             ;

! Test data:
!!€gt-norm: sæhkálak # Even-syllable test
!!€ sæhkálak:             sæhkálak+A+Sg+Nom
!!€ sähkálak:             sæhkálak+A+Sg+Nom
!!€ sæhkálagáv:           sæhkálak+A+Sg+Acc
!!€ sähkálagáv:           sæhkálak+A+Sg+Acc
!!€ sæhkálak:             sæhkálak+A+Attr
!!€ sähkálak:             sæhkálak+A+Attr
!!€ sæhkálabbo:           sæhkálak+A+Der/Comp+A+Sg+Nom 
!!€ sähkálabbo:           sæhkálak+A+Der/Comp+A+Sg+Nom 

LEXICON ÅLLAGASJ 
+CmpNP/Last: ÅLLAGSJ_CMP_INFL ;

LEXICON ÅLLAGSJ_CMP_INFL !!= @CODE@ Adjectives on -asj, attribute same as pred.  No comparatives. 2 in Spiik. Sg Acc: ållagattjav, Attr: ållagasj.
! : VUOHTA ; !moved to ATTR
                        :%>     ATTR          ;
                 +Sg+Nom:%>     K-CONS        ;
!        +Sg+Nom+Err/Orth:a%>    PxV           ; !
                        :a%>    SURGULASJ-EVEN ; ! Declined as four-syllable noun
! :a%>Q1 BU/MUS ;
        +Der5+Der/AAdv+Adv:a%>Q1t K-CONS        ;
!               +Cmp/SgNom:%>     R             ; !is only last cmp
!               +Cmp/SgGen:a%>Q1  R             ; !is only last cmp
!               +Cmp/PlGen:a%>Q1j R             ; !is only last cmp

! Test data:
!!€gt-norm: belulasj # Even-syllable test
!!€ belulasj:             belulasj+A+Sg+Nom
!!€ belulattjav:          belulasj+A+Sg+Acc
!!€ belulasj:             belulasj+A+Attr

LEXICON DÁRBULASJ 
+CmpNP/Last: DÁRBULASJ_CMP_INFL ;

LEXICON DÁRBULASJ_CMP_INFL !!= @CODE@ Adjectives on -asj, attribute same as pred. Odd-syllable comparison. Sg Acc: dárbulattjav, Attr: dárbulasj. Essive -attjan, -adtjan is subtaged. Err/Orth also -ahttja.  
! :asj VUOHTA ; !moved to ATTR
                 +Sg+Nom:asj%>      K-CONS            ;
                        :asj%>      ATTR              ;
                 +Sg+Ill:attja%>j   K-CONS            ;
        +Err/Orth+Sg+Ill:adtja%>j   K-CONS            ;
                    +Ess:attja%>n   K-CONS            ;
           +Err/Orth+Ess:adtja%>n   K-CONS            ;
                        :attja%>    N-EVENWEAKSTEMadj ; !This seems right. TO
                        :%>         ABBO/AMOS         ;
        +Der5+Der/AAdv+Adv:attja%>t   K-CONS            ;
!               +Cmp/SgNom:asj%>      R                 ;
!               +Cmp/SgGen:attja%>    R                 ;
!               +Cmp/PlGen:attja%>j   R                 ;
                 +Sg+Ill:attja%>s%> PxCi              ; !
                 +Sg+Nom:attja%>    PxV               ; !
        +Sg+Ill+Err/Orth:adtja%>s%> PxCi              ; !
        +Sg+Nom+Err/Orth:adtja%>    PxV               ; !


           +Err/Orth+Sg+Ill:ahttja%>j   K-CONS            ;
              +Err/Orth+Ess:ahttja%>n   K-CONS            ;
                  +Err/Orth:ahttja%>    N-EVENWEAKSTEMadj ; 
+Err/Orth+Der5+Der/AAdv+Adv:ahttja%>t   K-CONS            ;


! Test data:
!!€gt-norm: dábálasj # Even-syllable test
!!€ dábálasj:             dábálasj+A+Sg+Nom
!!€ dábálattjav:          dábálasj+A+Sg+Acc
!!€ dábálasj:             dábálasj+A+Attr
!!€ dábális:              dábálasj+A+Attr
!!€ dábálabbo:            dábálasj+A+Der/Comp+A+Sg+Nom 


LEXICON ASIDASJ
+CmpNP/Last: ASIDASJ_CMP_INFL  ;

LEXICON ASIDASJ_CMP_INFL !!= @CODE@ Adjectives on -asj, -is attr. Odd-syllable comparison. Sg Acc: asidattjav, Attr: asidis.
                        :asj        VUOHTA            ;
! :is VUOHTA ; !moved to ATTR
                        :is%>       ATTR              ; ! Special Attr form
                 +Sg+Nom:asj%>      K-CONS            ;
        +Err/Orth+Sg+Ill:adtja%>j   K-CONS            ;
                 +Sg+Ill:attja%>j   K-CONS            ;
                    +Ess:attja%>n   K-CONS            ;
           +Err/Orth+Ess:adtja%>n   K-CONS            ;
                        :attja%>    N-EVENWEAKSTEMadj ; !This seems right. TO
! :attja%> BU/MUS ; !unusual comp. and superl.
                        :%>         ABBO/AMOS         ;
        +Der5+Der/AAdv+Adv:attja%>t   K-CONS            ;
!               +Cmp/SgNom:asj%>      R                 ;
!               +Cmp/SgGen:attja%>    R                 ;
!               +Cmp/PlGen:attja%>j   R                 ;
                +Sg+Ill:attja%>s%> PxCi              ; !
                +Sg+Nom:attja%>    PxV               ; !
       +Err/Orth+Sg+Ill:adtja%>s%> PxCi              ; !
       +Err/Orth+Sg+Nom:adtja%>    PxV               ; !

! Test data:
!!€gt-norm: gågulasj # Even-syllable test
!!€ gågulasj:             gågulasj+A+Sg+Nom
!!€ gågulattjav:          gågulasj+A+Sg+Acc
!!€ gågulis:              gågulasj+A+Attr
!!€ gågulabbo:            gågulasj+A+Der/Comp+A+Sg+Nom 

LEXICON UDNODIBME !!= @CODE@ Adjectives on -dibme, attribute on -is. Odd-syllable comparison. Sg Acc: udnodimev, Attr: udnodis.
! :is VUOHTA ; !moved to ATTR
                 :is%>      ATTR              ; ! Special Attr form
                 :ibme%>    NE-EVENCASEadj    ;
                 :ibme%>X4  ILL-EVENCASEadj   ;
                 :ibme%>Q1  N-EVENWEAKSTEMadj ;
                 :%>        ABBO/AMOS         ;
 +Der5+Der/AAdv+Adv:ibme%>Q1t K-CONS            ;
        +Cmp/SgGen:ibme%>Q1  R                 ;
        +Cmp/PlGen:ibme%>Q1j R                 ;
!                 :          DUVVAT            ; ! this cant be correct?

! Test data:
!!€gt-norm: gælvodibme # Even-syllable test
!!€ gælvodibme:            gælvodibme+A+Sg+Nom
!!€ gälvodibme:            gælvodibme+A+Sg+Nom
!!€ gælvodimev:            gælvodibme+A+Sg+Acc
!!€ gälvodimev:            gælvodibme+A+Sg+Acc
!!€ gælvodis:              gælvodibme+A+Attr
!!€ gälvodis:              gælvodibme+A+Attr
!!€ gælvodabbo:            gælvodibme+A+Der/Comp+A+Sg+Nom 
!!€ gälvodabbo:            gælvodibme+A+Der/Comp+A+Sg+Nom 

LEXICON TJALMEDIBME !!= @CODE@  Like UDNODIBME but no comparatives. Sg Acc: tjalmedimev, Attr: tjalmedis.
! :is VUOHTA ; !moved to ATTR
                 :is%>      ATTR              ; ! Special Attr form
                 :ibme%>    NE-EVENCASEadj    ;
                 :ibme%>X4  ILL-EVENCASEadj   ;
                 :ibme%>Q1  N-EVENWEAKSTEMadj ;
 +Der5+Der/AAdv+Adv:ibme%>Q1t K-CONS            ;
        +Cmp/SgGen:ibme%>Q1  R                 ;
        +Cmp/PlGen:ibme%>Q1j R                 ;
 !                :          DUVVAT            ; ! this cant be correct?

! Test data:
!!€gt-norm: huvsodibme # Even-syllable test
!!€ huvsodibme:            huvsodibme+A+Sg+Nom
!!€ huvsodimev:            huvsodibme+A+Sg+Acc
!!€ huvsodis:              huvsodibme+A+Attr

LEXICON SUOLASIEHKE !!= @CODE@ -siehke. Sg Acc: suolasiegev, attr: suolasiek
                 :hke%>    NE-EVENCASEadj    ;
          +Sg+Nom:hke%>    K-VOW            ;
                 :k%>      ATTR              ;
                 :hke%>X4  ILL-EVENCASEadj   ;
                 :hke%>Q1  N-EVENWEAKSTEMadj ;
! :hke%>Q1 BU/MUS ; !no comparatives for these
 +Der5+Der/AAdv+Adv:hke%>Q1t K-VOW                 ;

! Test data:
!!€gt-norm: hánessiehke # Even-syllable test
!!€ hánessiehke:              hánessiehke+A+Sg+Nom
!!€ hánessiegev:              hánessiehke+A+Sg+Acc
!!€ hánessiek:                hánessiehke+A+Attr








!! ## Odd-syllable stems
! ----------------------------------------------------------------------------------------------------
!! ###  With CG Sorted by attr


LEXICON TJIEGOS !!= @CODE@ Attr same as pred. For adjectives with -e in second syllable e>á: divtes>diktásav in StrG. As a. in Spiik.  Sg Acc: tjiehkusav, Attr: tjiegos. Consonant gradation. 
                   :%>X3   ATTR           ;
                   :%>X3   N-ODDCASE      ; ! no pointer to E-ODDCASE, as adjs have no short Ess
                   :%>X5   OTH-ODDCASEadj ;
                   :%>X5   ABBO/AMOS      ;
 +Der5+Der/AAdv+Adv:%>X5it K-CONS         ;
                   :%>X5   ODDCmp         ;

! Test data:
!!€gt-norm: hánes # Odd-syllable test
!!€ måskas:              måskas+A+Sg+Nom
!!€ moasskasav:          måskas+A+Sg+Acc
!!€ måskas:              måskas+A+Attr
!!€ moasskasabbo:        måskas+A+Der/Comp+A+Sg+Nom 
!!€ bihtja:              bihtja+A+Sg+Nom
!!€ bihttjagav:          bihtja+A+Sg+Acc
!!€ bihtja:              bihtja+A+Attr
!!€ bihttjagabbo:        bihtja+A+Der/Comp+A+Sg+Nom 

LEXICON LINES !!= @CODE@ Attr ending on -a. Adjektives ending on -es. Does same as TJIEGOS, but with attr -a.   As g. in Spiik.  lines, Sg Acc: lidnásav, attr: lidna. Consonant gradation.
                   :a%>    ATTR           ;
                   :es%>Q1 VUOHTA         ;
                   :es%>Q1 N-ODDCASE      ; ! no pointer to E-ODDCASE, as adjs have no short Ess
                   :ás%>   OTH-ODDCASEadj ;
                   :ás%>   ABBO/AMOS      ;
 +Der5+Der/AAdv+Adv:ás%>it K-CONS         ;
         +Cmp/SgNom:es%>Q1 R              ;
                   :ás%>   ODDCmp         ;

! Test data:
!!€gt-norm: lines # Odd-syllable test
!!€ lines:               lines+A+Sg+Nom
!!€ lidnásav:            lines+A+Sg+Acc
!!€ lidna:               lines+A+Attr
!!€ lidnásabbo:          lines+A+Der/Comp+A+Sg+Nom 


LEXICON GALMAS !!= @CODE@  Attr ending on -a or -å. Adjectives on -as, ås- and ás. As e. in Spiik. Sg Acc: galmmasav, attr: galmma, Consonant gradation.
                   :%>    ATTR           ;
                   :s%>X2 VUOHTA         ;
                   :s%>X2 N-ODDCASE      ; ! no pointer to E-ODDCASE, as adjs have no short Ess
                   :s%>X5 OTH-ODDCASEadj ;
                   :s%>X5 ABBO/AMOS      ;
 +Der5+Der/AAdv+Adv:s%>it K-CONS         ;
         +Cmp/SgNom:s%>X2 R              ;
                   :s%>X5 ODDCmp         ;

! Test data:
!!€gt-norm: njuoskas # Odd-syllable test
!!€ njuoskas:               njuoskas+A+Sg+Nom
!!€ njuosskasav:            njuoskas+A+Sg+Acc
!!€ njuosska:               njuoskas+A+Attr
!!€ njuosskasabbo:          njuoskas+A+Der/Comp+A+Sg+Nom 



LEXICON OAMES !!= @CODE@ Attr ending on -e. Adjectives on -es with attribute -e. As g2. in Spiik. Sg Acc: oabmásav, Attr: oabme. Consonant gradation.
                   :%>        ATTR           ;
                   :s%>Q1     VUOHTA         ;
                   :s%>Q1     N-ODDCASE      ; ! no pointer to E-ODDCASE, as adjs have no short Ess
                   :s%>X5     OTH-ODDCASEadj ;
                   :s%>X5     ABBO/AMOS      ;
 +Der5+Der/AAdv+Adv:%>X4sit   K-CONS         ;
         +Cmp/SgNom:s%>Q1     R              ;
                   :%>X4s     ODDCmp         ;

! Test data:
!!€gt-norm: goastes # Odd-syllable test
!!€ goastes:               goastes+A+Sg+Nom
!!€ goasstásav:            goastes+A+Sg+Acc
!!€ goasste:               goastes+A+Attr
!!€ goasstásabbo:          goastes+A+Der/Comp+A+Sg+Nom 


LEXICON SUOHKAT !!= @CODE@ Attr III -is, not suohkkadis but SUOHKKIS. With CG to attr, not from nom to Acc. Same as JALGGAT only with this CG. SUOHKKIS. Without CG between nom and acc. Adjectives on -at and -åt, with attribute III -is. As f. in Spiik. Sg Acc: suohkadav, attr: suohkkis,
                   :is%>     ATTR           ;
                   :a%>Q1t   VUOHTA         ;
                   :a%>Q1t   N-ODDCASE      ; ! no pointer to E-ODDCASE, as adjs have no short Ess
                   :a%>Q1d   OTH-ODDCASEadj ;
                   :a%>Q1d   ABBO/AMOS      ;
 +Der5+Der/AAdv+Adv:a%>Q1dit K-CONS         ;
         +Cmp/SgNom:a%>Q1t   R              ;
                   :a%>Q1d   ODDCmp         ;

!!€gt-norm: rávvat # Odd-syllable test
!!€ rávvat:               rávvat+A+Sg+Nom
!!€ rávvadav:             rávvat+A+Sg+Acc
!!€ rávvis:               rávvat+A+Attr
!!€ rávvadabbo:           rávvat+A+Der/Comp+A+Sg+Nom 


LEXICON MÅJDÅS !!= @CODE@ Adjectives with no attr. With CG. Sg Acc: måjddåsav. If there is an attribute that dosn't fit to any lexicon it mus be hardcoded.
                   :%>X3   N-ODDCASE      ; ! no pointer to E-ODDCASE, as adjs have no short Ess
                   :%>X5   OTH-ODDCASEadj ;
                   :%>X5   ABBO/AMOS      ;
 +Der5+Der/AAdv+Adv:%>X5it K-CONS         ;
                   :%>X5   ODDCmp         ;

!!€gt-norm: rávdes # Odd-syllable test
!!€ rávdes:                rávdes+A+Sg+Nom
!!€ rávddásav:             rávdes+A+Sg+Acc
!!€ rávddásabbo:           rávdes+A+Der/Comp+A+Sg+Nom 




!! Without CG
!!------------------------------------------------------------

LEXICON VIEKSES !!= @CODE@ Attr same as pred. Without CG, but With vowel changes.  Sg Acc: væksásav, Attr: viekses. Like TJIEGOS only without the CG but with vowel changes. Mayby change this to a lexicon withput attr and then hardcode attr?
                   :%>     ATTR           ;
                   :%>     N-ODDCASE      ; ! no pointer to E-ODDCASE, as adjs have no short Ess
                   :%>X5   OTH-ODDCASEadj ;
                   :%>X5   ABBO/AMOS      ;
 +Der5+Der/AAdv+Adv:%>X5it K-CONS         ;
                   :%>X5   ODDCmp         ;


LEXICON ALEK !!= @CODE@ Attr same as pred. Without CG, without any vowel changes. Like TJIEGOS only without the CG an vowelchanges. 
                   :%>   ATTR           ;
                   :%>   N-ODDCASE      ; ! no pointer to E-ODDCASE, as adjs have no short Ess
                   :%>   OTH-ODDCASEadj ;
                   :%>   ABBO/AMOS      ;
 +Der5+Der/AAdv+Adv:%>it K-CONS         ;
                   :%>   ODDCmp         ;


! Test data:
!!€gt-norm: purpur # Odd-syllable test
!!€ purpur:               purpur+A+Sg+Nom
!!€ purpurav:             purpur+A+Sg+Acc
!!€ purpur:               purpur+A+Attr
!!€ purpurabbo:           purpur+A+Der/Comp+A+Sg+Nom 

LEXICON BASSTEL !!= @CODE@  Attr ening on -is. Without CG. Adjs on -et, -l, -r, sm om -k, -sj with attr -is and no consonant gradation. As b. in Spiik. Sg Acc: basstelav, Attr: basstelis. Many of these entries might be instances of derivations, like belak, deblak, and maybe also basstel, bargán.
                   :is%>   ATTR           ;
                   :       VUOHTA         ;
                   :%>     N-ODDCASE      ; ! no pointer to E-ODDCASE, as adjs have no short Ess
                   :%>     OTH-ODDCASEadj ;
                   :%>     ABBO/AMOS      ;
 +Der5+Der/AAdv+Adv:%>it   K-CONS         ;
         +Cmp/SgNom:%>     R              ;
                   :%>     ODDCmp         ;

! Test data:
!!€gt-norm: goavrret # Odd-syllable test
!!€ goavrret:              goavrret+A+Sg+Nom
!!€ goavrredav:            goavrret+A+Sg+Acc
!!€ goavrredis:            goavrret+A+Attr
!!€ goavrredabbo:          goavrret+A+Der/Comp+A+Sg+Nom 

LEXICON MUTTÁK !!= @CODE@ Two attr enings -is and same as pred. Without CG. Adjs on -ák/-ak/-ek, two attr: -is and same as pred. As c. in Spiik. Sg Acc: muttágav, Attr: muttágis and mutták. These seem to be instances of the adjectival -k derivation. Unclear whether such derivation have different attr forms or not, and thats maybe why some of these derivations are found in BASSTEL lexicon.
                   :%>   ATTR           ;
            +Use/NG:is%> ATTR           ;
                   :%>   N-ODDCASE      ; ! no pointer to E-ODDCASE, as adjs have no short Ess
                   :%>   OTH-ODDCASEadj ;
                   :%>   ABBO/AMOS      ;
 +Der5+Der/AAdv+Adv:%>it K-CONS         ;
         +Cmp/SgNom:%>   R              ;
                   :%>   ODDCmp         ;

! Test data:
!!€gt-norm: bárvak # Odd-syllable test
!!€ bárvak:               bárvak+A+Sg+Nom
!!€ bárvagav:             bárvak+A+Sg+Acc
!!€ bárvak:               bárvak+A+Attr
!!€ bárvagis:             bárvak+A+Attr
!!€ bárvagabbo:           bárvak+A+Der/Comp+A+Sg+Nom 


LEXICON JALGGAT !!= @CODE@ Attr III -is, not jalggadis but JALGGIS. Without CG.  Adjectives on -at, with attribute III -is. As f. in Spiik. Sg Acc: jalggadav, attr: jalggis,
                   :%>is   ATTR           ;
                   :%>at   VUOHTA         ;
                   :%>at   N-ODDCASE      ; ! no pointer to E-ODDCASE, as adjs have no short Ess
                   :%>ad   OTH-ODDCASEadj ;
                   :%>ad   ABBO/AMOS      ;
 +Der5+Der/AAdv+Adv:%>adit K-CONS         ;
         +Cmp/SgNom:%>at   R              ;
                   :%>ad   ODDCmp         ;


!!€gt-norm: russjkat # Odd-syllable test
!!€ russjkat:               russjkat+A+Sg+Nom
!!€ russjkadav:             russjkat+A+Sg+Acc
!!€ russjkis:               russjkat+A+Attr
!!€ russjkadabbo:           russjkat+A+Der/Comp+A+Sg+Nom 


LEXICON TJÅRGGÅT !!= @CODE@ Attr III -is, not tjårggådis but tjårggis. Without CG. Same as JALGGAT only for adjectives ending ot -åt. Adjectives on -åt, with attribute III -is. As f. in Spiik. Sg Acc: jalggadav, attr: jalggis,
                   :%>is ATTR           ;
                   :%>åt   VUOHTA         ;
                   :%>åt   N-ODDCASE      ; ! no pointer to E-ODDCASE, as adjs have no short Ess
                   :%>åd   OTH-ODDCASEadj ;
                   :%>åd   ABBO/AMOS      ;
 +Der5+Der/AAdv+Adv:%>ådit K-CONS         ;
         +Cmp/SgNom:%>åt   R              ;
                   :%>åd   ODDCmp         ;


!!€gt-norm: russjkat # Odd-syllable test
!!€ russjkat:               russjkat+A+Sg+Nom
!!€ russjkadav:             russjkat+A+Sg+Acc
!!€ russjkis:               russjkat+A+Attr
!!€ russjkadabbo:           russjkat+A+Der/Comp+A+Sg+Nom 



LEXICON RIHTSOK !!= @CODE@ No attr, without CG and also without any vowelchanges. The lexicon gives no attribute, either because the adjective dosnºt have attr, because there is stemvowel change in attr that the lexicon canºt handle or because there are strange atrributes that donºt fit to any other lexicon (these attributes are hardcoded). Sg Acc: rihtsogav. 
                   :%>   VUOHTA           ;
                   :%>   N-ODDCASE        ;
                   :%>   OTH-ODDCASEadj ;
                   :%>   ABBO/AMOS      ;
 +Der5+Der/AAdv+Adv:%>it K-CONS         ;
         +Cmp/SgNom:%>   R                ;
                   :%>   ODDCmp         ;

!!€gt-norm: rihtsok # Odd-syllable test
!!€ rihtsok:                rihtsok+A+Sg+Nom
!!€ rihtsogav:              rihtsok+A+Sg+Acc
!!€ rihtsogabbo:            rihtsok+A+Der/Comp+A+Sg+Nom 



!---------------------------------------------------------------------------------------------------------------------------------------
!! exception lexicons for odd-syll

LEXICON IENNILS !!= @CODE@ no comparatives, attr same as pred.
 :%> ATTR           ;
 :   VUOHTA         ;
 :%> N-ODDCASE      ;
 :%> OTH-ODDCASEadj ;
 :   NAMÁK          ;
 :%> R              ;

!!€gt-norm: ieŋŋils # Odd-syllable test
!!€ ieŋŋils:                 ieŋŋils+A+Sg+Nom
!!€ ieŋŋilsav:               ieŋŋils+A+Sg+Acc


LEXICON RÁDAS !!= @CODE@  Presently only used for "rádas". This word has special consonant gradation d>dd. Attr same as pred. Sg Acc: ráddasav, Attr: rádas. Consonant gradation.
                   :as%>    ATTR           ;
                   :as%>    N-ODDCASE      ; ! no pointer to E-ODDCASE, as adjs have no short Ess
                   :das%>   OTH-ODDCASEadj ;
                   :das%>   ABBO/AMOS      ;
 +Der5+Der/AAdv+Adv:das%>it K-CONS         ;
                   :das%>   ODDCmp         ;

! Test data:
!!€gt-norm: rádas # Odd-syllable test
!!€ rádas:               rádas+A+Sg+Nom
!!€ ráddasav:            rádas+A+Sg+Acc
!!€ rádas:               rádas+A+Attr
!!€ ráda:                rádas+A+Attr # from LEXATTR
!!€ ráddasabbo:          rádas+A+Der/Comp+A+Sg+Nom 


LEXICON LUOBES !!= @CODE@  Err/Orth lexicon! Does the same as TJIEGOS only e>a instead of usuall e>á, must be some err/orth. Sg Acc: luohpasav, Attr: luobes. Consonant gradation. NO Attr, must be hardcoded

                   :es%>Q1 VUOHTA         ;
                   :es%>Q1 N-ODDCASE      ; ! no pointer to E-ODDCASE, as adjs have no short Ess
                   :as%>   OTH-ODDCASEadj ;
                   :as%>   ABBO/AMOS      ;
 +Der5+Der/AAdv+Adv:as%>it K-CONS       ;
         +Cmp/SgNom:es%>Q1 R             ;
                   :as%>   ODDCmp         ;



! double comparatives


LEXICON LÅSSÅT !!= @CODE@ Two attr, two comp. As f3. in Spiik. So far the only word i this lexicon i "låssåt", because both låssis and låsså are attr and comparative is both låsep(hybrid?) and låssådabbo.
                              +Use/NG:is%>          ATTR           ;
                                     :å%>           ATTR           ;
                                     :åt            VUOHTA         ;
                                     :åt%>          N-ODDCASE      ; ! no pointer to E-ODDCASE, as adjs have no short Ess
                                     :åd%>          OTH-ODDCASEadj ;
                              +Use/NG:åd%>          ABBO/AMOS      ;
                                     :e%>Q1         BU/MUS         ;
! +Der1+Der/Comp:e%>Q1 BUStem ; ! Because we want låsebut
! +Der1+Der/Superl:e%>Q1muss BUOREMUS ;
          +Der1+Der/Comp+A+Der2+Der/Dimin+Sg+Nom:e»Q1busj%>    K-CONS         ;
              +Der1+Der/Comp+A+Der2+Der/Dimin:e»Q1busj%>    ATTR           ;
                 +Der1+Der/Comp+A+Der2+Der/Dimin:e»Q1buttja%>  ÁRMMOGIS-EVEN  ;
                 +Err/Lex+Use/-Spell+Der1+Der/Comp+A+Der2+Der/Dimin:e»Q1budtja%>  ÁRMMOGIS-EVEN  ;
 +Der1+Der/Comp+A+Der2+Der/Dimin+Der5+Der/AAdv+Adv:e»Q1buttja%>t K-CONS         ;
                     +Der5+Der/AAdv+Adv:åd%>it        K-CONS         ;
                            +Cmp/SgNom:åt%>          R              ;
                                     :åd%>          ODDCmp         ;

!!€gt-norm: låssåt # Odd-syllable test
!!€ låssåt:               låssåt+A+Sg+Nom
!!€ låssådav:             låssåt+A+Sg+Acc
!!€ låssis:               låssåt+A+Attr
!!€ låsså:                låssåt+A+Attr
!!€ låssådabbo:           låssåt+A+Der/Comp+A+Sg+Nom 
!!€ låsep:                låssåt+A+Der/Comp+A+Sg+Nom 

LEXICON STUORAK !!= @CODE@  Only for stuorak. It hase two attributes. Has even-syllable comparison: stuoráp and stuorámus.Sg Acc: stuoragav, attr: stuor and stuorra. This might be a -k derivation of adjective stuorre attr stuor(ra). The comparison is thus based on the original adjective and thus it naturally is an even syll comparison.
                   :%>     ATTR           ;
            +Use/NG:ra%>   ATTR           ;
                   :ak     VUOHTA         ;
                   :ak%>   N-ODDCASE      ;
                   :ag%>   OTH-ODDCASEadj ;
                   :á%>    BU/MUS         ;
 +Der5+Der/AAdv+Adv:ag%>it K-CONS         ;
         +Cmp/SgNom:ak%>   R              ;
                   :ag%>   ODDCmp         ;

!!€gt-norm: stuorak # Odd-syllable test
!!€ stuorak:               stuorak+A+Sg+Nom
!!€ stuoragav:             stuorak+A+Sg+Acc
!!€ stuorra:               stuorak+A+Attr
!!€ stuor:                 stuorak+A+Attr
!!€ stuoráp:               stuorak+A+Der/Comp+A+Sg+Nom 


LEXICON ALLAK !!= @CODE@ Adjs on -ak, attr.on -a. Have both gasep/gaggagabbo and alep/allagabbo as comparatives. As d. in Spiik. So far only the adjectives "allak" and "gassak" go to this lexicon.
                                     :a%>           ATTR           ;
! :a VUOHTA ; !moved to ATTR
                                     :ak            VUOHTA         ;
                                     :ak%>          N-ODDCASE      ; ! no pointer to E-ODDCASE, as adjs have no short Ess
                                     :ag%>          OTH-ODDCASEadj ;
                                     :e%>Q1         BU/MUS         ;
                                     +Use/NG:ag%>          ABBO/AMOS      ;
! +Der1+Der/Comp:e%>Q1 BUStem ;
! +Der1+Der/Superl:e%>Q1muss BUOREMUS ;
! +Der1+Der/Comp+A+Der5+Der/AAdv+Adv:ag%>appot K ;
! +Der1+Der/Superl+A+Der5+Der/AAdv+Adv:ag%>abmus%>it K ;
          +Der1+Der/Comp+A+Der2+Der/Dimin+Sg+Nom:e»Q1busj%>    K-CONS         ;
              +Der1+Der/Comp+A+Der2+Der/Dimin:e»Q1busj%>    ATTR           ;
                 +Der1+Der/Comp+A+Der2+Der/Dimin:e»Q1buttja%>  ÁRMMOGIS-EVEN  ;
 +Err/Lex+Use/-Spell+Der1+Der/Comp+A+Der2+Der/Dimin:e»Q1budtja%>  ÁRMMOGIS-EVEN  ;
 +Der1+Der/Comp+A+Der2+Der/Dimin+Der5+Der/AAdv+Adv:e»Q1buttja%>t K-CONS         ;
                     +Der5+Der/AAdv+Adv:ag%>it        K-CONS          ;
                            +Cmp/SgNom:ak%>          R              ;
                                      :ag%>          ODDCmp         ;

!!€gt-norm: gassak # Odd-syllable test
!!€ gassak:               gassak+A+Sg+Nom
!!€ gassagav:             gassak+A+Sg+Acc
!!€ gassa:                gassak+A+Attr
!!€ gassagabbo:           gassak+A+Der/Comp+A+Sg+Nom
!!€ gasep:                gassak+A+Der/Comp+A+Sg+Nom

LEXICON GÅBDDÅK !!= @CODE@  Adjs on -åk, attr. on -å. Has even-syllable comparison: gåbdep and gåbdemus. So far "gåbddåk" is the only word in this lexicon. As d2. in Spiik. Sg Acc: gåbddågav, Attr: gåbddå.

                                     :å%>           ATTR           ;
                                     :åk            VUOHTA         ;
                                     :åk%>          N-ODDCASE      ; ! no pointer to E-ODDCASE, as adjs have no short Ess
                                     :åg%>          OTH-ODDCASEadj ;
                                     :e%>Q1         BU/MUS         ;
! +Der1+Der/Comp:e%>Q1 BUStem ;
! +Der1+Der/Superl:e%>Q1muss BUOREMUS ;
                +Der1+Der/Comp+A+Der5+Der/AAdv+Adv:åg%>appot     K-CONS         ;
              +Der1+Der/Superl+A+Der5+Der/AAdv+Adv:åg%>abmus%>it K-CONS         ;
            +Der1+Der/Comp+A+Der2+Der/Dimin+Sg+Nom:e»Q1busj%>    K-CONS         ;
                +Der1+Der/Comp+A+Der2+Der/Dimin:e»Q1busj%>    ATTR           ;
                   +Der1+Der/Comp+A+Der2+Der/Dimin:e»Q1buttja%>  ÁRMMOGIS-EVEN  ;
+Err/Lex+Use/-Spell+Der1+Der/Comp+A+Der2+Der/Dimin:e»Q1budtja%>  ÁRMMOGIS-EVEN  ;
 +Der1+Der/Comp+A+Der2+Der/Dimin+Der5+Der/AAdv+Adv:e»Q1buttja%>t K-CONS         ;
                     +Der5+Der/AAdv+Adv:åg%>it        K-CONS         ;
                             +Cmp/SgNom:åk%>          R              ;
                                       :åg%>          ODDCmp         ;

!!€gt-norm: gåbddåk # Odd-syllable test
!!€ gåbddåk:               gåbddåk+A+Sg+Nom
!!€ gåbddågav:             gåbddåk+A+Sg+Acc
!!€ gåbddå:                gåbddåk+A+Attr
!!€ gåbdep:                gåbddåk+A+Der/Comp+A+Sg+Nom 




!!
!! Inherent comparatives and superlatives
!!

LEXICON NUORTTALABBO   !!= @CODE@ Inherent comparatives, gives both comp and superl. Most of the words are the compared forms of -el(a) words, like nuorttal, lullel.

                                             +Gram/Comp:ap%>        ATTR            ;
                      			      +Use/NG+Gram/Comp:up%>        ATTR            ;
                                             +Gram/Comp:            ABBO            ;
                                     +Der1+Der/Superl+A:            AMOS            ;
	     
                     +Gram/Comp+Der2+Der/Dimin+A+Sg+Nom:appu»sj%>   K-CONS          ;
                            +Gram/Comp+Der2+Der/Dimin+A:appu»sj%>   ATTR            ;
                            +Gram/Comp+Der2+Der/Dimin+A:appu»tj%>   CompSup-ODDCASE ;
          +Gram/Comp+Der2+Der/Dimin+A+Der5+Der/AAdv+Adv:appu»tj%>it K-CONS          ;
                           +Gram/Comp+Der5+Der/AAdv+Adv:appo%>t     K-CONS          ;
                   +Der1+Der/Superl+A+Der5+Der/AAdv+Adv:abmus%>it   K-CONS          ;
														     
            +Err/Orth+Der5+Der/AAdv+Adv+Der1+Der/Comp+A:æppo%>t     K-CONS          ; !
          +Err/Orth+Der1+Der/Superl+A+Der5+Der/AAdv+Adv:ebmus%>it   K-CONS          ; !
            +Err/Orth+Gram/Comp+Der2+Der/Dimin+A+Sg+Nom:eppu»sj%>   K-CONS          ; !
            +Err/Orth+Gram/Comp+Der2+Der/Dimin+A+Sg+Nom:uppu»sj%>   K-CONS          ;
                   +Err/Orth+Gram/Comp+Der2+Der/Dimin+A:eppu»sj%>   ATTR            ; !
                   +Err/Orth+Gram/Comp+Der2+Der/Dimin+A:uppu»sj%>   ATTR            ;
                   +Err/Orth+Gram/Comp+Der2+Der/Dimin+A:eppu»tj%>   CompSup-ODDCASE ; !
                   +Err/Orth+Gram/Comp+Der2+Der/Dimin+A:uppu»tj%>   CompSup-ODDCASE ;
 +Err/Orth+Gram/Comp+Der2+Der/Dimin+A+Der5+Der/AAdv+Adv:eppu»tj%>it K-CONS          ; !
 +Err/Orth+Gram/Comp+Der2+Der/Dimin+A+Der5+Der/AAdv+Adv:uppu»tj%>it K-CONS          ;

! Test data:
!!€gt-norm: guddnelabbo # Even-syllable test
!!€ guddnelabbo:           guddnelabbo+A+Sg+Nom
!!€ guddnelamos:           guddnelabbo+A+Der/Superl+A+Sg+Nom
!!€ guddnelap:             guddnelabbo+A+Attr
!!€ guddnelup:             guddnelabbo+A+Attr

LEXICON GASSKALAMOS   !!= @CODE@ Inherent superlatives, gives onlys superl. Words that are lexicalized in their superlative forms. 
                   +Gram/Superl:%>            AMOS    ;
 +Gram/Superl+Der5+Der/AAdv+Adv:%>abmus%>it   K-CONS  ;







!!€gt-norm: ájtodamos # Even-syllable test
!!€ ájtodamos:           ájtodamos+A+Sg+Nom


!! ## Contracted stems
! ----------------

LEXICON SÁDNES !!= @CODE@  Attr same as pred. Sg Acc: sáddnáv, Attr: sádnes.
                        :%>Q1s ATTR              ;
                 +Sg+Nom:%>Q1s K-CONS            ;
                        :%>X4  ILL-EVENCASEadj   ;
                        :%>X4  N-EVENWEAKSTEMadj ;
        +Sg+Nom+Err/Orth:%>X4  PxV               ; !
                    +Ess:%>X4n K-CONS            ;
                        :%>X4  BU/MUS            ;
      +Der5+Der/AAdv+Adv:%>X4t K-CONS            ;
              +Cmp/SgGen:%>X4  R                 ;
              +Cmp/PlGen:%>X4j R                 ;

!!€gt-norm: hávres # Contracted test
!!€ hávres:                 hávres+A+Sg+Nom
!!€ hávrráv:                hávres+A+Sg+Acc
!!€ hávres:                 hávres+A+Attr
!!€ hávrráp:                hávres+A+Der/Comp+A+Sg+Nom 



LEXICON GOAVSOS  !!= @CODE@ Attr same as pred. Sg Acc: goaksuv, Attr: goavsos.(goavsos is so far the only word in this lexicon)
                        :%>Q3s ATTR              ;
                 +Sg+Nom:%>Q3s K-CONS            ;
                        :%>X4  ILL-EVENCASEadj   ;
                        :%>X4  N-EVENWEAKSTEMadj ;
        +Sg+Nom+Err/Orth:%>X4  PxV               ; !
                    +Ess:%>X4n K-CONS            ;
                        :%>X4  BU/MUS            ;
      +Der5+Der/AAdv+Adv:%>X4t K-CONS            ;
              +Cmp/SgGen:%>X4  R                 ;
              +Cmp/PlGen:%>X4j R                 ;

!!€gt-norm: goavsos # Contracted test
!!€ goavsos:                 goavsos+A+Sg+Nom
!!€ goaksuv:                 goavsos+A+Sg+Acc 
!!€ goaksusav:               goavsos+A+Sg+Acc # From lexicon TJIEGOS
!!€ goavsos:                 goavsos+A+Attr
!!€ goaksup:                 goavsos+A+Der/Comp+A+Sg+Nom 
!!€ goaksusabbo:             goavsos+A+Der/Comp+A+Sg+Nom  # from lexicon TJIEGOS

LEXICON SUVRES  !!= @CODE@ Sg Acc: suvrráv, Attr: suvra.
                        :a%>Q1  ATTR              ;
                        :e%>Q1s VUOHTA            ;
                 +Sg+Nom:e%>Q1s K-CONS            ;
                        :á%>    ILL-EVENCASEadj   ;
                        :á%>    N-EVENWEAKSTEMadj ;
        +Sg+Nom+Err/Orth:á%>    PxV               ; !
                    +Ess:á%>n   K-CONS            ;
                        :á%>    BU/MUS            ;
      +Der5+Der/AAdv+Adv:á%>t   K-CONS            ;
              +Cmp/SgNom:e%>Q1s R                 ;
              +Cmp/SgGen:á%>    R                 ;
              +Cmp/PlGen:á%>j   R                 ;

!!€gt-norm: suvres # Contracted test
!!€ suvres:                  suvres+A+Sg+Nom
!!€ suvrráv:                 suvres+A+Sg+Acc
!!€ suvrrásav:               suvres+A+Sg+Acc # From lexicon SJÆVNNJAT
!!€ suvra:                   suvres+A+Attr
!!€ suvrráp:                 suvres+A+Der/Comp+A+Sg+Nom 
!!€ suvrrásabbo:             suvres+A+Der/Comp+A+Sg+Nom  # from LINES

LEXICON GÅLMAKTES !!= @CODE@  Attr same as pred. without cg but with vowel changes. Sg Acc: gålmaktáv, Attr: gålmaktes. VIEKSES makes odd-syll same thing. 
                        :%>s   ATTR              ;
                 +Sg+Nom:%>s   K-CONS            ;
                        :%>X4  ILL-EVENCASEadj   ;
                        :%>X4  N-EVENWEAKSTEMadj ;
        +Sg+Nom+Err/Orth:%>X4  PxV               ; !
                    +Ess:%>X4n K-CONS            ;
                        :%>X4  BU/MUS            ;
      +Der5+Der/AAdv+Adv:%>X4t K-CONS            ;
              +Cmp/SgGen:%>X4  R                 ;
              +Cmp/PlGen:%>X4j R                 ;



!! ----


!! # Comparation

LEXICON BU/MUS !!= @CODE@  comparison for even-syll adjectives. Also derivates diminutive and adverbs from the comparisions.
                                 +Der1+Der/Comp+A:           BUStem       ;
                               +Der1+Der/Superl+A:muss       BUOREMUS     ;
               +Der1+Der/Comp+A+Der5+Der/AAdv+Adv:but        K-CONS       ;
             +Der1+Der/Superl+A+Der5+Der/AAdv+Adv:musá%>t    K-CONS       ;
           +Der1+Der/Comp+A+Der2+Der/Dimin+A+Sg+Nom:bu»sj%>    K-CONS       ;
               +Der1+Der/Comp+A+Der2+Der/Dimin+A:bu»sj%>    ATTR         ;
                  +Der1+Der/Comp+A+Der2+Der/Dimin+A:bu»ttja%>  CompSup-EVEN ;
+Err/Lex+Use/-Spell+Der1+Der/Comp+A+Der2+Der/Dimin+A:bu»dtja%>  CompSup-EVEN ;
 +Der1+Der/Comp+A+Der2+Der/Dimin+A+Der5+Der/AAdv+Adv:bu»ttja%>t K-CONS       ;


LEXICON ABBO/AMOS !!= @CODE@  comparison for odd-syll adjectives.  Also derivates diminutive and adverbs from the comparisions.
!+Der1+Der/Comp+A+Sg+Nom:ap%> K ;
!+Der1+Der/Comp+A+Sg+Nom:up%> K ;
! +Der1+Der/Comp+A+Sg+Nom:abbo%> K ;
! +Der1+Der/Comp+A+Sg+Nom:ubbo%> K ;
! +Der1+Der/Comp+A+Sg+Nom+Err/Orth:æbbo%> K ; !
                                       +Der1+Der/Comp+A:ap%>        ATTR            ;
                   			     +Use/NG+Der1+Der/Comp+A:up%>        ATTR            ;
                                        +Der1+Der/Comp+A:            ABBO            ;
                                      +Der1+Der/Superl+A:            AMOS            ;

                  +Der1+Der/Comp+A+Der2+Der/Dimin+A+Sg+Nom:appu»sj%>   K-CONS          ;
                      +Der1+Der/Comp+A+Der2+Der/Dimin+A:appu»sj%>   ATTR            ;
                         +Der1+Der/Comp+A+Der2+Der/Dimin+A:appu»tj%>   CompSup-ODDCASE ;
         +Der1+Der/Comp+A+Der2+Der/Dimin+A+Der5+Der/AAdv+Adv:appu»tj%>it K-CONS          ;
                        +Der1+Der/Comp+A+Der5+Der/AAdv+Adv:appo%>t     K-CONS          ;
                      +Der1+Der/Superl+A+Der5+Der/AAdv+Adv:abmus%>it   K-CONS          ;
														     
                +Der5+Der/AAdv+Adv+Der1+Der/Comp+A+Err/Orth:æppo%>t     K-CONS          ; !
              +Der1+Der/Superl+A+Der5+Der/AAdv+Adv+Err/Orth:ebmus%>it   K-CONS          ; !
          +Der1+Der/Comp+A+Der2+Der/Dimin+A+Sg+Nom+Err/Orth:eppu»sj%>   K-CONS          ; !
          +Der1+Der/Comp+A+Der2+Der/Dimin+A+Sg+Nom+Err/Orth:uppu»sj%>   K-CONS          ;
              +Der1+Der/Comp+A+Der2+Der/Dimin+A+Err/Orth:eppu»sj%>   ATTR            ; !
              +Der1+Der/Comp+A+Der2+Der/Dimin+A+Err/Orth:uppu»sj%>   ATTR            ;
                 +Der1+Der/Comp+A+Der2+Der/Dimin+A+Err/Orth:eppu»tj%>   CompSup-ODDCASE ; !
                 +Der1+Der/Comp+A+Der2+Der/Dimin+A+Err/Orth:uppu»tj%>   CompSup-ODDCASE ;
 +Der1+Der/Comp+A+Der2+Der/Dimin+A+Der5+Der/AAdv+Adv+Err/Orth:eppu»tj%>it K-CONS          ; !
 +Der1+Der/Comp+A+Der2+Der/Dimin+A+Der5+Der/AAdv+Adv+Err/Orth:uppu»tj%>it K-CONS          ;




! Comparative
! -----------

LEXICON BUStem !!= @CODE@ Comparative even-syll, case and attr.
                                 :p        ATTR    ;
                          +Sg+Nom:p        K-CONS  ;
 +Sg+Nom+Use/Marg+Err/Orth:bu%>     PxV  ; ! !	   
                          +Sg+Gen:bu       K-VOW  ;
                          +Sg+Abe:bu#dagi  K-VOW  ;
                          +Sg+Abe:bu#dagá  K-VOW  ;
                          +Sg+Abe:bu#tagá  K-VOW  ;
 +Sg+Gen+Use/Marg+Err/Orth:bu%>     PxV  ; ! !	   
                          +Sg+Acc:buv      K-CONS  ;
 +Sg+Acc+Use/Marg+Err/Orth:bu%>     PxV  ; ! !	   
                          +Sg+Ill:buj      K-CONS  ;
                          +Sg+Ine:bun      K-CONS  ;
 +Sg+Ine+Use/Marg+Err/Orth:bun%>    PxCs ; ! !	   
                          +Sg+Ela:bus      K-CONS  ;
 +Sg+Ela+Use/Marg+Err/Orth:bust%>   PxCs ; ! !	   
                          +Sg+Com:bujn     K-CONS  ;
                  +Sg+Com+Err/Orth:bujna    K-VOW  ; !
 +Sg+Com+Use/Marg+Err/Orth:bujn%>   PxCs ; ! !	   
                             +Ess:bun      K-CONS  ;
                          +Pl+Nom:bu       K-VOW  ;
                          +Pl+Gen:buj      K-CONS  ;
                          +Pl+Abe:buj#dagi K-VOW  ;
                          +Pl+Abe:buj#dagá K-VOW  ;
                          +Pl+Abe:buj#tagá K-VOW  ;
                          +Pl+Acc:bujt     K-CONS  ;
 +Pl+Acc+Use/Marg+Err/Orth:bujd%>   PxCs ; ! !	   
                          +Pl+Ill:bujda    K-VOW  ;
                          +Pl+Ine:bujn     K-CONS  ;
 +Pl+Ine+Use/Marg+Err/Orth:bujn%>   PxCs ; ! !	   
                          +Pl+Ela:bujs     K-CONS  ;
 +Pl+Ela+Use/Marg+Err/Orth:bujst%>  PxCs ; ! !	   
                          +Pl+Com:buj      K-CONS  ;

LEXICON ABBO !!= @CODE@ Comparative odd-syll, get case and attr. With the dialect differences "-ubbo" and "-æbbo".
! -abbo
         +Sg+Nom:abbo%>       K-VOW ;
         +Sg+Gen:appo%>       K-VOW ;
         +Sg+Abe:appo%>#dagi  K-VOW ;
         +Sg+Abe:appo%>#dagá  K-VOW ;
         +Sg+Abe:appo%>#tagá  K-VOW ;
         +Sg+Acc:appo%>v      K-CONS ;
         +Sg+Ill:abbu%>j      K-CONS ;
         +Sg+Ine:appo%>n      K-CONS ;
         +Sg+Ela:appo%>s      K-CONS ;
         +Sg+Com:appu%>jn     K-CONS ;
         +Sg+Com:appo%>jn     K-CONS ;
 +Sg+Com+Err/Orth:appu%>jna    K-VOW ; !
 +Sg+Com+Err/Orth:appo%>jna    K-VOW ; !
            +Ess:abbo%>n      K-CONS ;
         +Pl+Nom:appo%>       K-VOW ;
         +Pl+Gen:appo%>j      K-CONS ;
         +Pl+Abe:appo%>j#dagi K-VOW ;
         +Pl+Abe:appo%>j#dagá K-VOW ;
         +Pl+Abe:appo%>j#tagá K-VOW ;
         +Pl+Acc:appo%>jt     K-CONS ;
         +Pl+Ill:appo%>jda    K-VOW ;
         +Pl+Ine:appo%>jn     K-CONS ;
         +Pl+Ela:appo%>js     K-CONS ;
         +Pl+Com:appo%>j      K-CONS ;
! -ubbo						  K-CONS
 +Sg+Nom+Err/Orth:ubbo%>       K-VOW ;
 +Sg+Gen+Err/Orth:uppo%>       K-VOW ;
 +Sg+Abe+Err/Orth:uppo%>#dagi  K-VOW ;
 +Sg+Abe+Err/Orth:uppo%>#dagá  K-VOW ;
 +Sg+Abe+Err/Orth:uppo%>#tagá  K-VOW ;
 +Sg+Acc+Err/Orth:uppo%>v      K-CONS ;
 +Sg+Ill+Err/Orth:ubbu%>j      K-CONS ;
 +Sg+Ine+Err/Orth:uppo%>n      K-CONS ;
 +Sg+Ela+Err/Orth:uppo%>s      K-CONS ;
 +Sg+Com+Err/Orth:uppu%>jn     K-CONS ;
 +Sg+Com+Err/Orth:uppo%>jn     K-CONS ;
 +Sg+Com+Err/Orth:uppu%>jna    K-VOW ; !
 +Sg+Com+Err/Orth:uppo%>jna    K-VOW ; !
    +Ess+Err/Orth:ubbo%>n      K-CONS ;
 +Pl+Nom+Err/Orth:uppo%>       K-VOW ;
 +Pl+Gen+Err/Orth:uppo%>j      K-CONS ;
 +Pl+Abe+Err/Orth:uppo%>j#dagi K-VOW ;
 +Pl+Abe+Err/Orth:uppo%>j#dagá K-VOW ;
 +Pl+Abe+Err/Orth:uppo%>j#tagá K-VOW ;
 +Pl+Acc+Err/Orth:uppo%>jt     K-CONS ;
 +Pl+Ill+Err/Orth:uppo%>jda    K-VOW ;
 +Pl+Ine+Err/Orth:uppo%>jn     K-CONS ;
 +Pl+Ela+Err/Orth:uppo%>js     K-CONS ;
 +Pl+Com+Err/Orth:uppo%>j      K-CONS ;
! -æbbo						  K-CONS
 +Sg+Nom+Err/Orth:æbbo%>       K-VOW ; !
 +Sg+Gen+Err/Orth:æppo%>       K-VOW ; !
 +Sg+Abe+Err/Orth:æppo%>#dagi  K-VOW ; !
 +Sg+Abe+Err/Orth:æppo%>#dagá  K-VOW ; !
 +Sg+Abe+Err/Orth:æppo%>#tagá  K-VOW ; !
 +Sg+Acc+Err/Orth:æppo%>v      K-CONS ; !
 +Sg+Ill+Err/Orth:æbbu%>j      K-CONS ; !
 +Sg+Ine+Err/Orth:æppo%>n      K-CONS ; !
 +Sg+Ela+Err/Orth:æppo%>s      K-CONS ; !
 +Sg+Com+Err/Orth:eppu%>jn     K-CONS ; !
 +Sg+Com+Err/Orth:æppo%>jn     K-CONS ; !
 +Sg+Com+Err/Orth:eppu%>jna    K-VOW ; !
 +Sg+Com+Err/Orth:æppo%>jna    K-VOW ; !
    +Ess+Err/Orth:æbbo%>n      K-CONS ; !
 +Pl+Nom+Err/Orth:æppo%>       K-VOW ; !
 +Pl+Gen+Err/Orth:æppo%>j      K-CONS ; !
 +Pl+Abe+Err/Orth:æppo%>j#dagi K-VOW ; !
 +Pl+Abe+Err/Orth:æppo%>j#dagá K-VOW ; !
 +Pl+Abe+Err/Orth:æppo%>j#tagá K-VOW ; !
 +Pl+Acc+Err/Orth:æppo%>jt     K-CONS ; !
 +Pl+Ill+Err/Orth:æppo%>jda    K-VOW ; !
 +Pl+Ine+Err/Orth:æppo%>jn     K-CONS ; !
 +Pl+Ela+Err/Orth:æppo%>js     K-CONS ; !
 +Pl+Com+Err/Orth:æppo%>j      K-CONS ; !


!! ## Superlative
! -----------

LEXICON BUOREMUS !!= @CODE@ Superlative even-syll, get attr and nom case.
                            +Attr:%>  K-CONS            ;
                          +Sg+Nom:%>  K-CONS            ; ! the short form, twol shortens ss#
 +Sg+Nom+Use/Marg+Err/Orth:a%> PxV          ; ! ! ! separate nom bec of the short form
                                 :a%> BUOREMUS-OBL ; ! Add -a, directly to the obliques

LEXICON BUOREMUS-OBL
    +Ess:n  K-CONS                    ; ! essive has strong stem
 +Sg+Ill:j  K-CONS                    ; ! so has illative
! +Sg+Ill+Err/Orth:s%> PxC ; !^P^ !MARG ! this Px form not mentioned in Spiik
        :Q1 CompSup-EVENWEAKSTEM ; ! These words go like muorra

LEXICON AMOS !!= @CODE@ Superlative odd-syll, get case and attr. With the dialect differences "-umos" and "-æmos".
                                    +Attr:amos%>         K-CONS    ;
                                  +Sg+Nom:amos%>         K-CONS    ; 
                 +Sg+Nom+Use/Marg+Err/Orth:abmus%>a%>     PxV       ; ! !
                                  +Sg+Gen:abmus%>a       K-CONS    ;
                                  +Sg+Abe:abmus%>a#dagi  K-VOW    ;
                                  +Sg+Abe:abmus%>a#dagá  K-VOW    ;
                                  +Sg+Abe:abmus%>a#tagá  K-VOW    ;
                 +Sg+Gen+Use/Marg+Err/Orth:abmus%>a%>     PxV       ; ! !
                                  +Sg+Acc:abmus%>av      K-CONS    ;
                 +Sg+Acc+Use/Marg+Err/Orth:abmus%>a%>     PxV       ; ! !
                                  +Sg+Ill:abmus%>ij      K-CONS    ;
                                  +Sg+Ine:abmus%>in      K-CONS    ;
                 +Sg+Ine+Use/Marg+Err/Orth:abmus%>in%>    PxCs      ; ! !
                                  +Sg+Ela:abmus%>is      K-CONS    ;
                 +Sg+Ela+Use/Marg+Err/Orth:abmus%>ist%>   PxCs      ; ! !
                                  +Sg+Com:abmus%>ijn     K-CONS    ;
                          +Sg+Com+Err/Orth:abmus%>ijna    K-VOW    ; !
                 +Sg+Com+Use/Marg+Err/Orth:abmus%>ijn%>   PxCs      ; ! !
                                     +Ess:abmus%>in      K-CONS    ;
                                  +Pl+Nom:abmus%>a       K-VOW    ;
                                  +Pl+Gen:abmus%>ij      K-CONS    ;
                                  +Pl+Abe:abmus%>ij#dagi K-VOW    ;
                                  +Pl+Abe:abmus%>ij#dagá K-VOW    ;
                                  +Pl+Abe:abmus%>ij#tagá K-VOW    ;
                                  +Pl+Acc:abmus%>ijt     K-CONS    ;
                 +Pl+Acc+Use/Marg+Err/Orth:abmus%>ijd%>   PxCs      ; ! !
                                  +Pl+Ill:abmus%>ijda    K-VOW    ;
                                  +Pl+Ine:abmus%>ijn     K-CONS    ;
                 +Pl+Ine+Use/Marg+Err/Orth:abmus%>ijn%>   PxCs      ; ! !
                                  +Pl+Ela:abmus%>ijs     K-CONS    ;
                 +Pl+Ela+Use/Marg+Err/Orth:abmus%>ijst%>  PxCs      ; ! !
                                  +Pl+Com:abmus%>ij      K-CONS    ;

                            +Attr+Err/Orth:umos%>         K-CONS   ;
                          +Sg+Nom+Err/Orth:umos%>         K-CONS   ;
                 +Sg+Nom+Use/Marg+Err/Orth:ubmus%>a%>     PxV      ; ! !
                          +Sg+Gen+Err/Orth:ubmus%>a       K-VOW   ;
                          +Sg+Abe+Err/Orth:ubmus%>a#dagi  K-VOW   ;
                          +Sg+Abe+Err/Orth:ubmus%>a#dagá  K-VOW   ;
                          +Sg+Abe+Err/Orth:ubmus%>a#tagá  K-VOW   ;
                 +Sg+Gen+Use/Marg+Err/Orth:ubmus%>a%>     PxV      ; ! !
                          +Sg+Acc+Err/Orth:ubmus%>av      K-CONS   ;
                 +Sg+Acc+Use/Marg+Err/Orth:ubmus%>a%>     PxV      ; ! !
                          +Sg+Ill+Err/Orth:ubmus%>ij      K-CONS   ;
                          +Sg+Ine+Err/Orth:ubmus%>in      K-CONS   ;
                 +Sg+Ine+Use/Marg+Err/Orth:ubmus%>in%>    PxCs     ; ! !
                          +Sg+Ela+Err/Orth:ubmus%>is      K-CONS   ;
                 +Sg+Ela+Use/Marg+Err/Orth:ubmus%>ist%>   PxCs     ; ! !
                          +Sg+Com+Err/Orth:ubmus%>ijn     K-CONS   ;
                          +Sg+Com+Err/Orth:ubmus%>ijna    K-CONS   ;
                 +Sg+Com+Use/Marg+Err/Orth:ubmus%>ijn%>   PxCs     ; ! !
                             +Ess+Err/Orth:ubmus%>in      K-CONS   ;
                          +Pl+Nom+Err/Orth:ubmus%>a       K-VOW   ;
                          +Pl+Gen+Err/Orth:ubmus%>ij      K-CONS   ;
                          +Pl+Abe+Err/Orth:ubmus%>ij#dagi K-VOW   ;
                          +Pl+Abe+Err/Orth:ubmus%>ij#dagá K-VOW   ;
                          +Pl+Abe+Err/Orth:ubmus%>ij#tagá K-VOW   ;
                          +Pl+Acc+Err/Orth:ubmus%>ijt     K-CONS   ;
                 +Pl+Acc+Use/Marg+Err/Orth:ubmus%>ijd%>   PxCs     ; ! !
                          +Pl+Ill+Err/Orth:ubmus%>ijda    K-VOW   ;
                          +Pl+Ine+Err/Orth:ubmus%>ijn     K-CONS   ;
                 +Pl+Ine+Use/Marg+Err/Orth:ubmus%>ijn%>   PxCs     ; ! !
                          +Pl+Ela+Err/Orth:ubmus%>ijs     K-CONS   ;
                 +Pl+Ela+Use/Marg+Err/Orth:ubmus%>ijst%>  PxCs     ; ! !
                          +Pl+Com+Err/Orth:ubmus%>ij      K-CONS   ;

                            +Attr+Err/Orth:æmos%>         K-CONS   ; !
                          +Sg+Nom+Err/Orth:æmos%>         K-CONS   ; !
         +Sg+Nom+Err/Orth+Use/Marg+Err/Orth:ebmus%>a%>     PxV      ; ! ! !
                          +Sg+Gen+Err/Orth:ebmus%>a       K-VOW   ; !
                          +Sg+Abe+Err/Orth:ebmus%>a#dagi  K-VOW   ; !
                          +Sg+Abe+Err/Orth:ebmus%>a#dagá  K-VOW   ; !
                          +Sg+Abe+Err/Orth:ebmus%>a#tagá  K-VOW   ; !
         +Sg+Gen+Err/Orth+Use/Marg+Err/Orth:ebmus%>a%>     PxV      ; ! ! !
                          +Sg+Acc+Err/Orth:ebmus%>av      K-CONS   ; !
         +Sg+Acc+Err/Orth+Use/Marg+Err/Orth:ebmus%>a%>     PxV      ; ! ! !
                          +Sg+Ill+Err/Orth:ebmus%>ij      K-CONS   ; !
                          +Sg+Ine+Err/Orth:ebmus%>in      K-CONS   ; !
         +Sg+Ine+Err/Orth+Use/Marg+Err/Orth:ebmus%>in%>    PxCs     ; ! ! !
                          +Sg+Ela+Err/Orth:ebmus%>is      K-CONS   ; !
         +Sg+Ela+Err/Orth+Use/Marg+Err/Orth:ebmus%>ist%>   PxCs     ; ! ! !
                          +Sg+Com+Err/Orth:ebmus%>ijn     K-CONS   ; !
                          +Sg+Com+Err/Orth:ebmus%>ijna    K-VOW   ; ! !SUB
         +Sg+Com+Err/Orth+Use/Marg+Err/Orth:ebmus%>ijn%>   PxCs     ; ! ! !
                             +Ess+Err/Orth:ebmus%>in      K-CONS   ; !
                          +Pl+Nom+Err/Orth:ebmus%>a       K-VOW   ; !
                          +Pl+Gen+Err/Orth:ebmus%>ij      K-CONS   ; !
                          +Pl+Abe+Err/Orth:ebmus%>ij#dagi K-VOW   ; !
                          +Pl+Abe+Err/Orth:ebmus%>ij#dagá K-VOW   ; !
                          +Pl+Abe+Err/Orth:ebmus%>ij#tagá K-VOW   ; !
                          +Pl+Acc+Err/Orth:ebmus%>ijt     K-CONS   ; !
         +Pl+Acc+Err/Orth+Use/Marg+Err/Orth:ebmus%>ijd%>   PxCs     ; ! ! !
                          +Pl+Ill+Err/Orth:ebmus%>ijda    K-VOW   ; !
                          +Pl+Ine+Err/Orth:ebmus%>ijn     K-CONS   ; !
         +Pl+Ine+Err/Orth+Use/Marg+Err/Orth:ebmus%>ijn%>   PxCs     ; ! ! !
                          +Pl+Ela+Err/Orth:ebmus%>ijs     K-CONS   ; !
         +Pl+Ela+Err/Orth+Use/Marg+Err/Orth:ebmus%>ijst%>  PxCs     ; ! ! !
                          +Pl+Com+Err/Orth:ebmus%>ij      K-CONS   ; !


!! Comparative and Superlative sub-lexica
! -----------

LEXICON CompSup-EVEN !!= @CODE@
         +Sg+Nom+Use/Marg+Err/Orth:    PxV                  ; ! ! !Spiik not explicit here, thus guessing.
                             +Ess:n   K-CONS               ;
                   ! +Ess+Err/Orth:n%> PxC ; !^P^ !MARG !No Ess+Px according to Spiik, Grundstrom
                          +Sg+Ill:j   K-CONS               ;
         +Sg+Ill+Use/Marg+Err/Orth:s%> PxCi                 ; ! !
                                 :Q1  CompSup-EVENWEAKSTEM ;

LEXICON CompSup-EVENWEAKSTEM  !!= @CODE@
                          +Sg+Gen:       K-CONS   ;
                          +Sg+Abe:#dagi  K-VOW   ;
                          +Sg+Abe:#dagá  K-VOW   ;
                          +Sg+Abe:#tagá  K-VOW   ;
         +Sg+Gen+Use/Marg+Err/Orth:       PxV      ; ! !
                          +Sg+Acc:v      K-CONS   ;
         +Sg+Acc+Use/Marg+Err/Orth:       PxV      ; ! !
                          +Sg+Ine:n      K-CONS   ;
         +Sg+Ine+Use/Marg+Err/Orth:n%>    PxC      ; ! !
                          +Sg+Ela:s      K-CONS   ;
         +Sg+Ela+Use/Marg+Err/Orth:st%>   PxC      ; ! !
                          +Pl+Nom:       K-VOW   ;
         +Pl+Nom+Use/Marg+Err/Orth:       PxNomPlV ; ! !
                          +Pl+Gen:j      K-CONS   ;
                          +Pl+Abe:j#dagi K-VOW   ;
                          +Pl+Abe:j#dagá K-VOW   ;
                          +Pl+Abe:j#tagá K-VOW   ;
         +Pl+Gen+Use/Marg+Err/Orth:j%>    PxC      ; ! !
                          +Pl+Acc:jt     K-CONS   ;
         +Pl+Acc+Use/Marg+Err/Orth:jd%>   PxC      ; ! !
                          +Pl+Ill:jda    K-VOW   ;
         +Pl+Ill+Use/Marg+Err/Orth:jdis%> PxCs     ; ! !
                          +Pl+Ine:jn     K-CONS   ;
         +Pl+Ine+Use/Marg+Err/Orth:jn%>   PxC      ; ! !
                          +Pl+Ela:js     K-CONS   ;
         +Pl+Ela+Use/Marg+Err/Orth:jst%>  PxC              ; ! !
                          +Pl+Com:j      K-CONS   ;
         +Pl+Com+Use/Marg+Err/Orth:j%>    PxC      ; ! !
                  +Sg+Com+Err/Orth:X5jna  K-VOW   ; !
                          +Sg+Com:X5jn   K-CONS   ; ! X5 for o:u in ComSg. Note two dummies in a row!
         +Sg+Com+Use/Marg+Err/Orth:X5jn%> PxC      ; ! !

LEXICON CompSup-ODDCASE
 +Sg+Nom+Use/Marg+Err/Orth:        PxC  ; ! !
                          +Sg+Gen:a       K-VOW ;
                          +Sg+Abe:a#dagi  K-VOW ;
                          +Sg+Abe:a#dagá  K-VOW ;
                          +Sg+Abe:a#tagá  K-VOW ;
         +Sg+Gen+Use/Marg+Err/Orth:        PxC    ; ! !
                          +Sg+Acc:av      K-CONS ;
         +Sg+Acc+Use/Marg+Err/Orth:        PxC    ; ! !
                          +Sg+Ill:ij      K-CONS ; ! Px ?? !!
         +Sg+Ill+Use/Marg+Err/Orth:as%>    PxCs   ; ! !
                          +Sg+Ine:in      K-CONS ;
         +Sg+Ine+Use/Marg+Err/Orth:in%>    PxCs   ; ! !
                          +Sg+Ela:is      K-CONS ;
         +Sg+Ela+Use/Marg+Err/Orth:ist%>   PxCs   ; ! !
                          +Sg+Com:ijn     K-CONS ;
                  +Sg+Com+Err/Orth:ijna    K-VOW ; !
         +Sg+Com+Use/Marg+Err/Orth:ijn%>   PxCs   ; ! !
                             +Ess:in      K-CONS ;
                          +Pl+Nom:a       K-VOW ;
                          +Pl+Gen:ij      K-CONS ;
                          +Pl+Abe:ij#dagi K-VOW ;
                          +Pl+Abe:ij#dagá K-VOW ;
                          +Pl+Abe:ij#tagá K-VOW ;
         +Pl+Gen+Use/Marg+Err/Orth:ij%>    PxCs   ; ! !
                          +Pl+Acc:ijt     K-CONS ;
         +Pl+Acc+Use/Marg+Err/Orth:ijd%>   PxCs   ; ! !
                          +Pl+Ill:ijda    K-VOW ;
         +Pl+Ill+Use/Marg+Err/Orth:ijdas%> PxC    ; ! !
                          +Pl+Ine:ijn     K-CONS ;
         +Pl+Ine+Use/Marg+Err/Orth:ijn%>   PxCs   ; ! !
                          +Pl+Ela:ijs     K-CONS ;
         +Pl+Ela+Use/Marg+Err/Orth:ijst%>  PxCs ; ! !
                          +Pl+Com:ij      K-CONS ;
         +Pl+Com+Use/Marg+Err/Orth:ij%>    PxCs ; ! !




! Adj declension
! ---------------

LEXICON ATTR  !!= @CODE@ Sends attributes to
     +Attr:   NAMÁK  ;
     +Attr:   K-CONS ; ! Plain attributive.
 +Cmp/Attr:   R      ;
     +Attr:   VUOHTA ;
! -:#%- ProperNoun ;   !already in LEXICON R !
! : ALIT ;  ! both comp and independent adj


LEXICON ATTR_PrsPrc  !!= @CODE@ Attr without -vuohta derivation.
     +Attr:   K  ;
     +Attr:   NAMÁK  ;
     +Attr:   K-CONS ; ! Plain attributive
 +Cmp/Attr:    R     ;
! -:#%- ProperNoun ;   !already in LEXICON R !
! : ALIT ;  ! both comp and independent adj



!! ## Derivation of adjectives
! ------------------------

LEXICON DenominalAdjsV1 !!= @CODE@ ! even noun stems are sent here
      +Der1+Der/Car+A:Q1      DIBME  ;
      +Der1+Der/Car+A:Q1dis%> ATTR   ;
!     +Der1+Der/Car+A:Q1dis   VUOHTA ; !moved to ATTR
!     +Der1+Der/Car+A:Q1d     DUVVAT ; ! this is incorrect, denominal -dibme adjectives cannot be derived to a verb, at least it should be -dimeduvvat, not only -duvvat?
!                +Abe:Q1dak   K-CONS ; !not deivation but case, moved to inflection lexicons
!                +Abe:Q1da    K-VOW ; !not deivation but case, moved to inflection lexicons
                   
LEXICON DenominalAdjsV1_1 !!= @CODE@ ! even noun stems without grade alternation are sent here
      +Der1+Der/Car+A:      DIBME  ;
      +Der1+Der/Car+A:dis%> ATTR   ;
!     +Der1+Der/Car+A:dis   VUOHTA ; !moved to ATTR
!     +Der1+Der/Car+A:d     DUVVAT ; ! this is incorrect, denominal -dibme adjectives cannot be derived to a verb, at least it should be -dimeduvvat, not only -duvvat?
!                +Abe:dak   K-CONS ; !not deivation but case, moved to inflection lexicons
!                +Abe:da    K-VOW ; !not deivation but case, moved to inflection lexicons

LEXICON DenominalAdjsV2 !!= @CODE@ ! even noun stems are sent here. -asj derivation
 +Der1+Der/lasj+A:l DÁRBULASJ ;


LEXICON DenominalAdjsKINO !!= @CODE@ ! unassimilated nouns are sent here
      +Der1+Der/Car+A:      DIBME      ;
      +Der1+Der/Car+A:dis%> ATTR       ;
!     +Der1+Der/Car+A:dis   VUOHTA     ; !moved to ATTR
!     +Der1+Der/Car+A:d     DUVVAT     ; ! this is incorrect, denominal -dibme adjectives cannot be derived to a verb, at least it should be -dimeduvvat, not only -duvvat?
!     +Der4+Der/ahtes:g     AHTES      ; Only for odd-syllable stems
!     +Der4+Der/ahtes:d     AHTES      ;
                 +Abe:dak   K-CONS     ;
                 +Abe:da    K-VOW     ;
     +Der1+Der/lasj+A:l     DÁRBULASJ  ;


LEXICON DenominalAdjsODD !!= @CODE@  ! gives derivation -ahtes
                       +Der4+Der/ahtes:        AHTES ;
  !+Der4+Der/ahtes+A+Der5+Der/AAdv+Adv:ahtá%>  K     ;
 ! +Der4+Der/ahtes+A+Der5+Der/AAdv+Adv:ahti%>  K     ;
!                                 +Abe:adak    K     ; Odd-syllable stems donºt get the abessiv -dak, itºs only for párra and contracted stems.
!                                 +Abe:ada     K     ;

LEXICON DenominalAdjsContr !!= @CODE@
     +Der1+Der/Car+A:      DIBME  ;
     +Der1+Der/Car+A:dis%> ATTR   ;
!    +Der1+Der/Car+A:dis   VUOHTA ; !moved to ATTR
!    +Der1+Der/Car+A:d     DUVVAT ; ! this is incorrect, denominal -dibme adjectives cannot be derived to a verb, at least it should be -dimeduvvat, not only -duvvat?
                +Abe:dak   K-CONS ;
                +Abe:da    K-VOW ;

!! Derivations to adjectives, hardcoded in adjectives stems file


LEXICON DIEHTEMAHTES !!= @CODE@ ! odd syllable For hardcoded -ahtes words.
                           :ahtes%>     ATTR              ;
!        +Der5+Der/AAdv+Adv:ahtá%>      K-VOW            ; !- ahtá is not adverb, according to Bruce Morén-Duolljá its a case like essive.
!        +Der5+Der/AAdv+Adv:ahti%>      K-VOW            ;
                    +Sg+Nom:ahtes%>     K-CONS            ;
           +Sg+Nom+Err/Orth:ahttá%>     PxV               ; !
                +Ess+Use/NG:ahttá%>n    K-CONS            ;
!                          :ahtes       VUOHTA            ; !moved to ATTR
                           :ahttá%>     ILL-EVENCASEadj   ;
                    +Use/NG:ahttá%>     N-EVENWEAKSTEMadj ;
                 +Cmp/SgGen:ahttá%>     R                 ;     
                 +Cmp/PlGen:ahttá%>j    R                 ;
!                          :ahttá%>     BU/MUS            ; !Outcommendted Der/Comp and Der/Super 19.96.2020, gives very strange meaning
                           :ahttás%>    OTH-ODDCASEadj    ;
!                          :ahttás%>    ABBO/AMOS         ; !Outcommendted Der/Comp and Der/Super 19.96.2020, gives very strange meaning
         +Der5+Der/AAdv+Adv:ahttás%>it  K-CONS            ;
                           :ahttás%>    ODDCmp            ;


LEXICON LÁGÁSJ !!= @CODE@
      +Attr:%>   K-CONS         ;
 +Cmp/SgGen:%>a  R              ;
 +Cmp/PlGen:%>ij R              ;
           :     VUOHTA         ;
           :%>   N-ODDCASE      ;
           :%>   OTH-ODDCASEadj ;
   -+Use/Circ:#%-  ProperNoun     ; ! ! Definitely circular x 35000.

LEXICON BÁJNUK !!= @CODE@ ! hardcoded denominal derivations, latus has changed from o>u, a>a, e>á (Bájnno>bájnuk, juolgge>juolgák, giella>gielak.  Attr same as pred, no comp in this lexicon. 
      +Attr:%>   K-CONS         ;
 +Cmp/SgGen:%>a  R              ;
 +Cmp/PlGen:%>ij R              ;
      +Attr:     VUOHTA         ;
           :%>   N-ODDCASE      ;
           :%>   OTH-ODDCASEadj ;

LEXICON TSÅHPÅK !!= @CODE@ ! hardcoded denominal derivations latus has changed from o>u, a>a, e>á AND -GIS attr. Attr same as pred is err/orth taged. no comp in this lexicon. 

          +Attr:is%>   K-CONS         ;
 +Err/Orth+Attr:%>     K-CONS         ;
     +Cmp/SgGen:%>a    R              ;
     +Cmp/PlGen:%>ij   R              ;
          +Attr:       VUOHTA         ;
               :%>     N-ODDCASE      ;
               :%>     OTH-ODDCASEadj ;

LEXICON GIEVLEK !!= @CODE@ ! hardcoded derivations, not same as BÁJNUK since latus has unexpected vowel. Latus hasn't changed o>u, a>a, e>á. Goes directly to BÁJNUK, only made to sort these different kinds of derivations. Many of these may be derivated from verbs or other adjectives.

: BÁJNUK ; 


LEXICON SJERVAK !!= @CODE@ ! hardcoded derivations, not same as TSÅHPÅK since latus has unexpected vowel. Latus hasn't changed o>u, a>a, e>á. Goes directly to TSÅHPÅK, only made to sort these different kinds of derivations. Many of these may be derivated from verbs or other adjectives.

: TSÅHPÅK ;

LEXICON DIBME !!= @CODE@ ! even and contracted
                   :dibme%>    NE-EVENCASEadj    ;
                   :dibme%>X4  ILL-EVENCASEadj   ;
                   :dibme%>Q1  N-EVENWEAKSTEMadj ;
!       +Use/-Spell:d          ABBO/AMOS         ; !Outcommendted Der/Comp and Der/Super 19.96.2020, gives very strange meaning
         +Cmp/SgGen:dibme%>Q1  R                 ;
         +Cmp/PlGen:dibme%>Q1j R                 ;
 +Der5+Der/AAdv+Adv:dibme%>Q1t K-CONS            ;


LEXICON LIS !!= @CODE@ ! Handlernomen på -is?
                        :%>     ATTR                     ;
                 +Sg+Nom:%>     K-CONS                   ;
        +Sg+Nom+Err/Orth:a%>    PxV                      ; !
              +Cmp/SgGen:a%>Q1  R                        ;
              +Cmp/PlGen:a%>Q1j R                        ;
                    +Ess:a%>n   K-CONS                   ;
                        :a%>Q1  N-EVENWEAKSTEM-OTHadj    ;
                        :a%>    ILL-EVENCASEadj          ;
                        :a%>Q1  N-EVENWEAKSTEM-SG-COMadj ;
!                       : VUOHTA ; !moved to ATTR


LEXICON Ahkásasj !!= @CODE@ ! lexicalized and denominal -asj derivations
      +Attr:%>     K-CONS        ;
 +Cmp/SgGen:a%>Q1  R             ;
 +Cmp/PlGen:a%>Q1j R             ;
    +Sg+Nom:%>     K-CONS        ;
           :a%>    ÁRMMOGIS-EVEN ;
           :       VUOHTA        ;


LEXICON STÁVVALIS !!= @CODE@ ! Must be "stávvalis" in bot pred and attr, as "guovddelis". OK& Kintel 2012: stávval attr stávvalis this is err/orth taged, also as second compound, this is err/orth taged. No comparison.
                    :is%>      ATTR           ; !
             +Sg+Nom:is%>      K-CONS         ;
                    :issa%>    ÁRMMOGIS-EVEN  ; ! Declined as four-syllable noun
  +Der5+Der/AAdv+Adv:issa%>Q1t K-CONS         ;
          +Cmp/SgGen:issa%>Q1  R              ;
          +Cmp/PlGen:issa%>Q1j R              ;

 +Err/Orth+Cmp/SgGen:%>a       R              ; !stávval as pred
 +Err/Orth+Cmp/PlGen:%>ij      R              ; !stávval as pred
           +Err/Orth:%>Q1      VUOHTA         ; !stávval as pred
           +Err/Orth:%>        N-ODDCASE      ; !stávval as pred
           +Err/Orth:%>        OTH-ODDCASEadj ; !stávval as pred



!! Derivations to adjectives, continuation lexicon not for hardcoded adjectives




LEXICON AHTES !!= @CODE@ ! odd syllable, only a continuation lexicon for words that are not in adjectives stems. Just as DIEHTEMAHTES, only with the +A tag that adjectives already get i stems file.
                           +A:ahtes%>     ATTR              ;
!        +A+Der5+Der/AAdv+Adv:ahtá%>      K-VOW            ; !- ahtá is not adverb, according to Bruce Morén-Duolljá its a case like essive.
!        +A+Der5+Der/AAdv+Adv:ahti%>      K-VOW            ;
                    +A+Sg+Nom:ahtes%>     K-CONS            ;
           +A+Sg+Nom+Err/Orth:ahttá%>     PxV               ; !
                       +A+Ess:ahttá%>n    K-CONS            ;
!                          +A:ahtes       VUOHTA            ; !moved to ATTR
                           +A:ahttá%>     ILL-EVENCASEadj   ;
                           +A:ahttá%>     N-EVENWEAKSTEMadj ;
                 +A+Cmp/SgGen:ahttá%>     R                 ;     
                 +A+Cmp/PlGen:ahttá%>j    R                 ;
!                          +A:ahttá%>     BU/MUS            ; !Outcommendted Der/Comp and Der/Super 19.96.2020, gives very strange meaning
                            +A:ahttás%>   OTH-ODDCASEadj    ;
!                           +A:ahttás%>   ABBO/AMOS         ; !Outcommendted Der/Comp and Der/Super 19.96.2020, gives very strange meaning
          +A+Der5+Der/AAdv+Adv:ahttás%>it K-CONS            ;
                            +A:ahttás%>   ODDCmp            ;


LEXICON AHKES !!= @CODE@  
                        +A:%>Q1s ATTR              ;
                 +A+Sg+Nom:%>Q1s K-CONS            ;
                        +A:%>X4  ILL-EVENCASEadj   ;
                        +A:%>X4  N-EVENWEAKSTEMadj ;
        +A+Sg+Nom+Err/Orth:%>X4  PxV               ; !
                    +A+Ess:%>X4n K-CONS            ;
                        +A:%>X4  BU/MUS            ;
      +A+Der5+Der/AAdv+Adv:%>X4t K-CONS            ;
              +A+Cmp/SgGen:%>X4  R                 ;
              +A+Cmp/PlGen:%>X4j R                 ;



LEXICON AGAdj !!= @CODE@ ! denominal derivations go here, attr same as pred, no comp in this lexicon
      +A+Attr:%>   K-CONS         ;
 +A+Cmp/SgGen:%>a  R              ;
 +A+Cmp/PlGen:%>ij R              ;
      +A+Attr:     VUOHTA         ;
           +A:%>   N-ODDCASE      ;
           +A:%>   OTH-ODDCASEadj ;



