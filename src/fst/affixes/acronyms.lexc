! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@uit.no or feedback@divvun.no


LEXICON UNIT   !!= * **@CODE@**  As acro, but without paradigm
 RHyph ;
 ENDLEX ;

LEXICON ACRO_LONDON
 acroconnector_oddcons ;
!+N+Prop:%> LONDONUCASE ;

LEXICON ACRO_ACCRA
 acroconnector_vow ;
!+N+Prop:%> ACCRADECL ;

LEXICON ACRO_BERN
 acroconnector_cons ;
+Use/NG:%> BERNUCASE ;

LEXICON ACRO_NYSTØ
 acroconnector_vow ;
!+N+Prop:%> NYSTØ-OBL ;


LEXICON ACRONOUN_cons   !!= * **@CODE@** is the lexicon for **nouns** (not +Prop) like ATV
 acroconnector_cons ;    

LEXICON ACRONOUN_vow   !!= * **@CODE@** is the lexicon for **nouns** (not +Prop) like ATV
 acroconnector_vow ;    
! +N+Prop:%> ACCRADECL ;

LEXICON DYN_ACRO_vow
 +Use/Circ+Use/SpellNoSugg: DYN_ACRO_V ;    

LEXICON DYN_ACRO_cons
 +Use/Circ+Use/SpellNoSugg: DYN_ACRO_C ;    

LEXICON DYN_ACRO_C
 +N+Prop+Sem/Org+ACR+Dyn: acroconnector_cons ;

LEXICON DYN_ACRO_V
 +N+Prop+Sem/Org+ACR+Dyn: acroconnector_vow ;



LEXICON ACRO_vow  !!= @CODE@ Acronyms ending in vowels and which are pronounced as a word, like NATO and UNESCO (Giellagálldo bievddegirjje 2/2016). These only get the case endings: NATO:v, NATO:n, etc. 
 acroconnector_vow  ;

LEXICON acroconnector_vow ! Here comes a set of possible symbols to put between the abbreviation and its suffix.
                acronull_vow  ;
          :%>%: acrosuff_vow  ; 
                RHyph         ; ! Acro compounds: CD-2
          
 +Err/Orth:     acrosuff_vow  ; ! The lexica are split in order to avoid the POS
 +Err/Orth:%>'  acrosuff_vow  ; ! tag for other symbols using the acroconnector
 +Err/Orth:%>%` acrosuff_vow  ; ! lexicon. (cf. the punct-sme-lex.txt file)
 +Err/Orth:%>%´ acrosuff_vow  ;
 +Err/Orth:     R             ; ! Without hyphen, not normative:  NRK23

LEXICON acronull_vow 
 +Sg+Nom:      A-VOW  ;
 +Sg+Gen:      A-VOW  ;
 +Sg+Acc:%>%:v A-CONS ;
 +Pl+Nom:      A-VOW  ;






LEXICON acrosuff_vow 
			acrosuff ;
+Err/Orth:a acrosuff ;

LEXICON acrosuff
 +Sg+Ill:j   A-CONS ;
 +Sg+Ine:n   A-CONS ; ! NRK:n
 +Sg+Ela:s   A-CONS ; ! NRK:s
 +Sg+Com:jn  A-CONS ;
    +Ess:n   A-CONS ; ! NBR:n
 +Pl+Gen:j   A-CONS ;
 +Pl+Acc:jt  A-CONS ;
 +Pl+Ill:jda A-VOW ;
 +Pl+Ine:jn  A-CONS ;
 +Pl+Ela:js  A-CONS ;
 +Pl+Com:j   A-CONS ;


LEXICON ACRO_cons  !!= @CODE@ "Even-syll" acronyms ending on consonants, but also acronyms ending in vowels which are not pronounced as words, like LO and CV (Giellagálldo bievddegirjje 2/2016). These get case endings like a regular a-stem noun with both the vowel and case ending: LO:av, LO:an, etc. 
 acroconnector_cons ;

LEXICON acroconnector_cons ! Here comes a set of possible symbols to put between the abbreviation and its suffix. 
                acronull_cons  ;
          :%>%: acrosuff_cons  ; 
                RHyph          ; ! Acro compounds: CD-2
          
 +Err/Orth:     acrosuff_cons  ; ! The lexica are split in order to avoid the POS
 +Err/Orth:%>'  acrosuff_cons  ; ! tag for other symbols using the acroconnector
 +Err/Orth:%>%` acrosuff_cons  ; ! lexicon. (cf. the punct-sme-lex.txt file)
 +Err/Orth:%>%´ acrosuff_cons  ;
 +Err/Orth:     R                  ; ! Without hyphen, not normative:  NRK23


LEXICON acronull_cons
 +Sg+Nom:       A-CONS ;
 +Sg+Gen:%>%:a  A-VOW  ;
 +Sg+Acc:%>%:av A-CONS ;
 +Pl+Nom:%>%:a  A-VOW  ;

 +Sg+Gen+Err/Orth:      A-CONS ;
 +Pl+Nom+Err/Orth:      A-CONS ;
 +Sg+Gen+Err/Orth:%>a   A-VOW ;
 +Pl+Nom+Err/Orth:%>a   A-VOW ;

LEXICON acrosuff_cons
+Err/Orth:  acrosuff ;
         :a acrosuff ;


LEXICON ACRO_oddcons  !!= @CODE@ "Odd-syll" acronyms ending in consonants.  These get case endings like odd syllable nouns, with both the vowel and case ending: SÁNAG:av, SÁNAG:ij, instead of SÁNAG:aj. 
 acroconnector_oddcons ;

LEXICON acroconnector_oddcons ! Here comes a set of possible symbols to put between the abbreviation and its suffix. 
                acronull_oddcons  ;
          :%>%: acrosuff_oddcons  ; 
                RHyph          ; ! Acro compounds: CD-2
          
 +Err/Orth:     acrosuff_oddcons  ; ! The lexica are split in order to avoid the POS
 +Err/Orth:%>'  acrosuff_oddcons  ; ! tag for other symbols using the acroconnector
 +Err/Orth:%>%` acrosuff_oddcons  ; ! lexicon. (cf. the punct-sme-lex.txt file)
 +Err/Orth:%>%´ acrosuff_oddcons  ;
 +Err/Orth:     R                  ; ! Without hyphen, not normative:  NRK23


LEXICON acronull_oddcons
          +Sg+Nom:       A-CONS ;
          +Sg+Gen:%>%:a  A-VOW  ;
          +Pl+Nom:%>%:a  A-VOW  ;
          +Sg+Acc:%>%:av A-CONS ;
 +Sg+Gen+Err/Orth:       A-CONS ;
 +Pl+Nom+Err/Orth:       A-CONS ;
 +Sg+Gen+Err/Orth:%>a    A-VOW  ;
 +Pl+Nom+Err/Orth:%>a    A-VOW  ;


LEXICON acrosuff_oddcons
+Err/Orth:  acrosuff ;
         :i acrosuff ;



!------Der/r------
 +Der1+Der/r+CmpN/SgN+CmpN/SgG+CmpN/PlG:»ár GAHPER ;  !

LEXICON A-CONS
+Cltl+Use/MT:#% la # ;  
# ;
 
LEXICON A-VOW
+Cltl+Use/MT:#% l # ;  
# ;
