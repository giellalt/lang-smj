! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no


LEXICON UNIT   !!= * __@CODE@__  As acro, but without paradigm
+N+ABBR: RHyph ;
+N+ABBR: ENDLEX ;

LEXICON ACRO_LONDON
+N+Prop+ACR: acroconnector_cons ;
!+N+Prop:%> LONDONUCASE ;

LEXICON ACRO_ACCRA
+N+Prop+ACR: acroconnector_vow ;
!+N+Prop:%> ACCRADECL ;

LEXICON ACRO_BERN
+N+Prop+ACR: acroconnector_cons ;
+N+Prop+ACR:%> BERNUCASE ;

LEXICON ACRO_NYSTØ
+N+Prop+ACR: acroconnector_vow ;
!+N+Prop:%> NYSTØ-OBL ;


LEXICON ACRONOUN_cons   !!= * __@CODE@__ is the lexicon for __nouns__ (not +Prop) like ATV
 +N+ACR: acroconnector_cons ;    

LEXICON ACRONOUN_vow   !!= * __@CODE@__ is the lexicon for __nouns__ (not +Prop) like ATV
 +N+ACR: acroconnector_vow ;    
! +N+Prop:%> ACCRADECL ;

LEXICON DYN_ACRO_vow
 +N+Prop+Sem/Org+ACR+Dyn: acroconnector_vow ;

LEXICON DYN_ACRO_cons
 +N+Prop+Sem/Org+ACR+Dyn: acroconnector_cons ;


LEXICON ACRO_vow  !!= @CODE@ Acronyms ending in vowels and which are pronounced as a word, like NATO and UNESCO (Giellagálldo bievddegirjje 2/2016). These only get the case endings: NATO:v, NATO:n, etc. 
+N+Prop+ACR: acroconnector_vow  ;

LEXICON acroconnector_vow ! Here comes a set of possible symbols to put between the abbreviation and its suffix.
                acronull_vow  ;
          :%>%: acrosuff_vow  ; 
                RHyph         ; ! Acro compounds: CD-2
          
 +Err/Orth:     acrosuff_vow  ; ! The lexica are split in order to avoid the POS
 +Err/Orth:%>'  acrosuff_vow  ; ! tag for other symbols using the acroconnector
 +Err/Orth:%>%` acrosuff_vow  ; ! lexicon. (cf. the punct-sme-lex.txt file)
 +Err/Orth:%>%´ acrosuff_vow  ;
 +Err/Orth:     R             ; ! Without hyphen, not normative:  NRK23

LEXICON acronull_vow 
         +Sg+Nom:%> # ;
         +Sg+Gen:%> # ;
         +Pl+Nom:%> # ;


LEXICON acrosuff_vow 
			acrosuff ;
+Err/Orth:a acrosuff ;

LEXICON acrosuff
 +Sg+Acc:v            # ;
 +Sg+Ill:j            # ;
 +Sg+Ine:n            # ; ! NRK:n
 +Sg+Ela:s            # ; ! NRK:s
 +Sg+Com:jn           # ;
     +Ess:n           # ; ! NBR:n
 +Pl+Gen:j            # ;
 +Pl+Acc:jt           # ;
 +Pl+Ill:jda          # ;
 +Pl+Ine:jn           # ;
 +Pl+Ela:js           # ;
 +Pl+Com:j            # ;
 

LEXICON ACRO_cons  !!= @CODE@ Mostly acronyms ending in consonants, but also acronyms ending in vowels which are not pronounced as words, like LO and CV (Giellagálldo bievddegirjje 2/2016). These get case endings like a regular a-stem noun with both the vowel and case ending: LO:av, LO:an, etc. 
+N+Prop+ACR: acroconnector_cons ;

LEXICON acroconnector_cons ! Here comes a set of possible symbols to put between the abbreviation and its suffix. 
                acronull_cons  ;
          :%>%: acrosuff_cons  ; 
                RHyph          ; ! Acro compounds: CD-2
          
 +Err/Orth:     acrosuff_cons  ; ! The lexica are split in order to avoid the POS
 +Err/Orth:%>'  acrosuff_cons  ; ! tag for other symbols using the acroconnector
 +Err/Orth:%>%` acrosuff_cons  ; ! lexicon. (cf. the punct-sme-lex.txt file)
 +Err/Orth:%>%´ acrosuff_cons  ;
 +Err/Orth:     R                  ; ! Without hyphen, not normative:  NRK23


LEXICON acronull_cons
         +Sg+Nom:%> # ;
         +Sg+Gen:%>%:a # ;
         +Pl+Nom:%>%:a # ;
+Sg+Gen+Err/Orth:%> # ;
+Pl+Nom+Err/Orth:%> # ;
+Sg+Gen+Err/Orth:%>a # ;
+Pl+Nom+Err/Orth:%>a # ;

LEXICON acrosuff_cons
+Err/Orth:  acrosuff ;
         :a acrosuff ;


!------Der/r------
 +Der1+Der/r+CmpN/SgN+CmpN/SgG+CmpN/PlG:»ár GAHPER ;  !
