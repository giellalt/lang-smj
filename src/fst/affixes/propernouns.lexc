! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@uit.no or feedback@divvun.no

! ================================ !
!! # Lule Sáme Proper noun morphology !
! ================================ !

! Table of content
! --------
! * Even syllable proper nouns
! ** Unstressed last syllable
! ** Stressed last syllable
! ** 4-syllable stems
! ** Plurals
! * Odd syllable proper nouns
! ** Regular odd stems
! ** Plurals
! * Contracted proper nouns
! ** Regular contracted stems
! ** Plurals
! * Sme/sma lexicons

! V-finals stems with unstressed last syllable get an even syllable case marking, with or without cg. This includes lexicons ACCRA, GIRUNA, RONDANE, GATA, BETFAGE, MARJA and SUOBMA. C-final trisyllabic stems go to BASUDIS and get an even syllable case marking. Both v- and c-final names with a stressed last syllable get even syllable case marking. This includes lexicons DUBAI, NYSTØ and BERN.

! C-final stems with unstressed last syllable get an odd syllable case marking, without without cg. This includes lexicons LONDON, JOKULL, ANAR, PIPP, DUORTNUS.

! Stems ending -oj, -åj or -es in sg.nom. get regular contracted case marking with I-III cg.


! Name lexica

!! ## Even syllable proper nouns
! =============

!! Unstressed last syllable
! ---------------------

!! Words in ACCRA lexicons end on vowel, have no CG and get "even-syllable" case marking where case suffixes are added directly. Illative e:i, but not o:u. Last syllable is unstressed. Both non-assimilated and assmilated stems (although not all are fully, or correctly, assmilated)

LEXICON ACCRA-ani  !!= @CODE@ Vowel-final names where case endings are added directly, no cg. Illative e changes to i. Animales.
 +N+Prop+Sem/Ani@U.Cap.Obl@:%>00@U.Cap.Obl@ ACCRADECL ;

!!€gt-norm: Tjuorri # Even-syllable test
!!€ Tjuorri:               Tjuorri+N+Prop+Sem/Ani+Sg+Nom
!!€ Tjuorrij:              Tjuorri+N+Prop+Sem/Ani+Sg+Ill
!!€ Tjuorris:              Tjuorri+N+Prop+Sem/Ani+Sg+Ela

LEXICON ACCRA-obj !!= @CODE@ Vowel-final names where case endings are added directly, no cg. Object names
 +N+Prop+Sem/Obj:%> ACCRADECL ;

!!€gt-norm: Gestapo # Even-syllable test
!!€ Gestapo:               Gestapo+N+Prop+Sem/Obj+Sg+Nom
!!€ Gestapoj:              Gestapo+N+Prop+Sem/Obj+Sg+Ill
!!€ Gestapos:              Gestapo+N+Prop+Sem/Obj+Sg+Ela

LEXICON ACCRA-org !!= @CODE@ Vowel-final names where caseendings are added directly, no cg. organizations
 +N+Prop+Sem/Org:%> ACCRADECL-ORG ;

!!€gt-norm: Giellatekno # Even-syllable test
!!€ Giellatekno:               Giellatekno+N+Prop+Sem/Org+Sg+Nom
!!€ Giellateknoj:              Giellatekno+N+Prop+Sem/Org+Sg+Ill
!!€ Giellateknos:              Giellatekno+N+Prop+Sem/Org+Sg+Ela

LEXICON ACCRA-mal !!= @CODE@ Vowel-final names where case are added directly, no cg. Male names
                            +N+Prop+Sem/Mal:%>            ACCRADECL ;
                            +N+Prop+Sem/Mal:%>            RÁVKKE_NAMÁK ;


+N+Prop+Sem/Mal+Attr@U.Cap.Obl@:%>000@U.Cap.Obl@ #     ;

!!€gt-norm: Antonio # Even-syllable test
!!€ Antonio:               Antonio+N+Prop+Sem/Mal+Sg+Nom
!!€ Antonioj:              Antonio+N+Prop+Sem/Mal+Sg+Ill
!!€ Antonios:              Antonio+N+Prop+Sem/Mal+Sg+Ela



LEXICON ACCRA-fem !!= @CODE@ Vowel-final names where case endings are added directly, no cg. Female names
                            +N+Prop+Sem/Fem:%>            ACCRADECL ;
                            +N+Prop+Sem/Fem:%>            RÁVKKE_NAMÁK ;

+N+Prop+Sem/Fem+Attr@U.Cap.Obl@:%>000@U.Cap.Obl@ #     ;

!!€gt-norm: Barbro # Even-syllable test
!!€ Barbro:               Barbro+N+Prop+Sem/Fem+Sg+Nom
!!€ Barbroj:              Barbro+N+Prop+Sem/Fem+Sg+Ill
!!€ Barbros:              Barbro+N+Prop+Sem/Fem+Sg+Ela

LEXICON ACCRA-femsur !!= @CODE@ Vowel-final names where case endings are added directly, no cg. Female names also used as surnames
                            +N+Prop+Sem/Fem:%>            ACCRADECL ;
                            +N+Prop+Sem/Sur:%>            ACCRADECL ;
                            +N+Prop+Sem/Fem:%>            RÁVKKE_NAMÁK ;

!            +N+Prop+Sem/Fem+Attr@U.Cap.Obl@:%>000@U.Cap.Obl@ #     ;

!!€gt-norm: Sara # Even-syllable test
!!€ Sara:               Sara+N+Prop+Sem/Fem+Sg+Nom
!!€ Saraj:               Sara+N+Prop+Sem/Fem+Sg+Ill
!!€ Saraj:               Sara+N+Prop+Sem/Sur+Sg+Ill
!!€ Saras:               Sara+N+Prop+Sem/Fem+Sg+Ela

LEXICON ACCRA-malfem !!= @CODE@ Vowel-final names where case endings are added directly, no cg. Names that can be both female and male names
                            +N+Prop+Sem/Fem:%>            ACCRADECL ;
                            +N+Prop+Sem/Mal:%>            ACCRADECL ;
                            +N+Prop+Sem/Fem:%>            RÁVKKE_NAMÁK ;
                            +N+Prop+Sem/Mal:%>            RÁVKKE_NAMÁK ;

+N+Prop+Sem/Fem+Attr@U.Cap.Obl@:%>000@U.Cap.Obl@ #     ;
+N+Prop+Sem/Mal+Attr@U.Cap.Obl@:%>000@U.Cap.Obl@ #     ;

!!€gt-norm: Janne # Even-syllable test
!!€ Janne:               Janne+N+Prop+Sem/Mal+Sg+Nom
!!€ Janne:               Janne+N+Prop+Sem/Fem+Sg+Nom
!!€ Jannij:              Janne+N+Prop+Sem/Mal+Sg+Ill
!!€ Jannes:              Janne+N+Prop+Sem/Mal+Sg+Ela

LEXICON ACCRA-objplc !!= @CODE@ Vowel-final names where case endings are added directly, no cg. Names that can be both objects and place names
        +N+Prop+Sem/Obj:%> ACCRADECL     ;
        +N+Prop+Sem/Plc:%> ACCRADECL-PLC ;
        +N+Prop+Sem/Sur:%> ACCRADECL ;
        +N+Prop+Sem/Plc:%> ACCRA-plc-der ;

LEXICON ACCRA-plc-der
@U.Cap.Opt@ VUONAK ;

!!€gt-norm: Soria-Moria # Even-syllable test
!!€ Soria-Moria:               Soria-Moria+N+Prop+Sem/Obj+Sg+Nom
!!€ Soria-Moriaj:              Soria-Moria+N+Prop+Sem/Obj+Sg+Ill
!!€ Soria-Morias:              Soria-Moria+N+Prop+Sem/Obj+Sg+Ela
!!€ Soria-Moriaj:              Soria-Moria+N+Prop+Sem/Plc+Sg+Ill
!!€ soria-moriak:              Soria-Moria+N+Prop+Sem/Plc+Der/k+N+Sg+Nom
!!€ soria-morialasj:           Soria-Moria+N+Prop+Sem/Plc+Der/lasj+N+Sg+Nom


LEXICON ACCRA-femplc !!= @CODE@ Vowel-final names where case endings are added directly, no cg. Names that can be both female and place names
 +N+Prop+Sem/Fem:%> ACCRADECL     ;
 +N+Prop+Sem/Plc:%> ACCRADECL-PLC ;
 +N+Prop+Sem/Sur:%> ACCRADECL-PLC ;
 +N+Prop+Sem/Plc:%> ACCRA-plc-der ;
 +N+Prop+Sem/Fem:%> RÁVKKE_NAMÁK        ;


!!€gt-norm: Salla # Even-syllable test
!!€ Salla:               Salla+N+Prop+Sem/Fem+Sg+Nom
!!€ Salla:               Salla+N+Prop+Sem/Plc+Pl+Nom
!!€ Sallaj:              Salla+N+Prop+Sem/Fem+Sg+Ill
!!€ Sallas:              Salla+N+Prop+Sem/Fem+Sg+Ela
!!€ sallak:              Salla+N+Prop+Sem/Plc+Der/k+N+Sg+Nom
!!€ sallalasj:           Salla+N+Prop+Sem/Plc+Der/lasj+N+Sg+Nom

LEXICON ACCRA-sur !!= @CODE@ Vowel-final names where case endings are added directly, no cg. Surnames
                            +N+Prop+Sem/Sur:%>            ACCRADECL ;
!            +N+Prop+Sem/Sur+Attr@U.Cap.Obl@:%>000@U.Cap.Obl@ #     ;

!!€gt-norm: Tønne # Even-syllable test
!!€ Tønne:              Tønne+N+Prop+Sem/Sur+Sg+Nom
!!€ Tønnij:              Tønne+N+Prop+Sem/Sur+Sg+Ill
!!€ Tønnes:              Tønne+N+Prop+Sem/Sur+Sg+Ela

LEXICON ACCRA-malsur !!= @CODE@  Vowel-final names where case endings are added directly, no cg. Names that can be both male- and surnames
                  +N+Prop+Sem/Sur:%>               ACCRADECL ;
                  +N+Prop+Sem/Mal:%>               ACCRADECL ;
                  +N+Prop+Sem/Mal:%>               RÁVKKE_NAMÁK    ;
! +N+Prop+Sem/Sur+Attr@U.Cap.Obl@:%>000@U.Cap.Obl@ #         ;
! +N+Prop+Sem/Mal+Attr@U.Cap.Obl@:%>000@U.Cap.Obl@ #         ;


!!€gt-norm: Valio # Even-syllable test
!!€ Valio:               Valio+N+Prop+Sem/Sur+Sg+Nom
!!€ Valio:               Valio+N+Prop+Sem/Mal+Sg+Nom
!!€ Valioj:              Valio+N+Prop+Sem/Sur+Sg+Ill
!!€ Valios:              Valio+N+Prop+Sem/Sur+Sg+Ela

LEXICON ACCRA-A-plc
:a ACCRA-plc ;

LEXICON ACCRA-A-org
:a ACCRA-org ;


LEXICON ACCRA-A_MWE-plc
:a ACCRA_MWE-plc ;

LEXICON ACCRA-A-fem
:a ACCRA-fem ;


LEXICON ACCRA-plc !!= @CODE@ Vowel-final names where caseendings are added directly, no cg. Place names
                +N+Prop+Sem/Plc:%>                ACCRADECL-PLC ;
                +N+Prop+Sem/Sur:%>                ACCRADECL ;
     +N+Prop+Sem/Plc@U.Cap.Opt@:%>00@U.Cap.Opt@ VUONAK        ;

!!€gt-norm: Burma # Even-syllable test
!!€ Burma:               Burma+N+Prop+Sem/Plc+Sg+Nom
!!€ Burmaj:              Burma+N+Prop+Sem/Plc+Sg+Ill
!!€ Burmas:              Burma+N+Prop+Sem/Plc+Sg+Ela
!!€ burmak:              Burma+N+Prop+Sem/Plc+Der/k+N+Sg+Nom
!!€ burmalasj:           Burma+N+Prop+Sem/Plc+Der/lasj+N+Sg+Nom

LEXICON ACCRA_MWE-plc !!= @CODE@ Vowel-final names where caseendings are added directly, no cg. Place names
                   +N+Prop+Sem/Plc:%>                ACCRADECL-PLC ;

LEXICON MOLDAVIA-plc
:a ACCRA-plc ;

LEXICON MOLDAVIA-LOAN-plc
:a ACCRA-LOAN-plc ;

LEXICON MALAWI-plc
: PIPPI-plc ;

LEXICON MACKENZIE-plc
:e ACCRA-plc ;

LEXICON GIRUNA-plc !!= @CODE@ For proper Kiruna. Same as ACCRA. Different lexicon because of sma.
                   :a%> ACCRA-plc ; 

!LEXICON ACCRA-surplc !!= @CODE@ Vowel-final names where case endings are added directly, no cg. Names that are both surnames and place names
!                            +N+Prop+Sem/Sur:%>            ACCRADECL     ;
!                            +N+Prop+Sem/Plc:%>            ACCRADECL-PLC ;
!+N+Prop+Sem/Sur+Attr@U.Cap.Obl@:%>000@U.Cap.Obl@ #         ;
!          +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@:%>000@U.Cap.Opt@ VUONAK    ;

!!€gt-norm: Veitsiluoto # Even-syllable test
!!€ Veitsiluoto:               Veitsiluoto+N+Prop+Sem/Plc+Sg+Nom
!!€ Veitsiluoto:               Veitsiluoto+N+Prop+Sem/Plc+Sg+Nom
!!€ Veitsiluotoj:              Veitsiluoto+N+Prop+Sem/Plc+Sg+Ill
!!€ Veitsiluotos:              Veitsiluoto+N+Prop+Sem/Plc+Sg+Ela
!!€ veitsiluotok:              Veitsiluoto+N+Prop+Sem/Plc+Der/k+N+Sg+Nom
!!€ veitsiluotolasj:           Veitsiluoto+N+Prop+Sem/Plc+Der/lasj+N+Sg+Nom

LEXICON ACCRA-LOAN-org !!= @CODE@ Only nominatives. Vowel-final names where case endings are added directly, no cg. organizations
 +N+Prop+Sem/Org:%> ACCRADECL-LOAN ;

!!€gt-norm: Samenes Idrettsforbund-Norge # Even-syllable test
!!€ Samenes Idrettsforbund-Norge:              Samenes Idrettsforbund-Norge+N+Prop+Sem/Org+Sg+Nom
!!$ Samenes Idrettsforbund-Norge:              Samenes Idrettsforbund-Norge+N+Prop+Sem/Org+Sg+Ela # negative test

LEXICON ACCRA-LOAN-obj !!= @CODE@ Only nominatives. Vowel-final names where case endings are added directly, no cg. Object names
 +N+Prop+Sem/Obj:%> ACCRADECL-LOAN ;

!!€gt-norm: The Norwegian Sami Experience # Even-syllable test
!!€ The Norwegian Sami Experience:        The Norwegian Sami Experience+N+Prop+Sem/Obj+Sg+Nom
!!$ The Norwegian Sami Experience:        The Norwegian Sami Experience+N+Prop+Sem/Obj+Sg+Ill # Negative test

LEXICON ACCRA-LOAN-plc !!= @CODE@ Only nominatives. Vowel-final names where case endings are added directly, no cg.Place names
                 +N+Prop+Sem/Plc:%> ACCRADECL-LOAN-PLC ;
                 +N+Prop+Sem/Sur:%> ACCRADECL-LOAN-PLC ;
! +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@+Err/Orth:%>@U.Cap.Opt@ VUONAK ;

!!€gt-norm: Kautokeino # Even-syllable test
!!€ Kautokeino:              Kautokeino+N+Prop+Sem/Plc+Sg+Nom
!!$ Kautokeinos:              Kautokeino+N+Prop+Sem/Org+Sg+Ela # negative test


!LEXICON ACCRA !!= @CODE@ Vowel-final names without Gradation.. Not in use?
! +N+Prop:%> ACCRADECL ;

! Separate lexicon for ACCRA y final names because these have to go NYSTØ in smj, but cannot be NYSTØ in sma

LEXICON ACCRA-Y-fem 
  NYSTØ-fem ;
  
LEXICON ACCRA-Y-obj
 NYSTØ-obj ;
 
LEXICON ACCRA-Y-plc
 NYSTØ-plc ;

LEXICON ACCRA-Y_MWE-plc
+N+Prop+Sem/Plc:%>                NYSTØDECL-PLC ;

LEXICON ACCRA-Y-mal
 NYSTØ-mal ;

LEXICON ACCRA-Y-malsur
 NYSTØ-mal ;
 NYSTØ-sur ;

 
LEXICON ACCRA-Y-sur
 NYSTØ-sur ;

LEXICON ACCRA-Y-org
 NYSTØ-org ;

LEXICON ACCRA-Y-ani
 NYSTØ-org ;

!LEXICON ACCRA-LOAN-surplc ! Names that are both surnames and place names
! +N+Prop+Sem/Sur:%> ACCRADECL-LOAN ;
! +N+Prop+Sem/Sur+Attr@U.Cap.Obl@+Err/Orth:%>@U.Cap.Obl@ # ;
! +N+Prop+Sem/Plc:%> ACCRADECL-LOAN ;
! +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@+Err/Orth:%>@U.Cap.Opt@ VUONAK ;

LEXICON ACCRADECL
! These sublexica are irrelevant for ACCRA, but added
! for the sake of the lexicon MARJA
 @U.Cap.Obl@ ACCRA-DC ;

LEXICON ACCRADECL-ORG
! These sublexica are irrelevant for ACCRA, but added
! for the sake of the lexicon MARJA
 @U.Cap.Obl@ ACCRA-DC-ORG ;

LEXICON ACCRADECL-PLC
! These sublexica are irrelevant for ACCRA, but added
! for the sake of the lexicon MARJA
 @U.Cap.Obl@ ACCRA-DC ;
 @U.Cap.Opt@ LASJ     ;

LEXICON ACCRADECL-LOAN
! These sublexica are irrelevant for ACCRA, but added
! for the sake of the lexicon MARJA
 @U.Cap.Obl@ ACCRA-DC-LOAN ;

LEXICON ACCRADECL-LOAN-PLC
! These sublexica are irrelevant for ACCRA, but added
! for the sake of the lexicon MARJA
 @U.Cap.Obl@ ACCRA-DC-LOAN ;
 @U.Cap.Opt@ LASJ          ;


LEXICON ACCRA-DC
            +Attr:      K-VOW  ;
          +Sg+Nom:      K-VOW  ;
       +Cmp/SgNom:      RHyph  ;
             +Ess:n     K-CONS ;
          +Sg+Ill:Q4j   K-CONS ; !Q4 to get e:i
 +Sg+Ill+Err/Orth:%:j   K-CONS ; !Q4 to get e:i
          +Sg+Gen:      K-VOW  ;
       +Cmp/SgGen:      RHyph  ;
          +Sg+Acc:v     K-CONS ;
          +Sg+Ine:n     K-CONS ;
          +Sg+Ela:s     K-CONS ;
 +Sg+Acc+Err/Orth:%:v   K-CONS ;
 +Sg+Ine+Err/Orth:%:n   K-CONS ;
 +Sg+Ela+Err/Orth:%:s   K-CONS ;
          +Pl+Nom:      K-VOW  ;
          +Pl+Gen:Q4j   K-CONS ; !Q4 to get e:i
          +Pl+Gen:Q4j   K-CONS ; !Q4 to get e:i
          +Pl+Acc:Q4jt  K-CONS ;
          +Pl+Ill:Q4jda K-VOW  ;
          +Pl+Ine:Q4jn  K-CONS ;
          +Pl+Ela:Q4js  K-CONS ;
          +Pl+Com:Q4j   K-CONS ; !Q4 to get e:i
          +Sg+Com:Q4jn  K-CONS ;
 +Sg+Com+Err/Orth:X5jn  K-CONS ; ! Norgejn
 +Sg+Com+Err/Orth:%:jn  K-CONS ; ! Norgejn

 

LEXICON ACCRA-DC-ORG
          +Sg+Nom:      K-VOW  ;
       +Cmp/SgNom:      RHyph  ;
             +Ess:n     K-CONS ;
          +Sg+Ill:Q4j   K-CONS ; !Q4 to get e:i
          +Sg+Gen:      K-VOW  ;
       +Cmp/SgGen:      RHyph  ;
          +Sg+Acc:v     K-CONS ;
          +Sg+Ine:n     K-CONS ;
          +Sg+Ela:s     K-CONS ;
          +Pl+Nom:      K-VOW  ;
          +Pl+Gen:Q4j   K-CONS ; !Q4 to get e:i
          +Pl+Gen:Q4j   K-CONS ; !Q4 to get e:i
          +Pl+Acc:Q4jt  K-CONS ;
          +Pl+Ill:Q4jda K-VOW  ;
          +Pl+Ine:Q4jn  K-CONS ;
          +Pl+Ela:Q4js  K-CONS ;
          +Pl+Com:Q4j   K-CONS ; !Q4 to get e:i
          +Sg+Com:Q4jn  K-CONS ;
 +Sg+Com+Err/Orth:X5jn  K-CONS ; ! Norgejn



LEXICON ACCRA-DC-LOAN
                +Attr:      K-VOW  ;
              +Sg+Nom:      K-VOW  ;
           +Cmp/SgNom:      RHyph  ;
                 +Ess:n     K-CONS ;
              +Sg+Ill:Q4j   K-CONS ; ! !Q4 to get e:i
              +Sg+Gen:      K-VOW  ;
  +Err/Orth+Cmp/SgGen:      RHyph  ;
              +Sg+Acc:v     K-CONS ;
              +Sg+Ine:n     K-CONS ;
              +Sg+Ela:s     K-CONS ;
!    +Pl+Nom+Err/Orth:      K-VOW  ;
!    +Pl+Gen+Err/Orth:Q4j   K-CONS ; ! !Q4 to get e:i
!    +Pl+Gen+Err/Orth:Q4j   K-CONS ; ! !Q4 to get e:i
!    +Pl+Acc+Err/Orth:Q4jt  K-CONS ;
!    +Pl+Ill+Err/Orth:Q4jda K-VOW  ;
!    +Pl+Ine+Err/Orth:Q4jn  K-CONS ;
!    +Pl+Ela+Err/Orth:Q4js  K-CONS ;
!    +Pl+Com+Err/Orth:Q4j   K-CONS ; ! !Q4 to get e:i
              +Sg+Com:Q4jn  K-CONS ;
              +Sg+Com:X5jn  K-CONS ; ! !SUB Norgejn



!! In smj RONDANE is same as ACCRA, in use in smi because of diffrences in sme. No -lasj or -k. Last syllable is unstressed. Non-assimilated-stems.


LEXICON RONDANE-plc !!= @CODE@ E-final names, with no cg. elative -s, ill -ij. Place names
 +N+Prop+Sem/Plc:e%> RONDANEDECL ;


!!€gt-norm: Bakkane # Even-syllable test
!!€ Bakkane:               Bakkane+N+Prop+Sem/Plc+Sg+Nom
!!€ Bakkane:               Bakkane+N+Prop+Sem/Plc+Sg+Gen
!!€ Bakkanev:              Bakkane+N+Prop+Sem/Plc+Sg+Acc
!!€ Bakkanen:              Bakkane+N+Prop+Sem/Plc+Sg+Ine
!!€ Bakkanij:              Bakkane+N+Prop+Sem/Plc+Sg+Ill
!!€ Bakkanes:              Bakkane+N+Prop+Sem/Plc+Sg+Ela
!!€ Bakkanijn:             Bakkane+N+Prop+Sem/Plc+Sg+Com
!!€ Bakkanijda:            Bakkane+N+Prop+Sem/Plc+Pl+Ill
!!€ Bakkanijs:             Bakkane+N+Prop+Sem/Plc+Pl+Ela
!!$ bakkanek:              Bakkane+N+Prop+Sem/Plc+Der/k+N+Sg+Nom
!!$ bakkanelasj:           Bakkane+N+Prop+Sem/Plc+Der/lasj+N+Sg+Nom

LEXICON RONDANE-SG-plc !!= @CODE@ E-final names, with no cg. elative -s, ill -ij. Place names
 +N+Prop+Sem/Plc:e%> RONDANEDECL ;

!!€gt-norm: Bakkane # Even-syllable test
!!€ Bakkane:               Bakkane+N+Prop+Sem/Plc+Sg+Nom
!!€ Bakkane:               Bakkane+N+Prop+Sem/Plc+Sg+Gen
!!€ Bakkanev:              Bakkane+N+Prop+Sem/Plc+Sg+Acc



LEXICON RONDANE-LOAN !!= @CODE@ Only nominative.Place names
 +N+Prop+Sem/Plc:e%> RONDANEDECL-LOAN ;

!!€gt-norm: Azorene # Even-syllable test
!!€ Azorene:               Azorene+N+Prop+Sem/Plc+Sg+Nom
!!$ Azorenij:              Azorene+N+Prop+Sem/Plc+Sg+Ill # Negative test


LEXICON RONDANE-SG-LOAN !!= @CODE@ Only nominative. Place names
 +N+Prop+Sem/Plc:e%> RONDANEDECL-LOAN ;


LEXICON RONDANE-sur !!= @CODE@ Surnames
                 +N+Prop+Sem/Sur:e%> RONDANEDECL ;
 +N+Prop+Sem/Sur+Attr@U.Cap.Obl@:e%>00@U.Cap.Obl@ # ;

!!€gt-norm: Benneche # Even-syllable test
!!€ Benneche:               Benneche+N+Prop+Sem/Sur+Sg+Nom
!!€ Bennechij:              Benneche+N+Prop+Sem/Sur+Sg+Ill
!!€ Benneches:              Benneche+N+Prop+Sem/Sur+Sg+Ela

LEXICON RONDANE-obj !!= @CODE@ Objects
 +N+Prop+Sem/Obj:e%> RONDANEDECL ;

!!€gt-norm: Office # Even-syllable test
!!€ Office:              Office+N+Prop+Sem/Obj+Sg+Nom
!!€ Office:              Office+N+Prop+Sem/Obj+Sg+Gen
!!€ Officev:             Office+N+Prop+Sem/Obj+Sg+Acc
!!€ Officen:             Office+N+Prop+Sem/Obj+Sg+Ine
!!€ Officij:             Office+N+Prop+Sem/Obj+Sg+Ill
!!€ Offices:             Office+N+Prop+Sem/Obj+Sg+Ela
!!€ Officijn:            Office+N+Prop+Sem/Obj+Sg+Com

LEXICON RONDANE-org !!= @CODE@ Organizations
 +N+Prop+Sem/Org:e%> RONDANEDECL ;

!!€gt-norm: Picture # Even-syllable test
!!€ Picture:               Picture+N+Prop+Sem/Org+Sg+Nom
!!€ Picturij:              Picture+N+Prop+Sem/Org+Sg+Ill
!!€ Pictures:              Picture+N+Prop+Sem/Org+Sg+Ela

LEXICON RONDANE-mal !!= @CODE@ Male names
 +N+Prop+Sem/Mal:e%>  RONDANEDECL ;
 +N+Prop+Sem/Mal:e%>  RÁVKKE_NAMÁK ;

!!€gt-norm: Lawrence # Even-syllable test
!!€ Lawrence:               Lawrence+N+Prop+Sem/Mal+Sg+Nom
!!€ Lawrencij:              Lawrence+N+Prop+Sem/Mal+Sg+Ill
!!€ Lawrences:              Lawrence+N+Prop+Sem/Mal+Sg+Ela

LEXICON RONDANE-fem !!= @CODE@ Female names
 +N+Prop+Sem/Fem:e%> RONDANEDECL ;
 +N+Prop+Sem/Fem:%>  RÁVKKE_NAMÁK      ;

!!€gt-norm: Jannike # Even-syllable test
!!€ Jannike:               Jannike+N+Prop+Sem/Fem+Sg+Nom
!!€ Jannikij:              Jannike+N+Prop+Sem/Fem+Sg+Ill
!!€ Jannikes:              Jannike+N+Prop+Sem/Fem+Sg+Ela

LEXICON RONDANEDECL
! These sublexica are irrelevant for ACCRA, but added
! for the sake of the lexicon MARJA
 @U.Cap.Obl@ RONDANE-DC ;

!LEXICON RONDANEDECL-ORG
!! These sublexica are irrelevant for ACCRA, but added
!! for the sake of the lexicon MARJA
! @U.Cap.Obl@ RONDANE-DC-ORG ;

LEXICON RONDANEDECL-LOAN
! These sublexica are irrelevant for ACCRA, but added
! for the sake of the lexicon MARJA
 @U.Cap.Obl@ RONDANE-DC-LOAN ;


LEXICON RONDANE-DC
          +Sg+Nom:      K-VOW  ;
       +Cmp/SgNom:      RHyph  ;
             +Ess:n     K-CONS ;
          +Sg+Ill:Q4j   K-CONS ; !Q4 to get e:i
          +Sg+Gen:      K-VOW  ;
       +Cmp/SgGen:      RHyph  ;
          +Sg+Acc:v     K-CONS ;
          +Sg+Ine:n     K-CONS ;
          +Sg+Ela:s     K-CONS ;
          +Pl+Nom:      K-VOW  ;
          +Pl+Gen:Q4j   K-CONS ; !Q4 to get e:i
          +Pl+Gen:Q4j   K-CONS ; !Q4 to get e:i
          +Pl+Acc:Q4jt  K-CONS ;
          +Pl+Ill:Q4jda K-VOW  ;
          +Pl+Ine:Q4jn  K-CONS ;
          +Pl+Ela:Q4js  K-CONS ;
          +Pl+Com:Q4j   K-CONS ; !Q4 to get e:i
          +Sg+Com:Q4jn  K-CONS ;
 +Sg+Com+Err/Orth:X5jn  K-CONS ; ! Norgejn


!LEXICON RONDANE-DC-ORG
!          +Sg+Nom:      K     ;
!       +Cmp/SgNom:      RHyph ;
!             +Ess:n     K     ;
!          +Sg+Ill:Q4j   K     ; !Q4 to get e:i
!          +Sg+Gen:      K     ;
!       +Cmp/SgGen:      RHyph ;
!          +Sg+Acc:v     K     ;
!          +Sg+Ine:n     K     ;
!          +Sg+Ela:s     K     ;
!          +Pl+Nom:      K     ;
!          +Pl+Gen:Q4j   K     ; !Q4 to get e:i
!          +Pl+Gen:Q4j   K     ; !Q4 to get e:i
!          +Pl+Acc:Q4jt  K     ;
!          +Pl+Ill:Q4jda K     ;
!          +Pl+Ine:Q4jn  K     ;
!          +Pl+Ela:Q4js  K     ;
!          +Pl+Com:Q4j   K     ; !Q4 to get e:i
!          +Sg+Com:Q4jn  K     ;
! +Sg+Com+Err/Orth:X5jn  K     ; ! Norgejn



LEXICON RONDANE-DC-LOAN
              +Sg+Nom:      K-VOW  ;
           +Cmp/SgNom:      RHyph  ;
        +Ess+Err/Orth:n     K-CONS ;
     +Sg+Ill+Err/Orth:Q4j   K-CONS ; ! !Q4 to get e:i
     +Sg+Gen+Err/Orth:      K-VOW  ;
  +Err/Orth+Cmp/SgGen:      RHyph  ;
     +Sg+Acc+Err/Orth:v     K-CONS ;
     +Sg+Ine+Err/Orth:n     K-CONS ;
     +Sg+Ela+Err/Orth:s     K-CONS ;
!    +Pl+Nom+Err/Orth:      K      ;
!    +Pl+Gen+Err/Orth:Q4j   K      ; ! !Q4 to get e:i
!    +Pl+Gen+Err/Orth:Q4j   K      ; ! !Q4 to get e:i
!    +Pl+Acc+Err/Orth:Q4jt  K      ;
!    +Pl+Ill+Err/Orth:Q4jda K      ;
!    +Pl+Ine+Err/Orth:Q4jn  K      ;
!    +Pl+Ela+Err/Orth:Q4js  K      ;
!    +Pl+Com+Err/Orth:Q4j   K      ; ! !Q4 to get e:i
     +Sg+Com+Err/Orth:Q4jn  K-CONS ;
     +Sg+Com+Err/Orth:X5jn  K-CONS ; ! !SUB Norgejn


!! GATA are Norwegian place names that end on -gata. Gets even-syllable casemarking. Last syllable is unstressed. Non-assimilated stems.

LEXICON GATA-plc  !!= @CODE@ Norwegian place names that end on -gata. Gets even-syllable casemarking. Last syllable is unstressed.
     +N+Prop+Sem/Plc:gata ACCRADECL-PLC ;

!!€gt-norm: Munkegata # Even-syllable test
!!€ Munkegata:               Munkegata+N+Prop+Sem/Plc+Sg+Nom
!!€ Munkegataj:              Munkegata+N+Prop+Sem/Plc+Sg+Ill
!!€ Munkegatas:              Munkegata+N+Prop+Sem/Plc+Sg+Ela

!! Words in MARJA end on vowel, with CG, even-syllable case marking. Illative change e to á, illative i stays i. Last syllable is unstressed. Real lule sami stems.

LEXICON MARJA-fem !!= @CODE@ Odd-syllable with cg. Female names
                 +N+Prop+Sem/Fem:%>               MARJADECL      ;
                 +N+Prop+Sem/Fem:»                DiminutiveEVEN ;
                 +N+Prop+Sem/Fem:»Q1              GUSJ           ;
                 +N+Prop+Sem/Fem:%>               RÁVKKE_NAMÁK         ;
 +N+Prop+Sem/Fem+Attr@U.Cap.Obl@:%>000@U.Cap.Obl@ #              ;




!!€gt-norm: Gáddjá # Even-syllable test
!!€ Gáddjá:               Gáddjá+N+Prop+Sem/Fem+Sg+Nom
!!€ Gáddjáj:              Gáddjá+N+Prop+Sem/Fem+Sg+Ill
!!€ Gádjás:               Gáddjá+N+Prop+Sem/Fem+Sg+Ela

!LEXICON MARJA-I-fem !!= @CODE@ Female names ending on i. Presently not used in SMJ.
!                            +N+Prop+Sem/Fem:%>                MARJADECL ;
!+N+Prop+Sem/Fem+Attr@U.Cap.Obl@+Err/Orth:%>000@U.Cap.Obl@ #         ;

!LEXICON MARJA-U-fem !!= @CODE@ Female names ending on u. Presently not used in SMJ.
!                            +N+Prop+Sem/Fem:%>                MARJADECL ;
!+N+Prop+Sem/Fem+Attr@U.Cap.Obl@+Err/Orth:%>000@U.Cap.Obl@ #         ;

!LEXICON MARJA-A-fem !!= @CODE@ Female names ending on a. Presently not used in SMJ.
!                            +N+Prop+Sem/Fem:%>                MARJADECL ;
!+N+Prop+Sem/Fem+Attr@U.Cap.Obl@+Err/Orth:%>000@U.Cap.Obl@ #         ;

LEXICON MARJA-ani !!= @CODE@ Animal names
 +N+Prop+Sem/Ani:%> MARJADECL ;

!!€gt-norm: Vilge # Even-syllable test 
!!€ Gávrásski:               Gávrásski+N+Prop+Sem/Ani+Sg+Nom
!!€ Gávrásskij:              Gávrásski+N+Prop+Sem/Ani+Sg+Ill
!!€ Gávráskis:              Gávrásski+N+Prop+Sem/Ani+Sg+Ela

!LEXICON MARJA-A-ani !!= @CODE@ Animal names ending on a. Presently not used in SMJ.
! +N+Prop+Sem/Ani:%> MARJADECL ;

LEXICON MARJA-mal !!= @CODE@ Male names
                 +N+Prop+Sem/Mal:%>               MARJADECL      ;
                 +N+Prop+Sem/Mal:»                DiminutiveEVEN ;
                 +N+Prop+Sem/Mal:»Q1              GUSJ           ;
                 +N+Prop+Sem/Mal:%>               RÁVKKE_NAMÁK         ;
 +N+Prop+Sem/Mal+Attr@U.Cap.Obl@:%>000@U.Cap.Obl@ #              ;


!!€gt-norm: Biet-Ánnda # Even-syllable test
!!€ Biet-Ánnda:               Biet-Ánnda+N+Prop+Sem/Mal+Sg+Nom
!!€ Biet-Ánndaj:              Biet-Ánnda+N+Prop+Sem/Mal+Sg+Ill
!!€ Biet-Ándas:              Biet-Ánnda+N+Prop+Sem/Mal+Sg+Ela

!LEXICON MARJA-I-mal !!= @CODE@ Male named ending on i. Presently not used in SMJ.
!                            +N+Prop+Sem/Mal:%>                MARJADECL ;
!+N+Prop+Sem/Mal+Attr@U.Cap.Obl@+Err/Orth:%>000@U.Cap.Obl@ #         ;

!LEXICON MARJA-A-mal !!= @CODE@ Male named ending on a. Presently not used in SMJ.
!                            +N+Prop+Sem/Mal:%>                MARJADECL ;
!+N+Prop+Sem/Mal+Attr@U.Cap.Obl@+Err/Orth:%>000@U.Cap.Obl@ #         ;

!LEXICON MARJA-U-mal !!= @CODE@ Male named ending on u. Presently not used in SMJ.
!                            +N+Prop+Sem/Mal:%>                MARJADECL ;
!+N+Prop+Sem/Mal+Attr@U.Cap.Obl@+Err/Orth:%>000@U.Cap.Obl@ #         ;

LEXICON MARJA-obj !!= @CODE@ Objects
 +N+Prop+Sem/Obj:%> MARJADECL ;

!!€gt-norm: Bern-Konvensjåvnnå # Even-syllable test
!!€ Bern-Konvensjåvnnå:               Bern-Konvensjåvnnå+N+Prop+Sem/Obj+Sg+Nom
!!€ Bern-Konvensjåvnnåj:              Bern-Konvensjåvnnå+N+Prop+Sem/Obj+Sg+Ill
!!€ Bern-Konvensjåvnås:               Bern-Konvensjåvnnå+N+Prop+Sem/Obj+Sg+Ela

!LEXICON MARJA-A-obj !!= @CODE@ Objectes ending on a. Presently not used in SMJ.
! +N+Prop+Sem/Obj:%> MARJADECL ;

!LEXICON MARJA-I-obj !!= @CODE@ Objects ending on i. Presently not used in SMJ.
! +N+Prop+Sem/Obj:%> MARJADECL ;

!LEXICON MARJA-U-obj !!= @CODE@ Objects ending on u. Presently not used in SMJ.
! +N+Prop+Sem/Obj:%> MARJADECL ;

LEXICON MARJA-org !!= @CODE@ Organizations
 +N+Prop+Sem/Org:%> MARJADECL-ORG ;

!!€gt-norm: Domænadoajmma # Even-syllable test
!!€ Domænadoajmma:               Domænadoajmma+N+Prop+Sem/Org+Sg+Nom
! !!€ Domänadoajmma               Domænadoajmma+N+Prop+Sem/Org+Sg+Nom !no æ:ä for propers?
!!€ Domænadoajmmaj:              Domænadoajmma+N+Prop+Sem/Org+Sg+Ill
! !!€ Domänadoajmmaj              Domænadoajmma+N+Prop+Sem/Org+Sg+Ill !no æ:ä for propers?
!!€ Domænadåjmas:              Domænadoajmma+N+Prop+Sem/Org+Sg+Ela
! !!€ Domänadåjmas              Domænadoajmma+N+Prop+Sem/Org+Sg+Ela !no æ:ä for propers?

!LEXICON MARJA-A-org !!= @CODE@ Organizations ending on a. Presently not used in SMJ.
! +N+Prop+Sem/Org:%> MARJADECL-ORG ;

!LEXICON MARJA-U-org !!= @CODE@ Organizations ending on u. Presently not used in SMJ.
! +N+Prop+Sem/Org:%> MARJADECL-ORG ;

!LEXICON MARJA-I-org !!= @CODE@ Organizations ending on i. Presently not used in SMJ.
! +N+Prop+Sem/Org:%> MARJADECL-ORG ;

! LEXICON MARJA-event !!= @CODE@ Organizations
!  +N+Prop+Sem/Event:%> MARJADECL-ORG ;


LEXICON MARJA-plc !!= @CODE@ Vowel final names with Gradation and Ill change (place names)
                +N+Prop+Sem/Plc:%>   MARJADECL-PLC ; ! Flags added in MARJADECL-PLC
!                +N+Prop+Sem/Sur:%>   MARJADECL-PLC ; ! Flags added in MARJADECL-PLC
!+N+Prop+Sem/Sur+Attr@U.Cap.Obl@:%>000@U.Cap.Obl@ # ;
                +N+Prop+Sem/Plc:%>Q1 MARJA-plc-der ;

!!€gt-norm: Dundarevuobme # Even-syllable test
!!€ Dundarevuobme:               Dundarevuobme+N+Prop+Sem/Plc+Sg+Nom
!!€ Dundarevuobmáj:              Dundarevuobme+N+Prop+Sem/Plc+Sg+Ill
!!€ Dundarevuomes:              Dundarevuobme+N+Prop+Sem/Plc+Sg+Ela

!LEXICON MARJA-A-plc !!= @CODE@ Vowel final names with Gradation and Ill change (place names). Presently not used in SMJ.
!        +N+Prop+Sem/Plc:%>   MARJADECL-PLC ;
! +N+Prop+Sem/Plc+Sg+Gen:%>Q1 MARJA-plc-der ;

!LEXICON MARJA-I-plc !!= @CODE@ Vowel final names with Gradation and Ill change (place names). Presently not used in SMJ.
!        +N+Prop+Sem/Plc:%>   MARJADECL-PLC ;
! +N+Prop+Sem/Plc+Sg+Gen:%>Q1 MARJA-plc-der ;

!LEXICON MARJA-U-plc !!= @CODE@ Vowel final names with Gradation and Ill change (place names). Presently not used in SMJ.
!        +N+Prop+Sem/Plc:%>   MARJADECL-PLC ;
! +N+Prop+Sem/Plc+Sg+Gen:%>Q1 MARJA-plc-der ;

LEXICON MARJA-sur !!= @CODE@ Surnames
                            +N+Prop+Sem/Sur:%>                MARJADECL ;
+N+Prop+Sem/Sur+Attr@U.Cap.Obl@:%>000@U.Cap.Obl@ #         ;

!!€gt-norm: Skálltje # Even-syllable test
!!€ Skálltje:               Skálltje+N+Prop+Sem/Sur+Sg+Nom
!!€ Skálltjáj:              Skálltje+N+Prop+Sem/Sur+Sg+Ill
!!€ Skáltjes:              Skálltje+N+Prop+Sem/Sur+Sg+Ela

!LEXICON MARJA-U-sur !!= @CODE@ Surnames ending on u. Presently not used in SMJ.
!                            +N+Prop+Sem/Sur:%>                MARJADECL ;
!+N+Prop+Sem/Sur+Attr@U.Cap.Obl@+Err/Orth:%>000@U.Cap.Obl@ #         ;

!LEXICON MARJA-I-sur !!= @CODE@ Surnames ending on i. Presently not used in SMJ.
!                            +N+Prop+Sem/Sur:%>                MARJADECL ;
!+N+Prop+Sem/Sur+Attr@U.Cap.Obl@+Err/Orth:%>000@U.Cap.Obl@ #         ;

!LEXICON MARJA-A-sur !!= @CODE@ Surnames ending on a. Presently not used in SMJ.
!                            +N+Prop+Sem/Sur:%>                MARJADECL ;
!+N+Prop+Sem/Sur+Attr@U.Cap.Obl@+Err/Orth:%>000@U.Cap.Obl@ #         ;

!LEXICON MARJA-LOAN-plc !!= @CODE@ Vowel final names with Gradation and Ill change (place names). Presently not used in SMJ.
! +N+Prop+Sem/Plc@U.Cap.Obl@:%>00@U.Cap.Obl@ MARJA-LOAN-DC ;
! +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@:%>Q1@U.Cap.Opt@ VUONAK ;

LEXICON MARJA-plc-der !!= @CODE@ = place name derivations and corresponding flag. Presently not used in SMJ.
 @U.Cap.Opt@ VUONAK ;

LEXICON SUOBMA-plc !!= @CODE@ Placenames. Like MARJA but no derivation
 +N+Prop+Sem/Plc:%> SUOBMADECL ;

!!€gt-norm: Suobma # Even-syllable test
!!€ Suobma:               Suobma+N+Prop+Sem/Plc+Sg+Nom
!!€ Suobmaj:              Suobma+N+Prop+Sem/Plc+Sg+Ill
!!€ Suomas:              Suobma+N+Prop+Sem/Plc+Sg+Ela

LEXICON SUOBMA-org !!= @CODE@ Placenames. Like MARJA but no derivation
 +N+Prop+Sem/Org:%> SUOBMADECL ;


LEXICON SUOBMADECL
 @U.Cap.Obl@ MARJA-DC ;

LEXICON MARJADECL-ORG ! Vowel-final names with Gradation and Ill change (person names)
 @U.Cap.Obl@ MARJA-DC-ORG ;

LEXICON MARJADECL-PLC ! Vowel-final names with Gradation and Ill change (person names)
 @U.Cap.Obl@ MARJA-DC ;
 @U.Cap.Opt@:@U.Cap.Opt@Q1» LASJ ;

LEXICON MARJADECL ! Vowel-final names with Gradation and Ill change (person names)
 @U.Cap.Obl@ MARJA-DC ;


LEXICON MARJA-DC
        +Sg+Nom:       K-VOW  ;
           +Ess:n      K-CONS ;
        +Sg+Ill:X4j    K-CONS ;
        +Sg+Gen:Q1     K-VOW  ;
        +Sg+Acc:Q1v    K-CONS ;
        +Sg+Ine:Q1n    K-CONS ;
        +Sg+Ela:Q1s    K-CONS ;
        +Pl+Nom:Q1     K-VOW  ;
        +Pl+Gen:Q1j    K-CONS ;
        +Pl+Acc:Q1jt   K-CONS ;
        +Pl+Ill:Q1jda  K-VOW  ;
        +Pl+Ine:Q1jn   K-CONS ;
        +Pl+Ela:Q1js   K-CONS ;
        +Pl+Com:Q1j    K-CONS ;
 +Sg+Com+Use/NG:Q1jn   K-CONS ;
        +Sg+Com:Q1X5jn K-CONS ;

     +Cmp/SgNom:       RHyph  ;
     +Cmp/SgGen:Q1     RHyph  ;
     +Cmp/PlGen:Q1j    RHyph  ;


LEXICON MARJA-DC-ORG
    +Sg+Nom:       K-VOW  ;
       +Ess:n      K-CONS ;
    +Sg+Ill:X4j    K-CONS ;
    +Sg+Gen:Q1     K-VOW  ;
    +Sg+Acc:Q1v    K-CONS ;
    +Sg+Ine:Q1n    K-CONS ;
    +Sg+Ela:Q1s    K-CONS ;
    +Pl+Nom:Q1     K-VOW  ;
    +Pl+Gen:Q1j    K-CONS ;
    +Pl+Gen:Q1j    RHyph  ;
    +Pl+Acc:Q1jt   K-CONS ;
    +Pl+Ill:Q1jda  K-VOW  ;
    +Pl+Ine:Q1jn   K-CONS ;
    +Pl+Ela:Q1js   K-CONS ;
    +Pl+Com:Q1j    K-CONS ;
    +Sg+Com:Q1X5jn K-CONS ;

 +Cmp/SgNom:       RHyph  ;
 +Cmp/SgGen:Q1     RHyph  ;



!LEXICON MARJA-LOAN-DC
!             +Sg+Nom:       K     ;
!          +Cmp/SgNom:       RHyph ;
!       +Ess+Err/Orth:n      K     ;
!    +Sg+Ill+Err/Orth:X4j    K     ;
!    +Sg+Gen+Err/Orth:Q1     K     ;
! +Err/Orth+Cmp/SgGen:Q1     RHyph ;
!    +Sg+Acc+Err/Orth:Q1v    K     ;
!    +Sg+Ine+Err/Orth:Q1n    K     ;
!    +Sg+Ela+Err/Orth:Q1s    K     ;
!    +Pl+Nom+Err/Orth:Q1     K     ;
!    +Pl+Gen+Err/Orth:Q1j    K     ;
!    +Pl+Gen+Err/Orth:Q1j    RHyph ;
!    +Pl+Acc+Err/Orth:Q1jt   K     ;
!    +Pl+Ill+Err/Orth:Q1jda  K     ;
!    +Pl+Ine+Err/Orth:Q1jn   K     ;
!    +Pl+Ela+Err/Orth:Q1js   K     ;
!    +Pl+Com+Err/Orth:Q1j    K     ;
!     +Sg+Com+Err/Orth:Q1X5jn K     ;


LEXICON GUSJ ! Gives derivation Iŋgágusj
@U.Cap.Obl@ GUSJDECL ;

LEXICON GUSJDECL
 +Der2+Der/gusj+N+Prop+Sg+Nom:gudtj%>   K-VOW                   ;
        +Der2+Der/gusj+N+Prop:gudtja%>  BERULASJ-EVEN           ;
        +Der2+Der/gusj+N+Prop:gudtja»Q1 DenominalNounsBEDNAGASJ ; ! Q1 to avoid -dtj- consonant center


LEXICON RÁVKKE_NAMÁK !gives RÁVKKE_NAMÁK and namák cmp, IŋŋgáRÁVKKE_NAMÁK and Iŋŋgánamák
@U.Cap.Obl@ RÁVKKE_NAMÁKDECL ;

LEXICON RÁVKKE_NAMÁKDECL 
   +Use/NG+Cmp/SgNom+Cmp#rávkke+N+Sem/Hum:rávkke MUORRA ;
 +Use/NG+Cmp/SgNom+Cmp#namák+A+CmpNP/Last:namág9 BÁJNUK ;


LEXICON DiminutiveEVEN
@U.Cap.Obl@ DiminutiveEVENDECL   ;

LEXICON DiminutiveEVENDECL
                        +Der2+Der/Dimin+N+Prop:X7sj       N-ODDCASE    ;
                        +Der2+Der/Dimin+N+Prop:X7tj       OTH-ODDCASE  ;
 +Der2+Der/Dimin+N+Der+Der/Dimin+N+Prop+Sg+Nom:X7tja»sj   K-VOW        ;
        +Der2+Der/Dimin+N+Der+Der/Dimin+N+Prop:X7tja»dtja GÅNÅGIS-EVEN ;


!LEXICON DiminutiveODD
!@U.Cap.Obl@ DiminutiveODDDECL  ;
!
!LEXICON DiminutiveODDDECL
!                    +Sg+Nom:          K-CONS            ;
!                           :a         GÅNÅGIS-EVEN      ;
!          +Der2+Der/Dimin+N+Der+Der/Dimin+N:adtja»X7sj   N-ODDCASE    ;
!          +Der2+Der/Dimin+N+Der+Der/Dimin+N:adtja»X7tj   OTH-ODDCASE  ;

!LEXICON JAVRRIE-plc !!= @CODE@  Placenames. This is a lex for Umesami names on -ie that have CG
! +N+Prop+Sem/Plc: JAVRRIEDECL ;

!LEXICON JAVRRIEDECL
! @U.Cap.Obl@ JAVRRIE-DC ;

!LEXICON JAVRRIE-DC
! +Sg+Nom:i%>e      K     ;
! +Sg+Nom:i%>e      RHyph ;
!    +Ess:i%>en     K     ;
! +Sg+Ill:á%>X4j    K     ;
! +Sg+Gen:i%>Q1e    K     ;
! +Sg+Gen:i%>Q1e    RHyph ;
! +Sg+Acc:i%>Q1ev   K     ;
! +Sg+Ine:i%>Q1en   K     ;
! +Sg+Ela:i%>Q1es   K     ;
! +Pl+Nom:i%>Q1e    K     ;
! +Pl+Gen:i%>Q1j    K     ;
! +Pl+Gen:i%>Q1j    RHyph ;
! +Pl+Acc:i%>Q1jt   K     ;
! +Pl+Ill:i%>Q1jda  K     ;
! +Pl+Ine:i%>Q1jn   K     ;
! +Pl+Ela:i%>Q1js   K     ;
! +Pl+Com:i%>Q1j    K     ;
! +Sg+Com:i%>Q1X5jn K     ;


!LEXICON NIKOSIIJA-plc !XXX Check.
! +N+Prop+Sem/Plc:%> NIKOSIIJADECL ;
! +N+Prop+Sem/Plc+Der1+Der/lasj@U.Cap.Opt@:%>@U.Cap.Opt@laddj DEARVVASLAS ;	!SUB
! +N+Prop+Sem/Plc+Der1+Der/lasj+A+Attr@U.Cap.Opt@:%>@U.Cap.Opt@laddj ATTR ;
! +N+Prop+Sem/Plc+Der1+Der/lasj+A@U.Cap.Opt@:%>@U.Cap.Opt@laddj JOHTOLAT ;
! +N+Prop+Sem/Plc+Der1+Der/lasj+A@U.Cap.Opt@:%>@U.Cap.Opt@laddja ISSORASSA- ;
! +N+Prop+Sem/Plc+Der1+Der/lasj+A+Sg+Nom@U.Cap.Opt@:%>@U.Cap.Opt@laddja PxV ; ! tentative.
! +N+Prop+Sem/Plc+Der1+Der/lasj+A+Ess@U.Cap.Opt@:%>@U.Cap.Opt@laddjan K ;
! +N+Prop+Sem/Plc+Der1+Der/lasj+A+Ess@U.Cap.Opt@:%>@U.Cap.Opt@laddjan PxC ; ! tentative, check.
! +N+Prop+Sem/Plc+Der1+Der/lasj+A@U.Cap.Opt@:%>@U.Cap.Opt@laddja EABBUCASE2 ;
!+N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@:%>@U.Cap.Opt@ VUONAK ;

!LEXICON NIKOSIIJADECL !Names with ending -iija, a-illative
!ACCRA-DC_PLC-ORG ;


!! Stressed last syllable
! ---------------------

!! These proper nouns are in essence partly assimilated loan word as foreign words with stressed last syllable are assimilated to sami by (often adapting the stressed syllable vowel, and) adding an unstressed syllable consisting of adapted (or if necesarry added) consonants and ending on vowel a (Morén-Duollja 2014). Proper nouns are only partly assimilated in that the stressed syllable vowel is not adapted in any way, neither are consonats inserted, only the final "a" remains. These proper nouns therefore work like regular a-stem nouns and get an even syllable case marking.


!! Words in lexicon NYSTØ end on vowel, no cg. Non-assimilated stems

!LEXICON NYSTØ-ani !!= @CODE@ Animal names. Presently not used in SMJ.
! +N+Prop+Sem/Ani:%> NYSTØDECL ;

LEXICON NYSTØ-fem !!= @CODE@ Femal names
                 +N+Prop+Sem/Fem:%>               NYSTØDECL ;
                 +N+Prop+Sem/Fem:%>               RÁVKKE_NAMÁK    ;
 +N+Prop+Sem/Fem+Attr@U.Cap.Obl@:%>000@U.Cap.Obl@ #         ;

!!€gt-norm: Britney # Even-syllable test
!!€ Britney:                Britney+N+Prop+Sem/Fem+Sg+Nom
!!€ Britneyav:              Britney+N+Prop+Sem/Fem+Sg+Acc
!!€ Britneyaj:              Britney+N+Prop+Sem/Fem+Sg+Ill
!!€ Britneyas:              Britney+N+Prop+Sem/Fem+Sg+Ela

!LEXICON NYSTØ-femsur !!= @CODE@ Names that can be both female and surnames. Presently not used in SMJ.
!   NYSTØ-fem ;
!   NYSTØ-sur ;

LEXICON NYSTØ-mal !!= @CODE@ Male name
                 +N+Prop+Sem/Mal:%>               NYSTØDECL ;
                 +N+Prop+Sem/Mal:%>               RÁVKKE_NAMÁK    ;
 +N+Prop+Sem/Mal+Attr@U.Cap.Obl@:%>000@U.Cap.Obl@ #         ;


!!€gt-norm: Taneli # Even-syllable test
!!€ Taneli:                Taneli+N+Prop+Sem/Mal+Sg+Nom
!!€ Taneliav:              Taneli+N+Prop+Sem/Mal+Sg+Acc
!!€ Taneliaj:              Taneli+N+Prop+Sem/Mal+Sg+Ill
!!€ Tanelias:              Taneli+N+Prop+Sem/Mal+Sg+Ela

LEXICON NYSTØ-obj !!= @CODE@ Objects
 +N+Prop+Sem/Obj:%> NYSTØDECL ;

!!€gt-norm: Infiniti # Even-syllable test
!!€ Infiniti:                 Infiniti+N+Prop+Sem/Obj+Sg+Nom
!!€ Infinitiav:               Infiniti+N+Prop+Sem/Obj+Sg+Acc
!!€ Infinitiaj:               Infiniti+N+Prop+Sem/Obj+Sg+Ill
!!€ Infinitias:               Infiniti+N+Prop+Sem/Obj+Sg+Ela

LEXICON NYSTØ-org !!= @CODE@  Organizations
 +N+Prop+Sem/Org:%> NYSTØDECL-ORG ;

!!€gt-norm: Kulturby # Even-syllable test
!!€ Kulturby:                Kulturby+N+Prop+Sem/Org+Sg+Nom
!!€ Kulturbyav:              Kulturby+N+Prop+Sem/Org+Sg+Acc
!!€ Kulturbyaj:              Kulturby+N+Prop+Sem/Org+Sg+Ill
!!€ Kulturbyas:              Kulturby+N+Prop+Sem/Org+Sg+Ela

LEXICON NYSTØ-LOAN-org !!= @CODE@ Organizations loan
 +N+Prop+Sem/Org:%> NYSTØDECL-LOAN ;

!!€gt-norm: Sameby # Even-syllable test
!!€ Sameby:                Sameby+N+Prop+Sem/Org+Sg+Nom
!!$ Samebyaj:              Sameby+N+Prop+Sem/Org+Sg+Ill
!!$ Samebyas:              Sameby+N+Prop+Sem/Org+Sg+Ela

LEXICON NYSTØ-sur !!= @CODE@ Sur names
                            +N+Prop+Sem/Sur:%>                NYSTØDECL ;
+N+Prop+Sem/Sur+Attr@U.Cap.Obl@:%>000@U.Cap.Obl@ #         ;

!!€gt-norm: Sandoz # Even-syllable test
!!€ Sandoz:                Sandoz+N+Prop+Sem/Sur+Sg+Nom
!!€ Sandozav:                Sandoz+N+Prop+Sem/Sur+Sg+Acc
!!€ Sandozaj:              Sandoz+N+Prop+Sem/Sur+Sg+Ill
!!€ Sandozas:              Sandoz+N+Prop+Sem/Sur+Sg+Ela

!LEXICON NYSTØ-surplc !!= @CODE@  Names that can be both sur- and placenames.
!   NYSTØ-sur ;
!   NYSTØ-plc ;

!!€gt-norm: Teigmo # Even-syllable test
!!€ Teigmo:                Teigmo+N+Prop+Sem/Plc+Sg+Nom
!!€ Teigmoav:              Teigmo+N+Prop+Sem/Plc+Sg+Acc
!!€ Teigmoaj:              Teigmo+N+Prop+Sem/Plc+Sg+Ill
!!€ Teigmoas:              Teigmo+N+Prop+Sem/Plc+Sg+Ela

LEXICON NYSTØ-LOAN-plc !!= @CODE@ Place names loan
                    +N+Prop+Sem/Plc:%>                NYSTØDECL-LOAN ;
                    +N+Prop+Sem/Sur:%>                NYSTØDECL-LOAN ;
    +N+Prop+Sem/Sur+Attr@U.Cap.Obl@:%>000@U.Cap.Obl@ # ;
+N+Prop+Sem/Plc@U.Cap.Opt@+Err/Orth:%>a0@U.Cap.Opt@ VUONAK         ;

!!€gt-norm: Bodø # Even-syllable test
!!€ Bodø:                Bodø+N+Prop+Sem/Plc+Sg+Nom
!!$ Bodøaj:              Bodø+N+Prop+Sem/Plc+Sg+Ill
!!$ Bodøas:              Bodø+N+Prop+Sem/Plc+Sg+Ela

LEXICON NYSTØ-plc !!= @CODE@ Place names
                +N+Prop+Sem/Plc:%>                NYSTØDECL-PLC ;
                +N+Prop+Sem/Sur:%>                NYSTØDECL ;
+N+Prop+Sem/Sur+Attr@U.Cap.Obl@:%>000@U.Cap.Obl@ # ;
     +N+Prop+Sem/Plc@U.Cap.Opt@:%>a0@U.Cap.Opt@ VUONAK        ;

!!€gt-norm: Borgå # Even-syllable test
!!€ Borgå:                Borgå+N+Prop+Sem/Plc+Sg+Nom
!!€ Borgåav:              Borgå+N+Prop+Sem/Plc+Sg+Acc
!!€ Borgåaj:              Borgå+N+Prop+Sem/Plc+Sg+Ill
!!€ Borgåas:              Borgå+N+Prop+Sem/Plc+Sg+Ela

LEXICON NYSTØ_MWE-plc !!= @CODE@ Place names
                   +N+Prop+Sem/Plc:%>                NYSTØDECL-PLC ;

!LEXICON NYSTØ !!= @CODE@  Vowel-final, heavy syll
! +N+Prop:%> NYSTØDECL ;

LEXICON NYSTØDECL
 @U.Cap.Obl@ NYSTØ-OBL ;

LEXICON NYSTØDECL-PLC
                   @U.Cap.Obl@ NYSTØ-OBL ;
 00@U.Cap.Opt@:%>a@U.Cap.Opt@ LASJ      ; ! XXX Sub?

LEXICON NYSTØDECL-ORG
 @U.Cap.Obl@ NYSTØ-OBL-ORG ;

LEXICON NYSTØDECL-LOAN
 @U.Cap.Obl@ NYSTØ-OBL-LOAN ;
! @U.Cap.Opt@ LASJ           ; ! XXX Sub?



LEXICON NYSTØ-OBL
               +Sg+Nom:    K-VOW ;
            +Cmp/SgNom:    RHyph ;
!              +Sg+Ela:%:s K     ; ! Nystø:s  !SUB
!              +Sg+Ine:%:n K     ; ! Nystø:s  !SUB
  +Cmp/SgGen#+Err/Orth:a#  RHyph ; ! !for placenames
           +Cmp/SgGen#:#   RHyph ; ! for placenames
                           NYOBL ;
             +Err/Orth:%:  NYOBL ; ! ! to NYOBL, optional delimiter marks. sub?
             +Err/Orth:%-  NYOBL ;
             +Err/Orth:1   NYOBL ;


LEXICON NYSTØ-OBL-ORG
               +Sg+Nom:    K-VOW     ;
            +Cmp/SgNom:    RHyph     ;
!              +Sg+Ela:%:s K         ; ! Nystø:s  !SUB
!              +Sg+Ine:%:n K         ; ! Nystø:s  !SUB
  +Cmp/SgGen#+Err/Orth:a#  RHyph     ; ! !for placenames
           +Cmp/SgGen#:#   RHyph     ; ! for placenames
                           NYOBL-ORG ;
             +Err/Orth:%:  NYOBL-ORG ; ! ! to NYOBL, optional delimiter marks. sub?
             +Err/Orth:%-  NYOBL-ORG ;
             +Err/Orth:1   NYOBL-ORG ;



LEXICON NYSTØ-OBL-LOAN
               +Sg+Nom:    K-VOW     ;
            +Cmp/SgNom:    RHyph     ;
!              +Sg+Ela:%:s K         ; ! Nystø:s  !SUB
!              +Sg+Ine:%:n K         ; ! Nystø:s  !SUB
  +Cmp/SgGen#+Err/Orth:a#  RHyph     ; ! !for placenames
  +Cmp/SgGen#+Err/Orth:#   RHyph     ; ! ! for placenames
             +Err/Orth:    NYOBL-ORG ;
             +Err/Orth:%:  NYOBL-ORG ; ! ! to NYOBL, optional delimiter marks. sub?
             +Err/Orth:%-  NYOBL-ORG ;
             +Err/Orth:1   NYOBL-ORG ;



!LEXICON NYSTØ-DC
! +laš:lažž DEARVVASLAS ;

LEXICON NYOBL
             +Sg+Nom:     K-VOW  ;
          +Cmp/SgNom:     RHyph  ;
    +Sg+Ill+Err/Orth:aj   K-CONS ;
       +Ess+Err/Orth:n    K-CONS ;
    +Sg+Gen+Err/Orth:     K-VOW  ;
 +Err/Orth+Cmp/SgGen:     RHyph  ;
    +Sg+Acc+Err/Orth:v    K-CONS ;
    +Sg+Ine+Err/Orth:n    K-CONS ;
    +Sg+Ela+Err/Orth:as   K-CONS ;
    +Pl+Nom+Err/Orth:     K-VOW  ;
    +Pl+Gen+Err/Orth:j    K-CONS ;
 +Err/Orth+Cmp/PlNom:j    RHyph  ;
    +Pl+Acc+Err/Orth:jt   K-CONS ;
    +Pl+Ill+Err/Orth:jda  K-VOW  ;
    +Pl+Ine+Err/Orth:jn   K-CONS ;
    +Pl+Ela+Err/Orth:js   K-CONS ;
    +Pl+Com+Err/Orth:j    K-CONS ;
    +Sg+Com+Err/Orth:jn   K-CONS ;
    +Sg+Nom+Err/Orth:a    K-VOW  ;
 +Err/Orth+Cmp/SgNom:a    RHyph  ;
             +Sg+Ill:aj   K-CONS ;
                +Ess:an   K-CONS ;
             +Sg+Gen:a    K-VOW  ;
 +Cmp/SgGen+Use/Circ:a    RHyph  ; !  !
             +Sg+Acc:av   K-CONS ;
             +Sg+Ine:an   K-CONS ;
             +Sg+Ela:as   K-CONS ;
             +Pl+Nom:a    K-VOW  ;
             +Pl+Gen:aj   K-CONS ;
 +Cmp/PlGen+Use/Circ:aj   RHyph  ; ! !
             +Pl+Acc:ajt  K-CONS ;
             +Pl+Ill:ajda K-VOW  ;
             +Pl+Ine:ajn  K-CONS ;
             +Pl+Ela:ajs  K-CONS ;
             +Pl+Com:aj   K-CONS ;
             +Sg+Com:ajn  K-CONS ;


LEXICON NYOBL-ORG
              +Ess+Err/Orth:n    K-CONS ;
                       +Ess:an   K-CONS ;
           +Pl+Acc+Err/Orth:jt   K-CONS ;
                    +Pl+Acc:ajt  K-CONS ; !
           +Pl+Com+Err/Orth:j    K-CONS ;
                    +Pl+Com:aj   K-CONS ; !
           +Pl+Ela+Err/Orth:js   K-CONS ;
                    +Pl+Ela:ajs  K-CONS ; !
           +Pl+Gen+Err/Orth:j    K-CONS ;
                    +Pl+Gen:aj   K-CONS ; !
           +Pl+Ine+Err/Orth:jn   K-CONS ;
                    +Pl+Ine:ajn  K-CONS ; !
           +Sg+Acc+Err/Orth:v    K-CONS ;
                    +Sg+Acc:av   K-CONS ;
           +Sg+Com+Err/Orth:jn   K-CONS ;
                    +Sg+Com:ajn  K-CONS ;
           +Sg+Ela+Err/Orth:as   K-CONS ;
                    +Sg+Ela:as   K-CONS ;
           +Sg+Ill+Err/Orth:aj   K-CONS ;
                    +Sg+Ill:aj   K-CONS ;
           +Sg+Ine+Err/Orth:n    K-CONS ;
                    +Sg+Ine:an   K-CONS ;
           +Pl+Ill+Err/Orth:jda  K-VOW  ;
                    +Pl+Ill:ajda K-VOW  ; !
           +Pl+Nom+Err/Orth:     K-VOW  ;
                    +Pl+Nom:a    K-VOW  ; !
           +Sg+Gen+Err/Orth:     K-VOW  ;
                    +Sg+Gen:a    K-VOW  ;
           +Sg+Nom+Err/Orth:a    K-VOW  ;
                    +Sg+Nom:     K-VOW  ;
                 +Cmp/SgNom:     RHyph  ;
           +Pl+Gen+Err/Orth:j    RHyph  ;
           +Pl+Gen+Use/Circ:aj   RHyph  ; ! !
 +Sg+Gen+Err/Orth+Cmp/SgGen:     RHyph  ;
 +Sg+Gen+Err/Orth+Cmp/SgGen:a    RHyph  ; !  !
 +Sg+Nom+Err/Orth+Cmp/SgNom:a    RHyph  ;




!! Words in DUBAI lexicon end on vowel+vowel and have no cg. Last syllable is stressed. Get even syllable case marking. Non-assimilated stems. Not sure if this lexicon is necessary, at least for smj's sake.

LEXICON DUBAI-fem !!= @CODE@ I-final names. No cg. Female names
! Different lexicon for female persons. Audhild.
                 +N+Prop+Sem/Fem:%>                DUBAIDECL ;
                 +N+Prop+Sem/Fem:%>                RÁVKKE_NAMÁK    ;
 +N+Prop+Sem/Fem+Attr@U.Cap.Obl@:%>000@U.Cap.Obl@i #         ;


!!€gt-norm: Mai  # Even-syllable test
!!€ Mai:                Mai+N+Prop+Sem/Fem+Sg+Nom
!!€ Maiaj:              Mai+N+Prop+Sem/Fem+Sg+Ill
!!€ Maias:              Mai+N+Prop+Sem/Fem+Sg+Ela

!LEXICON DUBAI-femsur !!= @CODE@ I-final names. No cg. Female names also used as surnames
!							+N+Prop+Sem/Fem:%>             DUBAIDECL ;
!							+N+Prop+Sem/Sur:%>             DUBAIDECL ;
!+N+Prop+Sem/Fem+Attr@U.Cap.Obl@:%>000@U.Cap.Obl@i #     ;

LEXICON DUBAI-obj !!= @CODE@ I-final names. No cg. Object names
 +N+Prop+Sem/Obj:%> DUBAIDECL ;

!!€gt-norm: Hyundai  # Even-syllable test
!!€ Hyundai:                Hyundai+N+Prop+Sem/Obj+Sg+Nom
!!€ Hyundaiaj:              Hyundai+N+Prop+Sem/Obj+Sg+Ill
!!€ Hyundaias:              Hyundai+N+Prop+Sem/Obj+Sg+Ela

LEXICON DUBAI-org !!= @CODE@ Organizations
! Different lexicon for organisations. Microsoft.
 +N+Prop+Sem/Org:%> DUBAIDECL-ORG ;

!!€gt-norm: Khoi # Even-syllable test
!!€ Khoi:               Khoi+N+Prop+Sem/Org+Sg+Nom
!!€ Khoiaj:              Khoi+N+Prop+Sem/Org+Sg+Ill
!!€ Khoias:              Khoi+N+Prop+Sem/Org+Sg+Ela

LEXICON DUBAI-mal !!= @CODE@ Male names
! Different lexicon for male persons. Finn. Torfinn.
                 +N+Prop+Sem/Mal:%>               DUBAIDECL ;
                 +N+Prop+Sem/Mal:%>               RÁVKKE_NAMÁK    ;
 +N+Prop+Sem/Mal+Attr@U.Cap.Obl@:%>i00@U.Cap.Obl@ #         ;


!!€gt-norm: Kublai # Even-syllable test
!!€ Kublai:               Kublai+N+Prop+Sem/Mal+Sg+Nom
!!€ Kublaiaj:              Kublai+N+Prop+Sem/Mal+Sg+Ill
!!€ Kublaias:              Kublai+N+Prop+Sem/Mal+Sg+Ela

LEXICON DUBAI-sur !!= @CODE@ Surnames
! Different lexicon for personal surnames. Blind
                            +N+Prop+Sem/Sur:%>             DUBAIDECL ;
+N+Prop+Sem/Sur+Attr@U.Cap.Obl@:%>i00@U.Cap.Obl@ #     ;

!!€gt-norm: Maarthai # Even-syllable test
!!€ Maarthai:               Maarthai+N+Prop+Sem/Sur+Sg+Nom
!!€ Maarthaiaj:              Maarthai+N+Prop+Sem/Sur+Sg+Ill
!!€ Maarthaias:              Maarthai+N+Prop+Sem/Sur+Sg+Ela

LEXICON DUBAI-plc !!= @CODE@ Place names
! Different lexicon for place names. Bern.
                   +N+Prop+Sem/Plc:%>                DUBAIDECL-PLC ;
 +N+Prop+Sem/Plc@U.Cap.Opt@:i%>a00@U.Cap.Opt@ VUONAK        ;

!!€gt-norm: Madurai # Even-syllable test
!!€ Madurai:                 Madurai+N+Prop+Sem/Plc+Sg+Nom
!!€ Maduraiaj:              Madurai+N+Prop+Sem/Plc+Sg+Ill
!!€ Maduraias:              Madurai+N+Prop+Sem/Plc+Sg+Ela
!!€ maduraiak:              Madurai+N+Prop+Sem/Plc+Der/k+N+Sg+Nom


LEXICON DUBAI_MWE-plc
                   +N+Prop+Sem/Plc:%>                DUBAIDECL-PLC ;
 +N+Prop+Sem/Plc@U.Cap.Opt@:i%>a00@U.Cap.Opt@ VUONAK        ;

LEXICON DUBAIDECL
 @U.Cap.Obl@ DUBAIUCASE ;

LEXICON DUBAIDECL-PLC
          @U.Cap.Obl@               DUBAIUCASE ;
          00@U.Cap.Opt@:ia@U.Cap.Opt@ LASJ       ;

LEXICON DUBAIDECL-ORG
 @U.Cap.Obl@ DUBAIUCASE-ORG ;


LEXICON DUBAIUCASE ! Final foot structure (X) => Loc:as
    +Sg+Nom:i     K-VOW  ;
 +Cmp/SgNom:i     RHyph  ;
    +Sg+Ill:iaj   K-CONS ;
       +Ess:ian   K-CONS ;
    +Sg+Gen:ia    K-VOW  ;
    +Sg+Gen:ia    K-VOW  ;
 +Cmp/SgGen:ia    RHyph  ;
    +Sg+Acc:iav   K-CONS ;
    +Sg+Ine:ian   K-CONS ;
    +Sg+Ela:ias   K-CONS ;
    +Pl+Nom:ia    K-VOW  ;
    +Pl+Gen:iaj   K-CONS ;
 +Cmp/PlGen:iaj   RHyph  ;
    +Pl+Acc:iajt  K-CONS ;
    +Pl+Ill:iajda K-VOW  ;
    +Pl+Ine:iajn  K-CONS ;
    +Pl+Ela:iajs  K-CONS ;
    +Pl+Com:iaj   K-CONS ;
    +Sg+Com:iajn  K-CONS ;



LEXICON DUBAIUCASE-ORG ! Final foot structure (X) => Loc:as
     +Sg+Nom:i      K-VOW  ;
  +Cmp/SgNom:i      RHyph  ;
     +Sg+Ill:iaj    K-CONS ;
        +Ess:ian    K-CONS ;
     +Sg+Gen:ia     K-VOW  ;
     +Sg+Gen:ia     K-VOW  ;
  +Cmp/SgGen:ia     RHyph  ;
     +Sg+Acc:iav    K-CONS ;
     +Sg+Ine:ian    K-CONS ;
     +Sg+Ela:ias    K-CONS ;
!    +Pl+Nom:ija    K-VOW  ;
!    +Pl+Gen:ijaj   K-CONS ;
!    +Pl+Gen:ijaj   RHyph  ;
!    +Pl+Acc:ijajt  K-CONS ;
!    +Pl+Ill:ijajda K-VOW  ;
!    +Pl+Ine:ijajn  K-CONS ;
!    +Pl+Ela:ijajs  K-CONS ;
!    +Pl+Com:ijaj   K-CONS ;
     +Sg+Com:iajn   K-CONS ;



!! Words in lexicon BERN end on conconant, no cg, even syllable case marking with -av, -aj, -as, etc. Last syllable is stressed. Both assimilated and non-assmilated stems.

LEXICON BERN-ani !!= @CODE@  Animals
!Different lexicon for animals. Eurokas.
 +N+Prop+Sem/Ani:%> BERNDECL ;

!!€gt-norm: Lillemor # Even-syllable test
!!€ Lillemor:               Lillemor+N+Prop+Sem/Ani+Sg+Nom
!!€ Lillemoraj:              Lillemor+N+Prop+Sem/Ani+Sg+Ill
!!€ Lillemoras:              Lillemor+N+Prop+Sem/Ani+Sg+Ela

LEXICON BERN-mal !!= @CODE@  Male names
! Different lexicon for male persons. Finn. Torfinn.
                 +N+Prop+Sem/Mal:%>               BERNDECL ;
                 +N+Prop+Sem/Mal:%>               RÁVKKE_NAMÁK   ;
 +N+Prop+Sem/Mal+Attr@U.Cap.Obl@:%>000@U.Cap.Obl@ #        ;


!!€gt-norm: Eystein # Even-syllable test
!!€ Eystein:                Eystein+N+Prop+Sem/Mal+Sg+Nom
!!€ Eysteinaj:              Eystein+N+Prop+Sem/Mal+Sg+Ill
!!€ Eysteinas:              Eystein+N+Prop+Sem/Mal+Sg+Ela


LEXICON BERN-surmal !!= @CODE@  name that are both sur- and male names
! Different lexicon for male persons & personal surnames.
                 +N+Prop+Sem/Mal:%>               BERNDECL ;
                 +N+Prop+Sem/Sur:%>               BERNDECL ;
                 +N+Prop+Sem/Mal:%>               RÁVKKE_NAMÁK   ;
 +N+Prop+Sem/Sur+Attr@U.Cap.Obl@:%>000@U.Cap.Obl@ #        ;
 +N+Prop+Sem/Mal+Attr@U.Cap.Obl@:%>000@U.Cap.Obl@ #        ;

!!€gt-norm: Pipin # Even-syllable test
!!€ Pipin:               Pipin+N+Prop+Sem/Sur+Sg+Nom
!!€ Pipinaj:              Pipin+N+Prop+Sem/Sur+Sg+Ill
!!€ Pipinas:              Pipin+N+Prop+Sem/Sur+Sg+Ela


LEXICON BERN-fem !!= @CODE@  Female name
! Different lexicon for female persons. Audhild.
                 +N+Prop+Sem/Fem:%>               BERNDECL ;
                 +N+Prop+Sem/Mal:%>               RÁVKKE_NAMÁK   ;
 +N+Prop+Sem/Fem+Attr@U.Cap.Obl@:%>000@U.Cap.Obl@ #        ;


!!€gt-norm: Ragnfrid # Even-syllable test
!!€ Ragnfrid:               Ragnfrid+N+Prop+Sem/Fem+Sg+Nom
!!€ Ragnfridaj:              Ragnfrid+N+Prop+Sem/Fem+Sg+Ill
!!€ Ragnfridas:              Ragnfrid+N+Prop+Sem/Fem+Sg+Ela

!LEXICON BERN-femplc !!= @CODE@ Both female and placenames. Not used in smj-propernouns
!! Different lexicon for female persons. Audhild.
!                            +N+Prop+Sem/Fem:%>                BERNDECL     ;
!                            +N+Prop+Sem/Plc:%>                BERNDECL-PLC ;
!+N+Prop+Sem/Fem+Attr@U.Cap.Obl@:%>000@U.Cap.Obl@ #            ;
!          +N+Prop+Sem/Plc@U.Cap.Opt@:%>a000@U.Cap.Opt@ VUONAK       ;


LEXICON BERN-sur !!= @CODE@ Surnames
! Different lexicon for personal surnames. Blind
                            +N+Prop+Sem/Sur:%>                BERNDECL ;
+N+Prop+Sem/Sur+Attr@U.Cap.Obl@:%>000@U.Cap.Obl@ #        ;

!!€gt-norm: Lind # Even-syllable test
!!€ Lind:               Lind+N+Prop+Sem/Sur+Sg+Nom
!!€ Lindaj:              Lind+N+Prop+Sem/Sur+Sg+Ill
!!€ Lindas:              Lind+N+Prop+Sem/Sur+Sg+Ela



LEXICON BERN-plc !!= @CODE@ Placenames
! Different lexicon for place names. Bern.
                   +N+Prop+Sem/Sur:%>                BERNDECL-PLC ;
                   +N+Prop+Sem/Plc:%>                BERNDECL-PLC ;
                   +N+Prop+Sem/Sur+Attr@U.Cap.Obl@:0000@U.Cap.Obl@ K ;
 +N+Prop+Sem/Plc@U.Cap.Opt@:%>a0@U.Cap.Opt@ VUONAK       ;

!!€gt-norm: Beijing # Even-syllable test
!!€ Beijing:               Beijing+N+Prop+Sem/Plc+Sg+Nom
!!€ Beijingaj:              Beijing+N+Prop+Sem/Plc+Sg+Ill
!!€ Beijingas:              Beijing+N+Prop+Sem/Plc+Sg+Ela

LEXICON BERN_MWE-plc !!= @CODE@ Placenames
! Different lexicon for place names. Bern.
!                   +N+Prop+Sem/Sur:%>                BERNDECL-PLC ;
                   +N+Prop+Sem/Plc:%>                BERNDECL-PLC ;

LEXICON BERN-objsur !!= @CODE@  Names used as both objects and surnames. 
! Different lexicon for names that are both surnames and places.
 +N+Prop+Sem/Sur:%> BERNDECL ;
 +N+Prop+Sem/Obj:%> BERNDECL ;
+N+Prop+Sem/Sur+Attr@U.Cap.Obl@:%>000@U.Cap.Obl@ # ;

!!€gt-norm: Stenbukk # Even-syllable test
!!€ Stenbukk:                Stenbukk+N+Prop+Sem/Obj+Sg+Nom
!!€ Stenbukkaj:              Stenbukk+N+Prop+Sem/Obj+Sg+Ill
!!€ Stenbukkas:              Stenbukk+N+Prop+Sem/Obj+Sg+Ela

LEXICON BERN-orgsur !!= @CODE@ Names used for both organizations and surnames. 
! Different lexicon for names that are both surnames and places.
 +N+Prop+Sem/Sur:%> BERNDECL     ;
 +N+Prop+Sem/Org:%> BERNDECL-ORG ;
+N+Prop+Sem/Sur+Attr@U.Cap.Obl@:%>000@U.Cap.Obl@ # ;

!!€gt-norm: Nord # Even-syllable test
!!€ Nord:               Nord+N+Prop+Sem/Org+Sg+Nom
!!€ Nordaj:              Nord+N+Prop+Sem/Org+Sg+Ill
!!€ Nordas:              Nord+N+Prop+Sem/Org+Sg+Ela

LEXICON BERN-obj !!= @CODE@ Objects. Obs: Different lexicon for organisations. Microsoft.
 +N+Prop+Sem/Obj:%> BERNDECL ;

!!€gt-norm: Sult # Even-syllable test
!!€ Sult:                Sult+N+Prop+Sem/Obj+Sg+Nom
!!€ Sultaj:              Sult+N+Prop+Sem/Obj+Sg+Ill
!!€ Sultas:              Sult+N+Prop+Sem/Obj+Sg+Ela

LEXICON BERN-org !!= @CODE@ Organizations
! Different lexicon for organisations. Microsoft.
 +N+Prop+Sem/Org:%> BERNDECL-ORG ;

!!€gt-norm: Laks # Even-syllable test
!!€ Laks:              Laks+N+Prop+Sem/Org+Sg+Nom
!!€ Laksaj:              Laks+N+Prop+Sem/Org+Sg+Ill
!!€ Laksas:              Laks+N+Prop+Sem/Org+Sg+Ela

LEXICON BERN-LOAN-org !!= @CODE@ Organizations loan.  
! Different lexicon for organisations. Microsoft.
 +N+Prop+Sem/Org:%> BERNDECL-LOAN ;

!!€gt-norm: Reinsamelag # Even-syllable test
!!€ Reinsamelag:               Reinsamelag+N+Prop+Sem/Org+Sg+Nom
!!$ Reinsamelagaj:              Reinsamelag+N+Prop+Sem/Org+Sg+Ill
!!$ Reinsamelagas:              Reinsamelag+N+Prop+Sem/Org+Sg+Ela

LEXICON BERN-LOAN-plc !!= @CODE@ Placenames loan. 
! Different lexicon for place names. Bern.
                           +N+Prop+Sem/Plc:%>                BERNDECL-LOAN ;
+N+Prop+Sem/Plc@U.Cap.Opt@+Err/Orth:%>a000@U.Cap.Opt@ VUONAK        ;

!!€gt-norm: Mehamn # Even-syllable test
!!€ Mehamn:               Mehamn+N+Prop+Sem/Plc+Sg+Nom
!!$ Mehamnaj:              Mehamn+N+Prop+Sem/Plc+Sg+Ill
!!$ Mehamnas:              Mehamn+N+Prop+Sem/Plc+Sg+Ela

LEXICON BERN-LOAN-obj !!= @CODE@ Objects loan. 
! Different lexicon for objects, concrete and abstract. Word.
 +N+Prop+Sem/Obj:%> BERNDECL-LOAN ;

!!€gt-norm: Verneplan # Even-syllable test
!!€ Verneplan:                Verneplan+N+Prop+Sem/Obj+Sg+Nom
!!$ Verneplanaj:              Verneplan+N+Prop+Sem/Obj+Sg+Ill
!!$ Verneplanas:              Verneplan+N+Prop+Sem/Obj+Sg+Ela

!LEXICON BERN-femsur ! Not in use.
! Different lexicon for female persons & personal surnames.
! +N+Prop+Sem/Fem:%> BERNDECL ;
! +N+Prop+Sem/Sur:%> BERNDECL ;
! +N+Prop+Sem/Fem+Attr@U.Cap.Obl@:%>@U.Cap.Obl@ # ; !SUB

!LEXICON BERN-surplc
!! Different lexicon for names that are both surnames and places.
! +N+Prop+Sem/Sur:%> BERNDECL ;
! +N+Prop+Sem/Plc:%>                BERNDECL-PLC ;
! +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@:%>a000@U.Cap.Opt@ VUONAK       ;

!LEXICON BERN
! +N+Prop:%> BERNDECL ;

LEXICON BERNDECL
 @U.Cap.Obl@ BERNUCASE ;

LEXICON BERNDECL-ORG
 @U.Cap.Obl@ BERNUCASE-ORG ;

LEXICON BERNDECL-PLC
                   @U.Cap.Obl@ BERNUCASE ;
 00@U.Cap.Opt@:%>a@U.Cap.Opt@ LASJ      ; ! XXX Sub?

LEXICON BERNDECL-LOAN
 @U.Cap.Obl@ BERNUCASE-LOAN ;
! @U.Cap.Opt@ LASJ           ; ! XXX Sub?

LEXICON BERNUCASE ! Final foot structure (X) => Loc:as
          +Sg+Nom:      K-CONS ;
       +Cmp/SgNom:      RHyph  ;
          +Sg+Ill:aj    K-CONS ;
 +Sg+Ill+Err/Orth:%:aj  K-CONS ;
             +Ess:an    K-CONS ;
          +Sg+Gen:a     K-VOW  ;
 +Sg+Gen+Err/Orth:%:a   K-VOW  ;
       +Cmp/SgGen:a     RHyph  ;
          +Sg+Acc:av    K-CONS ;
 +Sg+Acc+Err/Orth:%:av  K-CONS ;
          +Sg+Ine:an    K-CONS ;
 +Sg+Ine+Err/Orth:%:an  K-CONS ;
          +Sg+Ela:as    K-CONS ;
 +Sg+Ela+Err/Orth:%:as  K-CONS ;
          +Pl+Nom:a     K-VOW  ;
          +Pl+Gen:aj    K-CONS ;
       +Cmp/PlGen:aj    RHyph  ;
          +Pl+Acc:ajt   K-CONS ;
          +Pl+Ill:ajda  K-VOW  ;
          +Pl+Ine:ajn   K-CONS ;
          +Pl+Ela:ajs   K-CONS ;
          +Pl+Com:aj    K-CONS ;
          +Sg+Com:ajn   K-CONS ;
 +Sg+Com+Err/Orth:%:ajn K-CONS ;


LEXICON BERNUCASE-ORG ! Final foot structure (X) => Loc:as
    +Sg+Nom:     K-CONS ;
 +Cmp/SgNom:     RHyph  ;
    +Sg+Ill:aj   K-CONS ;
       +Ess:an   K-CONS ;
    +Sg+Gen:a    K-VOW  ;
 +Cmp/SgGen:a    RHyph  ;
    +Sg+Acc:av   K-CONS ;
    +Sg+Ine:an   K-CONS ;
    +Sg+Ela:as   K-CONS ;
    +Pl+Nom:a    K-VOW  ;
    +Pl+Gen:aj   K-CONS ;
    +Pl+Gen:aj   RHyph  ;
    +Pl+Acc:ajt  K-CONS ;
    +Pl+Ill:ajda K-VOW  ;
    +Pl+Ine:ajn  K-CONS ;
    +Pl+Ela:ajs  K-CONS ;
    +Pl+Com:aj   K-CONS ;
    +Sg+Com:ajn  K-CONS ;


LEXICON BERNUCASE-LOAN ! Final foot structure (X) => Loc:as
                     +Sg+Nom:     K-CONS ;
                  +Cmp/SgNom:     RHyph  ;
            +Sg+Ill+Err/Orth:aj   K-CONS ;
               +Ess+Err/Orth:an   K-CONS ;
            +Sg+Gen+Err/Orth:a    K-VOW  ;
  +Sg+Gen+Err/Orth+Cmp/SgGen:a    RHyph  ;
            +Sg+Acc+Err/Orth:av   K-CONS ;
            +Sg+Ine+Err/Orth:an   K-CONS ;
            +Sg+Ela+Err/Orth:as   K-CONS ;
!           +Pl+Nom+Err/Orth:a    K-VOW  ;
!           +Pl+Gen+Err/Orth:aj   K-CONS ;
!           +Pl+Gen+Err/Orth:aj   RHyph  ;
!           +Pl+Acc+Err/Orth:ajt  K-CONS ;
!           +Pl+Ill+Err/Orth:ajda K-VOW  ;
!           +Pl+Ine+Err/Orth:ajn  K-CONS ;
!           +Pl+Ela+Err/Orth:ajs  K-CONS ;
!           +Pl+Com+Err/Orth:aj   K-CONS ;
            +Sg+Com+Err/Orth:ajn  K-CONS ;


!! Lexicons OY work as BERN lexicons

LEXICON OY-plc
BERN-plc ;

LEXICON OY-sur
BERN-sur ;

LEXICON OY-fem
BERN-fem ;

!! Words in LONDONBERN are sent to both LONDON and BERN lexicons. Non-assmilated stems.

LEXICON LONDONBERN-plc
  +Use/NG:	 LONDON-plc ;
	 BERN-plc ;

!! 4-syllable stems
! -------------

!! Words in lexicon BASUDIS are trisyllabic in sg nom, and work like standard 4-syllable nouns. End on conconant and have cg. Even syllable case marking with acc -áv, ill -áj, ela -ás, etc. Real lule sami stems.

LEXICON BASUDIS-org!!= @CODE@  Only singular. Placenames
 +N+Prop+Sem/Org:%> BASUDISDECL-ORG ;

! !!€gt-norm: Ádjagas # 4-syllable test
! !!€ Ádjagas:                 Ádjagas+N+Prop+Sem/Org+Sg+Nom
! !!€ Ádjagassaj:              Ádjagas+N+Prop+Sem/Org+Sg+Ill
! !!€ Ádjagasás:               Ádjagas+N+Prop+Sem/Org+Sg+Ela

LEXICON BASUDIS-mal !!= @CODE@  Male names
 +N+Prop+Sem/Mal:%> BASUDISDECL ;
 +N+Prop+Sem/Mal:%> RÁVKKE_NAMÁK      ;

!!€gt-norm: Ájluhasj # 4-syllable test
!!€ Ájluhasj:                 Ájluhasj+N+Prop+Sem/Mal+Sg+Nom
!!€ Ájluhadtjaj:              Ájluhasj+N+Prop+Sem/Mal+Sg+Ill !should add dummy to prevent unusual dtj-stem?
!!€ Ájluhattjas:              Ájluhasj+N+Prop+Sem/Mal+Sg+Ela

!LEXICON BASUDIS-fem !!= @CODE@ Female names
! +N+Prop+Sem/Fem:%> BASUDISDECL ;
! +N+Prop+Sem/Fem:%> RÁVKKE_NAMÁK      ;
!
!!!€gt-norm: Magdalas # 4-syllable test
!!!€ Magdalas:                Magdalas+N+Prop+Sem/Fem+Sg+Nom
!!!€ Magdalassaj:              Magdalas+N+Prop+Sem/Fem+Sg+Ill
!!!€ Magdalasás:               Magdalas+N+Prop+Sem/Fem+Sg+Ela

!LEXICON BASUDIS-sur !!= @CODE@ Surnames. Presently not used in SMJ.
! +N+Prop+Sem/Sur:%> BASUDISDECL ;

LEXICON BASUDIS-plc !!= @CODE@ Place names
                   +N+Prop+Sem/Plc:%>                 BASUDISDECL-PLC ;
 +N+Prop+Sem/Plc@U.Cap.Opt@:%>aQ100@U.Cap.Opt@ VUONAK          ;

!!€gt-norm: Ulldevis # 4-syllable test
!!€ Ulldevis:                 Ulldevis+N+Prop+Sem/Plc+Sg+Nom
!!€ Ulldevissaj:              Ulldevis+N+Prop+Sem/Plc+Sg+Ill
!!€ Ulldevisás:               Ulldevis+N+Prop+Sem/Plc+Sg+Ela

!LEXICON BASUDIS-obj
! +N+Prop+Sem/Obj:%> BASUDISDECL ;

LEXICON BASUDISDECL
 @U.Cap.Obl@ BASUDISUCASE ;

LEXICON BASUDISDECL-PLC
 @U.Cap.Obl@ BASUDISUCASE ;
 +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@:%>aQ100@U.Cap.Opt@  LASJ;

LEXICON BASUDISDECL-ORG
 @U.Cap.Obl@ BASUDISUCASE-ORG ;

LEXICON BASUDISUCASE ! Bisyll. Non-Gradating C-Proper Names
    +Sg+Nom:        K-CONS ;
 +Cmp/SgNom:        RHyph  ;
       +Ess:an      K-CONS ;
    +Sg+Ill:aj      K-CONS ;
    +Sg+Gen:aQ1     K-VOW  ;
 +Cmp/SgGen:aQ1     RHyph  ;
    +Sg+Acc:aQ1v    K-CONS ;
    +Sg+Ine:aQ1n    K-CONS ;
    +Sg+Ela:aQ1s    K-CONS ;
    +Pl+Nom:aQ1     K-VOW  ;
    +Pl+Gen:aQ1j    K-CONS ;
 +Cmp/PlGen:aQ1j    RHyph  ;
    +Pl+Acc:aQ1jt   K-CONS ;
    +Pl+Ill:aQ1jda  K-VOW  ;
    +Pl+Ine:aQ1jn   K-CONS ;
    +Pl+Ela:aQ1js   K-CONS ;
    +Pl+Com:aQ1j    K-CONS ;
    +Sg+Com:aQ1X5jn K-CONS ;


LEXICON BASUDISUCASE-ORG ! Bisyll. Non-Gradating C-Proper Names
    +Sg+Nom:        K-CONS ;
 +Cmp/SgNom:        RHyph  ;
       +Ess:an      K-CONS ;
    +Sg+Ill:aj      K-CONS ;
    +Sg+Gen:aQ1     K-VOW  ;
 +Cmp/SgGen:aQ1     RHyph  ;
    +Sg+Acc:aQ1v    K-CONS ;
    +Sg+Ine:aQ1n    K-CONS ;
    +Sg+Ela:aQ1s    K-CONS ;
    +Pl+Nom:aQ1     K-VOW  ;
    +Pl+Gen:aQ1j    K-CONS ;
    +Pl+Gen:aQ1j    RHyph  ;
    +Pl+Acc:aQ1jt   K-CONS ;
    +Pl+Ill:aQ1jda  K-VOW  ;
    +Pl+Ine:aQ1jn   K-CONS ;
    +Pl+Ela:aQ1js   K-CONS ;
    +Pl+Com:aQ1j    K-CONS ;
    +Sg+Com:aQ1X5jn K-CONS ;



!! Plurals
! ------


!! Words in lexicon VARGGAT even-syllable sámi plurals .

LEXICON VARGGAT-plc !!= @CODE@  Plural stems, sáme names.  Place names
                   +N+Prop+Sem/Plc:%>                 VARGGATDECL ;
 +N+Prop+Sem/Plc@U.Cap.Opt@:%>Q100@U.Cap.Opt@ VUONAK      ;


LEXICON VARGGAT-org !!= @CODE@  Plural stems, sáme names. 
                   +N+Prop+Sem/Org:%>                 VARGGATDECL ;



!!€gt-norm: Bieva #  test
!!$ Bievva:                Bieva+N+Prop+Sem/Plc+Sg+Nom
!!€ Bieva:                 Bieva+N+Prop+Sem/Plc+Pl+Nom
!!€ Bievajda:              Bieva+N+Prop+Sem/Plc+Pl+Ill
!!€ Bievajs:               Bieva+N+Prop+Sem/Plc+Pl+Ela

!LEXICON VARGGAT-org !!= @CODE@  Organizations. Not In use
! +N+Prop+Sem/Org:%> VARGGATDECL ;
! +N+Prop+Sem/Org+Sg+Gen@U.Cap.Opt@:%>Q1@U.Cap.Opt@ VUONAK ;

!LEXICON VARGGAT-obj
! +N+Prop+Sem/Obj:%> VARGGATDECL ;
! +N+Prop+Sem/Obj+Sg+Gen@U.Cap.Opt@:%>Q1@U.Cap.Opt@ VUONAK ;


LEXICON VARGGATDECL
 @U.Cap.Obl@ VARGGATUCASE ;
! @U.Cap.Opt@:%>Q1@U.Cap.Opt@ VUONAK      ;

LEXICON VARGGATUCASE
        +Ess:n      K-CONS ;
     +Pl+Nom:Q1     K-VOW  ;
  +Cmp/PlNom:Q1     RHyph  ;
     +Pl+Gen:Q1j    K-VOW  ;
  +Cmp/PlGen:Q1j    RHyph  ;
     +Pl+Acc:Q1jt   K-CONS ;
     +Pl+Ill:Q1jda  K-VOW  ;
     +Pl+Ine:Q1jn   K-CONS ;
     +Pl+Ela:Q1js   K-CONS ;
     +Pl+Com:Q1j    K-CONS ;
!    +Sg+Com:Q1X5jn K-CONS ;


!! Words in lexicon ALEUHTAT even-syllables assimilated plurals.

LEXICON ALEUHTAT-plc !!= @CODE@ Plural names, not sami names. like -váre, -gårtje
        +N+Prop+Sem/Plc: ALEUHTATDECL-PLC ;



!LEXICON ALEUHTAT-org
! +N+Prop+Sem/Org: ALEUHTATDECL-ORG ;

LEXICON ALEUHTATDECL-PLC
        @U.Cap.Obl@               ALEUHTATCASE ;
        @U.Cap.Opt@               LASJ         ; ! XXX Sub?
 @U.Cap.Opt@:00@U.Cap.Opt@ VUONAK       ;

!LEXICON ALEUHTATDECL-ORG
!        @U.Cap.Obl@             ALEUHTATCASE ;
!        @U.Cap.Opt@             LASJ         ; ! XXX Sub?

LEXICON ALEUHTATCASE
        +Ess:%>n   K      ;
     +Pl+Nom:      K-VOW  ;
  +Cmp/PlNom:      RHyph  ;
     +Pl+Gen:%>j   K-CONS ;
  +Cmp/PlGen:%>j   RHyph  ;
     +Pl+Acc:%>jt  K-CONS ;
     +Pl+Ill:%>jda K-VOW  ;
     +Pl+Ine:%>jn  K-CONS ;
     +Pl+Ela:%>js  K-CONS ;
     +Pl+Com:%>j   K-CONS ;
!    +Sg+Com:%>jn  K-CONS ;




!! ## Odd syllable case marking
! =============

!! Words in lexicon LONDON end on conconant, no cg, case marking with -av, -ij, -is, etc. Last syllable is unstressed. Gets a regular odd syllable case marking. Both real lule sami stems, assimilated stems and non-assimilated stems

LEXICON LONDON-atr		!SUB
 +N+Prop+Attr@U.Cap.Obl@:%>00@U.Cap.Obl@ # ;

LEXICON LONDON-sur !!= @CODE@ Odd-syllable. Surnames. Final foot structure (X.) and (X..) => Loc:%>is
! Different lexicon for personal surnames.
                            +N+Prop+Sem/Sur:%>                LONDONDECL ;
+N+Prop+Sem/Sur+Attr@U.Cap.Obl@:%>000@U.Cap.Obl@ #          ; ! ! it shall be used in cases like Konrad Nielsenii

!!€gt-norm: Åstot # Odd-syllable test
!!€ Åstot:                Åstot+N+Prop+Sem/Sur+Sg+Nom
!!€ Åstodij:              Åstot+N+Prop+Sem/Sur+Sg+Ill
!!€ Åstodis:              Åstot+N+Prop+Sem/Sur+Sg+Ela

LEXICON LONDON-ani !!= @CODE@ Animals
 +N+Prop+Sem/Ani:%> LONDONDECL ;
!!€gt-norm: Jubmel # Odd-syllable test
!!€ Jubmel:                Jubmel+N+Prop+Sem/Ani+Sg+Nom
!!€ Jubmelij:              Jubmel+N+Prop+Sem/Ani+Sg+Ill
!!€ Jubmelis:              Jubmel+N+Prop+Sem/Ani+Sg+Ela

LEXICON LONDON-org !!= @CODE@  Only singular Organizations
 +N+Prop+Sem/Org:%> LONDONDECL-ORG ;

!!€gt-norm: Klassekampen # Odd-syllable test
!!€ Klassekampen:                Klassekampen+N+Prop+Sem/Org+Sg+Nom
!!€ Klassekampenij:              Klassekampen+N+Prop+Sem/Org+Sg+Ill
!!€ Klassekampenis:              Klassekampen+N+Prop+Sem/Org+Sg+Ela

LEXICON LONDON-orgplc
 +N+Prop+Sem/Org:%> LONDONDECL-ORG ;
 +N+Prop+Sem/Plc:%> LONDONDECL-PLC-SUR ;

LEXICON LONDON-mal !!= @CODE@ Male names
                 +N+Prop+Sem/Mal:%>               LONDONDECL ;
                 +N+Prop+Sem/Mal:%>               RÁVKKE_NAMÁK     ;
 +N+Prop+Sem/Mal+Attr@U.Cap.Obl@:%>000@U.Cap.Obl@ #          ; ! ! it shall be used in cases like Konrad Nielsenii


!!€gt-norm: Mátteus # Odd-syllable test
!!€ Matteus:                Matteus+N+Prop+Sem/Mal+Sg+Nom
!!€ Matteusij:              Matteus+N+Prop+Sem/Mal+Sg+Ill
!!€ Matteusis:              Matteus+N+Prop+Sem/Mal+Sg+Ela

LEXICON LONDON-malsur !!= @CODE@ Names that can be both male- and surnames. Not used in smj-propernouns
                  +N+Prop+Sem/Mal:%>               LONDONDECL ;
                  +N+Prop+Sem/Sur:%>               LONDONDECL ;
                  +N+Prop+Sem/Mal:%>               RÁVKKE_NAMÁK     ;
! +N+Prop+Sem/Mal+Attr@U.Cap.Obl@:%>000@U.Cap.Obl@ #          ; ! ! it shall be used in cases like Konrad Nielsenii


!!€gt-norm: Timeus # Odd-syllable test
!!€ Timeus:                Timeus+N+Prop+Sem/Mal+Sg+Nom
!!€ Timeusij:              Timeus+N+Prop+Sem/Mal+Sg+Ill
!!€ Timeusis:              Timeus+N+Prop+Sem/Mal+Sg+Ela

LEXICON LONDON-fem !!= @CODE@ Female names
                 +N+Prop+Sem/Fem:%>               LONDONDECL ;
                 +N+Prop+Sem/Fem:%>               RÁVKKE_NAMÁK     ;
 +N+Prop+Sem/Fem+Attr@U.Cap.Obl@:%>000@U.Cap.Obl@ #          ; ! ! it shall be used in cases like Konrad Nielsenii


!!€gt-norm: Luhták # Odd-syllable test
!!€ Luhták:                Luhták+N+Prop+Sem/Fem+Sg+Nom
!!€ Luhtágij:              Luhták+N+Prop+Sem/Fem+Sg+Ill
!!€ Luhtágis:              Luhták+N+Prop+Sem/Fem+Sg+Ela

LEXICON LONDON-malfem !!= @CODE@ Names that can be both male and female names.Not used in smj-propernouns
 +N+Prop+Sem/Fem:%> LONDONDECL ;
 +N+Prop+Sem/Mal:%> LONDONDECL ;
 +N+Prop+Sem/Mal:%> RÁVKKE_NAMÁK     ;
 +N+Prop+Sem/Fem:%> RÁVKKE_NAMÁK     ;

!!€gt-norm: Robin # Odd-syllable test
!!€ Robin:                Robin+N+Prop+Sem/Fem+Sg+Nom
!!€ Robinij:              Robin+N+Prop+Sem/Fem+Sg+Ill
!!€ Robinis:              Robin+N+Prop+Sem/Fem+Sg+Ela

LEXICON LONDON-malplc !!= @CODE@ Names that can be both male- and placenames.Not used in smj-propernouns
 +N+Prop+Sem/Plc:%> LONDONDECL-PLC-SUR ;
 +N+Prop+Sem/Mal:%> LONDONDECL         ;
 +N+Prop+Sem/Mal:%> RÁVKKE_NAMÁK             ;


!!€gt-norm: Jergol # Odd-syllable test
!!€ Jergol:               Jergol+N+Prop+Sem/Mal+Sg+Nom
!!€ Jergolij:              Jergol+N+Prop+Sem/Mal+Sg+Ill
!!€ Jergolis:              Jergol+N+Prop+Sem/Mal+Sg+Ela

LEXICON LONDON-plc !!= @CODE@ Only singular. Placenames
 +N+Prop+Sem/Plc:%> LONDONDECL-PLC-SUR ;
 +N+Prop+Sem/Sur:%> LONDONDECL-PLC-SUR ;

!!€gt-norm: Njierek # Odd-syllable test
!!€ Njierek:                Njierek+N+Prop+Sem/Plc+Sg+Nom
!!€ Njieregij:              Njierek+N+Prop+Sem/Plc+Sg+Ill
!!€ Njieregis:              Njierek+N+Prop+Sem/Plc+Sg+Ela

LEXICON LONDON_MWE-plc
LONDON-plc ;

LEXICON LONDON-orgsur !!= @CODE@ Names that can be both organizations and surnames.Not used in Smj-propernouns
                            +N+Prop+Sem/Org:%>                LONDONDECL-ORG ;
                            +N+Prop+Sem/Sur:%>                LONDONDECL     ;
+N+Prop+Sem/Sur+Attr@U.Cap.Obl@:%>000@U.Cap.Obl@ #              ; ! ! it shall be used in cases like Konrad Nielsenii

!!€gt-norm: Rieser # Odd-syllable test
!!€ Rieser:                Rieser+N+Prop+Sem/Sur+Sg+Nom
!!€ Rieserij:              Rieser+N+Prop+Sem/Sur+Sg+Ill
!!€ Rieseris:              Rieser+N+Prop+Sem/Sur+Sg+Ela

LEXICON LONDON-obj !!= @CODE@ Objects. 
 +N+Prop+Sem/Obj:%> LONDONDECL ;

!!€gt-norm: Rovdjursutredningen # Odd-syllable test
!!€ Rovdjursutredningen:                Rovdjursutredningen+N+Prop+Sem/Obj+Sg+Nom
!!€ Rovdjursutredningenij:              Rovdjursutredningen+N+Prop+Sem/Obj+Sg+Ill
!!€ Rovdjursutredningenis:              Rovdjursutredningen+N+Prop+Sem/Obj+Sg+Ela

LEXICON LONDON-LOAN-obj !!= @CODE@ Objects loan. Not used in smj-propernouns
 +N+Prop+Sem/Obj:%> LONDONDECL-LOAN ;

!!€gt-norm: Sameloven # Odd-syllable test
!!€ Sameloven:                Sameloven+N+Prop+Sem/Obj+Sg+Nom
!!$ Samelovenij:              Sameloven+N+Prop+Sem/Obj+Sg+Ill
!!$ Samelovenis:              Sameloven+N+Prop+Sem/Obj+Sg+Ela

LEXICON LONDON-LOAN-plc !!= @CODE@ Only nominatives. Placenames loan. Not used in Smj-propernouns
 +N+Prop+Sem/Plc:%> LONDONDECL-LOAN ;

!!€gt-norm: Jordandalen # Odd-syllable test
!!€ Jordandalen:                Jordandalen+N+Prop+Sem/Plc+Sg+Nom
!!$ Jordandalenij:              Jordandalen+N+Prop+Sem/Plc+Sg+Ill
!!$ Jordandalenis:              Jordandalen+N+Prop+Sem/Plc+Sg+Ela

LEXICON LONDON-LOAN-org !!= @CODE@  Only nominative. Organizations loan.Not used in smj-propernouns
 +N+Prop+Sem/Org:%> LONDONDECL-LOAN ;

!!€gt-norm: Samfunnsavdelingen # Odd-syllable test
!!€ Samfunnsavdelingen:                Samfunnsavdelingen+N+Prop+Sem/Org+Sg+Nom
!!$ Samfunnsavdelingenij:              Samfunnsavdelingen+N+Prop+Sem/Org+Sg+Ill
!!$ Samfunnsavdelingenis:              Samfunnsavdelingen+N+Prop+Sem/Org+Sg+Ela

!LEXICON LONDON-surplc
! +N+Prop+Sem/Plc:%> LONDONDECL_PLC-ORG ;
! +N+Prop+Sem/Sur:%> LONDONDECL ;
! +N+Prop+Sem/Sur+Attr@U.Cap.Obl@+Err/Orth:%>@U.Cap.Obl@ # ;  ! ! it shall be used in cases like Konrad Nielsenii

LEXICON LONDONDECL
 @U.Cap.Obl@ LONDONUCASE ;

LEXICON LONDONDECL-ORG
 @U.Cap.Obl@ LONDONUCASE-ORG ;

LEXICON LONDONDECL-PLC-SUR
 @U.Cap.Obl@ LONDONUCASE ;
! @U.Cap.Opt@ LASJ        ; ! odd syll dont take -lasj der

LEXICON LONDONDECL-LOAN
 @U.Cap.Obl@ LONDONUCASE-LOAN ;
 @U.Cap.Opt@ LASJ             ;


LEXICON LONDONUCASE ! Final foot structure (X.) and (X..) => Loc:is
            +Attr:     K-CONS ;
          +Sg+Nom:     K-CONS ;
       +Cmp/SgNom:     RHyph  ;
          +Sg+Gen:a    K-VOW  ;
 +Sg+Gen+Err/Orth:%:a  K-VOW  ;
       +Cmp/SgGen:a    RHyph  ;
          +Sg+Acc:av   K-CONS ;
 +Sg+Acc+Err/Orth:%:av K-CONS ;
          +Sg+Ill:ij   K-CONS ;
 +Sg+Ill+Err/Orth:%:ij K-CONS ;
          +Sg+Ine:in   K-CONS ;
 +Sg+Ine+Err/Orth:%:in K-CONS ;
          +Sg+Ela:is   K-CONS ;
 +Sg+Ela+Err/Orth:%:is K-CONS ;
 +Sg+Ill+Err/Orth:aj   K-CONS ;
 +Sg+Ine+Err/Orth:an   K-CONS ;
 +Sg+Ela+Err/Orth:as   K-CONS ;
          +Sg+Com:ijn  K-CONS ;
             +Ess:in   K-CONS ;
          +Pl+Nom:a    K-VOW  ;
          +Pl+Gen:ij   K-CONS ;
       +Cmp/PlGen:ij   RHyph  ;
          +Pl+Acc:ijt  K-CONS ;
          +Pl+Ill:ijda K-VOW  ;
          +Pl+Ine:ijn  K-CONS ;
          +Pl+Ela:ijs  K-CONS ;
          +Pl+Com:ij   K-CONS ;


LEXICON LONDONUCASE-ORG ! Final foot structure (X.) and (X..) => Loc:is
           +Sg+Nom:     K-CONS ;
        +Cmp/SgNom:     RHyph  ;
           +Sg+Gen:a    K-VOW  ;
 +Sg+Gen+Cmp/PlGen:a    RHyph  ;
           +Sg+Acc:av   K-CONS ;
           +Sg+Ill:ij   K-CONS ;
           +Sg+Ine:in   K-CONS ;
           +Sg+Ela:is   K-CONS ;
  +Sg+Ill+Err/Orth:aj   K-CONS ;
  +Sg+Ine+Err/Orth:an   K-CONS ;
  +Sg+Ela+Err/Orth:as   K-CONS ;
           +Sg+Com:ijn  K-CONS ;
              +Ess:in   K-CONS ;
           +Pl+Nom:a    K-VOW  ;
           +Pl+Gen:ij   K-CONS ;
           +Pl+Gen:ij   RHyph  ;
           +Pl+Acc:ijt  K-CONS ;
           +Pl+Ill:ijda K-VOW  ;
           +Pl+Ine:ijn  K-CONS ;
           +Pl+Ela:ijs  K-CONS ;
           +Pl+Com:ij   K-CONS ;



LEXICON LONDONUCASE-LOAN ! Final foot structure (X.) and (X..) => Loc:is
                     +Sg+Nom:     K-CONS ;
                  +Cmp/SgNom:     RHyph  ;
            +Sg+Gen+Err/Orth:a    K-VOW  ;
  +Sg+Gen+Err/Orth+Cmp/SgGen:a    RHyph  ;
            +Sg+Acc+Err/Orth:av   K-CONS ;
            +Sg+Ill+Err/Orth:ij   K-CONS ;
            +Sg+Ine+Err/Orth:in   K-CONS ;
            +Sg+Ela+Err/Orth:is   K-CONS ;
            +Sg+Ill+Err/Orth:aj   K-CONS ;
            +Sg+Ine+Err/Orth:an   K-CONS ;
            +Sg+Ela+Err/Orth:as   K-CONS ;
            +Sg+Com+Err/Orth:ijn  K-CONS ;
               +Ess+Err/Orth:in   K-CONS ;
!           +Pl+Nom+Err/Orth:a    K      ;
!           +Pl+Gen+Err/Orth:ij   K      ;
!           +Pl+Gen+Err/Orth:ij   RHyph  ; !
!           +Pl+Acc+Err/Orth:ijt  K      ;
!           +Pl+Ill+Err/Orth:ijda K      ;
!           +Pl+Ine+Err/Orth:ijn  K      ;
!           +Pl+Ela+Err/Orth:ijs  K      ;
!           +Pl+Com+Err/Orth:ij   K      ;


!! JOKULL-plc are placenames. Lexicon added to make the code compile (?)

LEXICON JOKULL-plc
 +N+Prop+Sem/Plc: LONDONDECL-PLC-SUR ; !!= @CODE@ Placenames. NB added to make the code compile, needs revision. Gets an odd syllable case marking. Non-assimilated stems.
 +N+Prop+Sem/Sur: LONDONDECL-PLC-SUR ;
 
!!€gt-norm: Drangajökull # Even-syllable test
!!€ Drangajökull:                Drangajökull+N+Prop+Sem/Plc+Sg+Nom
!!€ Drangajökullij:              Drangajökull+N+Prop+Sem/Plc+Sg+Ill
!!€ Drangajökullis:              Drangajökull+N+Prop+Sem/Plc+Sg+Ela


!! Words in lexicon ANAR  end on conconant, no cg, case marking with ill -ij, ela -is. Gets an odd syllable case marking. Lule sami stems.

!LEXICON ANAR-ani !!= @CODE@ Animals. Not used in smj-propernouns. 
! +N+Prop+Sem/Ani:%> ANARDECL ;

!LEXICON ANAR-obj !!= @CODE@ Objects. Not used in smj-propernouns
! +N+Prop+Sem/Obj:%> ANARDECL ;

!LEXICON ANAR-org !!= @CODE@ Organizations. Not used in smj-propernouns
! +N+Prop+Sem/Org:%> ANARDECL-ORG ;

LEXICON ANAR-mal !!= @CODE@ Male names. 
 +N+Prop+Sem/Mal:%> ANARDECL ;
 +N+Prop+Sem/Mal:%> RÁVKKE_NAMÁK   ;


!LEXICON ANAR-fem !!= @CODE@ Female names. Not used in smj-propernouns
! +N+Prop+Sem/Fem:%> ANARDECL ;

!LEXICON ANAR-sur !!= @CODE@ Surnames. Not used in smj-propernouns
! +N+Prop+Sem/Sur:%> ANARDECL ;

LEXICON ANAR-plc !!= @CODE@  Place names
 +N+Prop+Sem/Plc:%> ANARDECL-PLC ;

!!€gt-norm: Guhttás # Odd-syllable test
!!€ Guhttás:                 Guhttás+N+Prop+Sem/Plc+Sg+Nom
!!€ Guhttásij:               Guhttás+N+Prop+Sem/Plc+Sg+Ill
!!€ Guhttásis:               Guhttás+N+Prop+Sem/Plc+Sg+Ela

LEXICON ANARDECL ! Cns-fin, no cons.grad.
 @U.Cap.Obl@ ANAR-OBL ;

!LEXICON ANARDECL-ORG ! Cns-fin, no cons.grad.
! @U.Cap.Obl@ ANAR-OBL-ORG ;

LEXICON ANARDECL-PLC ! Cns-fin, no cons.grad.
 @U.Cap.Obl@ ANAR-OBL ;
! @U.Cap.Opt@ LASJ     ; ! odd syll dont take -lasj der

LEXICON ANAR-OBL
 @U.Cap.Obl@ LONDONDECL ;

!LEXICON ANAR-OBL-ORG
! @U.Cap.Obl@ LONDONDECL-ORG ;


!! Words in PIPPI lexicons are i-final, have no cg, no second syllable vowel change, and get odd syllable case marking with acc -hav, ill -hij, elat -his, etc. Works as "riebij", but without the -j in nominative (it should maybe be Sirij and Pippij in nom?) and without cg. The last syllable is unstressed. Non-assimilated stems. 


LEXICON PIPPI-ani !!= @CODE@ IVowel-final names where case endings are added directly, no cg. Animals.
 +N+Prop+Sem/Ani:i%> PIPPIDECL ;

!!€gt-norm: Rullahuuli # Even-syllable test
!!€ Rullahuuli:               Rullahuuli+N+Prop+Sem/Ani+Sg+Nom
!!€ Rullahuulihij:              Rullahuuli+N+Prop+Sem/Ani+Sg+Ill
!!€ Rullahuulihis:              Rullahuuli+N+Prop+Sem/Ani+Sg+Ela


LEXICON PIPPI-obj !!= @CODE@ Vowel-final names where case endings are added directly, no cg. Object names
 +N+Prop+Sem/Obj:i%> PIPPIDECL ;

!!€gt-norm: Audi # Even-syllable test
!!€ Audi:                 Audi+N+Prop+Sem/Obj+Sg+Nom
!!€ Audihij:              Audi+N+Prop+Sem/Obj+Sg+Ill
!!€ Audihis:              Audi+N+Prop+Sem/Obj+Sg+Ela

LEXICON PIPPI-org !!= @CODE@ Vowel-final names where caseendings are added directly, no cg. organizations
 +N+Prop+Sem/Org:i%> PIPPIDECL-ORG ;

!!€gt-norm: Kon-Tiki # Even-syllable test
!!€ Kon-Tiki:               Kon-Tiki+N+Prop+Sem/Org+Sg+Nom
!!€ Kon-Tikihij:              Kon-Tiki+N+Prop+Sem/Org+Sg+Ill
!!€ Kon-Tikihis:              Kon-Tiki+N+Prop+Sem/Org+Sg+Ela

LEXICON PIPPI-mal !!= @CODE@ Vowel-final names where case are added directly, no cg. Male names
                 +N+Prop+Sem/Mal:i%>              PIPPIDECL ;
                 +N+Prop+Sem/Mal:%>               RÁVKKE_NAMÁK    ;
 +N+Prop+Sem/Mal+Attr@U.Cap.Obl@:i%>00@U.Cap.Obl@ #         ;

!!€gt-norm: Gianni # Even-syllable test
!!€ Gianni:               Gianni+N+Prop+Sem/Mal+Sg+Nom
!!€ Giannihij:              Gianni+N+Prop+Sem/Mal+Sg+Ill
!!€ Giannihis:              Gianni+N+Prop+Sem/Mal+Sg+Ela

LEXICON PIPPI-fem !!= @CODE@ Vowel-final names where case endings are added directly, no cg. Female names
                 +N+Prop+Sem/Fem:i%>              PIPPIDECL ;
                 +N+Prop+Sem/Fem:%>               RÁVKKE_NAMÁK    ;
 +N+Prop+Sem/Fem+Attr@U.Cap.Obl@:i%>00@U.Cap.Obl@ #         ;


!!€gt-norm: Guri # Even-syllable test
!!€ Guri:               Guri+N+Prop+Sem/Fem+Sg+Nom
!!€ Gurihij:              Guri+N+Prop+Sem/Fem+Sg+Ill
!!€ Gurihis:              Guri+N+Prop+Sem/Fem+Sg+Ela

LEXICON PIPPI-femsur !!= @CODE@ Vowel-final names where case endings are added directly, no cg. Female names also used as surnames
                 +N+Prop+Sem/Fem:i%>              PIPPIDECL ;
                 +N+Prop+Sem/Sur:i%>              PIPPIDECL ;
                 +N+Prop+Sem/Fem:%>               RÁVKKE_NAMÁK    ;
 +N+Prop+Sem/Sur+Attr@U.Cap.Obl@:i%>00@U.Cap.Obl@ #         ;
 +N+Prop+Sem/Fem+Attr@U.Cap.Obl@:i%>00@U.Cap.Obl@ #         ;


!!€gt-norm: Turi # Even-syllable test
!!€ Turi:               Turi+N+Prop+Sem/Fem+Sg+Nom
!!€ Turihij:              Turi+N+Prop+Sem/Fem+Sg+Ill
!!€ Turihij:              Turi+N+Prop+Sem/Sur+Sg+Ill
!!€ Turihis:              Turi+N+Prop+Sem/Fem+Sg+Ela

LEXICON PIPPI-malfem !!= @CODE@ Vowel-final names where case endings are added directly, no cg. Names that can be both female and male names
                 +N+Prop+Sem/Fem:i%>              PIPPIDECL ;
                 +N+Prop+Sem/Mal:i%>              PIPPIDECL ;
                 +N+Prop+Sem/Mal:%>               RÁVKKE_NAMÁK    ;
                 +N+Prop+Sem/Fem:%>               RÁVKKE_NAMÁK    ;
 +N+Prop+Sem/Fem+Attr@U.Cap.Obl@:i%>00@U.Cap.Obl@ #         ;
 +N+Prop+Sem/Mal+Attr@U.Cap.Obl@:i%>00@U.Cap.Obl@ #         ;

!!€gt-norm: Kari # Even-syllable test
!!€ Kari:               Kari+N+Prop+Sem/Mal+Sg+Nom
!!€ Kari:               Kari+N+Prop+Sem/Fem+Sg+Nom
!!€ Karihij:              Kari+N+Prop+Sem/Mal+Sg+Ill
!!€ Karihis:              Kari+N+Prop+Sem/Mal+Sg+Ela

!LEXICON PIPPI-objplc !!= @CODE@ Vowel-final names where case endings are added directly, no cg. Names that can be both objects and place names
!                   +N+Prop+Sem/Obj:i%>                PIPPIDECL     ;
!                   +N+Prop+Sem/Plc:i%>                PIPPIDECL-PLC ;
! +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@:i%>000@U.Cap.Opt@ VUONAK        ;

!!€gt-norm:  # Even-syllable test


!LEXICON PIPPI-femplc !!= @CODE@ Vowel-final names where case endings are added directly, no cg. Names that can be both female and place names
!                   +N+Prop+Sem/Fem:i%>                PIPPIDECL     ;
!                   +N+Prop+Sem/Plc:i%>                PIPPIDECL-PLC ;
! +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@:i%>000@U.Cap.Opt@ VUONAK        ;

!!€gt-norm:  # Even-syllable test


LEXICON PIPPI-sur !!= @CODE@ Vowel-final names where case endings are added directly, no cg. Surnames
                            +N+Prop+Sem/Sur:i%>            PIPPIDECL ;
+N+Prop+Sem/Sur+Attr@U.Cap.Obl@:i%>00@U.Cap.Obl@ #     ;

!!€gt-norm: Sammallahti # Even-syllable test
!!€ Sammallahti:               Sammallahti+N+Prop+Sem/Sur+Sg+Nom
!!€ Sammallahtihij:              Sammallahti+N+Prop+Sem/Sur+Sg+Ill
!!€ Sammallahtihis:              Sammallahti+N+Prop+Sem/Sur+Sg+Ela

!LEXICON PIPPI-malsur !!= @CODE@  Vowel-final names where case endings are added directly, no cg. Names that can be both male- and surnames
!                            +N+Prop+Sem/Sur:i%>            PIPPIDECL ;
!                            +N+Prop+Sem/Mal:i%>            PIPPIDECL ;
!+N+Prop+Sem/Sur+Attr@U.Cap.Obl@:i%>000@U.Cap.Obl@ #     ;
!+N+Prop+Sem/Mal+Attr@U.Cap.Obl@:i%>000@U.Cap.Obl@ #     ;

!!€gt-norm:  # Even-syllable test

LEXICON PIPPI-plc !!= @CODE@ Vowel-final names where caseendings are added directly, no cg. Place names
                   +N+Prop+Sem/Plc:i%>                PIPPIDECL-PLC ;
                   +N+Prop+Sem/Sur:i%>                PIPPIDECL ;
+N+Prop+Sem/Sur+Attr@U.Cap.Obl@:i%>00@U.Cap.Obl@ # ;
 +N+Prop+Sem/Plc@U.Cap.Opt@:i%>0@U.Cap.Opt@ VUONAK        ;

!!€gt-norm: Lapinlampi # Even-syllable test
!!€ Lapinlampi:                 Lapinlampi+N+Prop+Sem/Plc+Sg+Nom
!!€ Lapinlampihij:              Lapinlampi+N+Prop+Sem/Plc+Sg+Ill
!!€ Lapinlampihis:              Lapinlampi+N+Prop+Sem/Plc+Sg+Ela

LEXICON PIPPI_MWE-plc
                   +N+Prop+Sem/Plc:i%>                PIPPIDECL-PLC ;

!LEXICON PIPPI-surplc !!= @CODE@ Vowel-final names where case endings are added directly, no cg. Names that are both surnames and place names
!                            +N+Prop+Sem/Sur:i%>                PIPPIDECL     ;
!                            +N+Prop+Sem/Plc:i%>                PIPPIDECL-PLC ;
!+N+Prop+Sem/Sur+Attr@U.Cap.Obl@:i%>000@U.Cap.Obl@ #             ;
!          +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@:i%>000@U.Cap.Opt@ VUONAK        ;

!!€gt-norm:  # Even-syllable test

LEXICON PIPPI-LOAN-plc !!= @CODE@ Only nominatives. Vowel-final names where case endings are added directly, no cg.Place names
                           +N+Prop+Sem/Plc:i%>                PIPPIDECL-LOAN ;
                           +N+Prop+Sem/Sur:i%>                PIPPIDECL-LOAN ;
+N+Prop+Sem/Sur+Attr@U.Cap.Obl@:i%>00@U.Cap.Obl@ # ;
+N+Prop+Sem/Plc@U.Cap.Opt@+Err/Orth:i%>000@U.Cap.Opt@ VUONAK         ; ! Galggá gus dát?

!!€gt-norm: Haltiatunturi # Even-syllable test
!!€ Haltiatunturi:               Haltiatunturi+N+Prop+Sem/Plc+Sg+Nom
!!$ Haltiatunturijij:              Haltiatunturi+N+Prop+Sem/Plc+Sg+Ill
!!$ Haltiatunturijis:              Haltiatunturi+N+Prop+Sem/Plc+Sg+Ela

!LEXICON PIPPI-LOAN-org !!= @CODE@ Only nominatives. Vowel-final names where case endings are added directly, no cg. organizations
! +N+Prop+Sem/Org:i%> PIPPIDECL-LOAN ;

!!€gt-norm:  # Even-syllable test

!LEXICON PIPPI-LOAN-obj !!= @CODE@ Only nominatives. Vowel-final names where case endings are added directly, no cg. Object names
! +N+Prop+Sem/Obj:i%> PIPPIDECL-LOAN ;

!!€gt-norm:  # Even-syllable test


!LEXICON PIPPI-LOAN-surplc ! Names that are both surnames and place names
! +N+Prop+Sem/Sur:i%> PIPPIDECL-LOAN ;
! +N+Prop+Sem/Sur+Attr@U.Cap.Obl@+Err/Orth:i%>@U.Cap.Obl@ # ;
! +N+Prop+Sem/Plc:i%> PIPPIDECL-LOAN ;
! +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@+Err/Orth:i%>@U.Cap.Opt@ VUONAK ;


LEXICON PIPPIDECL
! These sublexica are irrelevant for PIPPI, but added
! for the sake of the lexicon MARJA
 @U.Cap.Obl@ PIPPI-DC ;

LEXICON PIPPIDECL-ORG
! These sublexica are irrelevant for PIPPI, but added
! for the sake of the lexicon MARJA
 @U.Cap.Obl@ PIPPI-DC-ORG ;

LEXICON PIPPIDECL-PLC
! These sublexica are irrelevant for PIPPI, but added
! for the sake of the lexicon MARJA
 @U.Cap.Obl@ PIPPI-DC ;
! @U.Cap.Opt@ LASJ     ; ! odd syll dont take -lasj der

LEXICON PIPPIDECL-LOAN
! These sublexica are irrelevant for PIPPI, but added
! for the sake of the lexicon MARJA
 @U.Cap.Obl@ PIPPI-DC-LOAN ;
 !@U.Cap.Opt@+Err/Orth:@U.Cap.Opt@ LASJ          ;


LEXICON PIPPI-DC
          +Sg+Nom:      K-VOW  ;
       +Cmp/SgNom:      RHyph  ;
             +Ess:hin   K-CONS ;
          +Sg+Ill:hij   K-CONS ;
          +Sg+Gen:ha    K-VOW  ;
       +Cmp/SgGen:ha    RHyph  ;
          +Sg+Acc:hav   K-CONS ;
          +Sg+Ine:hin   K-CONS ;
          +Sg+Ela:his   K-CONS ;
          +Pl+Nom:ha    K-VOW  ;
          +Pl+Gen:hij   K-CONS ;
          +Pl+Gen:hij   K-CONS ;
          +Pl+Acc:hijt  K-CONS ;
          +Pl+Ill:hijda K-VOW  ;
          +Pl+Ine:hijn  K-CONS ;
          +Pl+Ela:hijs  K-CONS ;
          +Pl+Com:hij   K-CONS ;
          +Sg+Com:hijn  K-CONS ;
 +Sg+Com+Err/Orth:hijn  K-CONS ;


LEXICON PIPPI-DC-ORG
          +Sg+Nom:      K-VOW  ;
       +Cmp/SgNom:      RHyph  ;
             +Ess:hin   K-CONS ;
          +Sg+Ill:hij   K-CONS ;
          +Sg+Gen:ha    K-VOW  ;
       +Cmp/SgGen:ha    RHyph  ;
          +Sg+Acc:hav   K-CONS ;
          +Sg+Ine:hin   K-CONS ;
          +Sg+Ela:his   K-CONS ;
          +Pl+Nom:ha    K-VOW  ;
          +Pl+Gen:hij   K-CONS ;
          +Pl+Gen:hij   K-CONS ;
          +Pl+Acc:hijt  K-CONS ;
          +Pl+Ill:hijda K-VOW  ;
          +Pl+Ine:hijn  K-CONS ;
          +Pl+Ela:hijs  K-CONS ;
          +Pl+Com:hij   K-CONS ;

          +Sg+Com:hijn  K-CONS ;
 +Sg+Com+Err/Orth:hijn  K-CONS ;



LEXICON PIPPI-DC-LOAN
                     +Sg+Nom:      K-VOW  ;
                  +Cmp/SgNom:      RHyph  ;
               +Ess+Err/Orth:hin   K-CONS ;
            +Sg+Ill+Err/Orth:hij   K-CONS ;
            +Sg+Gen+Err/Orth:ha    K-VOW  ;
  +Sg+Gen+Err/Orth+Cmp/SgGen:ha    RHyph  ;
            +Sg+Acc+Err/Orth:hav   K-CONS ;
            +Sg+Ine+Err/Orth:hin   K-CONS ;
            +Sg+Ela+Err/Orth:his   K-CONS ;
!           +Pl+Nom+Err/Orth:      K      ;
!           +Pl+Gen+Err/Orth:Q4j   K      ; ! !Q4 to get e:i
!           +Pl+Gen+Err/Orth:Q4j   K      ; ! !Q4 to get e:i
!           +Pl+Acc+Err/Orth:Q4jt  K      ;
!           +Pl+Ill+Err/Orth:Q4jda K      ;
!           +Pl+Ine+Err/Orth:Q4jn  K      ;
!           +Pl+Ela+Err/Orth:Q4js  K      ;
!           +Pl+Com+Err/Orth:Q4j   K      ; ! !Q4 to get e:i
            +Sg+Com+Err/Orth:hijn  K-CONS ;
            +Sg+Com+Err/Orth:hijn  K-CONS ;


!! Words in lexicon DUORTNUS end on conconant, have cg and second syllable vowel change o:u, e:á. Odd syllable case marking. Real lule sami or one non-assimilated stem.

LEXICON DUORTNUS-mal !!= @CODE@ Male names
 +N+Prop+Sem/Mal:%> DUORTNUSDECL ;
 +N+Prop+Sem/Mal:%> RÁVKKE_NAMÁK       ;


!!€gt-norm: Mihkal # Odd-syllable test
!!€ Mihkal:                Mihkal+N+Prop+Sem/Mal+Sg+Nom
!!€ Mihkkalij:               Mihkal+N+Prop+Sem/Mal+Sg+Ill
!!€ Mihkkalis:               Mihkal+N+Prop+Sem/Mal+Sg+Ela

LEXICON DUORTNUS-sur !!= @CODE@ Male names
 +N+Prop+Sem/Sur:%> DUORTNUSDECL ;
+N+Prop+Sem/Sur+Attr@U.Cap.Obl@:%>000@U.Cap.Obl@ # ;

!!€gt-norm: Vándar # Odd-syllable test
!!€ Vándar:                  Vándar+N+Prop+Sem/Sur+Sg+Nom
!!€ Vánndarij:               Vándar+N+Prop+Sem/Sur+Sg+Ill
!!€ Vánndaris:               Vándar+N+Prop+Sem/Sur+Sg+Ela

!LEXICON DUORTNUS-obj !!= @CODE@ Odd-syllable ending on consonant, with cg. Objects. Presently not used in SMJ.
! +N+Prop+Sem/Obj:%> DUORTNUSDECL ;

LEXICON DUORTNUS-org !!= @CODE@ Odd-syllable ending on consonant, with cg. Organizations
 +N+Prop+Sem/Org:%> DUORTNUSDECL-ORG ;


LEXICON DUORTNUS-plc !!= @CODE@ Odd-syllable ending on consonant, with cg.Placenames
 +N+Prop+Sem/Plc:%> DUORTNUSDECL-PLC ;

!!€gt-norm: Hardangerduottar # Odd-syllable test
!!€ Hardangerduottar:               Hardangerduottar+N+Prop+Sem/Plc+Sg+Nom
!!€ Hardangerduoddara:               Hardangerduottar+N+Prop+Sem/Plc+Sg+Gen
!!€ Hardangerduoddarij:               Hardangerduottar+N+Prop+Sem/Plc+Sg+Ill
!!€ Hardangerduoddaris:               Hardangerduottar+N+Prop+Sem/Plc+Sg+Ela

!LEXICON DUORTNUS-fem
! +N+Prop+Sem/Fem:%> DUORTNUSDECL ;

!LEXICON DUORTNUS-sur
! +N+Prop+Sem/Sur:%> DUORTNUSDECL ;

LEXICON DUORTNUSDECL
 @U.Cap.Obl@ DUORTNUSUCASE ;

LEXICON DUORTNUSDECL-ORG
 @U.Cap.Obl@ DUORTNUSUCASE-ORG ;

LEXICON DUORTNUSDECL-PLC
 @U.Cap.Obl@ DUORTNUSUCASE ;
! @U.Cap.Opt@ LASJ          ; ! odd syll dont take -lasj der

LEXICON DUORTNUSUCASE ! Cns-final, cons.grad.
 !       :Q1     NE-ODDCASE ; ! Q1 is general WeG !Outcommented b/c not sure we want short essive for propers?
    +Sg+Nom:Q1     K-CONS ;
 +Cmp/SgNom:Q1     RHyph  ;
    +Sg+Gen:X5a    K-VOW  ;
 +Cmp/SgGen:X5a    RHyph  ;
    +Sg+Acc:X5av   K-CONS ;
    +Sg+Ill:X5ij   K-CONS ;
    +Sg+Ine:X5in   K-CONS ;
    +Sg+Ela:X5is   K-CONS ;
    +Sg+Com:X5ijn  K-CONS ;
       +Ess:X5in   K-CONS ;
    +Pl+Nom:X5a    K-VOW  ;
    +Pl+Gen:X5ij   K-CONS ;
 +Cmp/PlGen:X5ij   RHyph  ;
    +Pl+Acc:X5ijt  K-CONS ;
    +Pl+Ill:X5ijda K-VOW  ;
    +Pl+Ine:X5ijn  K-CONS ;
    +Pl+Ela:X5ijs  K-CONS ;
    +Pl+Com:X5ij   K-CONS ;


LEXICON DUORTNUSUCASE-ORG ! Cns-final, cons.grad.
:Q1 NE-ODDCASE ; ! Q1 is general WeG
+Cmp/SgGen:X5a RHyph ;
+Pl+Nom:X5a K-VOW ;
+Sg+Gen:X5a K-VOW ;
+Pl+Ill:X5ijda K-VOW ;
+Pl+Com:X5ij K-CONS ;
+Pl+Gen:X5ij K-CONS ;
+Pl+Gen:X5ij RHyph ;
+Sg+Ill:X5ij K-CONS ;
+Cmp/SgNom: RHyph ;
+Ess:X5in K-CONS ;
+Sg+Ine:X5in K-CONS ;
+Pl+Ine:X5ijn K-CONS ;
+Sg+Com:X5ijn K-CONS ;
+Sg+Ela:X5is K-CONS ;
+Pl+Ela:X5ijs K-CONS ;
+Pl+Acc:X5ijt K-CONS ;
+Sg+Acc:X5av K-CONS ;



LEXICON TIEMPEL-obj !!= @CODE@ Same as DUORTNUS, only without second syll vowel change. Odd syllanle case marking Lexicon presently only for two -tiempel-final words. Lule sami stems.
 +N+Prop+Sem/Obj:%> TIEMPELDECL ;

!!€gt-norm: Artemistiempel # Odd-syllable test 
!!€ Artemistiempel:                  Artemistiempel+N+Prop+Sem/Obj+Sg+Nom
!!€ Artemistiemmpelij:               Artemistiempel+N+Prop+Sem/Obj+Sg+Ill
!!€ Artemistiemmpelin:               Artemistiempel+N+Prop+Sem/Obj+Sg+Ine
!!€ Artemistiemmpelis:               Artemistiempel+N+Prop+Sem/Obj+Sg+Ela

LEXICON TIEMPEL-org !!= @CODE@ Same as DUORTNUS, only without second syll vowel change. Odd syllanle case marking Lexicon presently only for two -tiempel-final words. Lule sami stems.
 +N+Prop+Sem/Org:%> TIEMPELDECL ;

!!€gt-norm: Artemistiempel # Odd-syllable test 
!!€ Samovarteáhtar:                  Samovarteáhtar+N+Prop+Sem/Org+Sg+Nom
!!€ Samovarteáhttarij:               Samovarteáhtar+N+Prop+Sem/Org+Sg+Ill
!!€ Samovarteáhttarin:               Samovarteáhtar+N+Prop+Sem/Org+Sg+Ine
!!€ Samovarteáhttaris:               Samovarteáhtar+N+Prop+Sem/Org+Sg+Ela


LEXICON TIEMPELDECL
 @U.Cap.Obl@ TIEMPELUCASE ;
 
 LEXICON TIEMPELUCASE ! Cns-final, cons.grad.
!           :Q1   NE-ODDCASE ; ! Q1 is general WeG ! outcommented since we dont want short essive
     +Sg+Nom:Q1   K-CONS     ;
  +Cmp/SgNom:Q1   RHyph      ;
     +Sg+Gen:a    K-VOW      ;
  +Cmp/SgGen:a    RHyph      ;
     +Sg+Acc:av   K-CONS     ;
     +Sg+Ill:ij   K-CONS     ;
     +Sg+Ine:in   K-CONS     ;
     +Sg+Ela:is   K-CONS     ;
     +Sg+Com:ijn  K-CONS     ;
        +Ess:in   K-CONS     ;
     +Pl+Nom:a    K-VOW      ;
     +Pl+Gen:ij   K-CONS     ;
  +Cmp/PlGen:ij   RHyph      ;
     +Pl+Acc:ijt  K-CONS     ;
     +Pl+Ill:ijda K-VOW      ;
     +Pl+Ine:ijn  K-CONS     ;
     +Pl+Ela:ijs  K-CONS     ;
     +Pl+Com:ij   K-CONS     ;


LEXICON ÁJA-Plc ! Place names
+N+Prop+Sem/Plc:%> ÁJADECL ;

LEXICON ÁJA-Org ! Place names
+N+Prop+Sem/Org:%> ÁJADECL ;


LEXICON ÁJADECL
@U.Cap.Obl@ ÁJA-OBL ;

LEXICON ÁJA-OBL
    +Sg+Nom:X3   K-CONS ;
 +Cmp/SgNom:X3   RHyph  ;
    +Sg+Gen:a    K-VOW  ;
 +Cmp/SgGen:a    RHyph  ;
    +Sg+Acc:av   K-CONS ;
    +Sg+Ill:ij   K-CONS ;
    +Sg+Ine:in   K-CONS ;
    +Sg+Ela:is   K-CONS ;
    +Sg+Com:ijn  K-CONS ;
       +Ess:in   K-CONS ;
    +Pl+Nom:a    K-VOW  ;
    +Pl+Gen:ij   K-CONS ;
 +Cmp/PlGen:ij   RHyph  ;
    +Pl+Acc:ijt  K-CONS ;
    +Pl+Ill:ijda K-VOW  ;
    +Pl+Ine:ijn  K-CONS ;
    +Pl+Ela:ijs  K-CONS ;
    +Pl+Com:ij   K-CONS ;
             
             
!! Lexicon HEANDARAT is not in use in smj

!LEXICON HEANDARAT-plc !!= @CODE@  Not in use in smj-propernouns. Place names.
!                   +N+Prop+Sem/Plc:%>                 HEANDARATDECL-PLC ;
! +N+Prop+Sem/Plc+Sg+Gen@U.Cap.Opt@:%>aQ100@U.Cap.Opt@ VUONAK            ;

!LEXICON HEANDARAT-ani
! +N+Prop+Sem/Ani:%> HEANDARATDECL ;

!LEXICON HEANDARAT-obj
! +N+Prop+Sem/Obj:%> HEANDARATDECL ;

!LEXICON HEANDARAT-org
! +N+Prop+Sem/Org:%> HEANDARATDECL_PLC-ORG ;

!LEXICON HEANDARAT-mal
! +N+Prop+Sem/Mal:%>  HEANDARATDECL ;

!LEXICON HEANDARAT-fem
! +N+Prop+Sem/Fem:%>  HEANDARATDECL ;

!LEXICON HEANDARAT-sur
! +N+Prop+Sem/Sur:%> HEANDARATDECL ;

!LEXICON HEANDARATDECL
! @U.Cap.Obl@ HEANDARATUCASE ;

!LEXICON HEANDARATDECL-PLC
! @U.Cap.Obl@ HEANDARATUCASE ;
! @U.Cap.Opt@ LASJ           ; ! XXX Sub?


!LEXICON HEANDARATUCASE ! Bisyll. Non-Gradating C-Proper Names
! +Sg+Nom:a       K     ;
! +Sg+Nom:a       RHyph ;
!    +Ess:an      K     ;
! +Sg+Ill:aj      K     ;
! +Sg+Gen:aQ1     K     ;
! +Sg+Gen:aQ1     RHyph ;
! +Sg+Acc:aQ1v    K     ;
! +Sg+Ine:aQ1n    K     ;
! +Sg+Ela:aQ1s    K     ;
! +Pl+Nom:aQ1     K     ;
! +Pl+Gen:aQ1j    K     ;
! +Pl+Gen:aQ1j    RHyph ;
! +Pl+Acc:aQ1jt   K     ;
! +Pl+Ill:aQ1jda  K     ;
! +Pl+Ine:aQ1jn   K     ;
! +Pl+Ela:aQ1js   K     ;
! +Pl+Com:aQ1j    K     ;
! +Sg+Com:aQ1X5jn K     ;
!
!LEXICON HEANDARATUCASE-ORG ! Bisyll. Non-Gradating C-Proper Names
! +Sg+Nom:a       K     ;
! +Sg+Nom:a       RHyph ;
!    +Ess:an      K     ;
! +Sg+Ill:aj      K     ;
! +Sg+Gen:aQ1     K     ;
! +Sg+Gen:aQ1     RHyph ;
! +Sg+Acc:aQ1v    K     ;
! +Sg+Ine:aQ1n    K     ;
! +Sg+Ela:aQ1s    K     ;
!! +Pl+Nom:aQ1 K ;
!! +Pl+Gen:aQ1j K ;
!! +Pl+Gen:aQ1j RHyph ;
!! +Pl+Acc:aQ1jt K ;
!! +Pl+Ill:aQ1jda K ;
!! +Pl+Ine:aQ1jn K ;
!! +Pl+Ela:aQ1js K ;
!! +Pl+Com:aQ1j K ;
! +Sg+Com:aQ1X5jn K     ;
 
!! Words in lexicon EATNAMAT are odd-syllable plurals. Lule sami stems and non-assimilated stems.

LEXICON EATNAMAT-plc !!= @CODE@  Place names. Presently only for Vuolleednama
 +N+Prop+Sem/Plc: EATNAMATDECL-PLC ;
 
!!€gt-norm: Vuolleednama # Contrcted test
!!$ Vuolleednam:               Vuolleednama+N+Prop+Sem/Plc+Sg+Nom
!!€ Vuolleednama:              Vuolleednama+N+Prop+Sem/Plc+Pl+Nom
!!€ Vuolleednamijda:           Vuolleednama+N+Prop+Sem/Plc+Pl+Ill
!!€ Vuolleednamijs:            Vuolleednama+N+Prop+Sem/Plc+Pl+Ela 

LEXICON EATNAMAT-org !!= @CODE@  Organizations
 +N+Prop+Sem/Org: EATNAMATDECL-ORG ;

!!!€gt-norm: Vuorká-Dávvira # Contrcted test
!!!$ Vuorká-Dávvir:               Vuorká-Dávvira+N+Prop+Sem/Org+Sg+Nom
!!!€ Vuorká-Dávvira:              Vuorká-Dávvira+N+Prop+Sem/Org+Pl+Nom
!!!€ Vuorká-Dávvirijda:           Vuorká-Dávvira+N+Prop+Sem/Org+Pl+Ill
!!!€ Vuorká-Dávvirijs:            Vuorká-Dávvira+N+Prop+Sem/Org+Pl+Ela

LEXICON EATNAMATDECL-PLC
 @U.Cap.Obl@ EATNAMATCASE ;
! @U.Cap.Opt@ LASJ         ; ! odd syll dont take -lasj der

LEXICON EATNAMATDECL-ORG
 @U.Cap.Obl@ EATNAMATCASE ;

LEXICON EATNAMATCASE
       +Ess:%>in   K-CONS ;
    +Pl+Nom:%>a    K-VOW  ;
    +Pl+Gen:%>ij   K-CONS ;
 +Cmp/PlGen:%>ij   RHyph  ;
    +Pl+Acc:%>ijt  K-CONS ;
    +Pl+Ill:%>ijda K-VOW  ;
    +Pl+Ine:%>ijn  K-CONS ;
    +Pl+Ela:%>ijs  K-CONS ;
    +Pl+Com:%>ij   K-CONS ;




!! ## Contracted proper nouns
! =============

!! Words in lexicon DAVVISUOLLU are contracted propernouns ending on -åj/-oj. Lule sami stems

LEXICON DAVVISUOLU-plc !!= @CODE@ Contracted stems ending on -oj. Place names.
                   +N+Prop+Sem/Plc:%>                DAVVISUOLUDECL ;
 +N+Prop+Sem/Plc@U.Cap.Opt@:%>0000@U.Cap.Opt@ VUONAK         ;

!!€gt-norm: Victoriasuoloj # Contracted test
!!€ Victoriasuoloj:                Victoriasuoloj+N+Prop+Sem/Plc+Sg+Nom
!!€ Victoriasuolluj:               Victoriasuoloj+N+Prop+Sem/Plc+Sg+Ill
!!€ Victoriasuollus:               Victoriasuoloj+N+Prop+Sem/Plc+Sg+Ela

LEXICON DAVVISUOLUDECL !Inanim. Contracted -Names - from DAVVISUOLU
 @U.Cap.Obl@ DAVVISUOLU-OBL ;

LEXICON DAVVISUOLU-OBL
              +Sg+Nom:Q3j    K-CONS ;
                 +Ess:Q3n    K-CONS ;
  +CmpN/SgN+Cmp/SgNom:Q3j    RHyph  ;
              +Sg+Ill:X4j    K-CONS ;
              +Sg+Gen:X4     K-VOW  ;
              +Sg+Acc:X4v    K-CONS ;
              +Sg+Ine:X4n    K-CONS ;
              +Sg+Ela:X4s    K-CONS ;
!             +Pl+Nom:X4     K      ;
!             +Pl+Gen:X4j    K      ;
!             +Pl+Acc:X4jt   K      ;
!             +Pl+Ill:X4jda  K      ;
!             +Pl+Ine:X4jn   K      ;
!             +Pl+Ela:X4js   K      ;
!             +Pl+Com:X4j    K      ;
              +Sg+Com:X4X5jn K-CONS ; ! X5 for o:u in ComSg. Note two dummies in a row!
                 +Ess:n      K-CONS ;

!! Words in lexicon GEAVNNIS are contracted propernouns ending on -s.

!LEXICON GEAVNNIS-obj
! +N+Prop+Sem/Obj:%> GEAVNNISDECL ;

!LEXICON GEAVNNIS-org
! +N+Prop+Sem/Org:%> GEAVNNISDECL_PLC-ORG ;

!LEXICON GEAVNNIS-mal
! +N+Prop+Sem/Mal:%>  GEAVNNISDECL ;

!LEXICON GEAVNNIS-fem
! +N+Prop+Sem/Fem:%>  GEAVNNISDECL ;

!LEXICON GEAVNNIS-sur
! +N+Prop+Sem/Sur:%> GEAVNNISDECL ;


LEXICON GEAVNNIS-plc !!= @CODE@  Contracted stems ending on -es. Place names. Lule sami stems.
                   +N+Prop+Sem/Plc:%>                 GEAVNNISDECL-PLC ;
 +N+Prop+Sem/Plc@U.Cap.Opt@:%>Y7000@U.Cap.Opt@ VUONAK           ;

!!€gt-norm: Gaza-Sárges # Contracted test
!!€ Gaza-Sárges:                 Gaza-Sárges+N+Prop+Sem/Plc+Sg+Nom
!!€ Gaza-Sárggáj:              Gaza-Sárges+N+Prop+Sem/Plc+Sg+Ill
!!€ Gaza-Sárggás:               Gaza-Sárges+N+Prop+Sem/Plc+Sg+Ela


!LEXICON GEAVNNISDECL
! @U.Cap.Obl@ GEAVNNISUCASE ;

!LEXICON GEAVNNISDECL-ORG
! @U.Cap.Obl@ GEAVNNISUCASE-ORG ;

LEXICON GEAVNNISDECL-PLC
 @U.Cap.Obl@ GEAVNNISUCASE ;
! @U.Cap.Opt@:@U.Cap.Opt@Q1 LASJ          ; ! contracted syll dont take lasj der

LEXICON GEAVNNISUCASE
    +Sg+Nom:Q1s    K-CONS ;
 +Cmp/SgNom:Q1s    RHyph  ;
       +Ess:Q1n    K-CONS ; !do we want short essive for propers, makes Sirgen
    +Sg+Ill:X4j    K-CONS ;
       +Ess:X4n    K-CONS ; !Sirggán
    +Sg+Gen:X4     K-VOW  ;
 +Cmp/SgGen:X4     RHyph  ;
    +Sg+Acc:X4v    K-CONS ;
    +Sg+Ine:X4n    K-CONS ;
    +Sg+Ela:X4s    K-CONS ;
    +Pl+Nom:X4     K-VOW  ;
    +Pl+Gen:X4j    K-CONS ;
 +Cmp/PlGen:X4j    RHyph  ;
    +Pl+Acc:X4jt   K-CONS ;
    +Pl+Ill:X4jda  K-VOW  ;
    +Pl+Ine:X4jn   K-CONS ;
    +Pl+Ela:X4js   K-CONS ;
    +Pl+Com:X4j    K-CONS ;
    +Sg+Com:X4X5jn K-CONS ;


!LEXICON GEAVNNISUCASE-ORG
!    +Sg+Nom:Q1s    K     ;
! +Cmp/SgNom:Q1s    RHyph ;
!       +Ess:Q1n    K     ;
!    +Sg+Ill:X4j    K     ;
!       +Ess:X4n    K     ;
!    +Sg+Gen:X4     K     ;
! +Cmp/SgGen:X4     RHyph ;
!    +Sg+Acc:X4v    K     ;
!    +Sg+Ine:X4n    K     ;
!    +Sg+Ela:X4s    K     ;
!  ! +Pl+Nom:X4     K     ;
!  ! +Pl+Gen:X4j    K     ;
!  ! +Pl+Gen:X4j    RHyph ;
!  ! +Pl+Acc:X4jt   K     ;
!  ! +Pl+Ill:X4jda  K     ;
!  ! +Pl+Ine:X4jn   K     ;
!  ! +Pl+Ela:X4js   K     ;
!  ! +Pl+Com:X4j    K     ;
!    +Sg+Com:X4X5jn K     ;


!! Words in lexicon SUOLLOT are contracted plurals. Lule sami stems.

LEXICON SULLOT-plc !!= @CODE@ Plural names, only names ending on -suollu.
        +N+Prop+Sem/Plc: SULLOTDECL-PLC ;
        
!!€gt-norm: Falklandsuollu #  test
!!$ Falklandsuoloj:              Falklandsuollu+N+Prop+Sem/Plc+Sg+Nom
!!€ Falklandsuollu:              Falklandsuollu+N+Prop+Sem/Plc+Pl+Nom
!!€ Falklandsuollujda:           Falklandsuollu+N+Prop+Sem/Plc+Pl+Ill
!!€ Falklandsuollujs:            Falklandsuollu+N+Prop+Sem/Plc+Pl+Ela

LEXICON SULLOTDECL-PLC
        @U.Cap.Obl@               SULLOTCASE ;
!        @U.Cap.Opt@               LASJ       ; ! contracted syll dont take lasj der
 @U.Cap.Opt@:00@U.Cap.Opt@ VUONAK     ;

LEXICON SULLOTCASE
        +Ess:%>Q3n    K-CONS ;
        +Ess:%>X4n    K-CONS ;
     +Pl+Nom:%>X4     K-VOW  ;
  +Cmp/PlNom:%>X4     RHyph  ;
     +Pl+Gen:%>X4j    K-CONS ;
  +Cmp/PlGen:%>X4j    RHyph  ;
     +Pl+Acc:%>X4jt   K-CONS ;
     +Pl+Ill:%>X4jda  K-VOW  ;
     +Pl+Ine:%>X4jn   K-CONS ;
     +Pl+Ela:%>X4js   K-CONS ;
     +Pl+Com:%>X4j    K-CONS ;
!    +Sg+Com:%>X4X5jn K-CONS ;


 
!! ## Lexicons only used in sme/sma and that are sent to other lexicons in smj
! =============

!! ERVASTI is only used in smi-propenouns. Ervasti names are 3-syllable and are needed as a seperate lexicon because of sma. ERVASTI is same as ACCRA in smj and gets even syllable case marking.

LEXICON ERVASTI-fem
    :i  ACCRA-fem ;

LEXICON ERVASTI-mal
    :i  ACCRA-mal ;

!LEXICON ERVASTI-ani
!    :i  ACCRA-ani ;

LEXICON ERVASTI-obj
    :i  ACCRA-obj ;

LEXICON ERVASTI-sur
    :i  ACCRA-sur ;

LEXICON ERVASTI-org
    :i  ACCRA-org ;

LEXICON ERVASTI-plc
    :i  ACCRA-plc ;
    
LEXICON ERVASTI-plcmal
	:i  ACCRA-plc ;
    :i  ACCRA-mal ;

!LEXICON ERVASTI-LOAN-plc
!    :i  ACCRA-LOAN-plc ;

!! MAKI and NIEMI is only used in smi-propenouns. Maki names are even-syllable finnish names and are needed as a seperate lexicon because of sma. MÄKI is same as ACCRA in smj and gets even syllable case marking.

!LEXICON MAKI-fem
!  :i ACCRA-sur ;

!LEXICON MAKI-mal
!   :i ACCRA-sur ;

LEXICON MAKI-sur
   :i ACCRA-sur ;

!LEXICON MAKI-org
!   :i ACCRA-plc ;

LEXICON MAKI-plc
   :i ACCRA-plc ;

!LEXICON MAKI-LOAN-plc !Unødvendig lexicon, alle MAKI er LOAN, endres i smi
!   ACCRA-LOAN-plc ;
   
LEXICON NIEMI 
   :i ACCRA-plc ;
   
LEXICON flagK
@U.Cap.Obl@ K ;

! From SME?

!LEXICON CNAME_EVEN-org
!	FirstTag ;
	
!LEXICON CNAME_ODD-plc
!	LONDON-plc ;

! FirstTag

LEXICON FirstTag
+N+Prop+Attr:%> flag# ;

LEXICON flag#
@U.Cap.Obl@ # ;
    
!! HANNOLA is the same as ACCRA

LEXICON HANNOLA-sur
                           +N+Prop+Sem/Sur:a%>                ACCRADECL ;

LEXICON HANNOLA-plc
                   +N+Prop+Sem/Plc:a%>               ACCRADECL-PLC ;
 +N+Prop+Sem/Plc@U.Cap.Opt@:%>0000@U.Cap.Opt@ VUONAK        ;

LEXICON BISSAU-plc
ACCRA-plc ;

LEXICON PERU-plc
NYSTØ-plc ;
