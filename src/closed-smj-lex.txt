!This file contains the following  closed parts of speech

!Pronouns
!Subjunctions
!Conjunctions
!Numerals
!Particles
!Interjections

! Pronouns
! ========


!Pointing to all the pronominal subgrops
LEXICON Pronoun
 Personal ;
 Demonstrative ;
 Reflexive ;
 Reciprocal ;
 Interrogative ;
 Relative ;
 Indefinite ;


! Personal pronouns
! -----------------

LEXICON Personal
mån+Pron+Pers+Sg1:m persproncasesg ;
mån+Pron+Pers+Du1:m persproncasedu ;
mån+Pron+Pers+Pl1:m persproncasepl ;
dån+Pron+Pers+Sg2:d persproncasesgII ;
dån+Pron+Pers+Du2:d persproncasedu ;
dån+Pron+Pers+Pl2:d persproncasepl ;
sån+Pron+Pers+Sg3:s persproncasesgII ;
sån+Pron+Pers+Du3:s persproncasedu ;
sån+Pron+Pers+Pl3:s persproncasepl ;

LEXICON persproncasesg
 +Nom:ån K ;
 +Nom:ånnå K ;
 +Gen:uv K ;
 +Gen:uvva K ;
 +Acc:uv K ;
 +Acc:uvva K ;
 +Ill:unji K ;
 +Ine:ujna K ;
 +Ela:ujsta K ;
 +Com:ujna K ;
 
LEXICON persproncasesgII
 +Nom:ån K ;
 +Nom:ådnå K ;
 +Gen:uv K ;
 +Gen:uvva K ;
 +Acc:uv K ;
 +Acc:uvva K ;
 +Ill:unji K ;
 +Ine:ujna K ;
 +Ela:ujsta K ;
 +Com:ujna K ;

LEXICON persproncasedu
 +Nom:åj K ;
 +Nom:ådja K ;
 +Gen:unnu K ;
 +Acc:unnuv K ;
 +Ill:unnuj K ;
 +Ine:unnun K ;
 +Ela:unnus K ;
 +Com:unnujn K ;

LEXICON persproncasepl
! The second entry of each oblique case is a less common form
 +Nom:ij K ;
 +Nom:idja K ;
 +Gen:ijá K ;
 +Gen:ijáj K ;
 +Acc:ijáv K ;
 +Acc:ijájt K ;
 +Ill:idjij K ;
 +Ill:ijájda K ;
 +Ine:iján K ;
 +Ine:ijájn K ;
 +Ela:ijás K ;
 +Ela:ijájs K ;
 +Com:ijájn K ;
 +Com:ijáj K ;


! Demonstrative pronouns
! ----------------------

LEXICON Demonstrative
dat+Pron+Dem:da demcas ;
dat+Pron+Dem+Sg+Nom:datga K ;
dat+Pron+Dem+Sg+Gen:daŋga K ;
dat+Pron+Dem+Sg+Gen:daŋgas K ;
dát+Pron+Dem:dá demcas ;
dát+Pron+Dem+Sg:dá dátcas ;
duot+Pron+Dem:duo demcas ;
dåt+Pron+Dem:då demcas ;
dála+Pron+Dem:dála K ;
duola+Pron+Dem:duola K ;
dåvla+Pron+Dem:dåvla K ;
dakkár+Pron+Dem:dakkár dakkárcase ;
dakkir+Pron+Dem:dakkir dakkárcase ;
dákkár+Pron+Dem:dákkár dakkárcase ;
dákkir+Pron+Dem:dákkir dakkárcase ;
duokkár+Pron+Dem:duokkár dakkárcase ;
duokkir+Pron+Dem:duokkir dakkárcase ;
diekkár+Pron+Dem:diekkár dakkárcase ;
diekkir+Pron+Dem:diekkir dakkárcase ;
dåkkár+Pron+Dem:dåkkár dakkárcase ;
dåkkir+Pron+Dem:dåkkir dakkárcase ;



LEXICON demcas
 +Sg:0 demcassg ;
 +Pl:0 demcaspl ;

LEXICON demcassg 
 +Nom:t K ;
 +Gen:n K ;
 +Acc:v K ;
 +Acc:vva K ;
 +Ill:si K ;
 +Ine:n0na K ; ! was: n'n
 +Ela:ssta K ;
 +Com:jna K ;
 +Ess:nen K ;
 +Par:t K ;
 +Abe:bdá K ;

LEXICON demcaspl
 +Nom: K ;
 +Nom:ha K ;
 :j demcasplobl ;

LEXICON demcasplobl
 +Gen: K ;
 +Acc:t K ;
 +Ill:da K ;
 +Ine:n K ;
 +Ela:s K ;
 +Ela:sta K ;
 +Com: K ;

LEXICON dátcas
 +Nom:hta K ;
 +Nom:htak K ;
 +Gen:ñga K ;
 +Acc:vga K ;
 +Ill:siga K ;
 +Ine:nnága K ;
 +Ela:stága K ;
 +Com:jnak K ;
 
LEXICON dakkárcase
 +Attr: K ;
 : NAMÁK ;
 +Sg+Nom: K ;
 +Sg+Gen:a K ;
 +Sg+Acc:av K ;
 +Sg+Ill:ij K ;
 +Sg+Ine:in K ;
 +Sg+Ela:is K ;
 +Sg+Com:jn K ;
 +Ess:in K ;
 +Pl+Nom:a K ;
 +Pl+Gen:ij K ;
 +Pl+Acc:ijt K ;
 +Pl+Ill:ijda K ;
 +Pl+Ine:ijn K ;
 +Pl+Ela:ijs K ;
 +Pl+Com:ij K ;




! Reflexive pronouns
! ------------------

LEXICON Reflexive
!Two nominative reflexives, and pointer to the rest
iesj+Pron+Refl+Sg+Nom:iesj K ;
iesj+Pron+Refl+Du+Nom:ietja K ;
iesj+Pron+Refl+Pl+Nom:ietja K ;
iesj+Pron+Refl+Sg:0 refloblsg ;
iesj+Pron+Refl+Pl:0 refloblpl ;


LEXICON refloblsg
!The V/V2 alternation due to 3rd person vocalism
!Pointing directly to 1st and 3rd person sublexica for AG & Loc.
!Pointing to exceptional Pl sublexicon for AG and Loc.
+Acc:iehtja PxV1 ;
+Gen:iehtja PxV1 ;
+Acc:ietja PxV23 ;
+Gen:ietja PxV23 ;
+Acc+PxSg1:ietjam K ;	 ! Exceptional Sg12
+Gen+PxSg1:ietjam K ;  ! Exceptional Sg12
+Ill:iehtjas PxCi ;
+Ill:allas PxCi ;
+Ine:ietjan PxCi ;
+Ela:ietjast PxCi ;
+Com:ietjajn PxCi ;

LEXICON refloblpl
+Gen:ietjaj PxCiPlstem ;
+Acc:ietjajd PxCPlstem ;
+Ine:ietjajn PxCPlstem ;
+Ela:ietjajst PxCPlstem ;
+Com:ietjajn PxCPlstem ;

LEXICON PxCiPlstem
 +PxDu1:ma K ;
 +PxDu2:da K ;
 +PxDu3:ska K ;
 +PxPl1:ma K ;
 +PxPl2:da K ;
 +PxPl3:sa K ;

LEXICON  PxCPlstem
 +PxDu1:imme K ;
 +PxDu2:ihtte K ;
 +PxDu3:iskká K ;
 +PxPl1:imme K ;
 +PxPl2:ihtte K ;
 +PxPl3:isá K ;
 
! Reciprocal pronouns 
! -------------------

LEXICON Reciprocal 
gaska+Pron+Recipr:gaska PxCiPlstem ;
gaskajt+Pron+Recipr:gaskajd PxCPlstem ;
gaskan+Pron+Recipr:gaskan PxCPlstem ;


! Interrogative and relative pronouns
! -----------------------------------

LEXICON Interrogative
mij+Pron+Interr+Sg+Nom:mij K ;
mij+Pron+Interr+Sg+Nom:midja K ; !Alt. form
mij+Pron+Interr:ma oblintercas ;
mij+Pron+Interr+Sg+Par:mat K ;
mij+Pron+Interr+Pl:m idiosyncmij ;
guhti+Pron+Interr+Sg+Nom:guhti K ;
guhti+Pron+Interr+Pl+Nom:gudi K ;
guhti+Pron+Interr+Sg+Nom:gut K ;
guhti+Pron+Interr:ge oblintercas ;
guhti+Pron+Interr+Sg+Ela:gæssta K-s ; 
guhti+Pron+Interr+Sg+Ine:gæn'na K-s ;
makkár+Pron+Dem:makkár dakkárcase ;
makkir+Pron+Dem:makkir dakkárcase ;


LEXICON Relative
mij+Pron+Rel+Sg+Nom:mij K ;
mij+Pron+Rel+Sg+Nom:midja K ; !Alt. form
mij+Pron+Rel:ma oblintercas ;
mij+Pron+Rel+Sg+Par:mat K ;
mij+Pron+Rel+Pl:m idiosyncmij ;
guhti+Pron+Rel+Sg+Nom:guhti K ;
guhti+Pron+Rel+Pl+Nom:gudi K ;
guhti+Pron+Rel+Sg+Nom:gut K ;
guhti+Pron+Rel:ge oblintercas ;
guhti+Pron+Rel+Sg+Ela:gæssta K-s ; 
guhti+Pron+Rel+Sg+Ine:gæn'na K-s ;
makkár+Pron+Dem:makkár dakkárcase ;
makkir+Pron+Dem:makkir dakkárcase ;


LEXICON oblintercas
 +Sg+Gen:n K ;
 +Sg+Acc:v K ;
 +Sg+Acc:vva K-s ;
 +Sg+Ill:si K-s ; 
 +Sg+Com:jna K-s ;
 +Sg+Com:jnas K-s ;
 +Pl+Gen:j K ;
 +Pl+Acc:jt K ;
 +Pl+Ill:jda K-s ;
 +Pl+Ine:jna K-s ;
 +Pl+Ela:js K ;
 +Pl+Com:j K ;
 +Ess:nen K ;
 +Ess:n'na K-s ;
 +Ess:n'nas K-s ;

LEXICON idiosyncmij
 +Nom:a K-s ;
 +Nom:aha K-s ;
 +Gen:aj  K ; 
 +Acc:ejt K ; 
 +Ill:ejda K-s ;
! +Ine:ajna K-s ;
 +Ine:ajn K ;
 +Ela:ajsta K-s ;
 +Ela:ajs K ;

LEXICON K-s ! Alternative clitic lexicon
 +Clt:s # ;
 # ;


! Indefinite pronouns
! -------------------

LEXICON Indefinite

vehi+Pron+Indef:vehi K ;
aktak+Pron+Indef+Sg+Nom:aktak K ;
aktak+Pron+Indef+Sg+Ill:aktasik K ;
aktak+Pron+Indef:avta aktakoblcase ;
guhtik+Pron+Indef+Sg+Nom:guhtik # ;
guhtik+Pron+Indef+Pl+Nom:gudik # ;
guhtik+Pron+Indef:ge guhtikobl ;
guhtik+Pron+Indef+Sg+Ela:gæsstak # ;
guhtik+Pron+Indef+Sg+Ela:gæstástik # ;  
guhtik+Pron+Indef+Sg+Ine:gæn'nak # ;
guhtik+Pron+Indef+Sg+Ine:gæn'nanik # ;
iesj#guhtik+Pron+Indef+Sg+Nom:iesj#guhtik # ;
iesj#guhtik+Pron+Indef+Pl+Nom:iesj#gudik # ;
iesj#guhtik+Pron+Indef:iesjge guhtikobl ;
iesj#guhtik+Pron+Indef+Sg+Ela:iesj#gæsstak # ; 
iesj#guhtik+Pron+Indef+Sg+Ela:iesj#gæstástik # ; 
iesj#guhtik+Pron+Indef+Sg+Ine:iesj#gæn'nak # ;
iesj#guhtik+Pron+Indef+Sg+Ine:iesj#gæn'nanik # ;
fært#guhtik+Pron+Indef+Sg+Nom:fært#guhtik # ;
fært#guhtik+Pron+Indef+Pl+Nom:fært#gudik # ;
fært#guhtik+Pron+Indef:færtge guhtikobl ;
fært#guhtik+Pron+Indef+Sg+Ela:fært#gæsstak # ;
fært#guhtik+Pron+Indef+Sg+Ela:fært#gæstástik # ;  
fært#guhtik+Pron+Indef+Sg+Ine:fært#gæn'nak # ;
fært#guhtik+Pron+Indef+Sg+Ine:fært#gæn'nanik # ;
ietjá+Pron+Indef+Sg+Nom:ietjá K ;
ietjá+Pron+Indef+Sg+Nom:ietjes K ;
ietjá+Pron+Indef:ietjá oblietjácase ;
ietjá+Pron+Indef+Pl+Nom:iehtjáda K ;
ietjá+Pron+Indef+Pl:iehtjáda K ;
ietjá+Pron+Indef:iehtjád plurietjácase ;
ætjá+Pron+Indef+Sg+Nom:ætjá K ; !XXXse ietjá
juohkka+Pron+Indef+Attr:juohkka # ;
juohkka+Pron+Indef:juohkka NAMÁK ;
mige+Pron+Indef+Sg+Nom:mige # ;
mige+Pron+Indef+Sg+Nom:migek # ;
mige+Pron+Indef+Pl+Nom:mahkka # ;
mige+Pron+Indef+Pl+Acc:mejdek # ;
mige+Pron+Indef+Pl+Ill:mejdek # ;
mige+Pron+Indef:ma migeobl ;
muhtem+Pron+Indef:muhtem muhtemcase ;
muhtem+Pron+Indef+Pl+Nom:muhte K ;
muhtem+Pron+Indef:muhti goappátjagácase ;
nágin+Pron+Indef:nágin muhtemcase ;
soames+Pron+Indef+Sg+Nom:soames K ;
soames+Pron+Indef+Sg+Nom:soames NAMÁK ;
soames+Pron+Indef:soabmás oblsoamescase ;
goabbák+Pron+Indef+Attr:goabbák K ;
goabbák+Pron+Indef:goabbák NAMÁK ;
goabbák+Pron+Indef+Sg+Nom:goabbák K ;
goabbák+Pron+Indef+Sg+Ill:goabbásik K ;
goabbák+Pron+Indef:goappá aktakoblcase ;
juoga+Pron+Indef:juo juogacase ;
gájka+Pron+Indef:gájk gájkacase ;
fært+Pron+Indef+Attr:fært # ;
færtta+Pron+Indef+Attr:færtta # ;
fært+Pron+Indef:fært NAMÁK ;
færtta+Pron+Indef:færtta NAMÁK ;
goappátjagá+Pron+Indef+Pl+Nom:goappátjagá K ;
goappátjagá+Pron+Indef:goappátjagá goappátjagácase ;
goabbák+Pron+Indef+Sg+Nom:goabbák K ;
goabbák+Pron+Indef:goappág goabbákcase ;

! The indeclineable indefinites
! - - - - - - - - - - - - - - - 

irda iind ;
divna iind ;
gájk#divna iind ;
sæbbmi iind ;
sæmmi iind ;
enas iind ;
ihkenis iind ;
åbbånis iind ;
åvvånis iind ;
sudes iind ;  
sudes+Attr:suhtte K ; 
sudes+Attr:suhtta K ;
makkirak iind ;
makkárak iind ;
juonenik iind ;
juokkirik iind ;
juokkirak iind ;
juoppánik  iind ; !iness.




LEXICON guhtikobl
 +Sg+Gen:ñga K ;
 +Sg+Gen:ñge K ;
 +Sg+Gen:ñga NAMÁK ;
 +Sg+Gen:ñge NAMÁK ;
 +Sg+Acc:vga K ;
 +Sg+Ill:sik K ;
 +Sg+Ill:sisik K ;  
 +Sg+Com:jnak K ;
 +Pl+Gen:jga K ;
 +Pl+Gen:jga NAMÁK ;
 +Pl+Acc:jdik K ;
 +Pl+Acc:jdak K ;
 +Pl+Ill:jdak K ;
 +Pl+Ine:jnak K ;
 +Pl+Ela:jstak K ;
 +Pl+Com:jga K ;
 
LEXICON migeobl
 +Sg+Gen:ñga K ;
 +Sg+Gen:ñge K ; 
 +Sg+Gen:ñga NAMÁK ;
 +Sg+Gen:ñge NAMÁK ;
 +Sg+Acc:vga K ;
 +Sg+Ill:sik K ; 
 +Sg+Ine:n'nak K ; 
 +Sg+Ela:sstak K ; 
 +Sg+Com:jnak K ;
 +Ess:nenik K ;
 +Ess:n'nak K ;
 +Pl+Gen:jga K ;
 +Pl+Gen:jga NAMÁK ;
 +Pl+Acc:jdak K ;
 +Pl+Acc:jdik K ;
 +Pl+Ine:jnak K ;
 +Pl+Ela:jstak K ;
 +Pl+Com:jga K ;


LEXICON oblietjácase
 +Sg+Gen: K ;
 +Sg+Gen: NAMÁK ;
 +Sg+Acc:v K ;
 +Sg+Ill:j K ; 
 +Sg+Ela:s K ; 
 +Sg+Ine:n K ;
 +Sg+Com:jn K ;
 +Ess:n K ;
 
LEXICON plurietjácase
 +Pl+Gen:ij K ;
 +Pl+Acc:ijt K ;
 +Pl+Ill:ijda K-s ;
 +Pl+Ine:ijna K-s ;
 +Pl+Ela:ijs K ;
 +Pl+Com:ij K ;
 
LEXICON oblsoamescase
 +Sg+Gen:a K ;
 +Sg+Acc:av K ;
 +Sg+Ill:ij K ; 
 +Sg+Ela:is K ; 
 +Sg+Ine:in K ;
 +Sg+Com:ijn K ;
 +Pl+Gen:ij K ;
 +Pl+Gen:ij NAMÁK ;
 +Pl+Acc:ijt K ;
 +Pl+Ill:ijda K ;
 +Pl+Ine:ijn K ;
 +Pl+Ela:ijs K ;
 +Pl+Com:ij K ;
 
LEXICON aktakoblcase
 +Sg+Gen:k K ;
 +Sg+Gen: NAMÁK ;
 +Sg+Acc:k K ;
 +Sg+Ela:stik K ; 
 +Sg+Ine:nik K ;
 +Sg+Com:jnik K ;
 
LEXICON muhtemcase
 +Attr K ;
 +Sg+Nom: K ;
 : NAMÁK ;
 +Sg+Gen:a K ;
 +Sg+Acc:av K ;
 +Sg+Ill:ij K ; 
 +Sg+Ela:is K ; 
 +Sg+Ine:in K ;
 +Sg+Com:ijn K ;
 +Ess:in K ;
 +Pl+Nom:a K ;
 +Pl+Gen:ij K ;
 +Pl+Gen:ij NAMÁK ;
 +Pl+Acc:ijt K ;
 +Pl+Ill:ijda K ;
 +Pl+Ine:ijn K ;
 +Pl+Ela:ijs K ;
 +Pl+Com:ij K ;
 
LEXICON juogacase
 +Sg+Nom:ga K ;
 +Sg+Nom:ga NAMÁK ;
 +Sg+Gen:nga K ;
 +Sg+Acc:jdá K ;
 +Sg+Ill:sik K ; 
 +Sg+Ela:sstá K ; 
 +Sg+Ine:nna K ;
 +Sg+Ine:nne K ;
 +Sg+Com:jnak K ;
 +Ess:nin K ;
 
LEXICON gájkacase
 :gájk NAMÁK ;
 :gájkka NAMÁK ;
 +Attr:gájkka K ;
 +Sg+Nom:a K ;
 +Sg+Gen:aj K ;
 +Sg+Acc:ajt K ;
 +Sg+Ill:ajda K ; 
 +Sg+Ela:ajs K ; 
 +Sg+Ine:ajn K ;
 +Sg+Com:ajn K ;
 +Pl+Nom:ka K ;
 +Pl+Gen:ka K ;
 +Pl+Acc:ka K ;
 +Pl+Ill:ka K ;
 +Pl+Ine:ka K ;
 +Pl+Ela:ka K ;
 +Pl+Com:ka K ;
 
LEXICON goappátjagácase
 +Pl+Gen:j K ;
 +Pl+Acc:jt K ;
 +Pl+Ill:jda K ;
 +Pl+Ine:jn K ;
 +Pl+Ela:js K ;
 +Pl+Com:j K ;
 
LEXICON goabbákcase
 +Sg+Gen:a K ;
 +Sg+Acc:av K ;
 +Sg+Ill:ij K ; 
 +Sg+Ela:is K ; 
 +Sg+Ine:in K ;
 +Sg+Com:ijn K ;
 +Pl+Gen:ij K ;
 +Pl+Acc:ijt K ;
 +Pl+Ill:ijda K ;
 +Pl+Ine:ijn K ;
 +Pl+Ela:ijs K ;
 +Pl+Com:ij K ;


LEXICON iind
 +Pron+Indef:0 K ;




! Subjunction
! ===========

LEXICON Subjunction
ahte	Cs ;
at	Cs ;
dade	Cs ;
danen	Cs ;
dasik	Cs ;
degu	Cs ;
dagu	Cs ; !XXX se degu
desik	Cs ;
gå	Cs ;
ihka	Cs ;
juhte	Cs ;
juhti Cs ;
jus	Cs ;
jut	Cs ;
made Cs ;
men	Cs ;
nåv% gåk	Cs ;
vaj		Cs ;
vaják  Cs ;
!valla	Cs ; CC only.
val    Cs ;
vájk	Cs ;
vájku	Cs ;
ájnat	Cs ;
sihke Cs ;
åvddål% gå Cs ;
åvddåla% gå Cs ;


LEXICON Cs 
+CS:0 # ;





! Conjunction
! ===========


LEXICON Conjunction

!de		Cc ; ! Pcle only, "så".
ja		Cc ;
jali		Cc ;
ge		Cc ;
goappátja	Cc ;
jalik		Cc ;
juogu		Cc ;
vaj		Cc ;
valla		Cc ;
val         Cc ;
vájbi		Cc ;

LEXICON Cc
+CC:0 # ;




! Numerals
! ========



LEXICON Numeral					!^N^
!MILJON ;          ! a noun of its own		!^N^
UNDERDUHAT ;      ! for generator under 1000	!^N^
!JUSTDUHAT ;       ! going via 1000		!^N^
!OVERDUHAT ;       ! for generator over 1000	!^N^
!OLD ;             ! for "thirteen hundred, etc.	!^N^
!num-ordinal ;     ! The basic ordinal numbers	!^N^!Integrated in card.
!num-derived ;    ! still unimplemented		!^N^
!num-imprecise ;   ! still almost unimplemented	!^N^
ARABIC ;          ! for the arabic numerals	!^C^
ROMAN ;           ! for the roman numerals	!^C^
NUM-PREFIXES ;    ! for §34 etc.		!^C^
ISOLATED-NUMEXP ; ! for ½ etc.			!^C^




LEXICON UNDERDUHAT

akta AKTA ;
akta:åvtå ÅVTÅ ;
guokta GUOKTA ;
gålmmå GÅLMMÅ ;
niellja GUOKTA ;
vihtta GÅLMMÅ ;
guhtta GÅLMMÅ ;
gietjav GIETJAV ;
gáktsa GÁKTSA ;
aktse AKTSE ;
lågev LÅGEV ;
tjuohte TJUOHTE ;

 

!lågenan
!låhke
!låk

LEXICON AKTA
+Num: AKTA-NIE ;
+Num:X1 AKTA-OBL ;

LEXICON AKTA-NIE
 +Sg+Nom: K ;
 +Sg+Ill:j K ;
 +Ess:n K ;

LEXICON AKTA-OBL
 +Sg+Gen: K ; 
 +Sg+Acc:v K ;
 +Sg+Ine:n K ;
 +Sg+Ela:s K ;
 +Sg+Com:jn K ;
 +Sg+Par:t K ;
 +Pl+Nom: K ;
 +Pl+Gen:j K ;
 +Pl+Acc:jt K ;
 +Pl+Ill:jda K ;
 +Pl+Ine:jn K ;
 +Pl+Ela:js K ;
 +Pl+Com:j K ;
 
LEXICON ÅVTÅ
+Num+Sg+Gen: K ; 
+Num+Sg+Acc:v K ;
+Num+Sg+Ine:n K ;
+Num+Sg+Ela:s K ;
+Num+Sg+Com:jn K ;
+Num+Sg+Par:t K ;
+Num+Pl+Nom: K ;
+Num+Pl+Gen:j K ;
+Num+Pl+Acc:jt K ;
+Num+Pl+Ill:jda K ;
+Num+Pl+Ine:jn K ;
+Num+Pl+Ela:js K ;
+Num+Pl+Com:j K ;


LEXICON GUOKTA
+Num: GUOVTE- ;
+Num: GUOKTA- ;

LEXICON GUOVTE-
K ;

LEXICON GUOKTA-
K ; 

LEXICON GÅLMMÅ
+Num: K ; 

LEXICON GIETJAV
+Num: K ; 

LEXICON GÁKTSA
+Num: K ; 

LEXICON AKTSE
+Num: K ; 

LEXICON LÅGEV
+Num: K ; 

LEXICON TJUOHTE
+Num: K ; 




! Particles
! =========

LEXICON PCLE
+Pcle: # ;

LEXICON QPCLE
+Qst+Pcle: # ; !Only Qst?

LEXICON Particle
aj PCLE ;
dal PCLE ;
dat PCLE ;
de PCLE ;
gal PCLE ;
galla PCLE ;
gus QPCLE ;
gåla PCLE ;
ham PCLE ;
juo PCLE ;
lik PCLE ;
nav PCLE ;
ga PCLE ;
så PCLE ;
ban PCLE ;
rat PCLE ;
ga PCLE ;
vuojn PCLE ;
villap PCLE ;
gis PCLE ;
má PCLE ;
na PCLE ; 
ná PCLE ;
då PCLE ;
iehtja PCLE ;




! Interjections
! =============

LEXICON INTERJ
+Interj: # ;

LEXICON Interjection
hå	INTERJ ;
håj	INTERJ ;
ge	INTERJ ;
fuoj	INTERJ ;
vuoj	INTERJ ;
dale INTERJ ;



! Arabic numerals
! ---------------


LEXICON NUM-PREFIXES				!^C^
§	ARABIC ;      ! §24			!^C^
§§	ARABIC ;     ! §§24			!^C^
§% 	ARABIC ;    ! § 24			!^C^
§§% 	ARABIC ;   ! §§ 24			!^C^
%-	ARABIC ;     ! -24			!^C^
%‒	ARABIC ;     ! -24 U+2012 Figure line	!^C^
%–	ARABIC ;     ! -24 U+2013 en dash	!^C^
%—	ARABIC ;     ! -24 U+2014 em dash	!^C^
%―	ARABIC ;     ! -24 U+2015 horizontal l	!^C^
%+	ARABIC ;     ! +24			!^C^
%-% 	ARABIC ;	! - 24			!^C^
%‒% 	ARABIC ;	! - 24 2012		!^C^
%–% 	ARABIC ;	! - 24 2013	 	!^C^
%—% 	ARABIC ;	! - 24 2014		!^C^
%―% 	ARABIC ;	! - 24 2015 		!^C^
%+% 	ARABIC ;	! + 24			!^C^
%*	ARABIC ;     ! *24			!^C^
%$	ARABIC ;     ! $24  ! And the Euro?	!^C^
%<	ARABIC ;     ! <24			!^C^
%>	ARABIC ;     ! >24			!^C^
s%.	ARABIC ;     ! s.24			!^C^
	


LEXICON ARABIC					!^C^
1 ARABICLOOP ;					!^C^
2 ARABICLOOP ;					!^C^
3 ARABICLOOP ;					!^C^
4 ARABICLOOP ;					!^C^
5 ARABICLOOP ;					!^C^
6 ARABICLOOP ;					!^C^
7 ARABICLOOP ;					!^C^
8 ARABICLOOP ;					!^C^
9 ARABICLOOP ;					!^C^
%0 ARABICLOOP ;					!^C^

LEXICON ARABICLOOP				!^C^
ARABIC ;					!^C^
%-	ARABIC ;	      ! 1-2    multipart numbers	!^C^
%.%- 	ARABIC ;	      ! 1.-2   multipart numbers	!^C^
%-% 	ARABIC ;	      ! 1- 2   multipart numbers	!^C^
% %-	ARABIC ;	      ! 1 -2   multipart numbers	!^C^
% %-% 	ARABIC ;	      ! 1 - 2  multipart numbers	!^C^
% %-%-%   ARABIC ;	      ! 1 -- 2 multipart numbers	!^C^
% %-      ARABICDELIMITER ;   ! 1 -   multipart numbers		!^C^
% %-%-	  ARABICDELIMITER ;   ! 1 --  multipart numbers		!^C^
%. ARABIC ;							!^C^
%, ARABIC ;							!^C^
%: ARABIC ;							!^C^
%  ARABIC ;	   ! Allowing for "23 500".			!^C^
%/ ARABIC ;	   ! 24/12					!^C^
!%' ARABIC ;	   ! Since ' denotes cons.grad. This is not optimal either. 
!+Num:%- R ; !^C^	   ! Number compounds
+Num: R ; !^C^	   ! Hyphen removed bec. of "123- ja 124-viessu" !^C^
+Num+Ord:%. # ;							 !^C^
,%-+Num:,%- # ;		! 10,-					 !^C^
%%+Num+Nom:%% # ;	! 50%. It is wrong, but 75 % of us write "75%".		!^C^
% %%+Num+Nom:% %% # ;	! We must analyse the correct ones as well... 50 % etc.	!^C^
+Num: ARABICDELIMITER ; ! list of number-case delimiters			!^C^


LEXICON ARABICDELIMITER								!^C^
  ARABICCASE ;      ! This is the 1984s case.					!^C^
 :%: ARABICCASE ;    ! This is the 1984:s case.					!^C^
 :%' ARABICCASE ;    ! This is the 1984's case.					!^C^
 :%- ARABICCASE ;    ! This is the 1984-s case.					!^C^
                     ! So, what does the lg council prefer?			

LEXICON ARABICCASE								!^C^
+Nom:f # ;		! s. 123f.						!^C^
+Nom:ff # ;		! s. 123ff.						!^C^
+Nom:b # ;		! § 123b						!^C^
+Nom:c # ;		! § 123c						!^C^
+Nom:d # ;		! § 123d						!^C^
+Nom:e # ;		! § 123e						!^C^
! # ;		! 123 is now 123+Num as well as 123+Num+Nom etc. Out again!	!^C^
+Nom: # ;									!^C^
+Gen: # ;									!^C^
+Acc: # ;									!^C^
+Gen:a # ;	! But here 123a 123+Num+Gen					!^C^
+Acc:a # ;									!^C^
+Ill:i # ;									!^C^
+Ill:ij # ;									!^C^
+Ill:áj # ;	! Testing over the weekend.					!^C^
+Loc:s # ;									!^C^
+Loc:is # ;									!^C^
+Loc:as # ;									!^C^
+Com:in # ;									!^C^
+Com:ain # ;									!^C^
+Ess:n # ;									!^C^
+Ess:an # ;									!^C^
! No plural numbers, hmm.


! The Roman numerals !
! ------------------ !

LEXICON ROMAN						!^C^
 ROM-THOUSAND ;						!^C^
 ROM-HUNDRED ;						!^C^
 ROM-TEN ;						!^C^
 ROM-ONE ;						!^C^

LEXICON ROM-THOUSAND              !^C^
 M ROM-THOUSAND-TAG ;						!^C^
 MM ROM-THOUSAND-TAG ;					!^C^
 MMM ROM-THOUSAND-TAG ;					!^C^
 MMMM ROM-THOUSAND-TAG ;					!^C^
 MMMMM ROM-THOUSAND-TAG ;					!^C^

LEXICON ROM-THOUSAND-TAG					!^C^
 +Num: ROMNUMTAG ;						!^C^
 ROM-HUNDRED ;					!^C^
 ROM-SPLIT	;				!^C^

LEXICON ROM-HUNDRED					!^C^
 C ROM-HUNDRED-TAG ;						!^C^
 CC ROM-HUNDRED-TAG ;						!^C^
 CCC ROM-HUNDRED-TAG ;						!^C^
 CD ROM-HUNDRED-TAG ;						!^C^
 D ROM-HUNDRED-TAG ;						!^C^
 DC ROM-HUNDRED-TAG ;						!^C^
 DCC ROM-HUNDRED-TAG ;						!^C^
 DCCC ROM-HUNDRED-TAG ;						!^C^
 CM ROM-HUNDRED-TAG ;						!^C^

LEXICON ROM-HUNDRED-TAG					!^C^
 +Num: ROMNUMTAG ;						!^C^
 ROM-TEN ;					!^C^
 ROM-SPLIT ;					!^C^

LEXICON ROM-TEN						!^C^
 X ROM-TEN-TAG ;						!^C^
 XX ROM-TEN-TAG ;						!^C^
 XXX ROM-TEN-TAG ;						!^C^
 XL ROM-TEN-TAG ;						!^C^
 L ROM-TEN-TAG ;						!^C^
 LX ROM-TEN-TAG ;						!^C^
 LXX ROM-TEN-TAG ;						!^C^
 LXXX ROM-TEN-TAG ;						!^C^
 XC ROM-TEN-TAG ;						!^C^

LEXICON ROM-TEN-TAG					!^C^
 +Num: ROMNUMTAG ;						!^C^
 ROM-ONE ;						!^C^
 ROM-SPLIT ;						!^C^

LEXICON ROM-ONE						!^C^
 I ROM-ONE-TAG ;						!^C^
 II ROM-ONE-TAG ;						!^C^
 III ROM-ONE-TAG ;						!^C^
 IV  ROM-ONE-TAG ;						!^C^
 V ROM-ONE-TAG ;						!^C^
 VI ROM-ONE-TAG ;						!^C^
 VII ROM-ONE-TAG ;						!^C^
 VIII ROM-ONE-TAG ;						!^C^
 IX ROM-ONE-TAG ;						!^C^

LEXICON ROM-ONE-TAG					!^C^
 +Num: ROMNUMTAG ;						!^C^
 +N: ROMNUMTAG ;						!^C^ !The Olav viđeš fix (V gets N tag)
 ROM-SPLIT ;						!^C^

! Here, we split the Roman numerals, in order to account for cases like "Kapihtal II-IV".
! We may send this first part directly to ROM-TAG below (and get the +Num tag), or we may
! send them through a second loop, identical to the first one, but marked wit "2" (the lexica
! are called 2ROMAN, etc. We do this instead of making a loop, since we do not want cases
! like "II-IV-VI-VII-IX". If that should turn out to be a good idea, a loop would do the 
! trick.

LEXICON ROM-SPLIT					!^C^
 :%- 2ROMAN ; ! II-VI, etc.				!^C^


! Here goes loop 2.

LEXICON 2ROMAN						!^C^
 2ROM-THOUSAND ;						!^C^
 2ROM-HUNDRED ;						!^C^
 2ROM-TEN ;						!^C^
 2ROM-ONE ;						!^C^

LEXICON 2ROM-THOUSAND              !^C^
 M 2ROM-THOUSAND-TAG ;						!^C^
 MM 2ROM-THOUSAND-TAG ;					!^C^
 MMM 2ROM-THOUSAND-TAG ;					!^C^
 MMMM 2ROM-THOUSAND-TAG ;					!^C^
 MMMMM 2ROM-THOUSAND-TAG ;					!^C^

LEXICON 2ROM-THOUSAND-TAG					!^C^
 +Num: ROMNUMTAG ;						!^C^
 2ROM-HUNDRED ;					!^C^

LEXICON 2ROM-HUNDRED					!^C^
 C 2ROM-HUNDRED-TAG ;						!^C^
 CC 2ROM-HUNDRED-TAG ;						!^C^
 CCC 2ROM-HUNDRED-TAG ;						!^C^
 CD 2ROM-HUNDRED-TAG ;						!^C^
 D 2ROM-HUNDRED-TAG ;						!^C^
 DC 2ROM-HUNDRED-TAG ;						!^C^
 DCC 2ROM-HUNDRED-TAG ;						!^C^
 DCCC 2ROM-HUNDRED-TAG ;						!^C^
 CM 2ROM-HUNDRED-TAG ;						!^C^

LEXICON 2ROM-HUNDRED-TAG					!^C^
 +Num: ROMNUMTAG ;						!^C^
 2ROM-TEN ;					!^C^

LEXICON 2ROM-TEN						!^C^
 X 2ROM-TEN-TAG ;						!^C^
 XX 2ROM-TEN-TAG ;						!^C^
 XXX 2ROM-TEN-TAG ;						!^C^
 XL 2ROM-TEN-TAG ;						!^C^
 L 2ROM-TEN-TAG ;						!^C^
 LX 2ROM-TEN-TAG ;						!^C^
 LXX 2ROM-TEN-TAG ;						!^C^
 LXXX 2ROM-TEN-TAG ;						!^C^
 XC 2ROM-TEN-TAG ;						!^C^

LEXICON 2ROM-TEN-TAG					!^C^
 +Num: ROMNUMTAG ;					!^C^
 2ROM-ONE ;					!^C^

LEXICON 2ROM-ONE						!^C^
 I 2ROM-ONE-TAG ;						!^C^
 II 2ROM-ONE-TAG ;						!^C^
 III 2ROM-ONE-TAG ;						!^C^
 IV  2ROM-ONE-TAG ;						!^C^
 V 2ROM-ONE-TAG ;						!^C^
 VI 2ROM-ONE-TAG ;						!^C^
 VII 2ROM-ONE-TAG ;						!^C^
 VIII 2ROM-ONE-TAG ;						!^C^
 IX 2ROM-ONE-TAG ;						!^C^

LEXICON 2ROM-ONE-TAG						!^C^
 +Num: ROMNUMTAG ;						!^C^
 
LEXICON ROMNUMTAG 						!^C^
+Nom: # ;									!^C^
+Gen: # ;									!^C^
+Acc: # ;									!^C^
 :%: ROMNUMTAGOBL ;						!^C^
 :%' ROMNUMTAGOBL ;						!^C^
 :%- ROMNUMTAGOBL ;						!^C^
 :%´ ROMNUMTAGOBL ;						!^C^
     ROMNUMTAGOBL ;						!^C^

LEXICON ROMNUMTAGOBL 						!^C^
+Gen:a # ;	! But here 123a 123+Num+Gen					!^C^
+Acc:a # ;									!^C^
+Ill:i # ;									!^C^
+Ill:ij # ;									!^C^
+Ill:áj # ;					!^C^
+Loc:s # ;									!^C^
+Com:in # ;									!^C^
+Ess:n # ;									!^C^




! A final section with some isolated numeral expresssions !
! ------------------------------------------------------- !

      
LEXICON ISOLATED-NUMEXP					!^C^
½+Num:½ # ;						!^C^
¹+Num:¹ # ;						!^C^
²+Num:² # ;						!^C^
³+Num:³ # ;						!^C^
¼+Num:¼ # ;						!^C^








